.* MVS 3.8J PUBLIC DOMAIN
* /* START OF SPECIFICATIONS ****
*
*01*  MODULE-NAME = USSPARM
*
*01*  DESCRIPTIVE-NAME = USS PARAMETER DEFINITION MACRO
*
*
*01*  STATUS = RELEASE 2
*
*01*  FUNCTION = THE PURPOSE OF THIS MACRO IS TO GENERATE A PPB
*     (PARAMETER PROCESSING BLOCK). THE BLOCK IS USED IN CONJUNCTION
*     WITH A VPB ONLY. THE MACRO CREATS A 4 WORD TABLE CONSISTING OF
*     CHAIN POINTER,KEYWORD POINTER, REPLACEMENT POINTER, AND A DEFAULT
*     VALUE, THE HIGH ORDER BYTE OF EACH IS THE LENGTH OF THE DATA
*     POINTED TO. IN ADDITION THE LAST THREE DATA ITEMS ARE GENERATED
*     USING INTERNAL CALLS TO THE USSEND MACRO.
*
*02*    CHARACTER-CODE-DEPENDENCIES = NONE
*
*02*    DEPENDENCIES= NONE
*
*02*    RESTRICTIONS = NONE
*
*02*    REGISTER-CONVENTIONS = NONE
*
*02*    PATCH-LABEL = NONE
*
*01*  MODULE-TYPE = MACRO
*
*02*    PROCESSOR = ASSEM-370R
*
*02*    MACRO-SIZE = 19 STATEMENTS
*
*02*    ATTRIBUTES = LIST-EXEC
*
*01*  ENTRY-POINT = NA
*
*01*  EXIT-NORMAL = NA
*
*01*  EXIT-ERROR = NA
*
*01*  EXTERNAL-REFERENCES = NONE
*
*01*  TABLES = PPB (PARAMETER PROCESSING BLOCK)
*
*01*  INNER-MACROS = USSEND
*
*01*  SYSTEM-MACROS = NONE
*
*01*  CHANGE-ACTIVITY = AS-FOLLOWS
*                       NEW MACRO - FIRST RELEASE   32  DOS/VS
*                                                   04  OS/VS1
*                                                   03  OS/VS2
*            APAR OZ26263  ADDITIONAL VALIDITY CHECKS        @ZA26263
**** END OF SPECIFICATIONS ***/
         MACRO
&LABEL   USSPARM &PARM=,&REP=,&DEFAULT=
         GBLC  &Q                 USSEND OUTPUT VARIABLE
         GBLC  &T                 USSEND OUTPUT VARIABLE
         GBLC  &N                 STATEMENT LABEL PREFIX
         GBLB  &B                 SWITCH FOR USSCMD SPECIFIED
         GBLB  &Z                 SWITCH FOR USSTAB SPECIFIED
         AIF   (&Z EQ 1).SQ005
         SPACE 1
         MNOTE 4,'USSTAB MACRO NOT SPECIFIED'
.SQ005   ANOP
         AIF   (&B EQ 1).SQ007
         SPACE 1
         MNOTE 4,'USSCMD MACRO NOT SPECIFIED'
.SQ007   ANOP
         AIF   ('&PARM' NE '').SQ010
         SPACE 1
         MNOTE 4,'&N.001 PARM= PARAMETER OMITTED'
.SQ010   USSEND INC=P             INCREMENT USSPARM COUNTER
         SPACE 1
&LABEL   DC    0F'0'              ALIGN ON FULLWORD BOUNDARY
&N.P&T   DC    A(&N.P&Q)          ADDR NEXT USSPARM BLOCK
         DC    A(&N.K&T)          ADDR KEYWORD CHAR STRING
         DC    A(&N.A&T)          ADDR REPLACEMENT STRING
         DC    A(&N.D&T)          ADDR DEFAULT VALUE STRING
&N.K&T   USSEND GEN=&PARM,NAME=&N.L&T
&N.A&T   USSEND GEN=&REP,NAME=&N.B&T,OMIT=&N.K&T
&N.D&T   USSEND GEN=&DEFAULT,NAME=&N.E&T
         SPACE 1
         MEND

