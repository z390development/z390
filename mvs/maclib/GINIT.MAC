.* MVS 3.8J PUBLIC DOMAIN
         MACRO
&NAME    GINIT &BPX=0,&BPY=0,&BLC=0,&BLIM=32767,&BLP=0
         GBLA  &IHBBPX,&IHBBPY,&IHBBLC,&IHBBLIM,&IHBBLP
         GBLC  &IHBINIT
&IHBINIT    SETC  'IHBI'.'&SYSNDX'
&IHBINIT    DS    0H
         AIF   (T'&BLP NE 'N').NOBLP    VALIDATE BUFFER LOAD POINT
         AIF   (&BLP GT 32767).NOBLP
&IHBBLP    SETA  &BLP                  INITIALIZE BUFFER LOAD POINT
.ALSET   AIF   (T'&BPX NE 'N').RBPX     VALIDATE X-COORDINATE BEAM
         AIF   (&BPX GT 4095).RBPX      POSITION COUNTER
&IHBBPX    SETA  &BPX                  INITIALIZE BEAM POS COUNTER
.TAG1    AIF   (T'&BPY NE 'N').RBPY     VALIDATE Y-COORDINATE BEAM
         AIF   (&BPY GT 4095).RBPY      POSITION COUNTER
&IHBBPY    SETA  &BPY                  INITIALIZE BEAM POS COUNTER
.TAG2    AIF   (T'&BLC NE 'N').RBLC     VALIDATE BUFFER LOC COUNTER
         AIF   (&BLC GT 32767).RBLC
&IHBBLC    SETA  &BLC                  INITIALIZE BUFFER LOC COUNTER
.TAG3    AIF   (T'&BLIM NE 'N').RBLIM   VALIDATE BUFFER LIMIT
         AIF   (&BLIM GT 32767).RBLIM
&IHBBLIM   SETA  &BLIM                 INITIALIZE BUFFER LIMIT
.TAG4    AIF   (&IHBBLIM LE &IHBBLC).RBLIMA
         AGO   .NAMCHK
.NOBLP   IHBERMAC 180,BLP,0
&IHBBLP  SETA    0
         AGO   .ALSET
.RBPX    IHBERMAC 180,BPX,0
&IHBBPX    SETA  0
         AGO   .TAG1
.RBPY    IHBERMAC 180,BPY,0
&IHBBPY    SETA  0
         AGO   .TAG2
.RBLC    IHBERMAC 180,BLC,0
&IHBBLC    SETA  0
         AGO   .TAG3
.RBLIM   IHBERMAC 180,BLIM,32767
&IHBBLIM   SETA  32767
         AGO   .TAG4
.RBLIMA  IHBERMAC 180,BLIM,32767
&IHBBLIM   SETA  32767
.NAMCHK  AIF   ('&NAME' EQ '').EXIT
&NAME    EQU   &IHBINIT
IHB&NAME   EQU   &IHBBLP
.EXIT    MEND
