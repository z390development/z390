.* MVS 3.8J PUBLIC DOMAIN
         MACRO
&NAME    CALL3886  &IDCB,&IFRID,&IMF
         GBLB  &QUITSW
.*  FUNCTION  - GENERATE STANDARD PARAMETER LIST FOR 3886 OR CALL
.*              AN INNER MACRO (INN3886) TO HANDLE THE LIST AND
.*              EXECUTE FORMS OF THE SETDEV MACRO FOR 3886
.*             TEST FOR VALID 3886 PARAMETERS
         AIF   ('&IFRID' NE '').CKLNTH  TEST IF FRID SPECIFIED
         AIF   ('&IMF' NE '').CKLNTH    FRID NOT REQ FOR L OR E FM
.*             ISSUE MNOTE FOR FRID NOT SPECIFIED
         IHBERMAC  261
         AGO   .SETQUIT                  GO SET QUIT SWITCH ON
.CKLNTH  AIF   (K'&IFRID LE 4).TSTFORM  TEST IF LNTH OF FRID LE 4 CHAR
.*             ISSUE MNOTE FOR FRID INVALID
         IHBERMAC  254,,&IFRID
         AGO   .SETQUIT                  GO SET QUIT SWITCH ON
.TSTFORM AIF ('&IMF' EQ 'L' OR '&IMF(1)' EQ 'E').LSTOREX   TST L/E FM
.*             GENERATE CODE FOR STANDARD FORM OF MACRO
&NAME    BAL   1,*+16              BRANCH AROUND SETDEV PARAMETER LIST
.*       TEST IF DCB ADDR IN REG OR NOT SPECIFIED (L OR E FORM)
         AIF   ('&IDCB' EQ '').NOTADR
         AIF   ('&IDCB'(1,1) EQ '(').NOTADR
.*                                 NO GENERATE ADDR CONSTANT FOR DCB
         DC    A(&IDCB)            DCB ADDRESS
         AGO   .FRIDCST            GO GEN FRID CONSTANT
.NOTADR  ANOP
         DC    AL4(0)         DCB ADDRESS
.FRIDCST ANOP
.*                                 GENERATE FRID AND SETDEV FLAG
          AIF   ('&IFRID' NE '').GFRCST
          DC    CL4' '                        FORMAT RECORD IDENTIFER
          AGO   .GENFLGS
.GFRCST   ANOP
         DC    CL4'&IFRID'         FORMAT RECORD IDENTIFER
.GENFLGS ANOP
         DC    X'FF020000'         SETDEV FLAGS
         AIF   ('&IDCB'(1,1) NE '(').NOSTORE  TST IF DCBADDR IN PARMLST
         ST    &IDCB,0(1,0)             STORE DCB ADDR IN PARM LIST
.NOSTORE ANOP
         MEXIT
.SETQUIT ANOP
&QUITSW  SETB  1
         MEXIT
.LSTOREX ANOP
&NAME    INN3886 &IDCB,&IFRID,&IMF
         MEND
