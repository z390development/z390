.* MVS 3.8J PUBLIC DOMAIN
         MACRO
&ANYNAME SETDEV &DCBA,       ADDRESS OF DCB                            X
               &DEVT=,       DEVICE TYPE - SPECIFIED BY GENERIC NAME   X
               &FRID=,       FORMAT RECORD IDENTIFER FOR 3886          X
               &IREC=,       ADDRESS OF IREC MACRO FOR 3890            X
               &SUBRTN=,           SCI SUBROUTINE OPERAND              X
               &PROGRAM=,          SCI PROGRAM OPERAND                 X
               &MF=          MACRO FORM
         GBLB  &QUITSW
.*  FUNCTION -  LOAD DEVICE DEPENDENT CONTROL INFORMATION
.*  DEVICES SUPPORTED - 3886 , 3890
.*
&QUITSW  SETB  0                       INITIALIZE QUIT SWITCH TO 0
.*             TEST FOR VALID DCB,DEVT AND MF SPEC /ALIGN PARMLST ON WD
         TSTCOM &DCBA,&DEVT,&MF
.*             TEST FOR 3886  AND THEN PROCESS 3886 PARAMETERS
         AIF   ('&DEVT' NE '3886').NXTDEV1  TEST IF DEVICE = 3886
         AIF   (&QUITSW).SKIPSVC   ERRORS FOUND
&ANYNAME CALL3886 &DCBA,&FRID,&MF
         AIF   (NOT &QUITSW).GENEND    TEST IF QUIT SWITCH ON
         MEXIT
.*             TEST FOR 3890 AND THEN PROCESS 3890 PARAMETERS
.NXTDEV1 AIF   ('&DEVT' NE '3890').NXTDEV2 TEST FOR DEVICE = 3890
.*             YES GO SET UP PARAMETER LIST FOR 3890
&ANYNAME CALL3890 &DCBA,&IREC,&SUBRTN,&PROGRAM,&MF
         AIF   (NOT &QUITSW).GENEND    TEST IF QUIT SWITCH ON
         MEXIT
.*       GENERATE PROPER END LINKAGE- SVC   81 FOR STANDARD AND EXECUTE
.NXTDEV2 AIF   (&QUITSW).SKIPSVC   TEST FOR ERROR FOUND
.GENEND  AIF   ('&MF' EQ 'L').SKIPSVC
         SVC   81             ISSUE SVC FOR SETDEV
.SKIPSVC ANOP
         MEND

