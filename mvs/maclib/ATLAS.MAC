.* MVS 3.8J PUBLIC DOMAIN
         MACRO
&NAME    ATLAS &PARMADR=,&CHANPRG=R,&CNTPTR=P,&WRITS=
         AIF   ('&PARMADR' EQ '').ERROR1
         AIF   ('&PARMADR'(1,1) EQ '(').ISAREG
&NAME    L     1,&PARMADR  LOAD ADDRESS OF PARM LIST
         AGO   .SKIP
.ISAREG  AIF   ('&PARMADR(1)' EQ '1').PUTLBL
&NAME    LR    1,&PARMADR(1)  LOAD ADDRESS OF PARMLIST
         AGO   .SKIP
.PUTLBL  ANOP
&NAME    MVI   0(1),X'00'  CLEAR OPTION BYTES
         AGO   .HOP
.SKIP    MVI   0(1),X'00'  CLEAR OPTION BYTE
.HOP     MVI   4(1),X'00'
         AIF   ('&CHANPRG' EQ 'NR').TURNON
         AIF   ('&CHANPRG' NE 'R').ERROR2
         AGO   .JUMP
.TURNON  ANOP
         OI    0(1),X'80'  TURN ON NOT RE-EX BIT
.JUMP    AIF   ('&CNTPTR' EQ 'P').SETBIT
         AIF   ('&CNTPTR' NE 'F').ERROR3
         AGO   .STEP
.SETBIT  ANOP
         OI    4(1),X'40'  INDICATE PARTIAL COUNT
.STEP    AIF   ('&WRITS' EQ '').ALLOVER
         AIF   ('&WRITS' EQ 'YES').YEP
         AIF   ('&WRITS' NE 'NO').ERROR4
         OI    4(1),X'10' SET FOR NOT WRITE SPECIAL
         AGO   .ALLOVER
.YEP     ANOP
         OI    4(1),X'20' SET FOR WRITE SPECIAL
.ALLOVER ANOP
         SVC 86   ISSUE ATLAS SVC
         MEXIT
.ERROR1  MNOTE 12,'IHB001  PARMADR OPERAND REQ''D-NOT SPECIFIED'
         AGO   .END
.ERROR2  MNOTE 12,'IHB002  INVALID CHANPRG OPERAND SPECIFIED-&CHANPRG'
         AGO   .END
.ERROR3  MNOTE 12,'IHB002 INVALID CNTPTR OPERAND SPECIFIED-&CNTPTR'
         AGO   .END
.ERROR4  MNOTE 12,'IHB002 INVALID WRITS OPERAND SPECIFIED-&WRITS'
.END     MEND
