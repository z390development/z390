.* MVS 3.8J PUBLIC DOMAIN
         MACRO
&NAME    PARMLIST &CKLNGTH=,&CKMONKY=,&CKMOD11=,&CKMOD10=,&CKRANGE=,   X
               &RNGETST=,&CKOR=,&CKAND=,&CKNONUM=,&CKNUM=,&HIGUID=,    X
               &LOWGUID=,&ORGUID=,&ANDGUID=,&TRANSL=,&IDENT=
         GBLA  &IECNDGP(100)
.*             IECNDGP(1)  TOTAL BYTES COUNTER
         GBLB &IECSW(9),&IECSEQ(10),&IECSQ(7),&IECGDSW
.*             IECSQ(4) SEQUENCE INDICATOR
.*             IECGDSW  ON INDICATES GDU=YES CODED IN CONFIGUR MACRO
         LCLA  &A,&B,&C,&D,&E,&F
.*             &E   TOTAL NO OF CHECKS OTHER THAN CKLNGTH AND CKMONKY
.*             &F   CHECK FUNCTION FIELD
         LCLA  &G
         LCLB  &LEN(16),&LENE(16),&POS(16),&MODE(16),&ALE(16),&ME(16)
         LCLB  &HIE(16),&LOWE(16),&ORE(16),&ANDE(16),&NUME(16)
         LCLB  &NOCODE,&N
.*             &N   INDICATES LENGTH POSITIONS SHOULD BE CHECKED
         LCLC  &CHAR
         ACTR  1000
         AIF   (&IECSEQ(1) AND &IECSEQ(2) AND &IECSQ(1) AND &IECSQ(2)  X
               AND &IECSQ(3) AND NOT &IECSQ(5) AND NOT &IECSQ(6)).SEQ1
.SEQ2    ANOP
&NOCODE  SETB  1
         MNOTE 12,'MACRO SEQUENCE ERROR'
         AGO   .NAM
.SEQ1    AIF   (&IECSEQ(4) OR &IECSEQ(5) OR &IECSEQ(10) OR &IECSEQ(6)).X
               SEQ2
.NAM     ANOP
         AIF   ('&NAME' NE '').GDUYES
&NOCODE  SETB  1
         MNOTE 12,'NAME FIELD REQUIRED'
.GDUYES  AIF   (&IECGDSW).LEN
&NOCODE  SETB  1
         MNOTE 12,'PARMLIST MACRO NOT VALID UNLESS GDU=YES CODED IN    X
               CONFIGUR MACRO'
.LEN     ANOP
&E       SETA  0
&F       SETA  0
         AIF   (T'&CKLNGTH EQ 'O' AND N'&CKLNGTH EQ 0).MON
&F       SETA  &F+32768
&A       SETA  N'&CKLNGTH
         AIF   (&A LT 1).MON
         AIF   (T'&CKLNGTH(1) NE 'O').LEN1
&NOCODE  SETB  1
         MNOTE 12,'CKLNGTH OPERAND HAS LENGTH SUBOPERAND OMITTED'
         AGO   .LEN3
.LEN1    AIF   (T'&CKLNGTH(1) EQ 'N').LEN2
&NOCODE  SETB  1
         MNOTE 12,'LENGTH SUBOPERAND OF CKLNGTH OPERAND HAS INVALID    X
               TYPE ATTRIBUTE'
         AGO   .LEN3
.LEN2    ANOP
&B       SETA  &CKLNGTH(1)
         AIF   (&B GE 1 AND &B LE 17).LEN3
&NOCODE  SETB  1
         MNOTE 12,'LENGTH SPECIFIED IN CKLNGTH OPERAND NOT IN VALID    X
               RANGE OF 1 TO 17'
.LEN3    ANOP
         AIF   (&A GE 1 AND &A LE 17).LEN4
&NOCODE  SETB  1
         MNOTE 12,'CKLNGTH OPERAND HAS INVALID NUMBER OF SUBOPERANDS'
         AGO   .MON
.LEN4    ANOP
&B       SETA  &B-1      FORGET POSITION 1 BECAUSE NO CHECKS USE IT
&N       SETB  1
&C       SETA  1
         AIF   (&B EQ 0).LEN6
         AIF   (&B GT 16).LEN6
.LEN5    ANOP
         AIF   (&C GT &B).LEN6
&LEN(&C) SETB  1        SWITCHES INDICATING LENGTH POSITIONS USED
&C       SETA  &C+1
         AGO   .LEN5
.LEN6    ANOP
&A       SETA  &A-1     NUMBER OF ERRGUIDANCE OPERANDS
         AIF   (&A EQ 0).MON
&B       SETA  2
&A       SETA  N'&CKLNGTH
.LEN7    ANOP
         AIF   (T'&CKLNGTH(&B) EQ 'N').LEN8
&NOCODE  SETB  1
&CHAR    SETC  '&CKLNGTH(&B)'
         MNOTE 12,'CKLNGTH SUBOPERAND = &CHAR IS INVALID'
.LEN8    ANOP
         AIF   (&CKLNGTH(&B) GE 1 AND &CKLNGTH(&B) LE 16).LEN9
&NOCODE  SETB  1
&CHAR    SETC  '&CKLNGTH(&B)'
         MNOTE 12,'CKLNGTH SUBOPERAND = &CHAR NOT IN RANGE OF 1 TO 16'
         AGO   .LEN10
.LEN9    ANOP
&C       SETA  &CKLNGTH(&B)
&LENE(&C) SETB 1
.LEN10   ANOP
         AIF   (&B EQ &A).MON     IF LAST SUBOPERAND,BRANCH
&B       SETA  &B+1
         AGO   .LEN7       CONTINUE CHECKING CKLNGTH OPERAND
.MON     ANOP
         AIF   (T'&CKMONKY EQ 'O').MOD
         AIF   ('&CKMONKY' EQ 'NO').MON1
         AIF   ('&CKMONKY' EQ 'YES').MON1
&NOCODE  SETB  1
         MNOTE 12,'CKMONKY OPERAND INVALID'
         AGO   .MOD
.MON1    ANOP
         AIF   ('&CKMONKY' NE 'YES').MOD
&F       SETA  &F+64
.MOD     ANOP
         AIF   (T'&CKMOD10 EQ 'O' AND N'&CKMOD10 EQ 0).M
&E       SETA  &E+1
&F       SETA  &F+8192
&A       SETA  N'&CKMOD10
         AIF   (&A LT 1).M
         AIF   (T'&CKMOD10(1) NE 'O').MOD1
&NOCODE  SETB  1
         MNOTE 12,'CKMOD10 OPERAND HAS LENGTH SUBOPERAND OMITTED'
         AGO   .MOD3
.MOD1    AIF   (T'&CKMOD10(1) EQ 'N').MOD2
&NOCODE  SETB  1
         MNOTE 12,'LENGTH  SUBOPERAND OF CKMOD10 OPERAND HAS INVALID   X
               TYPE ATTRIBUTE'
         AGO   .MOD3
.MOD2    ANOP
&B       SETA  &CKMOD10(1)
         AIF   (&B GE 1 AND &B LE 15).MOD3
&NOCODE  SETB  1
         MNOTE 12,'LENGTH SPECIFIED IN CKMOD10 OPERAND NOT IN VALID    X
               RANGE OF 1 TO 15'
.MOD3    ANOP
         AIF   (T'&CKMOD10(2) NE 'O').MOD4
&NOCODE  SETB  1
         MNOTE 12,'CKMOD10 OPERAND HAS POSITION SUBOPERAND OMITTED'
         AGO   .MOD10
.MOD4    ANOP
         AIF   (T'&CKMOD10(2) EQ 'N').MOD5
&NOCODE  SETB  1
         MNOTE 12,'POSITION SUBOPERAND OF CKMOD10 OPERAND HAS INVALID  X
               TYPE ATTRIBUTE'
         AGO   .MOD10
.MOD5    ANOP
&B       SETA  &CKMOD10(2)
         AIF   (&B GE 2 AND &B LE 16).MOD6
&NOCODE  SETB  1
         MNOTE 12,'POSITION SPECIFIED IN CKMOD10 OPERAND NOT IN VALID  X
               RANGE OF 2 TO 16'
         AGO   .MOD10
.MOD6    ANOP
&B       SETA  &B-1
&C      SETA  &B+&CKMOD10(1)-1
         AIF   (&C LE 16).MOD7
&NOCODE  SETB  1
         MNOTE 12,'CKMOD10 LENGTH AND POSITION OPERANDS INVALID BECAUSEX
                SOME POSITIONS ARE BEYOND END OF DATA ENTRY'
         AGO   .MOD10
.MOD7    ANOP
         AIF   (&NOCODE).MOD10
         AIF   (NOT &POS(&B)).MOD8   IS POSITION ALREADY USED
&NOCODE  SETB  1
&D       SETA  &B+1
&CHAR    SETC  '&D'
         MNOTE 12,'POSITION = &CHAR CANNOT BE USED BY TWO CHECKS'
.MOD8    ANOP
&POS(&B) SETB  1             SET POSITION INDICATOR
         AIF   (NOT &N).MOD9
         AIF   (&POS(&B) AND &LEN(&C)).MOD9
&NOCODE  SETB  1
&D       SETA  &B+1
&CHAR    SETC  '&D'
         MNOTE 12,'LENGTH IN CKLNGTH OPERAND NOT LONG ENOUGH TO INCLUDEX
               POSITION = &CHAR IN CKMOD10 OPERAND'
.MOD9    ANOP
         AIF   (&B EQ &C).MOD10
&B       SETA  &B+1
         AGO   .MOD7
.MOD10   ANOP
&A       SETA  N'&CKMOD10-2
         AIF   (&A LE 0).M
&A       SETA  N'&CKMOD10
&B       SETA  3
.MOD11   ANOP
         AIF   (T'&CKMOD10(&B) EQ 'N').MOD12
&NOCODE  SETB  1
&CHAR    SETC  '&CKMOD10(&B)'
         MNOTE 12,'CKMOD10 SUBOPERAND = &CHAR IS INVALID'
         AGO   .MOD14
.MOD12   ANOP
         AIF   (&CKMOD10(&B) GE 1 AND &CKMOD10(&B) LE 16).MOD13
&NOCODE  SETB  1
         MNOTE 12,'CKMOD10 SUBOPERAND NOT IN RANGE OF 1 TO 16'
         AGO   .MOD14
.MOD13   ANOP
&C       SETA  &CKMOD10(&B)
&ME(&C)  SETB  1
.MOD14   ANOP
         AIF   (&B EQ &A).M   IF LAST SUBOPERAND,BRANCH
&B       SETA  &B+1
         AGO   .MOD11
.M       ANOP
         AIF   (T'&CKMOD11 EQ 'O' AND N'&CKMOD11 EQ 0).ALP
&E       SETA  &E+1
&F       SETA  &F+16384
&A       SETA  N'&CKMOD11
         AIF   (&A LT 1).ALP
         AIF   (T'&CKMOD11(1) NE 'O').M1
&NOCODE  SETB  1
         MNOTE 12,'CKMOD11 OPERAND HAS LENGTH SUBOPERAND OMITTED'
         AGO   .M3
.M1      AIF   (T'&CKMOD11(1) EQ 'N').M2
&NOCODE  SETB  1
         MNOTE 12,'LENGTH SUBOPERAND OF CKMOD11 OPERAND HAS INVALID    X
               TYPE ATTRIBUTE'
         AGO   .M3
.M2      ANOP
&B       SETA  &CKMOD11(1)
         AIF   (&B GE 1 AND &B LE 15).M3
&NOCODE  SETB  1
         MNOTE 12,'LENGTH SPECIFIED IN CKMOD11 OPERAND NOT IN VALID    X
               RANGE OF 1 TO 15'
.M3      ANOP
         AIF   (T'&CKMOD11(2) NE 'O').M4
&NOCODE  SETB  1
         MNOTE 12,'CKMOD11 OPERAND HAS POSITION SUBOPERAND OMITTED'
         AGO   .M10
.M4      ANOP
         AIF   (T'&CKMOD11(2) EQ 'N').M5
&NOCODE  SETB  1
         MNOTE 12,'POSITION SUBOPERAND OF CKMOD11 OPERAND HAS INVALID  X
               TYPE ATTRIBUTE'
         AGO   .M10
.M5      ANOP
&B       SETA  &CKMOD11(2)
         AIF   (&B GE 2 AND &B LE 16).M6
&NOCODE  SETB  1
         MNOTE 12,'POSITION SPECIFIED IN CKMOD11 OPERAND NOT IN VALID  X
               RANGE OF 2 TO 16'
         AGO   .M10
.M6      ANOP
&B       SETA  &B-1
&C       SETA  &B+&CKMOD11(1)-1
         AIF   (&C LE 16).M7
&NOCODE  SETB  1
         MNOTE 12,'CKMOD11 LENGTH AND POSITION OPERANDS INVALID  BECAUSX
               E SOME POSITIONS ARE BEYOND END OF DATA ENTRY'
         AGO   .M10
.M7      ANOP
         AIF   (&NOCODE).M10
         AIF   (NOT &POS(&B)).M8   IS POSITION ALREADY USED
&NOCODE  SETB  1
&D       SETA  &B+1
&CHAR    SETC  '&D'
         MNOTE 12,'POSITION = &CHAR CANNOT BE USED BY TWO CHECKS'
.M8      ANOP
&POS(&B) SETB  1
         AIF   (NOT &N).M9
         AIF   (&POS(&B) AND &LEN(&B)).M9
&NOCODE  SETB  1
&D       SETA  &B+1
&CHAR    SETC  '&D'
         MNOTE 12,'LENGTH IN CKLNGTH OPERAND NOT LONG ENOUGH TO  INCLUDX
               E POSITION = &CHAR IN CKMOD11 OPERAND'
.M9      ANOP
         AIF   (&B EQ &C).M10
&B       SETA  &B+1
         AGO   .M7
.M10     ANOP
&A       SETA  N'&CKMOD11-2
         AIF   (&A LE 0).ALP
&B       SETA  3
&A       SETA  N'&CKMOD11
.M11     ANOP
         AIF   (T'&CKMOD11(&B) EQ 'N').M12
&NOCODE  SETB  1
&CHAR    SETC  '&CKMOD11(&B)'
         MNOTE 12,'CKMOD11 SUBOPERAND = &CHAR IS INVALID'
         AGO   .M14
.M12     ANOP
         AIF   (&CKMOD11(&B) GE 1 AND &CKMOD11(&B) LE 16).M13
&NOCODE  SETB  1
         MNOTE 12,'CKMOD11 SUBOPERAND NOT IN RANGE OF 1 TO 16'
         AGO   .M14
.M13     ANOP
&C       SETA  &CKMOD11(&B)
&MODE(&C) SETB 1
.M14     ANOP
         AIF   (&B EQ &A).ALP  IF LAST SUBOPERAND,BRANCH
&B       SETA  &B+1
         AGO   .M11
.ALP     ANOP
         AIF   (T'&CKNONUM EQ 'O' AND N'&CKNONUM EQ 0).NUM
&E       SETA  &E+1
&F       SETA  &F+2048
&A       SETA  N'&CKNONUM
         AIF   (&A LT 1).NUM
         AIF   (T'&CKNONUM(1) NE 'O').ALP1
&NOCODE  SETB  1
         MNOTE 12,'CKNONUM OPERAND HAS POSITION1 SUBOPERAND OMITTED'
         AGO   .ALP3
.ALP1    AIF   (T'&CKNONUM(1) EQ 'N').ALP2
&NOCODE  SETB  1
         MNOTE 12,'POSITION1 SUBOPERAND OF CKNONUM OPERAND HAS INVALID X
               TYPE ATTRIBUTE'
         AGO   .ALP3
.ALP2    ANOP
&B       SETA  &CKNONUM(1)
         AIF   (&B GE 2 AND &B LE 17).ALP3
&NOCODE  SETB  1
         MNOTE 12,'POSITION1 SPECIFIED IN CKNONUM OPERAND NOT IN VALID X
               RANGE OF 2 TO 17'
.ALP3    ANOP
         AIF   (T'&CKNONUM(2) NE 'O').ALP4
&NOCODE  SETB  1
         MNOTE 12,'CKNONUM OPERAND HAS POSITION2 SUBOPERAND OMITTED'
         AGO   .ALP11
.ALP4    ANOP
         AIF   (T'&CKNONUM(2) EQ 'N').ALP5
&NOCODE  SETB  1
         MNOTE 12,'POSITION2 SUBOPERAND OF CKNONUM OPERAND HAS INVALID X
               TYPE ATTRIBUTE'
         AGO   .ALP11
.ALP5    ANOP
&B       SETA  &CKNONUM(2)
         AIF   (&B GE 2 AND &B LE 17).ALP6
&NOCODE  SETB  1
         MNOTE 12,'POSITION2 SPECIFIED IN CKNONUM OPERAND NOT IN VALID X
               RANGE OF 2 TO 17'
         AGO   .ALP11
.ALP6    AIF   (&CKNONUM(2) GE &CKNONUM(1)).ALP7
&NOCODE  SETB  1
         MNOTE 12,'POSITION2 MUST NOT BE LESS THAN POSITION1 IN        X
               CKNONUM OPERAND'
         AGO   .ALP11
.ALP7    ANOP
         AIF   (&NOCODE).ALP11
&B       SETA  &CKNONUM(1)-1
&C       SETA  &CKNONUM(2)-1
.ALP8    AIF   (NOT &POS(&B)).ALP9   IS POSITION ALREADY USED
&NOCODE  SETB  1
&D       SETA  &B+1
&CHAR    SETC  '&D'
         MNOTE 12,'POSITION = &CHAR CANNOT BE USED BY TWO CHECKS'
.ALP9    ANOP
&POS(&B) SETB  1
         AIF   (NOT &N).ALP10
         AIF   (&POS(&B) AND &LEN(&C)).ALP10
&NOCODE  SETB  1
&D       SETA  &B+1
&CHAR    SETC  '&D'
         MNOTE 12,'LENGTH IN CKLNGTH OPERAND NOT LONG ENOUGH TO  INCLUDX
               E POSITION = &CHAR IN CKNONUM OPERAND'
.ALP10   ANOP
         AIF   (&B EQ &C).ALP11
&B       SETA  &B+1
         AGO   .ALP8
.ALP11   ANOP
&A       SETA  N'&CKNONUM-2
         AIF   (&A LE 0).NUM
&B       SETA  3
&A       SETA  N'&CKNONUM
.ALP12   ANOP
         AIF   (T'&CKNONUM(&B) EQ 'N').ALP13
&NOCODE  SETB  1
&CHAR    SETC  '&CKNONUM(&B)'
         MNOTE 12,'CKNONUM SUBOPERAND = &CHAR IS INVALID'
         AGO   .ALP15
.ALP13   ANOP
         AIF   (&CKNONUM(&B) GE 1 AND &CKNONUM(&B) LE 16).ALP14
&NOCODE  SETB  1
&CHAR    SETC  '&CKNONUM(&B)'
         MNOTE 12,'CKNONUM SUBOPERAND = &CHAR NOT IN RANGE OF 1 TO 16'
         AGO   .ALP15
.ALP14   ANOP
&C       SETA  &CKNONUM(&B)
&ALE(&C) SETB  1
.ALP15   ANOP
         AIF   (&B EQ &A).NUM   IF LAST SUBOPERAND,BRANCH
&B       SETA  &B+1
         AGO   .ALP12         CONTINUE CHECKING
.NUM     ANOP
         AIF   (T'&CKNUM EQ 'O' AND N'&CKNUM EQ 0).RAN
&E       SETA  &E+1
&F       SETA  &F+1024
&A       SETA  N'&CKNUM
         AIF   (&A LT 1).RAN
         AIF   (T'&CKNUM(1) NE 'O').NUM1
&NOCODE  SETB  1
         MNOTE 12,'CKNUM OPERAND HAS POSITION1 SUBOPERAND OMITTED'
         AGO   .NUM3
.NUM1    AIF   (T'&CKNUM(1) EQ 'N').NUM2
&NOCODE  SETB  1
         MNOTE 12,'POSITION1 SUBOPERAND OF CKNUM OPERAND HAS INVALID   X
               TYPE ATTRIBUTE'
         AGO   .NUM3
.NUM2    ANOP
&B       SETA  &CKNUM(1)
         AIF   (&B GE 2 AND &B LE 17).NUM3
&NOCODE  SETB  1
         MNOTE 12,'POSITION1 SPECIFIED IN CKNUM OPERAND NOT IN VALID   X
               RANGE OF 2 TO 17'
.NUM3    ANOP
         AIF   (T'&CKNUM(2) NE 'O').NUM4
&NOCODE  SETB  1
         MNOTE 12,'CKNUM OPERAND HAS POSITION2 SUBOPERAND OMITTED'
         AGO   .NUM11
.NUM4    ANOP
         AIF   (T'&CKNUM(2) EQ 'N').NUM5
&NOCODE  SETB  1
         MNOTE 12,'POSITION2 SUBOPERAND OF CKNUM OPERAND HAS INVALID   X
               TYPE ATTRIBUTE'
         AGO   .NUM11
.NUM5    ANOP
&B       SETA  &CKNUM(2)
         AIF   (&B GE 2 AND &B LE 17).NUM6
&NOCODE  SETB  1
         MNOTE 12,'POSITION2 SPECIFIED IN CKNUM OPERAND NOT IN VALID   X
               RANGE OF 2 TO 17'
         AGO   .NUM11
.NUM6    ANOP
         AIF   (&NOCODE).NUM11
         AIF   (&CKNUM(2) GE &CKNUM(1)).NUM7
&NOCODE  SETB  1
         MNOTE 12,'POSITION2 MUST NOT BE LESS THAN POSITION1 IN CKNUM  X
               OPERAND'
         AGO   .NUM11
.NUM7    ANOP
&B       SETA  &CKNUM(1)-1
&C       SETA  &CKNUM(2)-1
.NUM8    AIF   (NOT &POS(&B)).NUM9   IS POSITION ALREADY USED
&NOCODE  SETB  1
&D       SETA  &B+1
&CHAR    SETC  '&D'
         MNOTE 12,'POSITION = &CHAR CANNOT BE USED BY TWO CHECKS'
.NUM9    ANOP
&POS(&B) SETB  1
         AIF   (NOT &N).NUM10
         AIF   (&POS(&B) AND &LEN(&B)).NUM10
&NOCODE  SETB  1
&D       SETA  &B+1
&CHAR    SETC  '&D'
         MNOTE 12,'LENGTH IN CKLNGTH OPERAND NOT LONG ENOUGH TO  INCLUDX
               E POSITION = &CHAR IN CKNUM OPERAND'
.NUM10   ANOP
         AIF   (&B EQ &C).NUM11
&B       SETA  &B+1
         AGO   .NUM8
.NUM11   ANOP
&A       SETA  N'&CKNUM-2
         AIF   (&A LE 0).RAN
&B       SETA  3
&A       SETA  N'&CKNUM
.NUM12   ANOP
         AIF   (T'&CKNUM(&B) EQ 'N').NUM13
&NOCODE  SETB  1
&CHAR    SETC  '&CKNUM(&B)'
         MNOTE 12,'CKNUM SUBOPERAND  = &CHAR IS INVALID'
         AGO   .NUM15
.NUM13   ANOP
         AIF   (&CKNUM(&B) GE 1 AND &CKNUM(&B) LE 16).NUM14
&NOCODE  SETB  1
         MNOTE 12,'CKNUM SUBOPERAND NOT IN RANGE OF 1 TO 16'
         AGO   .NUM15
.NUM14   ANOP
&C       SETA  &CKNUM(&B)
&NUME(&C) SETB 1
.NUM15   ANOP
         AIF   (&B EQ &A).RAN   IF LAST SUBOPERAND,BRANCH
&B       SETA  &B+1
         AGO   .NUM12           CONTINUE CHECKING
.RAN     ANOP
         AIF   (T'&CKRANGE EQ 'O' AND N'&CKRANGE EQ 0).RTEST
&E       SETA  &E+1
&F       SETA  &F+512
&A       SETA  N'&CKRANGE
         AIF   (&A LT 1).RTEST
         AIF   (T'&CKRANGE(1) NE 'O').RAN1
&NOCODE  SETB  1
         MNOTE 12,'CKRANGE OPERAND HAS POSITION1 OPERAND OMITTED'
         AGO   .RAN3
.RAN1    AIF   (T'&CKRANGE(1) EQ 'N').RAN2
&NOCODE  SETB  1
         MNOTE 12,'POSITION1 SUBOPERAND OF CKRANGE OPERAND HAS INVALID X
               TYPE ATTRIBUTE'
         AGO   .RAN3
.RAN2    ANOP
&B.      SETA  &CKRANGE(1)
         AIF   (&B GE 2 AND &B LE 17).RAN3
&NOCODE  SETB  1
         MNOTE 12,'POSITION1 SPECIFIED IN CKRANGE OPERAND NOT IN VALID X
               RANGE OF 2 TO 17'
.RAN3    ANOP
         AIF   (T'&CKRANGE(2) NE 'O').RAN4
&NOCODE  SETB  1
         MNOTE 12,'CKRANGE OPERAND HAS POSITION2 SUBOPERAND OMITTED'
         AGO   .RAN11
.RAN4    ANOP
         AIF   (T'&CKRANGE(2) EQ 'N').RAN5
&NOCODE  SETB  1
         MNOTE 12,'POSITION2 SUBOPERAND OF CKRANGE OPERAND HAS INVALID X
               TYPE ATTRIBUTE'
         AGO   .RAN11
.RAN5    ANOP
&B       SETA  &CKRANGE(2)
         AIF   (&B GE 2 AND &B LE 17).RAN6
&NOCODE  SETB  1
         MNOTE 12,'POSITION2 SPECIFIED IN CKRANGE OPERAND NOT IN VALID X
               RANGE OF 2 TO 17'
         AGO   .RAN11
.RAN6    ANOP
&B       SETA  &CKRANGE(1)-1
&C       SETA  &CKRANGE(2)-1
         AIF   (&C GE &B).RAN7
&NOCODE  SETB  1
         MNOTE 12,'CKRANGE POSITION2 SUBOPERAND MUST NOT BE LESS THAN  X
               POSITION1 SUBOPERAND'
         AGO   .RAN11
.RAN7    ANOP
         AIF   (&NOCODE).RAN11
         AIF   (NOT &POS(&B)).RAN8   IS POSITION ALREADY USED
&NOCODE  SETB  1
&D       SETA  &B+1
&CHAR    SETC  '&D'
         MNOTE 12,'POSITION = &CHAR CANNOT BE USED BY TWO CHECKS'
.RAN8    ANOP
&POS(&B) SETB  1
         AIF   (NOT &N).RAN9
         AIF   (&POS(&B) AND &LEN(&C)).RAN9
&NOCODE  SETB  1
&D       SETA  &A+1
&CHAR    SETC  '&D'
         MNOTE 12,'LENGTH IN CKLNGTH OPERAND NOT LONG ENOUGH TO  INCLUDX
               E POSITION = &CHAR IN CKRANGE OPERAND'
.RAN9    ANOP
         AIF   (&B EQ &C).RAN10
&B       SETA  &B+1
         AGO   .RAN7
.RAN10   ANOP
&A       SETA  N'&CKRANGE-2
&B       SETA  &CKRANGE(2)-&CKRANGE(1)+1
         AIF   (&A EQ &B).RAN11
.RAN101  ANOP
&NOCODE  SETB  1
         MNOTE 12,'NUMBER OF HIGHLOW CHARACTER SUBOPERANDS MUST EQUAL  X
               SIZE OF FIELD DEFINED BY POSITION SUBOPERANDS'
         AGO   .RTEST
.RAN11   ANOP
&A       SETA  N'&CKRANGE
         AIF   (&A LT 3).RAN101
&C       SETA  0
&B       SETA  3
.RAN16   ANOP
         AIF   (K'&CKRANGE(&B) EQ 2).RAN12
&NOCODE  SETB  1
         MNOTE 12,'THERE MUST BE ONLY 2 CHARS IN EACH HIGHLOW SUBOPERANX
               D OF THE CKRANGE OPERAND'
.RAN12   ANOP
&G       SETA  0
.RAN122  ANOP
&G       SETA  &G+1
         AIF   ('&CKRANGE(&B)'(1,1) EQ '0123456789'(1*&G,1)).RAN123
         AIF   (&G LT 10).RAN122
         AGO   .RAN126
.RAN123  ANOP
&G       SETA  0
.RAN124  ANOP
&G       SETA  &G+1
         AIF   ('&CKRANGE(&B)'(2,1) EQ '0123456789'(1*&G,1)).RAN13
         AIF   (&G LT 10).RAN124
.RAN126  ANOP
&NOCODE  SETB  1
&CHAR    SETC  '&CKRANGE(&B)'
         MNOTE 12,'CKRANGE SUBOPERAND = &CHAR IS INVALID'
.RAN13   ANOP
         AIF   (&B EQ &A).RTEST
&B       SETA  &B+1
         AGO   .RAN16
.RTEST   ANOP
         AIF   (T'&RNGETST EQ 'O').HIGH
         AIF   (T'&CKRANGE NE 'O').RTEST2
         MNOTE 3,'RNGETST OPERAND IGNORED BECAUSE CKRANGE OPERAND NOT  X
               CODED'
         AGO   .HIGH
.RTEST2  ANOP
         AIF   ('&RNGETST' EQ 'ERROR' OR '&RNGETST' EQ 'DATA').RTEST1
         MNOTE 3,'RNGETST OPERAND INVALID,OPERAND IGNORED'
         AGO   .HIGH
.RTEST1  ANOP
         AIF   ('&RNGETST' EQ 'DATA').HIGH
&F       SETA  &F+128
.HIGH    ANOP
         AIF   (T'&HIGUID EQ 'O' AND N'&HIGUID EQ 0).LOW
         AIF   (T'&CKRANGE NE 'O').HIGH6
         MNOTE 3,'HIGUID OPERAND IGNORED BECAUSE CKRANGE OPERAND NOT   X
               CODED'
         AGO   .LOW
.HIGH6   ANOP
&A       SETA  N'&HIGUID
         AIF   (&A EQ 0).LOW
&B       SETA  1
&C       SETA  N'&HIGUID
.HIGH1   ANOP
         AIF   (T'&HIGUID(&B) EQ 'N').HIGH2
&NOCODE  SETB  1
&CHAR    SETC  '&HIGUID(&B)'
         MNOTE 12,'HIGUID SUBOPERAND = &CHAR IS INVALID'
         AGO   .HIGH4
.HIGH2   AIF   (&HIGUID(&B) GE 1 AND &HIGUID(&B) LE 16).HIGH3
&NOCODE  SETB  1
&CHAR    SETC  '&HIGUID(&B)'
         MNOTE 12,'HIGUID SUBOPERAND = &CHAR NOT IN RANGE OF 1 TO 16'
         AGO   .HIGH4
.HIGH3   ANOP
&A       SETA  &HIGUID(&B)
&HIE(&A) SETB  1
.HIGH4   ANOP
         AIF   (&B EQ &C).HIGH8  IF LAST SUBOPERAND,BRANCH
&B       SETA  &B+1
         AGO   .HIGH1            CONTINUE CHECKING SUBOPERANDS
.HIGH8   AIF   (&C LE 16).LOW
&NOCODE  SETB  1
         MNOTE 12,'NO MORE THAN 16 HIGUID SUBOPERANDS CAN BE CODED'
.LOW     ANOP
         AIF   (T'&LOWGUID EQ 'O' AND N'&LOWGUID EQ 0).OR
         AIF   (T'&CKRANGE NE 'O').LOW6
         MNOTE 3,'LOWGUID OPERAND IGNORED BECAUSE CKRANGE OPERAND NOT  X
               CODED'
         AGO   .OR
.LOW6    ANOP
&A       SETA  N'&LOWGUID
         AIF   (&A EQ 0).OR
&B       SETA  1
&C       SETA  N'&LOWGUID
.LOW1    ANOP
         AIF   (T'&LOWGUID(&B) EQ 'N').LOW2
&NOCODE  SETB  1
&CHAR    SETC  '&LOWGUID(&B)'
         MNOTE 12,'LOWGUID SUBOPERAND = &CHAR IS INVALID'
         AGO   .LOW4
.LOW2    AIF   (&LOWGUID(&B) GE 1 AND &LOWGUID(&B) LE 16).LOW3
&NOCODE  SETB  1
&CHAR    SETC  '&LOWGUID(&B)'
         MNOTE 12,'LOWGUID SUBOPERAND = &CHAR NOT IN RANGE OF 1 TO 16'
         AGO   .LOW4
.LOW3    ANOP
&A       SETA  &LOWGUID(&B)
&LOWE(&A) SETB 1
.LOW4    ANOP
         AIF   (&B EQ &C).LOW5
&B       SETA  &B+1
         AGO   .LOW1             CONTINUE CHECKING SUBOPERANDS
.LOW5    AIF   (&C LE 16).OR
&NOCODE  SETB  1
         MNOTE 12,'NO MORE THAN 16 LOWGUID SUBOPERANDS CAN BE CODED'
.OR      ANOP
         AIF   (T'&CKOR EQ 'O' AND N'&CKOR EQ 0).ORERR
&E       SETA  &E+1
&F       SETA  &F+4096
&A       SETA  N'&CKOR
         AIF   (&A LT 1).ORERR
         AIF   (T'&CKOR(1) NE 'O').OR1
&NOCODE  SETB  1
         MNOTE 12,'CKOR OPERAND HAS POSITION SUBOPERAND OMITTED'
         AGO   .OR3
.OR1     AIF   (T'&CKOR(1) EQ 'N').OR2
&NOCODE  SETB  1
         MNOTE 12,'POSITION SUBOPERAND OF CKOR OPERAND HAS INVALID     X
               TYPE ATTRIBUTE'
         AGO   .OR3
.OR2     ANOP
&B       SETA  &CKOR(1)
         AIF   (&B GE 2 AND &B LE 17).OR3
&NOCODE  SETB  1
         MNOTE 12,'POSITION SPECIFIED IN CKOR OPERAND NOT IN VALID     X
               RANGE OF 2 TO 17'
.OR3     AIF   (&A GT 1).OR3A
&NOCODE  SETB  1
         MNOTE 12,'AT LEAST ONE CHECKCHAR SUBOPERAND MUST BE CODED IN  X
               CKOR OPERAND'
         AGO   .ORERR
.OR3A    ANOP
         AIF   (&A LE 6).OR3B
&NOCODE  SETB  1
         MNOTE 12,'NO MORE THAN 5 CHECKCHAR SUBOPERANDS CAN BE CODED   X
               IN CKOR OPERAND'
.OR3B    ANOP
&B       SETA  2
.OR4     ANOP
&C       SETA  0
.OR6     ANOP
&C       SETA  &C+1
         AIF   ('&CKOR(&B)' EQ '*F0F1F2F3F4F5F6F7F8F9C1C2'(2*&C,2)).OR7
         AIF   ('&CKOR(&B)' EQ '*C3C4C5C6C7C8C9D1D2D3D4D5'(2*&C,2)).OR7
         AIF   ('&CKOR(&B)' EQ '*D6D7D8D9E2E3E4E5E6E7E8E9'(2*&C,2)).OR7
         AIF   ('&CKOR(&B)' EQ '*051525404B4E505A5B5C5E60'(2*&C,2)).OR7
         AIF   ('&CKOR(&B)' EQ '*616B6F7A7B7C7E7F7F7F7F7F'(2*&C,2)).OR7
         AIF   (&C LT 12).OR6
&NOCODE  SETB  1
&CHAR    SETC  '&CKOR(&B)'
         MNOTE 12,'CKOR SUBOPERAND = &CHAR INVALID'
.OR7     AIF   (&B EQ &A).ORERR
&B       SETA  &B+1
         AGO   .OR4
.OR8     ANOP
         AIF   (&NOCODE).ORERR
&B       SETA  &CKOR(1)-1
         AIF   (NOT &POS(&B)).OR9
&NOCODE  SETB  1
&D       SETA  &B+1
&CHAR    SETC  '&D'
         MNOTE 12,'POSITION = &CHAR CANNOT BE USED BY TWO CHECKS'
.OR9     ANOP
&POS(&B) SETB  1
         AIF   (NOT &N).ORERR
         AIF   (&POS(&B) AND &LEN(&B)).ORERR
&NOCODE  SETB  1
&D       SETA  &B+1
&CHAR    SETC  '&D'
         MNOTE 12,'LENGTH IN CKLNGTH OPERAND NOT LONG ENOUGH TO        X
               INCLUDE POSITION = &CHAR IN CKOR OPERAND'
.ORERR   ANOP
         AIF   (T'&ORGUID EQ 'O' AND N'&ORGUID EQ 0).AND
         AIF   (T'&CKOR NE 'O').ORERR1
         MNOTE 3,'ORGUID OPERAND IGNORED IF CKOR OPERAND NOT CODED'
         AGO   .AND
.ORERR1  ANOP
&A       SETA  N'&ORGUID
         AIF   (&A EQ 0).AND
&B       SETA  1
&C       SETA  N'&ORGUID
.ORERR2  AIF   (T'&ORGUID(&B) EQ 'N').ORERR3
&NOCODE  SETB  1
&CHAR    SETC  '&ORGUID(&B)'
         MNOTE 12,'ORGUID SUBOPERAND = &CHAR IS INVALID'
         AGO   .ORERR5
.ORERR3  AIF   (&ORGUID(&B)  GE 1 AND &ORGUID(&B) LE 16).ORERR4
&NOCODE  SETB  1
&CHAR    SETC  '&ORGUID(&B)'
         MNOTE 12,'ORGUID SUBOPERAND = &CHAR NOT IN RANGE OF 1 TO 16'
         AGO   .ORERR5
.ORERR4  ANOP
&A       SETA  &ORGUID(&B)
&ORE(&A) SETB  1
.ORERR5  ANOP
         AIF   (&B EQ &C).ORERR6  IF LAST SUBOPERAND,BRANCH
&B       SETA  &B+1
         AGO   .ORERR2            CONTINUE CHECKING SUBOPERANDS
.ORERR6  AIF   (&C LE 16).AND
&NOCODE  SETB  1
         MNOTE 12,'NO MORE THAN 16 ORGUID SUBOPERANDS CAN BE CODED'
.AND     ANOP
         AIF   (T'&CKAND EQ 'O' AND N'&CKAND EQ 0).ANDER
&E       SETA  &E+1
&F       SETA  &F+256
&A       SETA  N'&CKAND
         AIF   (&A LT 1).ANDER
         AIF   (T'&CKAND(1) NE 'O').AND1
&NOCODE  SETB  1
         MNOTE 12,'CKAND OPERAND HAS POSITION1 SUBOPERAND OMITTED'
         AGO   .AND2A
.AND1    AIF   (T'&CKAND(1) EQ 'N').AND2
&NOCODE  SETB  1
         MNOTE 12,'POSITION1 SUBOPERAND OF CKAND OPERAND HAS INVALID   X
               TYPE ATTRIBUTE'
         AGO   .AND2A
.AND2    ANOP
&B       SETA  &CKAND(1)
         AIF   (&B GE 2 AND &B LE 17).AND2A
&NOCODE  SETB  1
         MNOTE 12,'POSITION1 SPECIFIED IN CKAND OPERAND NOT IN VALID   X
               RANGE OF 2 TO 17'
.AND2A   ANOP
         AIF   (T'&CKAND(2) NE 'O').AND2B
&NOCODE  SETB  1
         MNOTE 12,'CKAND OPERAND HAS POSITION2 SUBOPERAND OMITTED'
         AGO   .AND3
.AND2B   AIF   (T'&CKAND(2) EQ 'N').AND2C
&NOCODE  SETB  1
         MNOTE 12,'POSITION2 SUBOPERAND OF CKAND OPERAND HAS INVALID   X
               TYPE ATTRIBUTE'
         AGO   .AND3
.AND2C   ANOP
&B       SETA  &CKAND(2)
         AIF   (&B GE 2 AND &B LE 17).AND2D
&NOCODE  SETB  1
         MNOTE 12,'POSITION2 SPECIFIED IN CKAND OPERAND NOT IN VALID   X
               RANGE OF 2 TO 17'
         AGO   .AND3
.AND2D   AIF   (&CKAND(2) GE &CKAND(1)).AND3
&NOCODE  SETB  1
         MNOTE 12,'POSITION2 SUBOPERAND MUST NOT BE LESS THAN POSITION X
               1 SUBOPERAND IN CKAND OPERAND'
.AND3    AIF   (&A GT 2).AND3B
&NOCODE  SETB  1
         MNOTE 12,'AT LEAST ONE CHECKCHAR SUBOPERAND MUST BE CODED IN  X
               CKAND OPERAND'
         AGO   .ANDER
.AND3B   AIF   (&A LE 18).AND3A
&NOCODE  SETB  1
         MNOTE 12,'NO MORE THAN 16 CHECKCHAR SUBOPERANDS CAN BE CODED  X
               IN CKAND OPERAND'
.AND3A   ANOP
         AIF   (&NOCODE).AND3C
&B       SETA  &A-2
&C       SETA  &CKAND(2)-&CKAND(1)+1
         AIF   (&B EQ &C).AND3C
&NOCODE  SETB  1
         MNOTE 12,'NUMBER OF CHECKCHAR SUBOPERANDS IN CKAND OPERAND    X
               DOES NOT EQUAL THE DEFINED FIELD SIZE'
.AND3C   ANOP
&B       SETA  3
.AND4    ANOP
.AND5    ANOP
&C       SETA  0
.AND6    ANOP
&C       SETA  &C+1
         AIF  ('&CKAND(&B)' EQ '*F0F1F2F3F4F5F6F7F8F9C1C2'(2*&C,2)).A7
         AIF  ('&CKAND(&B)' EQ '*C3C4C5C6C7C8C9D1D2D3D4D5'(2*&C,2)).A7
         AIF  ('&CKAND(&B)' EQ '*D6D7D8D9E2E3E4E5E6E7E8E9'(2*&C,2)).A7
         AIF  ('&CKAND(&B)' EQ '*051525404B4E505A5B5C5E60'(2*&C,2)).A7
         AIF  ('&CKAND(&B)' EQ '*616B6F7A7B7C7E7F7F7F7F00'(2*&C,2)).A7
         AIF   (&C LT 12).AND6
&NOCODE  SETB  1
&CHAR    SETC  '&CKAND(&B)'
         MNOTE 12,'CKAND SUBOPERAND = &CHAR INVALID'
.A7      AIF   (&B EQ &A).AND8
&B       SETA  &B+1
         AGO   .AND4
.AND8    ANOP
         AIF   (&NOCODE).ANDER
&B       SETA  &CKAND(1)-1
&C       SETA  &CKAND(2)-1
         AIF   (&C LE 16).AND9
&NOCODE  SETB  1
         MNOTE 12,'CKAND CHECKS CAN NOT BE SPECIFIED BEYOND POSITION   X
               17 OR END OF DATA ENTRY'
         AGO   .ANDER
.AND9    ANOP
         AIF   (NOT &POS(&B)).AND10   IS POSITION ALREADY USED
&NOCODE  SETB  1
&D       SETA  &B+1
&CHAR    SETC  '&D'
         MNOTE 12,'POSITION = &CHAR CAN NOT BE USED BY TWO CHECKS'
.AND10   ANOP
&POS(&B) SETB  1
         AIF   (NOT &N).AND11
         AIF   (&POS(&B) AND &LEN(&B)).AND11
&NOCODE  SETB  1
&D       SETA  &B+1
&CHAR    SETC  '&D'
         MNOTE 12,'LENGTH IN CKLNGTH OPERAND NOT LONG ENOUGH TO  INCLUDX
               E POSITION = &CHAR IN CKAND OPERAND'
.AND11   AIF   (&B EQ &C).ANDER
&B       SETA  &B+1
         AGO   .AND9
.ANDER   ANOP
         AIF   (T'&ANDGUID EQ 'O' AND N'&ANDGUID EQ 0).TR
         AIF   (T'&CKAND NE 'O').ANDER1
         MNOTE 3,'ANDGUID OPERAND IGNORED IF CKAND OPERAND NOT CODED'
         AGO   .TR
.ANDER1  ANOP
&A       SETA  N'&ANDGUID
         AIF   (&A EQ 0).TR
&B       SETA  1
&C       SETA  N'&ANDGUID
.ANDER2  AIF   (T'&ANDGUID(&B) EQ 'N').ANDER3
&NOCODE  SETB  1
&CHAR    SETC  '&ANDGUID(&B)'
         MNOTE 12,'ANDGUID SUBOPERAND = &CHAR IS INVALID'
         AGO   .ANDER5
.ANDER3  AIF   (&ANDGUID(&B) GE 1 AND &ANDGUID(&B) LE 16).ANDER4
&NOCODE  SETB  1
&CHAR    SETC  '&ANDGUID(&B)'
         MNOTE 12,'ANDGUID SUBOPERAND = &CHAR NOT IN RANGE OF 1 TO 16'
         AGO   .ANDER5
.ANDER4  ANOP
&A       SETA  &ANDGUID(&B)
&ANDE(&A) SETB 1
.ANDER5  ANOP
         AIF   (&B EQ &C).ANDER6
&B       SETA  &B+1
         AGO   .ANDER2             CONTINUE CHECKING SUBOPERANDS
.ANDER6  AIF   (&C LE 16).TR
&NOCODE  SETB  1
         MNOTE 12,'NO MORE THAN 16 ANDGUID SUBOPERANDS CAN BE CODED'
.TR      AIF   (T'&TRANSL EQ 'O' OR '&TRANSL' EQ 'NO').ID
         AIF   ('&TRANSL' EQ 'YES').TR1
         MNOTE 3,'TRANSL OPERAND INVALID,OPERAND IGNORED'
         AGO   .ID
.TR1     ANOP
&F       SETA  &F+2
         AIF   ('&TRANSL' NE 'YES').TR2
         AIF   ('&IDENT' NE 'YES').TR2
&NOCODE  SETB  1
         MNOTE 12,'TRANSL=YES AND IDENT=YES CAN NOT BOTH BE CODED'
.TR2     AIF   (T'&CKLNGTH EQ 'O').TR3
&NOCODE  SETB  1
         MNOTE 12,'CKLNGTH OPERAND CAN NOT BE CODED IF TRANSL=YES'
.TR3     AIF   (&E EQ 0).ID
&NOCODE  SETB  1
         MNOTE 12,'NO CHECKS ARE ALLOWED WHEN TRANSL=YES IS CODED'
.ID      AIF   (T'&IDENT EQ 'O' OR '&IDENT' EQ 'NO').COMB
         AIF   ('&IDENT' EQ 'YES').ID1
         MNOTE 3,'IDENT OPERAND INVALID,OPERAND IGNORED'
         AGO   .COMB
.ID1     ANOP
&F       SETA  &F+1
.COMB    AIF   (&E LE 3).CKGEN
&NOCODE  SETB  1
         MNOTE 12,'EXCEPT FOR CKLNGTH AND CKMONKY,ONLY 3 OF THE REMAINIX
               G 7 CHECKS OPERANDS ARE ALLOWED IN A PARMLIST MACRO'
.CKGEN   AIF   (NOT &NOCODE).GEN
         MNOTE 12,'TEXT GENERATION SUPPRESSED'
         AGO   .MEND
.GEN     ANOP
&NAME    EQU   *
         DC    AL2(&F)           CHECK-FUNCTION FIELD
&IECNDGP(1) SETA &IECNDGP(1)+2
         AIF   (T'&CKLNGTH EQ 'O').GEN1
         DC    YL1(&CKLNGTH(1))    CKLNGTH LENGTH
         DC    B'&LENE(1)&LENE(2)&LENE(3)&LENE(4)&LENE(5)&LENE(6)&LENE(X
               7)&LENE(8)'    LENGTH ERROR GUIDANCE BYTE 1
         DC    B'&LENE(9)&LENE(10)&LENE(11)&LENE(12)&LENE(13)&LENE(14)&X
               LENE(15)&LENE(16)' LENGTH ERROR GUIDANCE BYTE 2
&IECNDGP(1) SETA &IECNDGP(1)+3
.GEN1    AIF   (T'&CKMOD11 EQ 'O').GEN2
&A       SETA  &CKMOD11(2)-2
&B       SETA  &CKMOD11(1)
         DC    YL1(&B*16+&A)        CKMOD11 POSITION2 AND POSITION1
         DC    B'&MODE(1)&MODE(2)&MODE(3)&MODE(4)&MODE(5)&MODE(6)&MODE(X
               7)&MODE(8)'  CKMOD11 ERROR GUIDANCE BYTE 1
         DC    B'&MODE(9)&MODE(10)&MODE(11)&MODE(12)&MODE(13)&MODE(14)&X
               MODE(15)&MODE(16)'  CKMOD11 ERROR GUIDANCE BYTE 2
&IECNDGP(1) SETA &IECNDGP(1)+3
.GEN2    ANOP
.GEN3    AIF   (T'&CKMOD10 EQ 'O').GEN4
&A       SETA  &CKMOD10(2)-2
&B       SETA  &CKMOD10(1)
         DC    YL1(&B*16+&A)        CKMOD10 POSITION2 AND POSITION1
         DC    B'&ME(1)&ME(2)&ME(3)&ME(4)&ME(5)&ME(6)&ME(7)&ME(8)'
         DC    B'&ME(9)&ME(10)&ME(11)&ME(12)&ME(13)&ME(14)&ME(15)&ME(16X
               )'   CKMOD10 ERROR GUIDANCE BYTE 2
&IECNDGP(1) SETA &IECNDGP(1)+3
.GEN4    AIF   (T'&CKOR EQ 'O').GEN5
&A       SETA  &CKOR(1)-2
         DC    YL1(&A)     CKOR POSITION
         DC    B'&ORE(1)&ORE(2)&ORE(3)&ORE(4)&ORE(5)&ORE(6)&ORE(7)&ORE(X
               8)'      CKOR  ERROR GUIDANCE BYTE 1
         DC    B'&ORE(9)&ORE(10)&ORE(11)&ORE(12)&ORE(13)&ORE(14)&ORE(15X
               )&ORE(16)' CKOR ERROR GUIDANCE BYTE 2
&IECNDGP(1) SETA &IECNDGP(1)+8
&A       SETA  N'&CKOR
&B       SETA  2
.GEN4A   ANOP
         DC    XL1'&CKOR(&B)'
         AIF   (&B EQ &A).GEN4B
&B       SETA  &B+1
         AGO   .GEN4A
.GEN4B   ANOP
&A       SETA  &A-1
.GEN4C   ANOP
         AIF   (&A EQ 5).GEN5
         DC    XL1'&CKOR(&B)'  CKOR CHECKCHARACTER
&A       SETA  &A+1
         AGO   .GEN4C
.GEN5    ANOP
.GEN6    AIF   (T'&CKNONUM EQ 'O').GEN7
&A       SETA  &CKNONUM(1)-2
&B       SETA  &CKNONUM(2)-2
         DC    YL1(&B*16+&A)   CKNONUM POSITION 2 AND POSITION 1
         DC    B'&ALE(1)&ALE(2)&ALE(3)&ALE(4)&ALE(5)&ALE(6)&ALE(7)&ALE(X
               8)'    CKNONUM ERROR GUIDANCE BYTE 1
         DC    B'&ALE(9)&ALE(10)&ALE(11)&ALE(12)&ALE(13)&ALE(14)&ALE(15X
               )&ALE(16)'  CKNONUM ERROR GUIDANCE BYTE 2
&IECNDGP(1) SETA &IECNDGP(1)+3
.GEN7    AIF   (T'&CKNUM EQ 'O').GEN8
&A       SETA  &CKNUM(1)-2
&B       SETA  &CKNUM(2)-2
         DC    YL1(&B*16+&A)  CKNUM POSITION 2 AND POSITION 1
         DC    B'&NUME(1)&NUME(2)&NUME(3)&NUME(4)&NUME(5)&NUME(6)&NUME(X
               7)&NUME(8)'  CKNUM ERROR GUIDANCE BYTE 1
         DC    B'&NUME(9)&NUME(10)&NUME(11)&NUME(12)&NUME(13)&NUME(14)&X
               NUME(15)&NUME(16)' CKNUM ERROR GUIDANCE BYTE 2
&IECNDGP(1) SETA &IECNDGP(1)+3
.GEN8    AIF   (T'&CKRANGE EQ 'O').GEN9
&A       SETA  &CKRANGE(1)-2
&B       SETA  &CKRANGE(2)-2
         DC    YL1(&B*16+&A)        CKRANGE POSITION2 AND POSITION1
         DC    B'&LOWE(1)&LOWE(2)&LOWE(3)&LOWE(4)&LOWE(5)&LOWE(6)&LOWE(X
               7)&LOWE(8)'  LOW ERROR GUIDANCE BYTE 1 CKRANGE
         DC    B'&LOWE(9)&LOWE(10)&LOWE(11)&LOWE(12)&LOWE(13)&LOWE(14)&X
               LOWE(15)&LOWE(16)'  LOW ERROR GUIDANCE BYTE 2 CKRANGE
         DC    B'&HIE(1)&HIE(2)&HIE(3)&HIE(4)&HIE(5)&HIE(6)&HIE(7)&HIE(X
               8)'    HIGH ERROR GUIDANCE BYTE 1   CKRANGE
         DC    B'&HIE(9)&HIE(10)&HIE(11)&HIE(12)&HIE(13)&HIE(14)&HIE(15X
               )&HIE(16)'  HIGH ERROR GUIDANCE BYTE 2 CKRANGE
&IECNDGP(1) SETA &IECNDGP(1)+5
&A       SETA  N'&CKRANGE
&B       SETA  3
.GEN2A   ANOP
         DC    XL1'&CKRANGE(&B)'
&IECNDGP(1) SETA &IECNDGP(1)+1
         AIF   (&B EQ &A).GEN9
&B       SETA  &B+1
         AGO   .GEN2A
.GEN9    ANOP
         AIF   (T'&CKAND EQ 'O').FINAL
&A       SETA  &CKAND(2)-2
&B       SETA  &CKAND(1)-2
         DC    YL1(&A*16+&B)  CKAND POSITION2 AND POSITION1
         DC    B'&ANDE(1)&ANDE(2)&ANDE(3)&ANDE(4)&ANDE(5)&ANDE(6)&ANDE(X
               7)&ANDE(8)' CKAND ERROR GUIDANCE BYTE 1
         DC    B'&ANDE(9)&ANDE(10)&ANDE(11)&ANDE(12)&ANDE(13)&ANDE(14)&X
               ANDE(15)&ANDE(16)' CKAND ERROR GUIDANCE BYTE 2
&IECNDGP(1) SETA &IECNDGP(1)+3
&A       SETA  N'&CKAND
&B       SETA  3
.GEN5A   ANOP
         DC    XL1'&CKAND(&B)'
&IECNDGP(1) SETA &IECNDGP(1)+1
         AIF   (&B EQ &A).FINAL
&B       SETA  &B+1
         AGO   .GEN5A
.FINAL   ANOP
&IECSQ(4) SETB 1
.MEND    MEND
