.* MVS 3.8J PUBLIC DOMAIN
* /* START OF SPECIFICATIONS ****
*
*01*  MODULE-NAME = USSEND
*
*01*  DESCRIPTIVE-NAME = USS END OF TABLE DEFINITION MACRO
*
*
*01*  STATUS = RELEASE 2
*
*01*  FUNCTION = THIS MACRO HAS TWO FUNCTIONS, A USER CALL WITH NO
*     PARAMETERS WHICH SIGNIFIES THE END OF THE USS TABLE, AND AN
*     INTERNAL CALL FROM USSTAB, USSPARM, USSCMD, USSMSG WHICH USES
*     COMMON SUBROUTINES WITHIN USSEND FOR PARAMETER COUNTING, PADDING
*     FOR HIGH ORDER ZEROES, AND A CHARACTER STRING GENERATOR. THE
*     FIRST CALL WILL CAUSE ALL THE INTERNAL COUNTERS TO BE CLOSED OUT,
*     AND AN END OF CHAIN PTR GENERATED FOR EACH BLOCK OR TABLE
*     CREATED. THE ENTIRE USS TABLE IS ALIGNED TO END ON A FULL WORD
*     BOUNDARY.
*
*02*    CHARACTER-CODE-DEPENDENCIES = NONE
*
*02*    DEPENDENCIES= NONE
*
*02*    RESTRICTIONS = NONE
*
*02*    REGISTER-CONVENTIONS = NONE
*
*02*    PATCH-LABEL = NONE
*
*01*  MODULE-TYPE = MACRO
*
*02*    PROCESSOR = ASSEM-370R
*
*02*    MACRO-SIZE = 57 STATEMENTS
*
*02*    ATTRIBUTES = LIST-EXEC
*
*01*  ENTRY-POINT = NA
*
*01*  EXIT-NORMAL = NA
*
*01*  EXIT-ERROR = NA
*
*01*  EXTERNAL-REFERENCES = NONE
*
*01*  TABLES = UDT (USS DEFINITION TABLE), VPB (COMMAND PROCESSING
*     BLOCK), PPB (PARAMETER PROCESSING BLOCK), MPB (MESSAGE PROCESSING
*     BLOCK)
*
*01*  INNER-MACROS = USSEND
*
*01*  SYSTEM-MACROS = NONE
*
*01*  CHANGE-ACTIVITY = AS-FOLLOWS
*                       NEW MACRO - FIRST RELEASE   32  DOS/VS
*                                                   04  OS/VS1
*                                                   03  OS/VS2
*            APAR OZ26263  ADDITIONAL VALIDITY CHECKS        @ZA26263
**** END OF SPECIFICATIONS ***/
         MACRO
&LABEL   USSEND &INC=,&PAD=,&GEN=,&NAME=,&OMIT=0
         GBLA  &C                 USSCMD MACRO COUNTER
         GBLA  &P                 USSPARM MACRO COUNTER
         GBLA  &M                 USSMSG MACRO COUNTER
         GBLC  &T                 MISC OUTPUT VARIABLE
         GBLC  &Q                 MISC OUTPUT VARIABLE
         GBLC  &N                 STATEMENT LABEL PREFIX
         GBLB  &Z                 SWITCH FOR USSTAB SPECIFIED
         LCLC  &F                 INTERNAL WORK VARIABLE
         AIF   (&Z EQ 1).SQ005
         SPACE 1
         MNOTE 4,'USSTAB MACRO NOT SPECIFIED'
.SQ005   ANOP
         AIF   ('&INC' EQ 'C').SQ030
         AIF   ('&INC' EQ 'P').SQ040
         AIF   ('&INC' EQ 'M').SQ050
         AIF   ('&PAD' NE '').SQ060
         SPACE 1
         AIF   ('&NAME' EQ '').SQ070
         AIF   ('&GEN' NE '').SQ000
&LABEL   EQU   &OMIT              HANDLE OMITTED PARAMETER
         MEXIT
.SQ000   ANOP
&LABEL   DC    AL1(&NAME-*-1)     LENGTH OF CHAR STRING
         AIF   ('&GEN'(1,1) EQ '''').SQ010
         DC    C'&GEN'            CHARACTER STRING
         AGO   .SQ020
.SQ010   DC    C&GEN              CHARACTER STRING
.SQ020   ANOP
&NAME    EQU   *                  TO CALC CHAR STRING LENGTH
         MEXIT
.SQ030   USSEND PAD=&C            PAD USSCMD MACRO COUNTER
&T       SETC  '&Q'               SET TEMP OUTPUT VARIABLE
&C       SETA  &C+1               INCREMENT USSCMD COUNTER
         USSEND PAD=&C            PAD USSCMD MACRO COUNTER
         MEXIT
.SQ040   USSEND PAD=&P            PAD USSPARM MACRO COUNTER
&T       SETC  '&Q'               SET TEMP OUTPUT VARIABLE
&P       SETA  &P+1               INCREMENT USSPARM COUNTER
         USSEND PAD=&P            PAD USSPARM MACRO COUNTER
         MEXIT
.SQ050   USSEND PAD=&M            PAD USSMSG MACRO COUNTER
&T       SETC  '&Q'               SET TEMP OUTPUT VARIABLE
&M       SETA  &M+1               INCREMENT USSMSG COUNTER
         USSEND PAD=&M            PAD USSMSG MACRO COUNTER
         MEXIT
.SQ060   ANOP
&F       SETC  '0000000'
&Q       SETC  '&F'(1,8-K'&N-K'&PAD)'&PAD'
&Q       SETC  '&Q'(2,K'&Q-1)
         MEXIT
.SQ070   USSEND PAD=&C            PAD USSCMD MACRO COUNTER
&N.C&Q   EQU   0                  END USSCMD BLOCK QUEUE
         USSEND PAD=&P            PAD USSPARM MACRO COUNTER
&N.P&Q   EQU   0                  END USSPARM BLOCK QUEUE
         USSEND PAD=&M            PAD USSMSG MACRO COUNTER
&N.M&Q   EQU   0                  END USSMSG BLOCK QUEUE
         SPACE 1
&LABEL   DC    0F'0'              ALIGN ON FULLWORD BOUNDARY
         SPACE 1
         MEND
