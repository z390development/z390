.* MVS 3.8J PUBLIC DOMAIN
         MACRO
&NAME    GDUTRANS   &TRCODE=,&TRLIST=
         GBLA  &IECTRCD,&IECGNUM,&IECCNTR,&IECNDGP(100),&IECGDAS
.*             IECTRCD    TRANSACTION CODE OF PREVIOUS MACRO
.*             IECGNUM   TOTAL NUMBER OF GUIDANCE DISPLAY UNITS
.*             IECNDGP(1)  TOTAL BYTES COUNTER
.*             IECCNTR   VALUE OF ID OPERAND OF LAST AS MACRO
.*             IECGDAS     ID OF LAST ASCTR MACRO
         GBLA  &IECIDTF
.*             IECIDTF   SIZE OF GDU IDENTIFIER TABLE
         GBLB  &IECSW(9),&IECSEQ(10),&IECSQ(7),&IECGDSW
.*             IECSQ(2)  SEQUENCE INDICATOR FOR GDUTRANS MACRO
.*             IECGDSW   INDICATES GDU=YES CODED IN CONFIGUR MACRO
         LCLA  &A
         LCLA  &B
         LCLB  &NOCODE
         LCLC  &CHAR
         ACTR  1000
               AIF  (&IECSEQ(1) AND &IECSEQ(2) AND &IECSQ(1) AND NOT &IX
               ECSEQ(4) AND NOT &IECSEQ(5) AND NOT &IECSEQ(6)).SEQ1
         AGO   .SEQ2
.SEQ1    AIF  (NOT &IECSEQ(10) AND NOT &IECSQ(3) AND NOT &IECSQ(4) AND X
               NOT &IECSQ(5) AND NOT &IECSQ(6) AND NOT &IECSQ(7)).SEQ3
.SEQ2    ANOP
&NOCODE  SETB  1
         MNOTE 12,'MACRO SEQUENCE ERROR'
.SEQ3    ANOP
         AIF   (&IECGDSW).SWOK
&NOCODE  SETB  1
         MNOTE 12,'GDU=YES MUST BE CODED IN CONFIGUR MACRO'
.SWOK    ANOP
         AIF   (T'&TRCODE NE 'O').TRCODE1
&NOCODE  SETB  1
         MNOTE 12,'TRCODE OPERAND CAN NOT BE OMITTED'
         AGO   .TRLIST1
.TRCODE1 AIF   (T'&TRCODE EQ 'N').TRCODE2
&NOCODE  SETB  1
         MNOTE 12,'TRCODE OPERAND HAS INVALID TYPE ATTRIBUTE'
         AGO   .TRLIST1
.TRCODE2 ANOP
         AIF   (&TRCODE GE 0 AND &TRCODE LE 99).TRCODE3
&NOCODE  SETB  1
         MNOTE 12,'TRCODE OPERAND NOT IN VALID RANGE OF 0 TO 99'
         AGO   .TRLIST1
.TRCODE3 ANOP
         AIF   (&IECSQ(2)).TRCODE4
         AIF   (&TRCODE EQ 0).TRLIST1
.TRCODE4 ANOP
         AIF   (&TRCODE GT &IECTRCD).TRLIST1
&NOCODE  SETB  1
         MNOTE 12,'TRCODE OPERAND MUST BE GREATER THAN TRCODE OPERAND  X
               OF PREVIOUS MACRO'
.TRLIST1 ANOP
         AIF   (T'&TRLIST NE 'O').TRLIST2
&NOCODE  SETB  1
         MNOTE 12,'TRLIST OPERAND CAN NOT BE OMITTED'
         AGO   .GEN
.TRLIST2 ANOP
         AIF   (T'&TRLIST EQ 'M').GEN
&NOCODE  SETB  1
         MNOTE 12,'TRLIST OPERAND HAS INVALID TYPE ATTRIBUTE'
.GEN     ANOP
         AIF   (&NOCODE).NOGEN
         AIF   (&IECSW(3)).NOASEQ
&IECCNTR SETA  &IECCNTR+1
IECASSEQ DC    &IECCNTR.X'00'    AREA STATION SEQUENCE
IECDENDX EQU   *
IECDESEQ EQU   *
IECFTGRP EQU   *
IECDEUTB EQU   *
&IECSW(3) SETB 1
.NOASEQ  ANOP
         AIF   (&IECSEQ(3)).GENIA
&B       SETA  &IECNDGP(&A)+&B
&IECNDGP(1) SETA &B+4*&IECCNTR+18  TOTAL NUMBER OF BYTES
.GENIA   ANOP
         AIF   (&IECSQ(2)).NONAME
IECGDSEQ EQU   *
         SPACE 1
*                GUIDANCE DISPLAY UNIT SEQUENCE TABLE
         SPACE 1
         DC    &IECGNUM.X'00'
&IECNDGP(1) SETA &IECNDGP(1)+&IECGNUM+&IECGDAS+&IECIDTF+22
         SPACE 1
         DC    0H'0'
IECGDTRN EQU   *        GDU  TRANSACTION TABLE
         SPACE 1
.NONAME  ANOP
&A       SETA  &IECTRCD+1
         AIF   (&TRCODE EQ &A AND &TRCODE EQ 1 AND NOT &IECSQ(2)).GEN1
.LOOP    AIF   (&TRCODE EQ &A OR &TRCODE EQ 0).GEN2
         AIF   (&IECTRCD NE 0 OR &IECSQ(2)).GEN3
.GEN1    ANOP
&IECTRCD SETA  1
&A       SETA  0
.GEN3    ANOP
         DC    YL2(0)
&IECNDGP(1) SETA &IECNDGP(1)+2
&A       SETA  &A+1
         AGO   .LOOP
.GEN2    ANOP
&IECTRCD SETA  &TRCODE
&IECSQ(2) SETB 1
&CHAR    SETC  '&TRLIST'
         DC    AL2(&CHAR-IEC15TAB)
&IECNDGP(1) SETA &IECNDGP(1)+2
         AGO   .MEND
.NOGEN   ANOP
         MNOTE 12,'TEXT GENERATION SUPPRESSED'
.MEND    ANOP
         MEND
