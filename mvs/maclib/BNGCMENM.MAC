.* MVS 3.8J PUBLIC DOMAIN
         PRINT OFF
         MACRO
&NAME    BNGHJN &LABEL,&HJN
         LCLA  &K
         LCLC  &HJA,&HJB,&HJC
&HJC     SETC  'PTF NO.'
&K       SETA  K'&SYSPARM
         AIF   (&K LT 4).CODE
&HJA     SETC  '&SYSPARM'(1,4)
         AIF   (&K LT 8).CODE
&HJB     SETC  '&SYSPARM'(5,4)
         AIF   (&K LT 15).CODE
&HJC     SETC  '&SYSPARM'(9,7)
.CODE    ANOP
         AIF   ('&LABEL' EQ '').NOBRNCH
         B     &LABEL .                 BR AROUND CONSTANTS
         SPACE 2
.NOBRNCH ANOP
         AIF   ('&NAME' EQ '').NOLABEL
         DC    C'&NAME' .              MODULE IDENTIFIER
.NOLABEL ANOP
         AIF   ('&HJA' EQ '').NEXT
         DC    X'&HJA' .                DATE OF MODIFICATION
.NEXT    ANOP
         AIF   ('HJB' EQ '').DATE
         AIF   ('&HJN' NE 'HJN').DATE
         DC    X'&HJB' .                HJN OF MODIFICATION
.DATE    ANOP
         DC    C'&HJC' .                PTF LEVEL
.NOPTF   ANOP
         AIF   ('&LABEL' EQ '').NOLBL
         SPACE 2
&LABEL   DS    0H
.NOLBL   ANOP
         MEND
         PRINT ON
***********************************************************************
*                                                                     *
* MODULE NAME = BNGCMENU                                              *
*                                                                     *
* DESCRIPTIVE NAME = CICS DEMF MENU DISPLAY MODULE                    *
*                                                                     *
*                                                                     *
* STATUS = RELEASE 1, LEVEL 1                                         *
*                                                                     *
* FUNCTION = 1) RECEIVES CONTROL FROM BNGCDISP;                       *
*            2) DISPLAYS THE FIRST MENU SCREEN THAT OFFERS A CHOICE   *
*               OF ALL, 3270, REMOT OR LOCAL DISPLAYS;                *
*            3) DISPLAYS THE SECOND MENU SCREEN THAT DESCRIBES THE    *
*               SYNTAX OF THE DEMF COMMANDS AND PARAMETERS WHICH USE  *
*               EXPLICIT PHYSICAL ADDRESSES OR SYMBOLIC NAMES;        *
*            4) RETURNS CONTROL TO CICS.                              *
*                                                                     *
* NOTES = SEE BELOW                                                   *
*                                                                     *
*    DEPENDENCIES =                                                   *
*                                                                     *
*    THE FOLLOWING ARE CICS TABLES/CONTROL BLOCKS AND THEIR LABELS    *
*    USED BY DEMF CICS MODULES (BNGCDISP,BNGC3270,BNGCLOCL,BNGCRMOT   *
*    BNGCMENU) :                                                      *
*                                                                     *
*    CICS TABLE AND                                                   *
*    CONTROL BLOCKS                  LABELS USED                      *
*    ----------------------          -----------                      *
*                                                                     *
*    COMMON SYSTEM AREA              CSATCTBA                         *
*    (CSA)                                                            *
*                                                                     *
*    TERMINAL CONTROL TABLE          TCTTEDA                          *
*    TERMINAL ENTRY (TCTTE)          TCTTETM                          *
*                                    TCTTELT                          *
*                                    TCTTETI                          *
*                                    TCTTECLT                         *
*                                    TCTTETEL                         *
*                                    TCTTETT                          *
*                                    TCTTEGU                          *
*                                    TCTTETA                          *
*                                                                     *
*    TERMINAL CONTROL TABLE          TCTLELF                          *
*    LINE ENTRY (TCTLE)              TCTLEFAA                         *
*                                    TCTLEPI                          *
*                                    TCTLEMFP                         *
*                                    TCTLEPA                          *
*                                    TCTLEDCB                         *
*                                    TCTLERLN                         *
*                                                                     *
*    TERMINAL INPUT/OUTPUT AREA      TIOADBA                          *
*    (TIOA)                          TIOATDL                          *
*                                                                     *
*    TASK CONTROL AREA               TCAFCAAA                         *
*    (TCA)                           TCAFCAA                          *
*                                    TCAPCPI                          *
*                                    TCASCSA                          *
*                                                                     *
*    FILE INPUT/OUTPUT AREA          FIOADBA                          *
*    (FIOA)                                                           *
*                                                                     *
*    RESTRICTIONS = NONE                                              *
*                                                                     *
*    REGISTER CONVENTIONS = R12 ADDRESS OF TCA                        *
*                           R13 ADDRESS OF CSA                        *
*                                                                     *
*    PATCH LABEL = NONE                                               *
*                                                                     *
* MODULE TYPE = CICS DISPLAY                                          *
*                                                                     *
*    PROCESSOR = ASSEMBLER H                                          *
*                                                                     *
*    MODULE SIZE = 2176 DECIMAL BYTES                                 *
*                                                                     *
*    ATTRIBUTES = QUASI-REENTRANT                                     *
*                                                                     *
* ENTRY POINT = BNGCMENU                                              *
*                                                                     *
*    PURPOSE = SEE FUNCTION                                           *
*                                                                     *
*    LINKAGE = R14 LOADED WITH THE RETURN ADDR (USED BY SUBROUTINE)   *
*                                                                     *
* INPUT = PARAM LIST ITEMS USED ARE : INPUT TIOA AT BEGINNING OF PGM  *
*                                                                     *
* OUTPUT = PARAM LIST ITEMS USED ARE : OUTPUT TIOA AT LABEL - C10     *
*                                                                     *
* EXIT-NORMAL = REGISTER 15 RETURN CODE RETURNED TO CALLER :          *
*               00 - OPERATION PERFORMED AS REQUESTED                 *
*                                                                     *
* EXIT-ERROR  = NONE                                                  *
*                                                                     *
* EXTERNAL REFERENCE = NONE                                           *
*                                                                     *
*    ROUTINES = NONE                                                  *
*                                                                     *
*    DATA AREAS = NONE                                                *
*                                                                     *
*    CONTROL BLOCK = DFHCSADS, DFHTCTTE, DFHTCTLE, DFHTIOA, DFHTCADS  *
*                                                                     *
* TABLES = NONE                                                       *
*                                                                     *
* MACROS = DFHSC, DFHTC, DFHPC                                        *
*                                                                     *
* CHANGE ACTIVITIES AS FOLLOWS:                                       *
* CHANGE 01   11/15/77  PROLOGUE ADDED                                *
*DUMMY APAR TO CORRECT PREREQ STRINGS ON PREVIOUS PTF'S        @OZ36693
*                                                                     *
***********************************************************************
         TITLE 'DISPLAY EXCEPTION MONITORING FACILITY MENU PROGRAM'
         PRINT OFF
         COPY  DFHCSADS
         COPY  DFHTCTTE
         COPY  DFHTCTLE
         COPY  DFHTIOA
         COPY  DFHTCADS
         PRINT ON
PARM1    DS    D                   1ST INPUT PARM - DEMF
PARM2    DS    D                   2ND INPUT PARM - MENU
PARM3    DS    D                   3RD INPUT PARM - X
***********************************************************************
BNGCMENU CSECT
SBA      EQU   X'11'               SET BUFFER ADDRESS
SF       EQU   X'1D'               START FIELD
IC       EQU   X'13'               INSERT CURSOR
PNA      EQU   X'60'               PROTECTED,NORMAL,ALPHAMERIC
PHA      EQU   X'E8'               PROTECTED,HIGH INTENSITY,ALPHAMERIC
UNA      EQU   X'40'               UNPROT,NORMAL,ALPHAMERIC
MNP      EQU   X'61'               MODIFIED,NORMAL,PROTECTED,ALPHAMERIC
L1P1     EQU   X'4040'             LINE 1 POSITION 1
L1P35    EQU   X'40E2'
L1P36    EQU   X'40E3'
L2P1LG   EQU   X'C150'             LINE 2 POSITION 1 -LARGE SCREEN
L2P29SM  EQU   X'C1C4'             LINE 2 POSITION 29-SMALL SCREEN
L2P70    EQU   X'C2D5'
L3P1SM   EQU   X'C150'             LINE 3 POSITION 1 -SMALL SCREEN
L4P1SM   EQU   X'C1F8'             LINE 4 POSITION 1 -SMALL SCREEN
L4P2SM   EQU   X'C1F9'
L4P4SM   EQU   X'C17B'
L4P15SM  EQU   X'C2C6'
L5P1SM   EQU   X'C260'
L5P2SM   EQU   X'C261'
L5P4SM   EQU   X'C2E3'
L5P15SM  EQU   X'C26E'
L6P1SM   EQU   X'C3C8'
L6P2SM   EQU   X'C3C9'
L6P4SM   EQU   X'C34B'
L6P15SM  EQU   X'C3D6'
L7P16SM  EQU   X'C37F'
L8P1SM   EQU   X'C4D8'
L8P2SM   EQU   X'C4D9'
L8P4SM   EQU   X'C45B'
L8P15SM  EQU   X'C4E6'
L9P16SM  EQU   X'C54F'
L10P1SM  EQU   X'C5E8'
L10P2SM  EQU   X'C5E9'
L10P3SM  EQU   X'C56A'
L10P4SM  EQU   X'C56B'
L11P8SM  EQU   X'C6D7'
L12P8SM  EQU   X'C67F'
L3P3LG   EQU   X'C2E2'
L3P25LG  EQU   X'C2F8'
L4P1LG   EQU   X'C3F0'
L4P23LG  EQU   X'C4C6'
L5P1LG   EQU   X'C540'
L5P23LG  EQU   X'C5D6'
L6P1LG   EQU   X'C650'
L6P20LG  EQU   X'C6E3'
L6P23LG  EQU   X'C6E6'
L7P1LG   EQU   X'C760'
L7P21LG  EQU   X'C7F4'
L7P23LG  EQU   X'C7F6'
L8P1LG   EQU   X'C8F0'
L8P21LG  EQU   X'C9C4'
L8P23LG  EQU   X'C9C6'
L9P1LG   EQU   X'4A40'
L9P21LG  EQU   X'4AD4'
L9P23LG  EQU   X'4AD6'
L10P21LG EQU   X'4BE4'
L10P23LG EQU   X'4BE6'
L12P1LG  EQU   X'4DF0'
*
L6P56    EQU   X'C7C7'
L6P61    EQU   X'C74C'
L7P50    EQU   X'C8D1'
L9P22LG  EQU   X'4AD5'
L9P24LG  EQU   X'4AD7'
L9P35LG  EQU   X'4AE2'
L10P22LG EQU   X'4BE5'
L10P24LG EQU   X'4BE7'
L10P35LG EQU   X'4BF2'
L11P21LG EQU   X'4CF4'
L11P22LG EQU   X'4CF5'
L11P24LG EQU   X'4CF7'
L11P35LG EQU   X'4DC2'
L12P36LG EQU   X'4ED3'
L13P21   EQU   X'4FD4'
L13P22   EQU   X'4FD5'
L13P24   EQU   X'4FD7'
L13P35   EQU   X'4FE2'
L14P36   EQU   X'50F3'
L15P21   EQU   X'D1F4'
L15P22   EQU   X'D1F5'
L15P23   EQU   X'D1F6'
L15P24   EQU   X'D1F7'
L16P28   EQU   X'D34B'
L17P28   EQU   X'D45B'
BASE1    EQU   3             BASE REGISTER ONE = 0-4095
BASE2    EQU   4             BASE REGISTER TWO = 4096-8191
TCTTEAR  EQU   5             POINTS TO TCTTE
TIOABAR  EQU   6             POINTS TO TIOA
***********************************************************************
         BALR  BASE1,0
         USING *,BASE1,BASE2
         LA    BASE2,2048(BASE1)
         LA    BASE2,2048(BASE2)
BNGCMENU BNGHJN GO                                             @OZ36693
         L     TCTTEAR,TCAFCAAA
         L     TIOABAR,TCTTEDA
***********************************************************************
         CLC   TIOADBA+15(2),=X'D1F5'    (L15P22) A PAGE 2 REQUEST-LG?
         BE    B10                        YES.
A10      CLC   TIOADBA+15(2),=X'C5E9'    (L10P2SM)A PAGE 2 REQUEST-SM?
         BNE   C10                        NO.
B10      MVC   PARM1(4),=C'PAGE'
***********************************************************************
C10      DFHSC TYPE=GETMAIN,CLASS=TERM,NUMBYTE=2000
         L     TCTTEAR,TCAFCAAA
         L     TIOABAR,TCASCSA
         ST    TIOABAR,TCTTEDA
         CLI   TCTTETM,C'1'        IS THIS A SMALL SCREEN?
         BE    SMALL               YES. USE SMALL MENU.
         CLC   PARM1(4),=C'PAGE'   IS THIS A PAGE 2 REQUEST?
         BE    PAGE2LG             YES.
         LA    1,LG1LTH            POINT TO LARGE 1 MENU
         LA    14,LG1MSG
EXIT     DS    0H
         LA    0,TIOATDL
         LR    15,1
         MVCL  0,14
         DFHTC TYPE=(WRITE,ERASE)
         DFHPC TYPE=RETURN
SMALL    CLC   PARM1(4),=C'PAGE'   IS THIS A PAGE 2 REQUEST?
         BE    PAGE2SM            YES.
         LA    1,SM1LTH         POINT TO SMALL 1 MENU
         LA    14,SM1MSG
         B     EXIT
PAGE2SM  LA    1,SM2LTH         POINT TO SMALL 2 MENU
         LA    14,SM2MSG
         B     EXIT
PAGE2LG  LA    1,LG2LTH         POINT TO LARGE 2 MENU
         LA    14,LG2MSG
         B     EXIT
LG1MSG   DC    AL2(LG1LTH-4),X'0000',AL1(SBA),AL2(L6P20LG),AL1(SF,MNP)
         DC    C'DEMF',AL1(SF,UNA),AL1(SBA),AL2(L6P56),AL1(SF,MNP)
         DC    C'MENU',AL1(SBA),AL2(L6P61),AL1(SF,PNA)
 DC AL1(SBA),AL2(L7P21LG),C'CICS VERSION'
 DC AL1(SBA),AL2(L7P50),C'PAGE 1 OF 2'
 DC AL1(SBA),AL2(L8P21LG),C'TYPE IN COMMAND OR X IN ? TO DISPLAY:'
 DC AL1(SBA),AL2(L9P21LG),C'(',AL1(SBA),AL2(L9P22LG)
 DC AL1(SF,UNA,IC),C'?',AL1(SBA),AL2(L9P24LG),AL1(SF,PNA),C')ALL'
 DC AL1(SBA),AL2(L9P35LG),C'RECAP OF ERRS ON ALL LINES'
 DC AL1(SBA),AL2(L10P21LG),C'(',AL1(SBA),AL2(L10P22LG),AL1(SF,UNA),C'?'
 DC AL1(SBA),AL2(L10P24LG),AL1(SF,PNA),C')3270'
 DC AL1(SBA),AL2(L10P35LG),C'ERR DATA ON YOUR TERMINAL'
 DC AL1(SBA),AL2(L11P21LG),C'(',AL1(SBA),AL2(L11P22LG),AL1(SF,UNA),C'?'
 DC AL1(SBA),AL2(L11P24LG),AL1(SF,PNA),C')REMOTE'
 DC AL1(SBA),AL2(L11P35LG),C'TEXT OF REMOTE 327X STATUS'
 DC AL1(SBA),AL2(L12P36LG),C'AND SENSE COMBINATIONS'
 DC AL1(SBA),AL2(L13P21),C'(',AL1(SBA),AL2(L13P22),AL1(SF,UNA),C'?'
 DC AL1(SBA),AL2(L13P24),AL1(SF,PNA),C')LOCAL'
 DC AL1(SBA),AL2(L13P35),C'TEXT OF LOCAL 327X STATUS'
 DC AL1(SBA),AL2(L14P36),C'AND SENSE COMBINATIONS'
 DC AL1(SBA),AL2(L15P21),AL1(SF,MNP),C'DEPRESS ENTER KEY FOR PAGE 2'
 DC C'--CONTAINS'
 DC AL1(SBA),AL2(L16P28),C'COMMAND FORMATS USED TO DIRECTLY'
 DC AL1(SBA),AL2(L17P28),C'ADDRESS A LINE, CTRL UNIT,OR TERML'
LG1LTH   EQU   *-LG1MSG
LG2MSG   DC    AL2(LG2LTH-4),X'0000',AL1(SBA),AL2(L1P1),AL1(SF,MNP)
         DC    C'DEMF',AL1(SF,UNA,IC),AL1(SBA),AL2(L1P35),AL1(SF,MNP)
         DC    C'MENU',AL1(SF,PNA)
 DC AL1(SBA),AL2(L2P1LG),C'CICS VERSION'
 DC AL1(SBA),AL2(L2P70),C'PAGE 2 OF 2'
 DC AL1(SBA),AL2(L3P3LG),C'KEY IN:'
 DC AL1(SBA),AL2(L3P25LG),C'YOU WILL SEE:'
 DC AL1(SBA),AL2(L4P1LG),C'DEMF LINE LLL',AL1(SBA),AL2(L4P23LG)
 DC C'ERRS BY CONTROL UNIT ON SELECTED LINE LLL'
 DC AL1(SBA),AL2(L5P1LG),C'DEMF CTRL LLL CC',AL1(SBA),AL2(L5P23LG)
 DC C'ERRS BY TERMINAL ON REMOTE CONTROL UNIT LLL CC'
 DC AL1(SBA),AL2(L6P1LG),C'DEMF CTRL CCC',AL1(SBA),AL2(L6P23LG)
 DC C'ERRS BY TERMINAL ON LOCAL CONTROL UNIT CCC'
 DC AL1(SBA),AL2(L7P1LG),C'DEMF 3270 LLL CC TT',AL1(SBA),AL2(L7P23LG)
 DC C'ERR DESCRIPTIVE DATA OF REMOTE TERMINAL LLL CC TT'
 DC AL1(SBA),AL2(L8P1LG),C'DEMF 3270 TTT',AL1(SBA),AL2(L8P23LG)
 DC C'ERR DESCRIPTIVE DATA OF LOCAL TERMINAL TTT'
 DC AL1(SBA),AL2(L9P1LG),C'DEMF 3270 SYMBNAME',AL1(SBA),AL2(L9P23LG)
 DC C'ERR DESCRIPTIVE DATA OF LOCAL OR REMOTE TERMINAL'
 DC AL1(SBA),AL2(L10P23LG),C'WITH CICS NAME',AL1(SBA),AL2(L12P1LG)
 DC C'TYPE X IN PLACE OF ? ON DISPLAYS TO PAGE OR VIEW LOWER LEVEL '
 DC C'OF DETAIL'
LG2LTH   EQU   *-LG2MSG
SM1MSG   DC    AL2(SM1LTH-4),X'0000',AL1(SBA),AL2(L1P1),AL1(SF,MNP)
         DC    C'DEMF',AL1(SF,UNA),AL1(SBA),AL2(L1P35),AL1(SF,MNP)
         DC    C'MENU',AL1(SF,PNA),C'CICS VERSION'
 DC AL1(SBA),AL2(L2P29SM),C'PAGE 1 OF 2'
 DC AL1(SBA),AL2(L3P1SM),C'TYPE IN COMMAND OR X IN ? TO DISPLAY:'
 DC AL1(SBA),AL2(L4P1SM),C'(',AL1(SBA),AL2(L4P2SM),AL1(SF,UNA,IC),C'?'
 DC AL1(SBA),AL2(L4P4SM),AL1(SF,PNA),C')ALL'
 DC AL1(SBA),AL2(L4P15SM),C'RECAP OF ERRS ON ALL LINES'
 DC AL1(SBA),AL2(L5P1SM),C'(',AL1(SBA),AL2(L5P2SM),AL1(SF,UNA),C'?'
 DC AL1(SBA),AL2(L5P4SM),AL1(SF,PNA),C')3270'
 DC AL1(SBA),AL2(L5P15SM),C'ERR DATA ON YOUR TERMINAL'
 DC AL1(SBA),AL2(L6P1SM),C'(',AL1(SBA),AL2(L6P2SM),AL1(SF,UNA),C'?'
 DC AL1(SBA),AL2(L6P4SM),AL1(SF,PNA),C')REMOTE'
 DC AL1(SBA),AL2(L6P15SM),C'TEXT OF REMOTE 327X STATUS'
 DC AL1(SBA),AL2(L7P16SM),C'AND SENSE COMBINATIONS'
 DC AL1(SBA),AL2(L8P1SM),C'(',AL1(SBA),AL2(L8P2SM),AL1(SF,UNA),C'?'
 DC AL1(SBA),AL2(L8P4SM),AL1(SF,PNA),C')LOCAL'
 DC AL1(SBA),AL2(L8P15SM),C'TEXT OF LOCAL 327X STATUS'
 DC AL1(SBA),AL2(L9P16SM),C'AND SENSE COMBINATIONS'
 DC AL1(SBA),AL2(L10P1SM),AL1(SF,MNP),C'DEPRESS ENTER KEY FOR PAGE 2'
 DC C'--CONTAINS'
 DC AL1(SBA),AL2(L11P8SM),C'COMMAND FORMATS USED TO DIRECTLY'
 DC AL1(SBA),AL2(L12P8SM),C'ADDRESS A LINE, CTRL UNIT,OR TERM'
SM1LTH   EQU   *-SM1MSG
SM2MSG   DC    AL2(SM2LTH-4),X'0000',AL1(SBA),AL2(L1P1),AL1(SF,MNP)
         DC    C'DEMF',AL1(SF,UNA,IC),AL1(SBA),AL2(L1P35),AL1(SF,MNP)
         DC    C'MENU',AL1(SF,PNA)
         DC    CL40'CICS VERSION                PAGE 2 OF 2'
         DC    CL40'  KEY IN:               YOU WILL SEE:'
         DC    CL40'DEMF LINE LLL       ERRS BY CONTROL UNIT'
         DC    CL40'DEMF CTRL LLL CC    ERRS BY REMOTE TERML'
         DC    CL40'DEMF CTRL CCC       ERRS BY LOCAL TERML'
         DC    CL40'DEMF 3270 LLL CC TT STATUS OF RMOT TERML'
         DC    CL40'DEMF 3270 TTT       STATUS OF LOCL TERML'
         DC    CL40'DEMF 3270 SYMBNAME  STATUS OF RMT OR LOC'
         DC    CL40'                    TERML WITH CICS NAME'
         DC    CL40'TYPE X IN PLACE OF ? ON DISPLAYS TO PAGE'
         DC    CL40'OR VIEW LOWER LEVEL OF DETAIL'
SM2LTH   EQU   *-SM2MSG
         LTORG
         END   BNGCMENU

