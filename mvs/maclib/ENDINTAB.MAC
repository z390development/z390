.* MVS 3.8J PUBLIC DOMAIN
         MACRO
&NAME    ENDINTAB
.* GLOBAL CMZELOG IS USED TO CONTROL THE ORDER OF THE MACROS
.* LOCAL  IN1     IS A STRING SCAN INDEX
.* LOCAL  ERRBIT  IS A GENERAL ERROR SWITCH
.* LOCAL  ERRCHAR IS A GENERAL CHARACTER IN ERROR
         GBLB  &CMZELOG(2)
         LCLA  &IN1
         LCLB  &ERRBIT
         LCLC  &ERRCHAR
         AGO   .ONW00
* /* START OF SPECIFICATIONS ****
*
*01*  MODULE-NAME = ENDINTAB
*
*01*  DESCRIPTIVE-NAME = END LOGON CHARACTERISTICS TABLE
*
*
*01*  STATUS = RELEASE 1
*
*01*  FUNCTION = CREATES END OF LOGON CHARACTERISTICS TABLE MARKER
*
*01*  NOTES = THE FOLLOWING MNOTES ARE ISSUED
*        8,'CARDS OUT OF SEQUENCE'
*        8,'LOGCHAR MUST PRECEED ENDINTAB'
*        8,'NAME ''&NAME'' IS LONGER THAN EIGHT CHARACTERS'
*        8,'CHARACTER ''&ERRCHAR'' IS INVALID IN NAME ''&NAME'''
*
*02*    CHARACTER-CODE-DEPENDENCIES = EBCDIC
*
*02*    DEPENDENCIES = NONE
*
*02*    RESTRICTIONS = VTAM MAPPING MACRO
*
*02*    REGISTER-CONVENTIONS = NONE, LIST FORM
*
*02*    PATCH-LABEL = NONE
*
*01*  MODULE-TYPE = MACRO
*
*02*    PROCESSOR = ASSEMBLER
*
*02*    MACRO-SIZE = 50 STATEMENTS
*
*02*    ATTRIBUTES = LIST
*
*01*  ENTRY-POINT = NA
*
*01*  EXIT-NORMAL = NA
*
*01*  EXIT-ERROR = NA
*
*01*  EXTERNAL-REFERENCES = AS-FOLLOWS
*
*02*    ROUTINES = NONE
*
*02*    DATA-SETS = NONE
*
*02*    DATA-AREA = NONE CMZELOG IS A TWO BIT GLOBAL WHICH CONTROLS THE
*       SEQUENCING OF THE INTAB, LOGCHAR, AND ENDINTAB MACROS.
*
*02*    CONTROL-BLOCKS-SYSTEM = REQ: COMMENTS:,
*                            COMMENTS: ENTER NONE OR LIST SYSTEM
*                                      CONTROL BLOCKS WITH FIELDS
*                                      REFERENCED OR SET, THIS
*                                      SECTION MAY BE EXPANDED;
*
*02*    CONTROL-BLOCKS-ATCAM = REQ: CHOOSE: NONE|
*                                        ISTATCVT=SPCATCVT|
*                                        ISTACB=SPCACB|
*                                        ISTACDEB=SPCACDEB|
*                                        ISTRPL=SPCRPL|
*                                        ISTPREFX=SPCPREFX|
*                                        ISTQAB=SPCQAB|
*                                        ISTDFNOD=SPCDFNOD|
*                                        ISTEXLST=SPCEXLST|
*                                        ISTLDO=SPCLDO|
*                                        ISTDNCB=SPCDNCB|
*                                        ISTRPLH=SPCRPLH|
*                                        ISTRDT=SPCRDT|
*                                        ISTBTU=SPCBTU|
*                                        ISTDVT=SPCDVT|
*                                        ISTAPB=SPCAPB|
*                                        ISTICB=SPCICB|
*                                        ISTICEX=SPCICEX|
*                                        ISTFSB=SPCFSB|
*                                        ISTFDB=SPCFDB,
*                           COMMENTS: ENTER NAME OF CONTROL BLOCK
*                                     USED, COPY THIS SECTION IF
*                                     MORE THAN ONE;
*
*01*  TABLES = NONE
*
*01*  INNER-MACROS = NONE
*
*01*  SYSTEM-MACROS = NONE
*
*01*  CHANGE-ACTIVITY = NONE
*
**** END OF SPECIFICATIONS ***/
*/*ENDINTAB: CHART (FMODE) */
*/*HEADER
*/*PROGRAM NAME:
*/*DATE:
*/*PROGRAMMER:                                                      */
*/*1A3 E ENDINTAB */
*/*1B3 P GLOBAL: CMZELOG(2) , LOCALS: IN1, ERRBIT, ERRCHAR */
*/*ONW00:1C3 D (NO,ERR0010:LT,YES,) IS CMZELOG(1) = 1 */
*/*1D3 D (NO,ERR0020:RT,YES,) IS CMZELOG(2) = 1 */
*/*ONW10:1F3 P SET CMZELOG(1) = 0 AND CMZELOG(2) = 1 */
*/*1G3 $ (,%2B2) */
*/*%2B2:2B2 D (NO,ONW30:LT,YES,) WAS A NAME GIVEN */
*/*ONW20:2C2 P SET IN1 = IN1+1 */
*/*2D2 D (YES,ONW30:LT,NO,) IN1 > NAME LENGTH */
*/*2E2 D (YES,ERR0030:BT,NO,:RT) IN1 > 8 */
*/*2F4 D (YES,ONW20:RT,INVL,ERR0040:LT,NO,) NAME(IN1) FROM A-Z */
*/*2G4 D (YES,ERR0040:LT,NO,) IN1 = 1 */
*/*2H4 D (NO,ERR0040:BT,YES,ONW20:RT) NAME(IN1) FROM 0-9 */
*/*ONW30:2G2 D (YES,ONW50:RT,NO,) ERRBIT = 1 */
*/*2H2 D (NO,ONW40:BT,YES,:LR) NAME SPECIFIED */
*/*2H1 P GENERATE NAME STMT */
*/*ONW40:2J2 P GENERATE END OF TABLE FLAGS */
*/*ONW50:2K2 R MEXIT */
*/*ERR0010:1E2 P (,ONW10) MNOTE ERROR, SET ERRBIT TO 1 */
*/*ERR0020:1E4 P (,ONW10) MNOTE ERROR, SET ERRBIT TO 1 */
*/*ERR0030:2F2 P (,ONW30) MNOTE ERROR, SET ERRBIT TO 1 */
*/*ERR0040:2J4 P (,ONW20:RT) MNOTE ERROR, SET ERRBIT TO 1 */
*/*ENDINTAB: END */
.* IF THE FIRST BIT IS NOT ON, SEQUENCE ERROR
.ONW00   AIF   (&CMZELOG(1) NE 1 ).ERR0010
.* IF THE SECOND BIT IS NOT ON, THERE WERE NO LOGCHAR MACROS ISSUED
         AIF   (&CMZELOG(2) NE 1 ).ERR0020
.ONW10   ANOP
.* SET THE BITS FOR ERROR IF ANY MORE MACROS ARE ISSUED. ENDINTAB IS
.* LAST MACRO TO BE CALLED
&CMZELOG(1) SETB 0
&CMZELOG(2) SETB 1
.* IF THERE IS NO NAME, SKIP TO GENERATE CODE
         AIF   ('&NAME' EQ '' ).ONW30
.* CHECK THE NAME LENGTH AND VALID EBCDIC
.ONW20   ANOP
&IN1     SETA  &IN1+1                  POINT TO NEXT CHARACTER
         AIF   (&IN1 GT K'&NAME ).ONW30
         AIF   (&IN1 GT 8 ).ERR0030
         AIF   ('&NAME'(&IN1,1) LT 'A' ).ERR0040
         AIF   ('&NAME'(&IN1,1) LE 'I' ).ONW20
         AIF   ('&NAME'(&IN1,1) LT 'J' ).ERR0040
         AIF   ('&NAME'(&IN1,1) LE 'R' ).ONW20
         AIF   ('&NAME'(&IN1,1) LT 'S' ).ERR0040
         AIF   ('&NAME'(&IN1,1) LE 'Z' ).ONW20
         AIF   (&IN1 EQ 1 ).ERR0040
         AIF   ('&NAME'(&IN1,1) LT '0' ).ERR0040
         AIF   ('&NAME'(&IN1,1) GT '9' ).ERR0040
         AGO   .ONW20
.* IF THE ERROR BIT WAS SET, SKIP GENERATION
.ONW30   AIF   (&ERRBIT EQ 1 ).ONW50
         AIF   ('&NAME' EQ '' ).ONW40
&NAME    EQU   *
.ONW40   ANOP
         DC    BL1'10000000'
.ONW50   ANOP
         MEXIT
.ERR0010 MNOTE 8,'CARDS OUT OF SEQUENCE'
&ERRBIT  SETB  1
         AGO   .ONW10
.ERR0020 MNOTE 8,'LOGCHAR MUST PRECEED ENDINTAB'
&ERRBIT  SETB  1
         AGO   .ONW10
.ERR0030 MNOTE 8,'NAME ''&NAME'' IS LONGER THAN EIGHT CHARACTERS'
&ERRBIT  SETB  1
         AGO   .ONW30
.ERR0040 ANOP
&ERRCHAR SETC  '&NAME'(&IN1,1)
         MNOTE 8,'CHARACTER ''&ERRCHAR'' IS INVALID IN NAME ''&NAME'''
&ERRBIT  SETB  1
         AGO   .ONW20
         MEND

