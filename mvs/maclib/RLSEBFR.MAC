.* MVS 3.8J PUBLIC DOMAIN
         MACRO                           HEADER STATEMENT
&NAME    RLSEBFR &DCB,&SECT,&MF=         PROTOTYPE STATEMENT
.*LOCALS DEFINED
         LCLC  &PAREN,&A                 LOCALS DEFINED
         LCLA  &ADCB,&ASECT,&ALL
         LCLA  &SAVCOD
         AIF   (T'&MF EQ 'O').SFORM      S-FORM ASSUMED IF PMF= OMITTED
         AIF   (N'&MF EQ 2).EFORM            CHECK FOR 2 OPERANDS
         AIF   ('&MF(1)' EQ 'L').LFORM       POSITIVE CHECK FOR 6
         AGO   .ERREMF
.EFORM   AIF   ('&MF(1)' NE 'E').ERREMF      POSITIVE CHECK FOR )
         AIF   ('&MF(2)'(1,1) EQ '(').REGMF  CHECK NOTATION OF LSTADDR
&NAME    LA    1,&MF(2)                  REG 1 HAS LSTADDR IN E-FORM
.CHECK   AIF   (T'&DCB EQ 'O' AND T'&SECT EQ 'O').END
         AIF   (T'&DCB NE 'O').CHANGE1
.SCTION  AIF   ('&SECT' NE 'ALL').SECTAD  CHECK FOR 'ALL'
         XC    4(4,1),4(1)               CLEAR SCT ADDR AND NUMB TO 0
         MVI   0(1),X'0C'                LIST WITH GIVEN PARAMS
         AGO   .END
.SECTAD  MVI   0(1),X'08'                PUT CORRECT CODE IN PARAMLST
         AIF   ('&SECT'(1,1) EQ '(').REGSCT
         LA    0,&SECT                   PUT BUFPRAM-ADDR IN PARAMLIST
         ST    0,4(1)                    PUT BUFPRAM-ADDR IN PARAMLIST
.END     SVC   71
         MEXIT     BRANCH OUT OF MACRO DEFINITION
.CHANGE1 AIF   (T'&SECT EQ 'O').CODESV    SET SWITCH IF OMITTED
.CHANGE2 AIF   ('&DCB'(1,1) EQ '(').REGDCB    CHECK FOR REG NOTATION
         AIF   (&SAVCOD EQ 1).SYMPAIN   IF SWITCH ON,EXPAND MORE
         LA    0,&DCB                    PUT DCBADDR IN PARAM LIST
         ST    0,0(1)                    PUT DCBADDR IN PARAM LIST
.GOON    AIF   (T'&SECT NE 'O').SCTION
         AGO   .END
.CODESV  ANOP
&SAVCOD  SETA  1                  SET SWITCH
         XC    1(3,1),1(1)        CLEAR 3 BYTE DCB FLD IN PARAMLST
         AGO   .CHANGE2
.SYMPAIN LA    0,&DCB             LOAD DCB ADR
         O     0,0(1)    OR IN REQ CODE ANDCLEARED DCB FLD
         ST    0,0(1)             STORE INTO PARLST WITH REQ CODE
         AGO   .END
.REGMF   ANOP
&PAREN   SETC  '&MF(2)'(2,K'&MF(2)-2)    ISOLATE REGISTER
&NAME    LR    1,&PAREN                  PUT LISTADR IN REG 1
         AGO   .CHECK
.REGDCB  ANOP
&PAREN   SETC  '&DCB'(2,K'&DCB-2)        ISOLATE REGISTER NUMBER
         AIF   (&SAVCOD EQ 1).REGPAIN
         ST    &PAREN,0(1)               PUT IN PARAM LIST
         AGO   .GOON
.REGPAIN LA    0,0(&PAREN)    CLR HI-ORDER BYTE OF DCB ADR REG
         O     0,0(1)    OR REQUEST CODE AND CLEARED DCB FLD
         ST    0,0(1)    RETURN TO PARAM LST WITH CODE INTACT
         AGO   .END
.REGSCT  ANOP
&PAREN   SETC  '&SECT'(2,K'&SECT-2)
         ST    &PAREN,4(1)               PUT BUFPRAM-ADDR IN PARAMLIST
         AGO   .END
.SFORM   ANOP
&NAME    LA    1,A&SYSNDX              LOAD ADR OF PARAM LIST
         AIF   (T'&DCB EQ 'O').NODCB
&PAREN   SETC  '&DCB'(1,1)
         AIF   ('&PAREN' EQ '(').REGSDCB    CHECK NOTATION
         AIF   ('&PAREN' LT 'A').ERRDCB  MUST BE IN SYMBOLIC FORM
         AIF   ('&PAREN' GT 'Z').ERRDCB  MUST BE IN SYMBOLIC FORM
&ADCB    SETA  1                   SET TO 1 IF SYMBOLIC
.OK1     AIF   (T'&SECT EQ 'O').NOSECT   IF OMITTED, GROSS ERROR
.AHEAD   AIF   ('&SECT' NE 'ALL').ADDRS  CHECK FOR 'ALL' CONDITION
         AIF   (&ADCB EQ 1).SETALL
         MVI   A&SYSNDX,X'0C'            INSERT PROPER CODE
.FORWRD  CNOP  2,4
         SVC   71  CALL BUFFER MANAGEMENT ROUTINE VIA SVC
         B     A&SYSNDX+8                BRANCH AROUND PARAM LIST
         AIF   (&ADCB EQ 1).SYM1   IF 1 GO TO SYM1
A&SYSNDX DC    A(0)                CODE AND DCBADR
.CHK1    AIF   (&ASECT EQ 1).SYM2  IF 1 GO TO SYM2
         DC    A(0)                BUFPRM
         MEXIT   BRACCH OUT OF MACRO DEFINITION
.SYM1    AIF   (&ALL EQ 1).ALL
A&SYSNDX DC    AL1(8)                   CODE FOR RLSE
         DC    AL3(&DCB)                DCBADR
         AGO   .CHK1
.ALL     ANOP
A&SYSNDX DC    AL1(12)                  CODE FOR RLSE ALL
         DC    AL3(&DCB)                DCBADR
         AGO   .CHK1
.SYM2    DC    A(&SECT)            BUFPRM PTR
         MEXIT
.ADDRS   AIF   (&ADCB EQ 1).CKON
         MVI   A&SYSNDX,X'08'            INSERT CORRECT CODE
.CKON    ANOP
&PAREN   SETC  '&SECT'(1,1)
         AIF   ('&PAREN' EQ '(').REGSSCT   CHECK NOTATION
&ASECT   SETA  1                   SET TO 1 IF SYMBOLIC
         AGO   .FORWRD
.SETALL  ANOP
&ALL     SETA  1
         AGO   .FORWRD
.REGSSCT ANOP
&PAREN   SETC  '&SECT'(2,K'&SECT-2)      ISOLATE REG NUMBER
         ST    &PAREN,A&SYSNDX+4         PUT BUFPRAM-ADDR IN PARAMLIST
         AGO   .FORWRD
.REGSDCB ANOP
&PAREN   SETC  '&DCB'(2,K'&DCB-2)
         ST    &PAREN,A&SYSNDX           PUT IN PARAM LIST
         AGO   .OK1
.LFORM   AIF   (T'&DCB EQ 'O' AND T'&SECT EQ 'O').FINI
         AIF   (T'&DCB NE 'O').CHK
         AIF   ('&SECT' EQ 'ALL').CODE
.SECT    AIF   ('&SECT' EQ 'ALL').CODE
&PAREN   SETC  '&SECT'(1,1)
         AIF   ('&PAREN' LT 'A' OR '&PAREN' GT 'Z').ERRSCT
&ASECT   SETA  1
.FINI    DS    0F                        GENERATED CODING
         AIF   (&ALL EQ 1).ALLCOD
&NAME    DC    AL1(8)                    CODE FOR RELEASE BUFFER
.CHK2    AIF   (&ADCB EQ 1).FILL1
         DC    AL3(0)                   DCBADR
.CHK3    AIF   (&ASECT EQ 1).FILL2
.OUT     DC    A(0)                     BUFPARAM
         MEXIT
.CHK     ANOP
&PAREN   SETC  '&DCB'(1,1)
         AIF   ('&PAREN' LT 'A' OR '&PAREN' GT 'Z').ERLDCB
&ADCB    SETA  1
.OK2     AIF   (T'&SECT EQ 'O').FINI
         AGO   .SECT
.CODE    ANOP
&ALL     SETA  1
         AGO   .FINI
.ALLCOD  ANOP
&NAME    DC    AL1(12)                  CODE FOR RLSEALL
         AGO   .CHK2
.FILL1   DC    AL3(&DCB)                DCBADR
         AGO   .CHK3
.FILL2   AIF   ('&SECT' EQ 'ALL').OUT
         DC    A(&SECT)                 BUFPARAM
         MEXIT
.ERREMF  IHBERMAC  35,,&MF         ERROR MSG FOR INVALID MF
         MEXIT
.ERRDCB  IHBERMAC  36,,DCBADDR
         AGO   .OK1
.ERLDCB  IHBERMAC  36,,DCBADR
         AGO   .OK2
.NODCB   IHBERMAC  6
         AGO   .OK1
.NOSECT  IHBERMAC  3
         AGO   .FORWRD
.ERRSCT  IHBERMAC  37,,BUFPARAM
         AGO   .FINI
         MEND

