.* MVS 3.8J PUBLIC DOMAIN
         MACRO
&NAME    GDULIST  &PARAMNO=,&NORGUID=,&DISPMSG=,&MSG=,&ENTRY=1,&IDENT=
         GBLA  &IECPARN,&IECNDGP(100),&IECGID
.*             IECPARN  PARAMETER LIST NUMBER OF LAST MACRO
.*             IECGID  NUMBER OF GDU IDENTIFIERS
         GBLB  &IECGDL,&IECTXT,&IECSW(9),&IECSEQ(10),&IECSQ(7),&IECGDSW
.*             IECGDL ON INDICATES NOT FIRST GDULIST MACRO IN THIS TRNS
.*             IECTXT  ON WHEN IMPLICIT TEXT ALREADY GENERATED
.*             IECSW(8) SET WHEN MSG TXT APPEARS.INDICATES TO FOLLOWING
.*                      TRLIST OR STEND MACRO THAT AN END OF LIST BYTE
.*                      HAS BEEN GENERATED BY THIS MACRO
         LCLA &A,&B,&C
         LCLB  &NOCODE,&NORSW(16),&D,&E
.*             &E INDICATES MSG OPERAND IS INVALID
.*             &D INDICATES ENTRY=M IS CODED
.*             &NORSW(16) USED TO DEFINE 2 BYTES OF NORMAL GUIDANCE
         LCLC  &CHAR
         ACTR  1000
         AIF   (&IECSEQ(1) AND &IECSEQ(2) AND &IECSQ(1) AND &IECSQ(2)  X
               AND &IECSEQ(4) AND NOT &IECSEQ(6)).SEQOK
&NOCODE  SETB  1
         MNOTE 12,'MACRO SEQUENCE ERROR'
.SEQOK   ANOP
         AIF   (&IECGDSW).SEQOK1
&NOCODE  SETB  1
         MNOTE 12,'GDULIST MACRO CAN NOT BE CODED UNLESS GDU=YES IN    X
               CONFIGUR MACRO'
.SEQOK1  ANOP
         AIF   (T'&PARAMNO NE 'O').PAR1
&NOCODE  SETB  1
         MNOTE 12,'PARAMNO OPERAND MUST BE CODED'
         AGO   .NOR1
.PAR1    ANOP
         AIF   (T'&PARAMNO EQ 'N').PAR2
&NOCODE  SETB  1
         MNOTE 12,'PARAMNO OPERAND HAS INVALID TYPE ATTRIBUTE'
         AGO   .NOR1
.PAR2    ANOP
         AIF   (&PARAMNO GE 1 AND &PARAMNO LE 127).PAR3
&NOCODE  SETB  1
         MNOTE 12,'PARAMNO OPERAND NOT IN VALID RANGE OF 1 TO 127'
         AGO   .NOR1
.PAR3    ANOP
         AIF   (&PARAMNO LE &IECPARN).NOR1
&NOCODE  SETB  1
         MNOTE 12,'PARAMNO OPERAND CAN NOT BE GREATER THAN HIGHEST     X
               PLN OPERAND IN PARAMNUM MACRO'
.NOR1    ANOP
&C       SETA  N'&NORGUID
         AIF   (T'&NORGUID EQ 'O' AND &C EQ 0).DIS1
         AIF   (&C LE 16).NOR2
&NOCODE  SETB  1
         MNOTE 12,'NUMBER OF SUBOPERANDS IN NORGUID OPERAND INVALID'
.NOR2    ANOP
&B       SETA  1
.NOR3    ANOP
         AIF   (T'&NORGUID(&B) EQ 'N').NOR4
&NOCODE  SETB  1
         MNOTE 12,'NORGUID SUBOPERAND INVALID'
         AGO   .DIS1
.NOR4    ANOP
         AIF   (&NORGUID(&B) GE 1 AND &NORGUID(&B) LE 16).NOR5
&NOCODE  SETB  1
         MNOTE 12,'NORGUID SUBOPERAND NOT IN VALID RANGE OF 1 TO 16'
         AGO   .NOR21
.NOR5    ANOP
&A       SETA  &NORGUID(&B)
&NORSW(&A) SETB 1
.NOR21   ANOP
         AIF   (&B EQ &C).DIS1   IF LAST SUBOPERAND,BRANCH
&B       SETA  &B+1
         AGO   .NOR3
.DIS1    ANOP
         AIF   (T'&DISPMSG EQ 'O').IDEN1
         AIF   (T'&DISPMSG EQ 'M').DIS2
&NOCODE  SETB  1
         MNOTE 12,'D1SPMSG OPERAND HAS INVALID TYPE ATTRIBUTE'
.DIS2    ANOP
         AIF   (T'&DISPMSG NE 'O' AND T'&IDENT NE 'O').DIS3
         AGO   .IDEN1
.DIS3    ANOP
&NOCODE  SETB  1
         MNOTE 12,'D1SPMSG AND IDENT OPERANDS CAN NOT BOTH BE CODED'
.IDEN1   ANOP
         AIF   (T'&IDENT EQ 'O').ENT1
         AIF   (T'&IDENT EQ 'N').IDEN2
&NOCODE  SETB  1
         MNOTE 12,'IDENT OPERAND HAS INVALID TYPE ATTRIBUTE'
         AGO   .ENT1
.IDEN2   ANOP
         AIF   (&IDENT GE 0 AND &IDENT LE 99).IDEN3
&NOCODE  SETB  1
         MNOTE 12,'IDENT OPERAND NOT IN VALID RANGE OF 0 TO  99'
         AGO   .ENT1
.IDEN3   ANOP
&B       SETA  &IDENT+1
         AIF   (&IDENT LE &IECGID).ENT1
&NOCODE  SETB  1
         MNOTE 12,'IDENT OPERAND SPECIFIED IS GREATER THAN NUMBER OF   X
               IDENTIFIERS SPECIFIED BY USER'
.ENT1    ANOP
         AIF   ('&ENTRY' EQ '1' OR '&ENTRY' EQ 'M').ENT2
         MNOTE 3,'ENTRY OPERAND INVALID,DEFAULT ASSUMED'
.ENT2    ANOP
&D       SETB  ('&ENTRY' EQ 'M')
         AIF   ('&ENTRY' EQ 'M' AND &IECGDL OR '&ENTRY' EQ '1').MSG1
&D       SETB  0
         MNOTE 3,'ENTRY=M ILLEGAL ON FIRST GDULIST MACRO,DEFAULT ASSUMEX
               D'
.MSG1    ANOP
         AIF   (T'&MSG EQ 'O').CKGEN
         AIF   (T'&MSG NE 'O' AND &IECTXT).MSG2
&E       SETB  1
         AGO   .CKGEN
.MSG2    AIF   ('&MSG'(1,1) EQ '''' AND '&MSG'(K'&MSG,1) EQ '''').MSG3
         MNOT3 3,'MSG OPERAND IN INCORRECT FORMAT,OPERAND IGNORED'
&E       SETB  1
         AGO   .CKGEN
.MSG3    ANOP
&B       SETA  K'&MSG-2
         AIF   (&B GE 0 AND &B LE 127).CKGEN
         MNOTE 3,'MESSAGE LENGTH EXCEEDS 127,OPERAND IGNORED'
&E       SETB  1
.CKGEN   ANOP
         AIF   (NOT &IECSW(8)).CKGEN1
&NOCODE  SETB  1
         MNOTE 12,'GDULIST MACRO NOT VALID FOLLOWING IMPLICIT TEXT IN  X
               THIS TRANSACTION'
.CKGEN1  ANOP
         AIF   (NOT &NOCODE).GENCODE
         MNOTE 12,'TEXT GENERATION SUPPRESSED'
         AGO   .MEND
.GENCODE ANOP
         DC    YL1(X'80'*&D+&PARAMNO) PARAM LIST NUMBER + REPEAT REC
         DC      B'&NORSW(1)&NORSW(2)&NORSW(3)&NORSW(4)&NORSW(5)&NORSW(X
               6)&NORSW(7)&NORSW(8)'  FIRST BYTE OF NORMAL GUIDANCE
         DC    B'&NORSW(9)&NORSW(10)&NORSW(11)&NORSW(12)&NORSW(13)&NORSX
               W(14)&NORSW(15)&NORSW(16)' SECOND BYTE OF NORMAL GUID
         AIF   (T'&DISPMSG EQ 'O').GEN1
&CHAR    SETC  '&DISPMSG'
         DC    AL2(&CHAR-IEC15TAB)
         AGO   .MSG5
.GEN1    AIF   (T'&IDENT EQ 'O').GEN2
         DC    YL2((IECIDENT-IEC15TAB)+14*&IDENT)
         AGO   .MSG5
.GEN2    DC    YL2(0)
.MSG5    ANOP
&B       SETA  0
         AIF   (T'&MSG EQ 'O' OR &E).SET
&IECSW(8) SETB 1
         DC    YL1(0)    END OF LIST BYTE
&B       SETA  K'&MSG-2
         DC    YL1(&B)   LENGTH OF MSG TEXT
         DC    C&MSG
&B       SETA  &B+1
&IECTXT  SETB  0  RESET TO PREVENT GDULIST EXPANSION FOLLOWING MSG TEXT
.SET     ANOP
&IECGDL  SETB  1
&IECNDGP(1) SETA &IECNDGP(1)+5+&B
.MEND    MEND

