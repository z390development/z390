.* MVS 3.8J PUBLIC DOMAIN
* /* MACCOMP Y-2 SC1D0/SJD48                                         */
* /* MACSTAT Y-2 73078/021160                                        */
         MACRO
&NAME    READ  &DECB,&TYPE,&DCB,&ADDR,&LENGTH,&KEY,&RCDREF,&NEXT,&MF=I,,
               &HDRADR=,&LNNUM=,&LNNUMA=,&LFMTNO=,&LFMTNOA=,    XL03127*
               &RPL=,&ACB=,&AREA=,&AREALEN=,&RECLEN=,&AAREA=,    Y02072*
               &AAREALN=,&ARECLEN=,&ECB=,&EXIT=,               @ZM41418*
               &OPTCD=,&ARG=,&BRANCH=                          @ZM41418
         GBLB  &IHBWR,&IHBT(2),&IHBS(4)
         LCLB  &ERR(2)
         AIF   ('&RPL' NE '').VTAM      ISSUE VTAM READ          Y02072
&ERR(1)  SETB  ('&DECB' EQ '')
&ERR(2)  SETB  ('&TYPE' EQ '')
         AIF   (&ERR(2)).E0
&IHBS(1) SETB  (K'&TYPE EQ 1)
&IHBS(2) SETB  (K'&TYPE EQ 2 OR '&TYPE' EQ 'SBM')
&IHBS(3) SETB  (K'&TYPE EQ 3 AND NOT('&TYPE' EQ 'SBM'))
&IHBS(4) SETB  (K'&TYPE EQ 4)
&IHBWR   SETB  0
&IHBT(1) SETB  (T'&LENGTH EQ 'N')
&IHBT(2) SETB  (T'&RCDREF EQ 'N')
         AIF   ('&TYPE'(1,1) EQ 'S' OR '&TYPE' EQ 'RBL').BSAMR  XL03127
         AIF   ('&TYPE'(1,1) EQ 'K').BISAMR
         AIF   ('&TYPE'(1,1) EQ 'D').BDAMR
         AIF   ('&TYPE'(1,1) EQ 'T').BTAMR
         IHBERMAC 63,,&TYPE             TYPE INVALID
         AGO   .E0
.BSAMR   AIF   (&ERR(1)).E0
&NAME    IHBRDWRS &DECB,&TYPE,&DCB,&ADDR,&LENGTH,&KEY,&RCDREF,         ,
               &HDRADR,&LNNUM,&LNNUMA,&LFMTNO,&LFMTNOA,MF=&MF   XL03127
         MEXIT
.BISAMR  AIF   (&ERR(1)).E0
&NAME    IHBRDWRK &DECB,&TYPE,&DCB,&ADDR,&LENGTH,&KEY,&RCDREF,MF=&MF
         MEXIT
.BTAMR   AIF   (&ERR(1)).E0
&NAME    IHBRDWRT &DECB,&TYPE,&DCB,&ADDR,&LENGTH,&KEY,&RCDREF,MF=&MF
         MEXIT
.BDAMR   AIF   (&ERR(1)).E0
&NAME    IHBRDWRD &DECB,&TYPE,&DCB,&ADDR,&LENGTH,&KEY,&RCDREF,&NEXT,MF=X
               &MF
         MEXIT
.VTAM    ANOP
&IHBWR   SETB  0              SET FOR ALL READS                  Y02072
&NAME    IHBRDWRA ACB=&ACB,AREA=&AREA,AREALEN=&AREALEN,          Y02072*
               RECLEN=&RECLEN,AAREA=&AAREA,AAREALN=&AAREALN,     Y02072*
               ARECLEN=&ARECLEN,ECB=&ECB,EXIT=&EXIT,ARG=&ARG,    Y02072*
               OPTCD=&OPTCD,                                   @ZM41418*
               IHBRW=X'1D',RPL=&RPL,BRANCH=&BRANCH              ZM30024
         MEXIT                                                   Y02072
.E0      AIF   (NOT &ERR(1)).E1
         IHBERMAC 07                    &DECB MISSING
.E1      AIF   (NOT &ERR(2)).MEND
         IHBERMAC 25                    &TYPE MISSING
.MEND    MEND
