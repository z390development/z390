.* MVS 3.8J PUBLIC DOMAIN
         MACRO
&NAME    OIL   &BYTE,&MASK,&REF=,&WREGS=(0,1,2)
.*             OR IMMEDIATE LOCKED
.*             04/24/72, LEVEL=1
.*
.*       THIS MACRO IS USED IN A MULTIPROCESSING ENVIRONMENT TO
.*       PROVIDE A LOCK ON A WORD CONTAINING THE BYTE WHICH IS THE
.*       TARGET OF AN OI INSTRUCTION.
.*
.*       &BYTE      SYMBOL NAMING THE BYTE TO WHICH THE OR IMMEDIATE
.*                  FUNCTION IS TO BE APPLIED.
.*       &MASK      A ONE-BYTE SELF-DEFINING TERM OR SYMBOL EQUATED
.*                  TO SUCH A TERM WHICH SPECIFIES A MASK AS IN
.*                  IMMEDIATE OPERAND OF OI INSTRUCTION.
.*       &REF       LABEL OF A FULL WORD IN STORAGE.  ADDRESS OF REF
.*                  LABEL MUST BE LESS THAN OR EQUAL TO THAT OF BYTE
.*                  (FIRST OPERAND).  LABEL MUST BE ADDRESSABLE WHEN
.*                  THE MACRO IS ISSUED, AND USING MUST HAVE BEEN
.*                  ISSUED ON A FULL-WORD LABEL.
.* NOTE  &BYTE, &MASK AND &REF MUST BE SPECIFIED ON MACRO CALL.
.*       &WREGS     DECIMAL NUMBERS OR SYMBOLS EQUATED TO DECIMAL
.*                  NUMBERS OF THREE WORK REGISTERS TO BE USED BY THIS
.*                  MACRO.  REGISTERS 0, 1 AND 2 ARE DEFAULT VALUES.
         LCLC  &XREGS(3)      FOR WREGS VALUES
         AIF   ('&BYTE' NE '').OIL10    BRANCH IF BYTE SPECIFIED
         IHBERMAC 1006,BYTE   BYTE REQUIRED BUT NOT SPECIFIED
         MEXIT
.OIL10   AIF   ('&MASK' NE '').OIL20    BRANCH IF MASK SPECIFIED
         IHBERMAC 1006,MASK   MASK REQUIRED BUT NOT SPECIFIED
         MEXIT
.OIL20   AIF   ('&REF' NE '').OIL30     BRANCH IF REF SPECIFIED
         IHBERMAC 1006,REF    REF REQUIRED BUT NOT SPECIFIED
         MEXIT
.OIL30   AIF   ('&WREGS(1)' NE '').OIL35 BRANCH IF FIRST REG SPECIFIED
&XREGS(1) SETC '0'            USE REGISTER 0 AS DEFAULT
         AGO   .OIL40
.OIL35   ANOP
&XREGS(1) SETC '&WREGS(1)'
.OIL40   AIF   ('&WREGS(2)' NE '').OIL45 BRANCH IF SECOND REG SPECIFIED
&XREGS(2) SETC '1'            USE REGISTER 1 AS DEFAULT
         AGO   .OIL50
.OIL45   ANOP
&XREGS(2) SETC '&WREGS(2)'
.OIL50   AIF   ('&WREGS(3)' NE '').OIL55 BRANCH IF THIRD REG SPECIFIED
&XREGS(3) SETC '2'            USE REGISTER 2 AS DEFAULT
         AGO   .OIL60
.OIL55   ANOP
&XREGS(3) SETC '&WREGS(3)'
.OIL60   ANOP  ,              ALL OPERANDS SPECIFIED
&NAME    L     &XREGS(1),&REF+(((&BYTE-&REF)/4)*4)
*                             LOAD WORD CONTAINING BYTE TO BE SET
         LA    &XREGS(3),&MASK.(0,0) PUT MASK IN LOW-ORDER BYTE OF REG
*                                                                YM1995
         SLL   &XREGS(3),24-(8*((&BYTE-&REF)-(((&BYTE-&REF)/4)*4)))(0)
*                             SHIFT MASK TO SAME POSITION IN WORD AS
*                             BYTE TO BE CHANGED                 YM1995
         LR    &XREGS(2),&XREGS(1) LOAD WORD CONTAINING BYTE TO BE SET
         OR    &XREGS(2),&XREGS(3) OR MASK INTO BYTE IN REGISTER
*                             USE COMPARE AND SWAP TO STORE UPDATED
*                             REGISTER IN STORAGE IF STORAGE WORD WAS
*                             NOT MODIFIED
         CS    &XREGS(1),&XREGS(2),&REF+(((&BYTE-&REF)/4)*4)
         BNE   *-8            DO AGAIN FROM LR IF WORD WAS MODIFIED
         MEND
