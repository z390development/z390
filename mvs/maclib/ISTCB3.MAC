.* MVS 3.8J PUBLIC DOMAIN
         MACRO
         ISTCB3 &ARG=,&ARGNAME=,&MF=,&KWTC=,&CALLER=,&TYPE=,   @ZA34940*
               &CHKAM=,&AM=,&BIT=NO,&INDRECT=NO                @ZA34940
.*                                                             @ZA34940
.*       CHANGE LEVEL = 0                                      @ZA34940
.*       DATE OF LAST CHANGE =  15  DEC  78  ( 78*348 )        @ZA34940
.*
.* 1. FUNCTION - THIS MACRO IS CALLED TO PROCESS VARIOUS       @ZA34940
.*               KEYWORDS SPECIFIED ON AN ACB OR RPL           @ZA34940
.*               MACRO. GLOBAL VARIABLES ARE SET TO LATER      @ZA34940
.*               BE USED TO BUILD THE SPECIFIED CONTROL BLOCK. @ZA34940
.*
.* 2. CALLERS - ISTCBACB, ISTCBRPL                             @ZA34940
.*
.* CHANGE ACTIVITY                                             @ZA34940
.*      CBMM SPLIT SUPPORT                                     @ZA34940
.*                                                             @ZA34940
.*
.* GLOBALS
         GBLA  &IDAENO                  NUMBER OF ELEMENTS IN ARRAY
         GBLA  &IDAEID(50)              ELEMENT KEYWORD TYPE CODE
         GBLA  &IDAETYP(50)             ELEMENT TYPE ARRAY
         GBLC  &IDAVALC(50)             ELEMENT CHARACTER VALUE ARRAY
         GBLC  &IDAVALX(50)             ELEMENT ARITHMETIC VALUE ARRAY
         GBLA  &IDACB11                 VALUE TYPE SET BY      @ZA34940
.*                                      ISTCB1                 @ZA34940
         GBLB  &IDACB01                 ERROR SWITCH
         GBLC  &IDACB12                 EXIT LIST CODES SET BY @ZA34940
.*                                      ISTCB1                 @ZA34940
         GBLC  &IDACB13                 OPERAND VALUE SET BY   @ZA34940
.*                                      ISTCB1                 @ZA34940
.* LOCALS
         LCLA  &VN2                     BIT OPERAND TYPE CODE
         LCLA  &VN3                     CHARACTER STRING OPERAND CODE
         LCLA  &AN3                     INDIRECT ADDRESS OPERAND CODE
.* INITIALIZATION
&VN2     SETA  2                        INITIALIZE BIT OPERAND TYPE
.*                                      CODE                    X04SVHS
&VN3     SETA  3                        INITIALIZE CHARACTER STRING
.*                                      OPERAND CODE            X04SVHS
&AN3     SETA  6                        INITIALIZE INDIRECT ADDRESS
.*                                      OPERAND CODE            X04SVHS
         AIF   ('&ARG' EQ '').IDA5000   IF KEYWORD NOT SPECIFIED
.*                                      EXIT FROM MACRO         X04SVHS
&IDAENO  SETA  &IDAENO+1                BUMP INDEX INTO ARRAYS  X04SVHS
&IDAEID(&IDAENO) SETA &KWTC             SET ELEMENT TYPE CODE   X04SVHS
         AIF   ('&CHKAM' EQ '').IDA1000 DETERMINE IF AM SHOULD BE
.*                                      CHECKED                 X04SVHS
         AIF   ('&CHKAM' EQ '&AM').IDA1000                      X04SVHS
.*                                      PROCESS KEYWORD IF AM IS
.*                                      CORRECT
         ISTERMAC 24,&ARG,&CHKAM        ISSUE ERROR MESSAGE    @ZA34940
&IDACB01 SETB  1                        SET ERROR SWITCH        X04SVHS
         MEXIT                          EXIT FROM MACRO         X04SVHS
.IDA1000 ANOP                                                   X04SVHS
.* PROCESS KEYWORD
         AIF   ('&INDRECT' NE 'YES').IDA3000                    X04SVHS
.*                                      CHECK IF INDIRECT ADDR IS
.*                                      VALID FOR THIS KEYWORD
         AIF   ('&ARG(1)' NE '*').IDA2000 CHECK IF INDIRECT     X04SVHS
.*                                      ADDRESS WAS SPECIFIED
         AIF   ('&MF' NE 'L').IDA2500   INDIRECT ADDRESS NOT VALID
.*                                      FOR LIST FORM OF MACRO  X04SVHS
         ISTERMAC 6,&ARGNAME,&ARG       ISSUE ERROR MESSAGE    @ZA34940
&IDACB01 SETB  1                        SET ERROR SWITCH        X04SVHS
         MEXIT                          EXIT FROM MACRO         X04SVHS
.IDA2000 ANOP                                                   X04SVHS
.*             AT THIS POINT, INDRECT=YES WAS SPECIFIED, BUT THE USER
.*             DID NOT SPECIFY INDIRECT ADDRESS
         AIF   ('&BIT' EQ 'YES').IDA9000 IF BIT KEYWORD ONLY OPERAND
.*                                      TYPE MUST BE SET        X04SVHS
&IDAVALC(&IDAENO) SETC '&ARG'           SET OPERAND VALUE FOR NON-BIT
.*                                      KEYWORDS                X04SVHS
&IDAETYP(&IDAENO) SETA &VN3             SET OPERAND TYPE (CHAR STRING)
.*                                                              X04SVHS
         MEXIT                                                  X04SVHS
.IDA2500 ANOP                                                   X04SVHS
.*             AT THIS POINT INDRECT=YES WAS SPECIFIED AND THE
.*             USER SPECIFIED AN INDIRECT ADDRESS.
&IDAVALC(&IDAENO) SETC '&ARG(2)'        SET VALUE FOR INDIRECT ADDRESS
.*                                                              X04SVHS
&IDAETYP(&IDAENO) SETA &AN3             SET OPERAND TYPE (IND ADDR)
.*                                                              X04SVHS
         MEXIT                                                  X04SVHS
.IDA3000 ANOP                                                   X04SVHS
.*             AT THIS POINT INDRECT=NO
         AIF   ('&BIT' EQ 'YES').IDA9000                        X04SVHS
.*                                      CHECK FOR BIT STRING KEYWORD
.*                                      IF YES, ONLY OPERAND TYPE
.*                                      MUST BE SET.
         ISTCB1 ARG=&ARG,ARGNAME=&ARGNAME,MF=&MF,              @ZA34940X
               CALLER=&CALLER,TYPE=&TYPE                       @ZA34940
.*             THE FOLLOWING VALUES (&IDACB11, &IDACB12, &IDACB13) ARE
.*             SET IN ISTCB1                                   @ZA34940
&IDAETYP(&IDAENO) SETA &IDACB11         SET ELEMENT TYPE        X04SVHS
&IDAVALC(&IDAENO) SETC '&IDACB13'       SET CHARACTER VALUE OF ELEMENT
.*                                                              X04SVHS
         AIF   ('&TYPE' NE 'X').IDA5000 IF TYPE NOT X, NO       X04SVHS
.*                                      EXLST VALUE TO BE SET
&IDAVALX(&IDAENO) SETC '&IDACB12'       SET EXLST VALUE         X04SVHS
.IDA5000 MEXIT                                                  X04SVHS
.IDA9000 ANOP                                                   X04SVHS
&IDAETYP(&IDAENO) SETA &VN2             SET BIT OPERAND TYPE CODE
.*                                      VALUE OPERAND WILL BE SET
.*                                      BY CALLER FOR BIT OPERANDS
         MEND                                                   X04SVHS

