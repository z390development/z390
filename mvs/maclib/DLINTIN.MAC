.* MVS 3.8J PUBLIC DOMAIN
         MACRO
         DLINTIN &N,&FLD,&EDIT
         GBLA  &IHBFNT,&IHBNSC(51),&IHBEND,&IHBI3,&IHBFLN
         GBLA  &IHBSIZ,&IHBFEA(14),&IHBDLH,&IHBFED
         GBLB  &IHBIMG,&IHBFLD,&IHBDEL,&IHBPDL,&IHBER1
         GBLB  &IHBFT(6)
         LCLA  &FLDLTH,&I5,&I6,&FONT,&BLEDIT,&HOLD,&I4,&I2
         LCLA  &FETEN,&FEDIG,&FEEND
         LCLB  &EDATCK,&F1,&F2,&F3,&F4,&F5,&F6,&F7,&F8,&F9,&F10,&CHEDIT
         LCLB  &B1,&B2,&B3,&B4,&B5,&B6,&B10
         LCLC  &CHAR
         ACTR  9999
.* STATEMENTS FROM HERE TO .DL490 BUILD TABLE OF FIELD ENDS
         AIF   (&N GT 0).DL490         TABLE TO BE BUILT,NO BRANCH
&IHBDLH  SETA  0                       INIT CTR OF CHAR DELIM FIELDS
&I4      SETA  2                       INIT INDEX THRU FLDN KEYWORDS
&FEEND   SETA  &IHBEND                 INIT FIELD END INDICATOR
.DL470   AIF   (T'&SYSLIST(&I4,1) EQ 'O').DL480 FIELD END OMITTED
         AIF   (T'&SYSLIST(&I4,1) NE 'N').DL480 FIELD END DELIM,YES BR
         AIF   (&SYSLIST(&I4,1) LE &FEEND OR &SYSLIST(&I4,1) GT 88).DL4X
               80                      FIELD END WITHIN LIMITS
&IHBFEA(&I4-1) SETA &SYSLIST(&I4,1)    MAKE TABLE ENTRY FOR FLD END
&FEEND   SETA  &SYSLIST(&I4,1)         CHANGE FIELD END INDICATION
.DL480   ANOP
&I4      SETA  &I4+1                   INCREMENT INDEX
         AIF   (&I4 LE 15).DL470       ALL FIELD ENDS CHECKED,NO CONT
         AGO   .END
.* STATEMENTS .DL490 TO .DL500 CHECK PROPER SPEC OF FLDN AND EDIT KYWDS
.DL490   ANOP
&IHBPDL  SETB  (&IHBDEL)               IND IF PREV FIELD CHAR DELIMTD
         AIF   ('&FLD' EQ '' AND '&EDIT' EQ '').END IF NO SPFCTN EXIT
         AIF   ('&FLD' NE '').DL500    IF JUST EDITN THEN ERR
         MNOTE 8,'***  IHB379  FLD PARAMETER MUST BE SPECIFIED WITH EDIX
               T PARAMETER. EDIT&N IGNORED'
         AGO   .END                     EXIT MACRO
.* STATEMENTS .DL500 TO .DL550 CHECK FOR FIELD END INDICATION
.DL500   AIF   (T'&FLD(1) NE 'O').DL520 IF FLD END OMITTED THEN ERROR
.DL510   MNOTE 8,'***  IHB380  FIELD END FOR FLD&N INVALID. FIELD &N NOX
               T GENERATED'
         AGO   .END                     EXIT MACRO
.DL520   AIF   (T'&FLD(1) NE 'N').DL530 FIELD END CHAR DELIMTD,YES BRCH
         AIF   (&FLD(1) LE &IHBEND OR &FLD(1) GT 88).DL510 VALID ADDRSS
&HOLD    SETA  &FLD(1)-4                *
&FETEN   SETA  &HOLD/10                 * SETUP FLD END ADDR
&FEDIG   SETA  &HOLD-(10*&FETEN)        *
IHBE&SYSNDX EQU &FETEN*16+&FEDIG
&IHBDEL  SETB  0                        IND FLD NOT CHAR DELINTD
         AGO   .DL550                   GOTO CHECK FIELD LENGTH
.DL530   AIF   (K'&FLD(1) NE 1).DL540   IF ONLY 1 CHAR SPCFD THEN
IHBE&SYSNDX EQU C'&FLD(1)'
&IHBDEL  SETB  1                        IND FLD CHAR DELIMETED
         AGO   .DL550                   GOTO CHECK FIELD  LENGTH
.DL540   AIF   (K'&FLD(1) NE 4).DL545   IF 4 CHAR FALLTHRU
         AIF   ('&FLD(1)'(2,2) NE '''''' AND '&FLD(1)'(2,2) NE'&&').DL5X
               10                       CHAR SPECIAL CASES
&CHAR    SETC  '&FLD(1)'(2,2)           YES SETUP FOR DC GENERATION
         AGO   .DL547                   GOTO SETUP EQU FOR DC GENERATN
.DL545   AIF   (K'&FLD(1) NE 3).DL510   CHAR DELIM SPCFD AS 3 CHAR
         AIF   ('&FLD(1)'(1,1) NE '''' OR '&FLD(1)'(3,1) NE '''').DL510
&CHAR    SETC  '&FLD(1)'(2,1)           SETUP FOR DC GENERATION
.DL547   ANOP
IHBE&SYSNDX EQU C'&CHAR'
&IHBDEL  SETB  1                        INDICATE FIELD CHAR DELIMETED
.* STATEMENTS .DL550 THRU .DL580 CHECK FIELD LENGTH SUBOPERAND
.DL550   AIF   (T'&FLD(2) EQ 'O' AND &IHBIMG EQ 1).DL580 OMTD WITH IMAG
         AIF   (T'&FLD(2) NE 'O' AND &IHBIMG EQ 0).DL570 SPEC WITH EDIT
         AIF   (&IHBIMG EQ 0).DL560     IMAGE=YES WITH FIELD LENGTH,ERR
         MNOTE 8,'***  IHB381  FIELD LENGTH FOR FLD&N INVALID WITH IMAGX
               E MODE. IGNORED'
         AGO   .DL580                   SKIP FLD LENGTH CHECK
.DL560   MNOTE 8,'***  IHB382  FIELD LENGTH FOR FLD&N INVALID. FIELD &NX
                NOT GENERATED'
&IHBDEL  SETB  (&IHBPDL)               RESTORE CHAR DELIM INDICATION
         AGO   .END                    EXIT MACRO
.DL570   AIF   (T'&FLD(2) NE 'N').DL560 FIELD LENGTH NUMERIC,NO ERROR
         AIF   (T'&FLD(2) EQ 'N' AND &FLD(2) EQ 0).DL560 FLD LNGTH=0,ER
         AIF   (&FLD(2) GT 127).DL560   FLD LENGTH GREATER THAN 127,ERR
&IHBFLN  SETA  &IHBFLN+&FLD(2)          ADD TO TOTAL OF FIELD LENGTHS
&FLDLTH  SETA  &FLD(2)                  SETUP FOR DC GENERATION
         AIF   (&IHBFLN LE 130).DL590   TOTAL FIELD LENGTHS GT 130,ERR
         MNOTE 8,'***  IHB383  TOTAL OF FIELD LENGTHS GT 130. FIELD &N X
               NOT GENERATED'
&IHBFLN  SETA  &IHBFLN-&FLD(2)          RESTORE FIELD LENGTH TOTAL
&IHBDEL  SETB  (&IHBPDL)               RESTORE CHAR DELIM FLD INDICATOR
         AGO   .END                    EXIT MACRO
.DL580   ANOP
&FLDLTH  SETA  0                        IND FLD LENGTH OF 0
.* STATEMENTS .DL590 THRU .DL630 GENERATE NOSCAN FIELDS
.DL590   AIF   (&IHBPDL EQ 1).DL620     PREV FLD CHAR DELIM,SKP NSCN GN
         AIF   (&IHBDEL EQ 0).DL594     IF FLD CHAR DELIMTD SKIP DOWN
&I2      SETA  1                        INIT INDEX IN FLD END TABLE
.DL592   AIF   (&IHBFEA(&I2) GT &IHBEND).DL596  *
&I2      SETA  &I2+1                            *SERCH FOR FLD END IN
         AIF   (&I2 LE 14).DL592                * FIELD END TABLE
&IHBFED  SETA  89                      NOT FOUND, ASSUME FE ADDR OF 89
         AGO   .DL620                  SKIP NOSCAN GENERATION
.DL594   ANOP
&IHBFED  SETA  &FLD(1)                 SET FLD ADDR TO THIS FLD S FLD
         AGO   .DL598                    END.  GOTO GEN NOSCAN FIELDS
.DL596   ANOP
&IHBFED  SETA  &IHBFEA(&I2)            SET FLD ADDR TO THE NEXT FLDSEND
.DL598   AIF   (&IHBI3 EQ 0).DL620     ANY NOSCANS TO GENERATE,YES GEN
&I5      SETA  1                        INIT INDEX TO NOSCAN TABLE
.DL600   AIF   (&IHBNSC(&I5) LE &IHBEND).DL615 * NOSCAN FIELD TO BE
         AIF   (&IHBNSC(&I5) GE &IHBFED).DL620 *  HERE
&IHBSIZ  SETA  &IHBSIZ+4                INCR FORM RECD SIZE
         AIF   (&IHBSIZ GT 212).DL605   MAX SIZE RCD ALREADY GEND
&HOLD    SETA  &IHBNSC(&I5)-4           *
&FETEN   SETA  &HOLD/10                 * SETUP NOSCAN FLD END IN BCD
&FEDIG   SETA  &HOLD-(10*&FETEN)        *
         DC    AL1(&FETEN*16+&FEDIG)    *** NOSCAN FIELD ***
         DC    AL1(128)
         DC    AL1(0)
         DC    AL1(0)
         AGO   .DL610
.DL605   ANOP
&IHBER1  SETB  1                        IND MAX FR GENERATED ALREADY
.DL610   ANOP
&IHBEND  SETA  &IHBNSC(&I5)             CHANGE LINE BEGIN INDICATION
&IHBNSC(&I5) SETA 0                     SET NOSCAN TAB ENTRY TO 0
.DL615   AIF   (&I5 EQ &IHBI3).DL620    TABLE ALL SEARCHED,NO CONT SRCH
&I5      SETA  &I5+1                    INCR INDEX TO NEXT TAB ENTRY
         AGO   .DL600                  CONTINUE TABLE SEARCH
.DL620   AIF   (&IHBDEL EQ 1).DL630    * IF THIS FIELD DELIMETED BY
&IHBEND  SETA  &FLD(1)                 * ADDRESS BUMP LINE BEGIN
.*STATEMENTS .DL630 THRU .DL680 CHECK FONT AND NCRIT SUBOPERANDS -FLDN
.DL630   ANOP
&I6      SETA  3                       INIT INDEX TO FLDN KEYWORD
&EDATCK  SETB  1                       SETUP DEFAULT FOR NCRIT
         AIF   (T'&FLD(3) EQ 'O').DL650 3RD SUBOPER OF FLDN OMITTED
         AIF   ('&FLD(3)' NE 'NCRIT').DL640 3RD SUBOPER EQ NCRIT
&EDATCK  SETB  0                       SETUP FOR NCRIT
&I6      SETA  4                       SET INDEX=4 OF FLDN KEYWORD
         AIF   (T'&FLD(4) EQ 'O').DL650 4TH SUBOPER-FLDN OMITTED
.DL640   ANOP
&F1      SETB  ('&FLD(&I6)' EQ 'NUMB')
&F2      SETB  ('&FLD(&I6)' EQ 'ANB1')
&F3      SETB  ('&FLD(&I6)' EQ 'NUMA')
&F4      SETB  ('&FLD(&I6)' EQ 'ANA1')
&F5      SETB  ('&FLD(&I6)' EQ 'ANA2')
&F6      SETB  ('&FLD(&I6)' EQ 'NHP1')
&F7      SETB  ('&FLD(&I6)' EQ 'GOTH')
&F8      SETB  ('&FLD(&I6)' EQ 'NHP2')
&F9      SETB  ('&FLD(&I6)' EQ 'MRKA')
&F10     SETB  ('&FLD(&I6)' EQ 'MRKB')
         AIF   (&F1 OR &F2 OR &F3 OR &F4 OR &F5 OR &F6 OR &F7 OR &F8   X
               OR &F9 OR &F10).DL660 VALID FONT SPECIFICATION,NO ERR
         MNOTE 8,'***  IHB384  FONT CODE FOR FLD&N INVALID. FONT ON DFRX
                ASSUMED'
.DL650   ANOP
&FONT    SETA  &IHBFNT                  USE FONT FROM DFR
         AGO   .DL678             SKIP FONT SETTING
.DL660   ANOP
&FONT    SETA  &F1*5+&F2*6+&F3*9+&F4*10+&F5*11+&F6*12+&F7*13+&F8*14+&F9X
               +&F10*2                 SETUP FONT
.DL678   ANOP      SETUP FOR FONT EQU NEEDED BY DFR GENERATION
&IHBFT(1) SETB ((&FONT EQ 9) OR &IHBFT(1))
&IHBFT(2) SETB ((&FONT EQ 10) OR (&FONT EQ 11) OR &IHBFT(2))
&IHBFT(3) SETB ((&FONT EQ 5) OR (&FONT EQ 2) OR &IHBFT(3))
&IHBFT(4) SETB ((&FONT EQ 6) OR &IHBFT(4))
&IHBFT(5) SETB ((&FONT EQ 12) OR (&FONT EQ 13) OR (&FONT EQ 14) OR     X
               &IHBFT(5))
&IHBFT(6) SETB ((&FONT EQ 1) OR &IHBFT(6))
         AIF   (N'&FLD LE &I6).DL680 TOO MANY SUBOPERS ON FLDN,YES ERR
         MNOTE 4,'***  IHB386  TOO MANY OPERANDS ON FLD&N -EXCESS OPERAX
               NDS IGNORED'
.DL680   ANOP
.* STATEMENTS .DL680 TO .DL820 CHECK EDITN KEYWORD
&CHEDIT  SETB  0                       INIT DEFAULT OF NO EDCHAR
&BLEDIT  SETA  4                       INIT DEFAULT OF NOBLOF
         AIF   (&IHBIMG EQ 0).DL700    EDIT=YES SKIP TO CHK EDITN
         AIF   ('&EDIT' EQ '').DL820   EDITN SPECIFIED WITH IMAGE,ERR
         MNOTE 8,'***  IHB387  EDIT&N PARAMETER INVALID FOR IMAGE MODE.X
                EDIT&N IGNORED'
         AGO   .DL820                  SKIP EDITN CHECK
.DL700   AIF   (T'&EDIT(1) EQ 'O').DL750 EDITN OMITTED,ASSUME DEFAULTS
&BLEDIT  SETA  2                              SETUP FOR ALBLOF
         AIF   ('&EDIT(1)' EQ 'NOBLOF').DL750 NOBLOF SPECIFIED,YES BRCH
&BLEDIT  SETA  0                              SETUP FOR HLBLOF
         AIF   ('&EDIT(1)' EQ 'HLBLOF').DL750 HLBLOF SPECIFIED,YES BRCH
&BLEDIT  SETA  4                              SETUP FOR ALBLOF
         AIF   ('&EDIT(1)' EQ 'ALBLOF').DL750 ALBLOF SPECIFIED,YES BRCH
&BLEDIT  SETA  1                              SETUP FOR HLBHIF
         AIF   ('&EDIT(1)' EQ 'HLBHIF').DL750 HLBHIF SPECIFIED,YES BRCH
&BLEDIT  SETA  5                              SETUP FOR ALBHIF
         AIF   ('&EDIT(1)' EQ 'ALBHIF').DL750 ALBHIF SPECIFIED,YES BRCH
&BLEDIT  SETA  3                              SETUP FOR ALBNOF
         AIF   ('&EDIT(1)' EQ 'ALBNOF').DL750 ALBNOF SPECIFIED,YES BRCH
&BLEDIT  SETA  4                              ASSUME ALBLOF
         MNOTE 8,'***  IHB388  INVALID SPECIFICATION ON EDIT&N -ALBLOF X
               ASSUMED'
.DL750   AIF   ((&FONT GE 12 AND &BLEDIT GE 3) OR &FONT LT 12).DL760
         MNOTE 8,'***  IHB389  INVALID EDIT OPTION FOR NHP OR GOTH FIELX
               D. ALBLOF ASSUMED'
&BLEDIT  SETA  4                              ASSUME ALBHIF FOR NHP FLD
.DL760   AIF   (T'&EDIT(2) EQ 'O').DL810      2ND SUBOPER OMITTED
         AIF   ('&EDIT(2)' EQ 'EDCHAR').DL800 EDCHAR SPCFD,NO ERR
         MNOTE 8,'***  IHB390  INVALID SPECIFICATION ON EDIT&N -EDCHAR X
               ASSUMED'
.DL800   ANOP
&CHEDIT  SETB  1                       SETUP FOR EDCHAR
.DL810   AIF   (N'&EDIT LE 2).DL820    TOO MANY EDITN OPERANDS,YES ERR
         MNOTE 4,'***  IHB391  TOO MANY OPERANDS ON EDIT&N. EXCESS OPERX
               ANDS IGNORED'
.DL820   ANOP
.* STATEMENTS .DL820 TO .END GENERATE FORMAT INFORMATION RECORD
&IHBSIZ  SETA  &IHBSIZ+4               INCR FR SIZE GENERATED
         AIF   (&IHBSIZ LE 212).DL830  MAX FR GEND ALREADY,YES ERR
&IHBFLD  SETB  1                       IND VALID FIELD GENERATED
&IHBER1  SETB  1                       IND MAX FR ALREADY GEND
         AGO   .END                    EXIT MACRO
.DL830   AIF   (&IHBDEL EQ 1).DL840    THIS FIELD CHAR DELIMETED
&IHBDLH  SETA  0                       INIT CTR OF CHAR DELIM FLDS IN
         AGO   .DL850                    A ROW TO 0
.DL840   ANOP
&IHBDLH  SETA  &IHBDLH+1               INCR NO OF FLDS CHAR DELIMETED
         AIF   (&IHBEND+&IHBDLH LE &IHBFED).DL850 SPACE ON LINE FOR FLD
         MNOTE 8,'***  IHB392  NO SPACE FOR FIELD &N ON DOCUMENT LINE. X
               FIELD &N IGNORED'
         AGO   .END                    EXIT MACRO
.DL850   DC    AL1(IHBE&SYSNDX)    *** FIELD INFORMATION RECORD ***
&B10     SETB  (NOT &IHBDEL)           SETUP FOR FIELD INFO RCD GENRTN
         DC    AL1(&B10*128+&EDATCK*64+&BLEDIT) DEL,CRIT,BLNK-FILL
         DC    AL1(&CHEDIT*128+&FLDLTH)  EDCHAR,FIELD LENGTH
         DC    AL1(&FONT) FONT
&IHBFLD  SETB  1                       IND VALID FIELD GEND
.END     MEXIT
         MEND

