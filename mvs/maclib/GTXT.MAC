.* MVS 3.8J PUBLIC DOMAIN
         MACRO
&NAME    GTXT  &A,&N
         GBLA  &IHBBLC,&IHBBLIM,&IHBCRSZ,&IHBBPX,&IHBBPY,&IHBVSP
         GBLA  &IHBXSP,&IHBYSP
         LCLA  &WORKX,&LENGTH,&INCX,&INCY
         AIF   (T'&A EQ 'O').NOGEN      VALIDATE CHAR STRING FORMAT
         AIF   ('&A'(1,1) NE '''').NOGEN
&WORKX   SETA  K'&A
         AIF   ('&A'(&WORKX,1) NE '''').NOGEN
&WORKX   SETA  0
         AIF   (T'&N EQ 'O').SETL       VALIDATE &N PARAMETER
         AIF   (T'&N NE 'N').ERRL
         AIF   (K'&A-2 LE &N).ERRL
&WORKX   SETA  &N
.SETL    ANOP
&LENGTH  SETA  K'&A-2-&WORKX
&NAME  DC C&A
&IHBBLC    SETA  &IHBBLC+&LENGTH
         AIF   (&IHBBLIM GE &IHBBLC).OK
         IHBERMAC 182
.OK      ANOP
         AIF   (&IHBVSP EQ 1).VS
         AIF   (&IHBCRSZ EQ 2).BIG        DETERMINE CHARACTER SIZE
         AIF   ('&IHBCRSZ' EQ '0').SMA
&INCX    SETA  56
&INCY    SETA  80
.TAT     ANOP
&IHBBPX    SETA  &IHBBPX+(&LENGTH*&INCX)
.LINE    AIF   (&IHBBPX GT 4095).MORE
         MEXIT
.MORE    ANOP
&IHBBPX    SETA  &IHBBPX-4095               UPDATE X- AND Y-COORDINATE
&IHBBPY    SETA  &IHBBPY-&INCY              BEAM POSITION COUNTERS
         AIF   (&IHBBPY LT 0).OVF         CHECK FOR OVERFLOW
         AGO   .LINE
.OVF     IHBERMAC 181
&IHBBPY    SETA  4095
         AGO   .LINE
.BIG     ANOP
&INCX    SETA  84
&INCY    SETA  120
         AGO   .TAT
.SMA     ANOP
&INCX    SETA  20
&INCY    SETA  27
         AGO   .TAT
.VS      ANOP
&IHBBPX    SETA  &IHBBPX+(&LENGTH*&IHBXSP)
&IHBBPY    SETA  &IHBBPY+(&LENGTH*&IHBYSP)
         AIF   (&IHBBPX LT 0 OR &IHBBPX GT 4095).VSXERR
.VSTSTY  AIF   (&IHBBPY LT 0 OR &IHBBPY GT 4095).VSYERR
         MEXIT
.VSXERR  IHBERMAC 186,BPX
         AGO   .VSTSTY
.VSYERR  IHBERMAC 186,BPY
         MEXIT
.ERRL    IHBERMAC 183,PARAM-2,0
         AGO   .SETL
.NOGEN   IHBERMAC  36,,MISSING-QUOTES
         MEND
