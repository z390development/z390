.* MVS 3.8J PUBLIC DOMAIN
         MACRO
&NAME    ENTR  &SCI1=,&SCIERR=,&PGMLEN=
         GBLA  &DISP               GLOBAL FOR INSTRUCTION DISPLACEMENT
         GBLC  &LBL1               GLOBAL FOR START SCI PGM
         LCLA  &PARM
         LCLB  &QUITSW
&LBL1    SETC  'IHB0&SYSNDX'       START OF SCI PGM OR SUBROUTINE
         AIF   ('&NAME' EQ '').ENT000 LABEL SPECIFIED
&LBL1    SETC  '&NAME'             YES,SET LABEL NAME
.ENT000  AIF   ('&PGMLEN' EQ '').ENT020 PGMLEN PARM PRESENT?
         AIF   (T'&PGMLEN NE 'N').ERR030 SELF-DEFINING TERM
&DISP    SETA  &PGMLEN+128         DISPLACEMENT FOR SCI INSTRUCTIONS
         AIF   (&DISP LE 128).ERR030 VERIFY PGMLEN PARAMETER
.ENT010  AIF   ('&SCI1' NE '').ERR010   PGMLEN ONLY?
         AIF   ('&SCIERR' NE '').ERR010 PGMLEN ONLY?
         AIF   (&QUITSW).FINI      IF ERROR FOUND,GO TO MACRO END
&LBL1    EQU   *                   START OF SCI SUBROUTINE
         MEXIT
.ENT020  AIF   (T'&SCIERR EQ 'O').ERR020 TEST FOR SCIERR OMITTED
&DISP    SETA  128                 SET SCI INSTRUCTION DISPLACEMENT
&LBL1    DC    CL8'ENTRMACR'       SCI PROGRAM NAME
         DC    AL2(0)              RESERVED
         DC    XL6'00'             SCI ERROR DATA FIELD
         AIF   (T'&SCI1 EQ 'O').ENT030 TEST SCI1 OMITTED
         AIF   (T'&SCI1 EQ 'N').ENT040 EXPLICIT ADDRESS FOR SCI1
         DC    AL2(&SCI1-&LBL1+128) FIRST EXECUTABLE SCI ADDRESS
         AGO   .ENT050             GO CHECK SCIERR
.ENT030  DC    AL2(160)            FIRST EXECUTABLE SCI ADDRESS
         AGO   .ENT050             GO CHECK SCIERR PARAMETER
.ENT040  ANOP
&PARM    SETA  &SCI1               DECIMAL ADDRESS VALUE FOR SCI1
         DC    AL2(&PARM)          FIRST EXECUTABLE SCI ADDRESS
.ENT050  AIF   (T'&SCIERR EQ 'N').ENT060 EXPLICIT ADDRESS FOR SCIERR
         DC    AL2(&SCIERR-&LBL1+128) SCI ERROR RTN ADDRESS
         AGO   .ENT070             CONTINUE MACRO EXPANSION
.ENT060  ANOP
&PARM    SETA  &SCIERR             SCIERR ADDRESS
         DC    AL2(&PARM)          SCI ERROR RTN ADDRESS
.ENT070  DC    XL12'00'            RESERVED
         MEXIT
.ERR010  MNOTE 12,'***  IHB405  MACRO CONTAINS INCONSISTENT OPERANDS'
         MEXIT
.ERR020  MNOTE 12,'***  IHB412  SCIERR OPERAND REQ''D- NOT SPECIFIED'
         MEXIT
.ERR030  MNOTE 12,'***  IHB413  INVALID PGMLEN OPERAND SPECIFIED'
&QUITSW  SETB  1                   SET MACRO TERMINATION SWITCH
         AGO   .ENT010             CONTINUE CHECKING MACRO OPERANDS
.FINI    MEND
