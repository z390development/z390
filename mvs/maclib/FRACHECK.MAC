.* MVS 3.8J PUBLIC DOMAIN
*
*
* $MAC(FRACHECK) COMP(SC1BN) PROD(RACF):
*
* DESCRIPTIVE_NAME = AUTHORIZATION CHECKING INTERFACE
*
*
* STATUS = OS/VS2 EBB1102
*
* FUNCTION = THIS INTERFACE ALLOWS THE CALLER TO CHECK THE USERS
*    AUTHORIZATION TO A GENERAL CLASS RESOURCE WHICH HAS HAD ITS
*    PROFILE LOADED INTO STORAGE BY A RACLIST.
*
* CHANGE ACTIVITY = A000000-999999                             @PC003PY
*                   $T1=UZ90211,SAF/RF15,810702,CNTDSD:            @T1A
*                   UZ90283                                    @ZA90283
*                   UZ90284                                    @ZA90284
*                   UZ90285                                    @ZA90285
*
*
         MACRO
&SYMBOL  FRACHECK &ENTITY=,&CLASS=,&WKAREA=,                           *
               &ATTR=,&MF=S,&INSTLN=,                                  *
               &ACEE=,&APPL=
.******* DEFINE LOCAL SET SYMBOLS
         LCLA  &BIT0,&BIT1,&BIT2,&BIT3,&BIT4,&BIT5,&BIT6,&BIT7,&CLNGTH
         LCLA  &OFFSET
         LCLB  &MFFLAG
.******* DEFINE GLOBAL SET VARIABLES                               @T1A
         GBLB  &ICHRAC            ICHRAC - A FLAG WHICH, WHEN      @T1A
.*                                ON, INDICATES THAT THIS MACRO    @T1A
.*                                IS BEING INVOKED INTERNALLY      @T1A
.*                                BY THE RACROUTE MACRO.           @T1A
         GBLC  &ICHL              SETS ADDRESSABILITY TO THE       @T1A
.*                                PARAMETER LIST FOR THE           @T1A
.*                                RACROUTE MACRO.                  @T1A
&OFFSET  SETA  32
.*
.******* PERFORM PARAMETER DIAGNOSIS
.*
         AIF   ('&ENTITY' EQ '' AND '&MF' EQ 'S' ).ERR00
.ERTN00  ANOP
.******* VALIDATE ATTR PARAMETER
         AIF   ('&ATTR' EQ 'READ' OR '&ATTR' EQ 'ALTER'                -
                OR '&ATTR' EQ '').RCHK000
         AIF   ( '&ATTR' EQ 'UPDATE' OR '&ATTR' EQ                     -
               'CONTROL' OR '&ATTR'(1,1) EQ '(').RCHK000
         AGO   .ERR04
.RCHK000 ANOP
.ERTN04  ANOP
.******* VALIDATE MF PARAMETER
         AIF   ('&MF' NE 'S' AND '&MF' NE 'L' AND '&MF'(1,2) NE        -
               '(E').ERR06
.ERTN06  ANOP
.******* REGISTER SPECIFICATION OF ADDR TYPE PARAMETERS NOT
.******* ALLOWED WITH LIST FORM
         AIF   ('&MF' NE 'L').RCHK020
         AIF   ('&ATTR' EQ '').RCHK010
         AIF   ('&ATTR'(1,1) EQ '(').ERR07
.RCHK010 ANOP
         AIF   ('&ENTITY' EQ '').RCHK011
         AIF   ('&ENTITY'(1,1) EQ '(').ERR07
.RCHK011 ANOP
         AIF   ('&CLASS' EQ '').RCHK012
         AIF   ('&CLASS'(1,1) EQ '(').ERR07
.RCHK012 ANOP
         AIF   ('&ACEE' EQ '').RCHK013
         AIF   ('&ACEE'(1,1) EQ '(').ERR07
.RCHK013 ANOP
         AIF   ('&APPL' EQ '').RCHK014
         AIF   ('&APPL'(1,1) EQ '(').ERR07
.RCHK014 ANOP
         AIF   ('&WKAREA' EQ '').RCHK015
         AIF   ('&WKAREA'(1,1) EQ '(').ERR07
.RCHK015 ANOP
         AIF   ('&INSTLN(1)' NE '&INSTLN').ERR07
.RCHK020 ANOP
.ERTN07  ANOP
.*
.******* PRODUCE LIST FORM IF REQUESTED
.*
         AIF   ('&MF' NE 'L').RCHK400
&SYMBOL  DS    0F
.RCHK100 ANOP
ICH0&SYSNDX EQU *
.RCHK105 ANOP
.******* PRODUCE FLAG BYTE WITH ATTR PARAMETER
&BIT0    SETA  0
&BIT4    SETA  0
&BIT5    SETA  0
&BIT6    SETA  0
         AIF   ('&ATTR' EQ '').RCHK125
         AIF   ('&ATTR'(1,1) EQ '(').RCHK130
         AIF   ('&ATTR' NE 'ALTER').RCHK110
&BIT0    SETA  1
         AGO   .RCHK130
.RCHK110 ANOP
         AIF   ('&ATTR' NE 'CONTROL').RCHK115
&BIT4    SETA  1
         AGO   .RCHK130
.RCHK115 ANOP
         AIF   ('&ATTR' NE 'UPDATE').RCHK120
&BIT5    SETA  1
         AGO   .RCHK130
.RCHK120 ANOP
         AIF   ('&ATTR' NE 'READ').RCHK130
.RCHK125 ANOP
&BIT6    SETA  1
.RCHK130 ANOP
&ICHL    DC    B'&BIT0.000&BIT4&BIT5&BIT6.0' - ATTR VALUE          @T1A
.******* RESERVED FIELD - 3 BYTES
         DC    3X'00' - RESERVED
.******* PRODUCE ENTITY NAME
         AIF   ('&ENTITY' EQ '').RCHK150
         AIF   ('&ENTITY'(1,1) EQ '(').RCHK150
         DC    A(&ENTITY) - ENTITY NAME ADDRESS
         AGO   .RCHK155
.RCHK150 ANOP
         DC    A(0)            - ENTITY NAME ADDRESS
.RCHK155 ANOP
.******* PRODUCE CLASS NAME ADDRESS IF REQUIRED
         AIF   ('&CLASS' EQ '').RCHK165
         AIF   ('&CLASS'(1,1) EQ '(').RCHK165
         AIF   ('&CLASS'(1,1) EQ '''').RCHK160
         DC    A(&CLASS) -  CLASS NAME ADDRESS
         AGO   .RCHK170
.RCHK160 ANOP
         DC    A(ICH1&SYSNDX) CLASS NAME ADDRESS WITHIN LIST
         AGO   .RCHK170
.RCHK165 ANOP
         DC    A(0) -       CLASS NAME ADDRESS FIELD
.RCHK170 ANOP
.******* PROCESS ACEE PARAMETER
         AIF   ('&ACEE' EQ '' ).RCHK175
         AIF   ('&ACEE'(1,1) EQ '(').RCHK175
         DC    A(&ACEE) - ACEE ADDRESS
         AGO   .RCHK180
.RCHK175 ANOP
         DC    A(0)    - ACEE ADDRESS
.RCHK180 ANOP
.******* PROCESS APPL PARAMETER
         AIF   ('&APPL' EQ '').RCHK190
         AIF   ('&APPL'(1,1) EQ '(').RCHK190
         AIF   ('&APPL'(1,1) EQ '''').RCHK185
         DC    A(&APPL) -   APPL NAME ADDRESS
         AGO   .RCHK195
.RCHK185 ANOP
         DC    A(ICH2&SYSNDX) APPL NAME ADDRESS WITHIN LIST
         AGO   .RCHK195
.RCHK190 ANOP
         DC    A(0) -       APPL NAME ADDRESS FIELD
.RCHK195 ANOP
.******* PROCESS WKAREA PARAMETER
         AIF   ('&WKAREA' EQ '').RCHK200
         AIF   ('&WKAREA'(1,1) EQ '(').RCHK200
         DC    A(&WKAREA) - WORK AREA ADDRESS
         AGO   .RCHK205
.RCHK200 ANOP
         DC    A(0) - WORK AREA ADDRESS
.RCHK205 ANOP
.******* PROCESS INSTLN PARAMETER
         AIF   ('&INSTLN' EQ '' OR                                     *
               '&INSTLN(1)' NE '&INSTLN').RCHK210
         DC    A(&INSTLN) - ADDRESS OF INSTALLATION DATA
         AGO   .RCHK215
.RCHK210 ANOP
         DC    A(0) - ADDRESS OF INSTALLATION DATA
.RCHK215 ANOP
.******* IF CLASSNAME SPECIFIED EXPLICITLY, PRODUCE IT BELOW LIST FORM
         AIF   ('&CLASS' EQ '').RCHK230
         AIF   ('&CLASS'(1,1) NE '''').RCHK230
ICH1&SYSNDX DC    CL8&CLASS    - CLASS NAME
.RCHK230 ANOP
.******* IF APPL NAME SPECIFIED EXPLICITLY, PRODUCE IT BELOW LIST FORM
         AIF   ('&APPL' EQ '').RCHK240
         AIF   ('&APPL'(1,1) NE '''').RCHK240
ICH2&SYSNDX DC    CL8&APPL - APPL NAME
.RCHK240 ANOP
.******* LIST FORM COMPLETE - TEST FOR STANDARD FORM PASS
.******* THROUGH LIST FORM
         AIF   (&MFFLAG).RCHK510
         MEXIT
.******* TEST FOR STANDARD FORM OR EXECUTE FORM OF MACRO
.RCHK400 ANOP
         AIF   ('&MF(1)' EQ 'E').RCHK600
.******* STANDARD FORM REQUESTED
&SYMBOL  DS    0H
&MFFLAG  SETB  1              SET TRUE FOR PASS THROUGH LIST FORM
.******* PRODUCE INSTRUCTIONS TO MODIFY IN-LINE LIST FORM
.******* WITH REGISTER ADDRS
         AIF   ('&ATTR' EQ '').RCHK410
         AIF   ('&ATTR'(1,1) NE '(').RCHK410
         STC   &ATTR(1),ICH0&SYSNDX - STORE ATTR VALUE
.RCHK410 ANOP
         AIF   ('&ENTITY' EQ '').RCHK420
         AIF   ('&ENTITY'(1,1) NE '(').RCHK420
         ST    &ENTITY(1),ICH0&SYSNDX+4 - STORE ENTITY NAME ADDRESS
.RCHK420 ANOP
         AIF   ('&CLASS' EQ '').RCHK430
         AIF   ('&CLASS'(1,1) NE '(').RCHK430
         ST    &CLASS(1),ICH0&SYSNDX+8 - STORE CLASS NAME ADDRESS
.RCHK430 ANOP
         AIF   ('&ACEE' EQ '').RCHK440
         AIF   ('&ACEE'(1,1) NE '(').RCHK440
         ST    &ACEE(1),ICH0&SYSNDX+12 - STORE ACEE ADDRESS
.RCHK440 ANOP
         AIF   ('&APPL' EQ '').RCHK450
         AIF   ('&APPL'(1,1) NE '(').RCHK450
         ST    &APPL(1),ICH0&SYSNDX+16 - STORE APPL ADDRESS
.RCHK450 ANOP
         AIF   ('&WKAREA' EQ '').RCHK460
         AIF   ('&WKAREA'(1,1) NE '(').RCHK460
         ST    &WKAREA(1),ICH0&SYSNDX+20 - STORE WKAREA ADDRESS
.RCHK460 ANOP
         AIF   ('&INSTLN' EQ '' OR '&INSTLN(1)' EQ '&INSTLN').RCHK470
         ST    &INSTLN(1),ICH0&SYSNDX+24 - STORE INSTALLATION DATA ADDR
.RCHK470 ANOP
.******* LIST FORM MODIFICATION COMPLETE - PRODUCE
.******* LIST FORM WITH BAL AROUND
         AIF   ('&CLASS' EQ '').RCHK480
         AIF   ('&CLASS'(1,1) NE '''').RCHK480
&OFFSET  SETA  &OFFSET+8
.RCHK480 ANOP
         AIF   ('&APPL' EQ '').RCHK485
         AIF   ('&APPL'(1,1) NE '''').RCHK485
&OFFSET  SETA  &OFFSET+8
.RCHK485 ANOP
         CNOP  0,4
         BAL   1,*+&OFFSET    POINT REG 1 AT PARM LIST
         AGO   .RCHK100       GO TO PRODUCE LIST
.RCHK510 ANOP
.***********************************************************       @T1A
.* IF THIS MACRO WAS INVOKED INTERNALLY BY THE RACROUTE            @T1A
.* MACRO, SIMPLY EXIT RATHER THAN GOING TO THE RACF ROUTINE.       @T1A
.***********************************************************       @T1A
         AIF   (&ICHRAC).RCHK520                                   @T1A
.******* RETURN FROM LIST FORM TO PRODUCE INTERFACE CODE
         L     15,16(0,0)            GET CVT ADDRESS
         L     15,X'3E0'(0,15)       GET RACF CVT ADDRESS
         LTR   15,15                 RACF CVT EXIST
         BZ    *+12                  NO, BRANCH TO SET NOT ACTIVE
         CLI   X'AC'(15),X'02'       RACF FUNCTION SUPPORTED (RCVTVERS)
         BNL   *+12                  YES, BRANCH
         LA    15,20(0,0)            RACF FUNCTION NOT SUPPORTED
         B     *+10                  SKIP CALL
         L     15,X'C4'(0,15)        GET ADDRESS OF FRACHECK ROUTINE
         BALR  14,15                 CALL FRACHECK ROUTINE
.RCHK520 ANOP                                                      @T1A
         MEXIT
.******* PRODUCE EXECUTE FORM OF MACRO
.******* PRODUCE INSTRUCTIONS TO MODIFY LIST
.******* PRODUCE SECOND FLAGS BYTE WITH ATTR SPECS
.RCHK600 ANOP
         AIF   (&ICHRAC).RCHK603                                   @T1A
&SYMBOL  IHBINNRA &MF(2) -    POINT REG1 AT LIST FORM
         AGO   .RCHK605                                            @T1A
.**************************************************************    @T1A
.*  IF THIS MACRO WAS INVOKED INTERNALLY BY THE RACROUTE MACRO,    @T1A
.*  PICK UP THE ADDRESS OF THE FRACHECK PARAMETER LIST FROM THE    @T1A
.*  RACROUTE PARAMETER LIST.                                       @T1A
.**************************************************************    @T1A
.RCHK603 ANOP                                                      @T1A
         A     1,36(1)         POINT REG 1 AT FRACHECK LIST        @T1A
.RCHK605 ANOP                                                      @T1A
         AIF   ('&ATTR' EQ '').RCHK650
         AIF   ('&ATTR'(1,1) EQ '(').RCHK640
         AIF   ('&ATTR' NE 'ALTER').RCHK610
         MVI   0(1),X'80' -   SET FOR ALTER ACCESS
         AGO   .RCHK650
.RCHK610 ANOP
         AIF   ('&ATTR' NE 'UPDATE').RCHK620
         MVI   0(1),X'04'               SET FOR UPDATE ACCESS
         AGO   .RCHK650
.RCHK620 ANOP
         AIF   ('&ATTR' NE 'READ').RCHK630
         MVI   0(1),X'02'               SET FOR READ ACCESS
         AGO   .RCHK650
.RCHK630 ANOP
         AIF   ('&ATTR' NE 'CONTROL').RCHK650
         MVI   0(1),X'08'               SET FOR CONTROL ACCESS
         AGO   .RCHK650
.RCHK640 ANOP
         STC   &ATTR(1),0(1) - STORE ATTR VALUE IN LIST FORM
.RCHK650 ANOP
.******* HANDLE ENTITY PARAMETER
         AIF   ('&ENTITY' EQ '').RCHK665
         AIF   ('&ENTITY'(1,1) NE '(').RCHK660
         ST    &ENTITY,4(1) - STORE ENTITY NAME ADDRESS
         AGO   .RCHK665
.RCHK660 ANOP
         LA    0,&ENTITY - MOVE ENTITY NAME ADDRESS
         ST    0,4(1) -      INTO PARM LIST
         AGO   .RCHK665
.RCHK665 ANOP
.******* HANDLE CLASS PARAMETER
         AIF   ('&CLASS' EQ '').RCHK675
         AIF   ('&CLASS'(1,1) EQ '''').ERR09
         AIF   ('&CLASS'(1,1) NE '(').RCHK670
         ST    &CLASS(1),8(1) - STORE CLASS NAME ADDRESS
         AGO   .RCHK675
.RCHK670 ANOP
         LA    0,&CLASS -     MOVE CLASS NAME ADDRESS
         ST    0,8(1) -      INTO PARM LIST
.ERTN09  ANOP
.RCHK675 ANOP
.******* HANDLE ACEE PARAMTER
         AIF   ('&ACEE' EQ '').RCHK685
         AIF   ('&ACEE'(1,1) NE '(').RCHK680
         ST    &ACEE(1),12(1) - STORE ACEE ADDRESS
         AGO   .RCHK685
.RCHK680 ANOP
         LA    0,&ACEE - ADDRESS ACEE
         ST    0,12(1) - STORE INTO PARAMTER LIST
.RCHK685 ANOP
.******* HANDLE APPL PARAMETER
         AIF   ('&APPL' EQ '').RCHK695
         AIF   ('&APPL'(1,1) EQ '''').ERR10
         AIF   ('&APPL'(1,1) NE '(').RCHK690
         ST    &APPL(1),16(1) - STORE APPL NAME ADDRESS
         AGO   .RCHK695
.RCHK690 ANOP
         LA    0,&APPL -      MOVE APPL NAME ADDRESS
         ST    0,16(1) -      INTO PARM LIST
.ERTN10  ANOP
.RCHK695 ANOP
.******* HANDLE WKAREA PARAMTER
         AIF   ('&WKAREA' EQ '').RCHK705
         AIF   ('&WKAREA'(1,1) NE '(').RCHK700
         ST    &WKAREA(1),20(1) - STORE WKAREA ADDRESS
         AGO   .RCHK705
.RCHK700 ANOP
         LA    0,&WKAREA - ADDRESS WKAREA
         ST    0,20(1) - STORE INTO PARAMTER LIST
.RCHK705 ANOP
.******* PRODUCE INSTALLATION DATA ADDRESS
         AIF   ('&INSTLN' EQ '').RCHK715
         AIF   ('&INSTLN'(1,1) NE '(').RCHK710
         ST    &INSTLN(1),24(1) - STORE INSTALLATION ADDR
         AGO   .RCHK715
.RCHK710 ANOP
         LA    0,&INSTLN - ADDRESS INSTALLATION DATA ADDRESS
         ST    0,24(1) - STORE INSTALLATION DATA ADDRESS
.RCHK715 ANOP
.******* MODIFICATION COMPLETE - AGO ABOVE TO PRODUCE ENTRY LOGIC
         AGO   .RCHK510
.******* DIAGNOSTICS
.ERR00   ANOP
         MNOTE 8,'*** ENTITY PARM REQUIRED ***'
         AGO   .ERTN00
.ERR04   ANOP
         MNOTE 8,'*** INVALID ATTR PARAMETER SPECIFIED ***'
         AGO   .ERTN04
.ERR06   ANOP
         MNOTE 8,'*** INVALID MF PARAMETER SPECIFIED ***'
         AGO   .ERTN06
.ERR07   ANOP
         MNOTE 8,'*** REGISTER ADDRESS SPECIFICATION NOT ALLOWED WITH L-
               IST FORM ***'
         AGO   .ERTN07
.ERR09   ANOP
         MNOTE 8,'*** ''CLASSNAME'' FORM NOT ALLOWED WITH MF=E ***'
         AGO   .ERTN09
.ERR10   ANOP
         MNOTE 8,'*** ''APPLNAME'' FORM NOT ALLOWED WITH MF=E ***'
         AGO   .ERTN10
         MEND
**/

