.* MVS 3.8J PUBLIC DOMAIN
* /* START OF SPECIFICATIONS ****
*
*01*  MODULE-NAME = USSMSG
*
*01*  DESCRIPTIVE-NAME = USS ALTERNATE MESSAGE DEFINITION MACRO
*
*
*01*  STATUS = RELEASE 2
*
*01*  FUNCTION = THE PURPOSE OF THIS MACRO IS TO GENERATE AN MPB
*     (MESSAGE PROCESSING BLOCK). THE BLOCK IS USED TO SPECIFY AN
*     ALTERNATE FORM OF A USS MESSAGE. THE MACRO CREATES A 4 WORD TABLE
*     CONSISTING OF CHAIN PTR, MESSAGE MEMBER STRING PTR, MESSAGE TEXT
*     PTR, AND MESSAGE NUMBER STRING ( HIGH ORDER BYTE=X'03'). A CALL
*     TO USSEND IS USED TO GENERATE THE MESSAGE TEXT DATA.
*
*02*    CHARACTER-CODE-DEPENDENCIES = NONE
*
*02*    DEPENDENCIES= NONE
*
*02*    RESTRICTIONS = NONE
*
*02*    REGISTER-CONVENTIONS = NONE
*
*02*    PATCH-LABEL = NONE
*
*01*  MODULE-TYPE = MACRO
*
*02*    PROCESSOR = ASSEM-370R
*
*02*    MACRO-SIZE = 28 STATEMENTS
*
*02*    ATTRIBUTES = LIST-EXEC
*
*01*  ENTRY-POINT = NA
*
*01*  EXIT-NORMAL = NA
*
*01*  EXIT-ERROR = NA
*
*01*  EXTERNAL-REFERENCES = NONE
*
*01*  TABLES = MPB (MESSAGE PROCESSING BLOCK)
*
*01*  INNER-MACROS = USSEND
*
*01*  SYSTEM-MACROS = NONE
*
*01*  CHANGE-ACTIVITY = AS-FOLLOWS
*                       NEW MACRO - FIRST RELEASE   32  DOS/VS
*                                                   04  OS/VS1
*                                                   03  OS/VS2
*            APAR OZ26263  ADDITIONAL VALIDITY CHECKS        @ZA26263
**** END OF SPECIFICATIONS ***/
         MACRO
&LABEL   USSMSG &MSG=,&TEXT=
         GBLC  &T                 USSEND OUTPUT VARIABLE
         GBLC  &Q                 USSEND OUTPUT VARIABLE
         GBLC  &N                 STATEMENT LABEL PREFIX
         GBLB  &Z                 SWITCH FOR USSTAB SPECIFIED
         LCLC  &R                 INTERNAL LABEL VARIABLE
         LCLA  &X                 INTERNAL INDEX VARIABLE
         AIF   (&Z EQ 1).SQ003
         SPACE 1
         MNOTE 4,'USSTAB MACRO NOT SPECIFIED'
.SQ003   ANOP
         AIF   ('&MSG' NE '').SQ005
         MNOTE 4,'&N.001 MSG= PARAMETER OMITTED'
.SQ005   SPACE 1
&LABEL   DC    0F'0'              ALIGN ON FULLWORD BOUNDARY
         USSEND INC=M             INCREMENT USSMSG COUNTER
&R       SETC  '&T'               SAVE USSMSG COUNTER
         AGO   .SQ020
.SQ010   SPACE 1
         USSEND INC=M             INCREMENT USSMSG COUNTER
.SQ020   ANOP
&X       SETA  &X+1
&N.M&T   DC    A(&N.M&Q)          ADDR NEXT USSMSG BLOCK
         DC    A(&N.N&T)          ADDR MESSAGE NUMBER STRING
         DC    A(&N.T&R)          ADDR MESSAGE TEXT STRING
&Q       SETC  '00&MSG(&X)'       PAD MESSAGE NUMBER
&Q       SETC  '&Q'(K'&Q-2,3)     TRUNC TO LENGTH = 3
&N.N&T   USSEND GEN=&Q,NAME=&N.O&T
         AIF   (&X LT N'&MSG).SQ010
&N.T&R   USSEND GEN=&TEXT,NAME=&N.U&R
         SPACE 1
         MEND

