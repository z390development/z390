.* MVS 3.8J PUBLIC DOMAIN
         MACRO
&NAME    CALL3890 &DCBX,&IRECX,&SRTNX,&PGMX,&MFX
         GBLB  &QUITSW
         LCLC  &LSTADDR,&DCBV,&IRECV,&LBL,&SRTNV               @ZA04479
         LCLA  &LEN
         LCLB  &N1,&N2                                         @ZA04479
&DCBV    SETC  '0'                     INITIALIZE DCB VALUE
&IRECV   SETC  '0'                     INITIALIZE IREC VALUE
&N2      SETB  1                  INIT REG OPERAND SW ON       @ZA04479
.*
.****************  VERIFY MACRO OPERANDS  ****************************
.*
.VERIFY  AIF   ('&MFX' NE '').VER020   TEST FOR LIST/EXECUTE FORMAT
.* STANDARD FORMAT
         AIF   ('&IRECX' EQ '').ERR010 ERROR IF IREC ABSENT
         AIF   ('&IRECX' EQ '(0)' OR '&IRECX' EQ '(1)').ERR020
.VER010  AIF   ('&SRTNX' EQ '').VER050  TEST FOR SUBRTN PARM
         AIF   ('&PGMX' EQ '').ERR030  INSURE PROGRAM PARM PRESENT
         AGO   .VER040                 GO VERIFY SUBRTN PARM
.VER020  AIF   ('&MFX' EQ 'L').VER030  TEST FOR LIST FORMAT
.* EXECUTE FORMAT
         AIF   ('&IRECX' EQ '(0)' OR '&IRECX' EQ '(1)').ERR020
         AGO   .VER040                 GO VERIFY SUBRTN OPERAND
.* LIST FORMAT
.VER030  AIF   ('&IRECX' EQ '').VER040 IREC SPECIFIED
         AIF   ('&IRECX'(1,1) EQ '(').ERR020 INSURE VALID IREC PARM
.* STANDARD, LIST, AND EXECUTE FORMAT                          @ZA04479
.VER040  AIF   ('&SRTNX' EQ '').VER050 SUBRTN SPECIFIED
         AIF   ('&SRTNX'(1,1) NE '(').VER045
         AIF   ('&SRTNX(2)' EQ '').ERR040 ADDRESS SPECIFIED
&SRTNV   SETC  '&SRTNX(2)'                                     @ZA04479
&N2      SETB  ('&SRTNV'(1,1) NE '(') SW = TRUE IF NOT IN REG  @ZA04479
         AIF   (&N2).VER041       BRANCH IF NOT REGISTER       @ZA04479
         AIF   ('&MFX' NE '' AND '&MFX' EQ 'L').ERR040         @ZA04479
.VER041  AIF   (T'&SRTNX(1) NE 'N').ERR040 SELF-DEFINING TERM  @ZA04479
&LEN     SETA  &SRTNX(1)               SET LENGTH OF SUBRTN
         AIF   (&LEN EQ 0).ERR040      LENGTH EQUAL 0
         AGO   .VER050                 GO TEST QUIT SWITCH
.VER045  AIF   (T'&SRTNX EQ 'N').ERR040 SELF-DEFINING TERM
.VER050  AIF   (&QUITSW).EOJ           ERRORS FOUND
         AIF   ('&MFX' EQ 'L').PARMLST TEST FOR LIST FORMAT
&LBL     SETC  'IHB1&SYSNDX'           SET LABEL
         AIF   ('&NAME' EQ '').VER060  MACRO LABEL PRESENT
&NAME    EQU   *
.VER060  AIF   ('&MFX' NE '').EXECUTE  TEST FOR EXECUTE FORMAT
.*
.****************  STANDARD FORMAT INSTRUCTION GENERATION  ***********
.*
.STNDARD AIF   ('&DCBX'(1,1) EQ '(').STD030 REGISTER NOTATION - DCB
&DCBV    SETC  '&DCBX'                 SET DCB VALUE FOR PARMLIST
.STD010  AIF   ('&IRECX'(1,1) EQ '(').STD040 REGISTER NOTATION - IREC
&IRECV   SETC  '&IRECX'                SET IREC VALUE FOR PARMLIST
.STD020  AIF   (&N2).STD021       BR IF SRTN ADDR NOT IN REG   @ZA04479
         ST    &SRTNX(2),IHB1&SYSNDX+16 STORE SRT ADDR IN LIST @ZA04479
.STD021  BAL   1,IHB1&SYSNDX+28   BRANCH TO ISSUE SETDEV SVC   @ZA04479
         AGO   .PARMLST                GO EXPAND PARMLIST
.STD030  ST    &DCBX(1),IHB1&SYSNDX    STORE DCB ADDRESS
         AGO   .STD010                 GO TEST IREC PARM
.STD040  ST    &IRECX(1),IHB1&SYSNDX+4 STORE IREC ADDRESS
         AGO   .STD020                 GO EXPAND INSTRUCTIONS
.*
.****************  EXECUTE FORMAT INSTRUCTION GENERATION  ************
.*
.EXECUTE ANOP
&LSTADDR SETC  '&MFX(2)'               SET ADDRESS OF LIST
         AIF   ('&LSTADDR' EQ '(1)').EXC020 LIST IN REGISTER 1
         AIF   ('&LSTADDR'(1,1) EQ '(').EXC010 ADDRESS LIST IN REGISTER
         LA    1,&MFX(2)               SETDEV PARAMETER LIST ADDRESS
         AGO   .EXC020                 GO TEST DCB PARM
.EXC010  LR    1,&MFX(2)               SETDEV PARAMETER LIST ADDRESS
.EXC020  AIF   ('&DCBX' NE '').EXC060  DCB SPECIFIED
.EXC030  AIF   ('&IRECX' NE '').EXC080 IREC SPECIFIED
.EXC040  AIF   ('&PGMX' NE '').EXC100  PROGRAM SPECIFIED
.EXC050  AIF   ('&SRTNX' NE '').EXC140 SUBRTN SPECIFIED
.EXC055  AGO   .EOJ                        FINISHED
.* STORE DCB ADDRESS IN SETDEV PARMLIST
.EXC060  AIF   ('&DCBX'(1,1) EQ '(').EXC070 REGISTER NOTATION
         LA    0,&DCBX                 DCB ADDRESS
         ST    0,0(0,1)                STORE DCB ADDRESS
         AGO   .EXC030                 GO TEST IREC PARM
.EXC070  ST    &DCBX(1),0(0,1)         STORE DCB ADDRESS
         AGO   .EXC030                 GO TEST IREC PARM
.* STORE IREC ADDRESS IN SETDEV PARMLIST
.EXC080  AIF   ('&IRECX'(1,1) EQ '(').EXC090 REGISTER NOTATION
         LA    0,&IRECX                IREC ADDRESS
         ST    0,4(0,1)                STORE IREC ADDRESS
         AGO   .EXC040                 GO TEST PROGRAM PARM
.EXC090  ST    &IRECX(1),4(0,1)        STORE IREC ADDRESS
         AGO   .EXC040                 GO TEST PROGRAM PARM
.* STORE PROGRAM NAME IN SETDEV PARMLIST
.EXC100  AIF   ('&SRTNX' NE '').EXC110 SUBRTN SPECIFIED ALSO
         MVC   20(8,1),IHB1&SYSNDX     STORE PROGRAM NAME
         BC    15,IHB1&SYSNDX+8        BRANCH AROUND CONSTANT
&LBL     DC    CL8'&PGMX'              SCI PROGRAM MEMBERNAME
         AGO   .EXC055                 EXPAND REMAINING INSTRUCTIONS
.* STORE PROGRAM AND SUBRTN IN SETDEV PARMLIST
.EXC110  MVC   12(16,1),IHB1&SYSNDX    STORE PROGRAM AND SUBRTN PARMS
         BC    15,IHB1&SYSNDX+16       BRANCH AROUND CONSTANTS
         AIF   ('&SRTNX(2)' NE '').EXC120 SUBRTN IN CORE
&LBL     DC    CL8'&SRTNX'             SCI SUBROUTINE MEMBERNAME
         DC    CL8'&PGMX'              SCI PROGRAM MEMBERNAME
         NI    11(1),X'7F'              RESET SUBRTN FLAG
         AGO   .EXC055                 EXPAND REMAINING INSTRUCTIONS
.EXC120  CNOP  0,4                     FULLWORD BOUNDARY ALLIGNMENT
&LBL     DC    A(&LEN)                 SCI SUBROUTINE LENGTH
         AIF   (&N2).EXC121       BR IF SRTN ADDR NOT IN LIST  @ZA04479
         DC    A(0)               SCI SUBROUTINE ADDRESS       @ZA04479
         AGO   .EXC122                                         @ZA04479
.EXC121  DC    A(&SRTNX(2))            SCI SUBROUTINE ADDRESS  @ZA04479
.EXC122  DC    CL8'&PGMX'              SCI PROGRAM MEMBERNAME  @ZA04479
         AIF   (&N2).EXC123       BR IF SRTN ADDR NOT IN REG   @ZA04479
         ST    &SRTNX(2),16(,1)   STORE SRTN ADDR IN PARM LIST @ZA04479
.EXC123  OI    11(1),X'80'        INDICATE SUBROUTINE RESIDENT @ZA04479
         AGO   .EXC055                 EXPAND REMAINING INSTRUCTIONS
.* STORE SCI SUBROUTINE PARAMETERS IN SETDEV PARMLIST
.EXC140  MVC   12(8,1),IHB1&SYSNDX     STORE SUBRTN PARM
         BC    15,IHB1&SYSNDX+8        BRANCH AROUND CONSTANTS
         AIF   ('&SRTNX(2)' NE '').EXC150 SUBRTN IN CORE
&LBL     DC    CL8'&SRTNX'             SCI SUBROUTNE MEMBERNAME
         NI    11(1),X'7F'              RESET SUBRTN FLAG
         AGO   .EXC055                 EXPAND REMAINING INSTRUCTIONS
.EXC150  CNOP  0,4                     FULLWORD BOUNDARY ALLIGNMENT
&LBL     DC    A(&LEN)                 SCI SUBROUTINE LENGTH
         AIF   (&N2).EXC151       BR IF SRTN ADDR NOT IN REG   @ZA04479
         DC    A(0)               SCI SUBROUTINE ADDRESS       @ZA04479
         ST    &SRTNX(2),16(,1)   STORE SRTN ADDR IN PARM LIST @ZA04479
         AGO   .EXC152                                         @ZA04479
.EXC151  DC    A(&SRTNX(2))       SCI SUBROUTINE ADDRESS       @ZA04479
.EXC152  OI    11(1),X'80'        INDICATE SUBROUTINE RESIDENT @ZA04479
         AGO   .EXC055                 EXPAND REMAINING INSTRUCTIONS
.*
.****************  SETDEV PARMLIST MACRO GENERATION  *****************
.*
.PARMLST AIF   ('&MFX'NE 'L').PARM040  TEST FOR LIST FORMAT
         AIF   ('&DCBX' NE '').PARM020 DCB PARM SPECIFIED
.PARM010 AIF   ('&IRECX' NE '').PARM030 IREC SPECIFIED
.PARM015 ANOP
&NAME    DC    A(&DCBV)                DCB ADDRESS
         AGO   .PARM050                CONTINUE EXPANSION
.PARM020 ANOP
&DCBV    SETC  '&DCBX'                 SET VALUE FOR DCB
         AGO   .PARM010                GO TEST IREC PARM
.PARM030 ANOP
&IRECV   SETC  '&IRECX'                SET IREC VALUE
         AGO   .PARM015                GO EXPAND DCB ADDRESS
.PARM040 ANOP
&LBL     DC    A(&DCBV)                DCB ADDRESS
.PARM050 DC    A(&IRECV)               IREC ADDRESS
         DC    XL3'FF0200'             SETDEV PARAMETERS
&N1      SETB  ('&SRTNX(2)' NE '')
         DC    BL1'&N1.0000000'        SETDEV FLAGS
         AIF   ('&SRTNX' NE '').PARM060 SUBRTN SPECIFIED
         DC    XL8'00'                 SCI SUBROUTINE PARAMETER
         AGO   .PARM080
.PARM060 AIF   (&N1).PARM070           SUBRTN IN MAIN STORAGE
         DC    CL8'&SRTNX'             SCI SUBROUTINE MEMBERNAME
         AGO   .PARM080
.PARM070 DC    A(&LEN)                 SCI SUBROUTINE LENGTH
         AIF   (&N2).PARM071      BR IF SRTN ADDR NOT IN REG   @ZA04479
         DC    A(0)               SCI SUBROUTINE ADDRESS       @ZA04479
         AGO   .PARM080                                        @ZA04479
.PARM071 DC    A(&SRTNX(2))       SCI SUBROUTINE ADDRESS       @ZA04479
.PARM080 AIF   ('&PGMX' NE '').PARM090 PROGRAM SPECIFIED
         DC    XL8'00'                 SCI PROGRAM MEMBERNAME
         MEXIT
.PARM090 DC    CL8'&PGMX'              SCI PROGRAM MEMBERNAME
         MEXIT
.*
.****************  MNOTES AND ERROR CONDITIONS  **********************
.*
.ERR010  MNOTE 12,'***  IHB001  IREC OPERAND REQ''D-NOT SPECIFIED'
&QUITSW  SETB  1                       SET MACRO TERMINATION SWITCH
         AGO   .VER010                 GO VERIFY NEXT OPERAND
.ERR020  ANOP
&QUITSW  SETB  1                       SET MACRO TERMINATION SWITCH
         AIF   ('&MFX' NE 'L').ERR025  LIST FORMAT
         MNOTE 12,'***  IHB006  INVALID REGISTER NOTATION WITH MF=L FORX
               M.'
         AGO   .VER040                 GO VERIFY NEXT PARAMETER
.ERR025  MNOTE 12,'***  IHB002  IREC OPERAND INVALID-&IRECX'
         AIF   ('&MFX' EQ '').VER010   GO TEST NEXT MACRO OPERAND
         AGO   .VER040                 GO VERIFY NEXT MACRO OPERAND
.ERR030  MNOTE 12,'***  IHB414  PROGRAM OPERAND REQ''D-NOT SPECIFIED'
&QUITSW  SETB  1                       SET MACRO TERMINATION SWITCH
         AGO   .VER040                 GO TEST NEXT MACRO OPERAND
.ERR040  MNOTE 12,'***  IHB415  INVALID SUBRTN OPERAND SPECIFIED'
&QUITSW  SETB  1                       SET MACRO TERMINATION SWITCH
         MEXIT
.EOJ     MEND
