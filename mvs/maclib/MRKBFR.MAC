.* MVS 3.8J PUBLIC DOMAIN
         MACRO
&NAME    MRKBFR &RPL=,&MARK=
         LCLA  &CODE                    CODE ACCUMULATOR
         LCLA  &I                       WORK VARIABLE
         LCLB  &ERR                     ERROR INDICATOR
         AIF   (N'&SYSLIST EQ 0).NOPOS  GO IF NO POSITIONAL OPERANDS
.POSLOOP ANOP
&I       SETA  &I+1                     POINT TO NEXT POS OPERAND
         IDAERMAC 1,&SYSLIST(&I)        ISSUE WARNING MNOTE
         AIF   (&I LT N'&SYSLIST).POSLOOP  GO IF MORE OPERANDS
.NOPOS   ANOP
         AIF   ('&RPL' NE '').ROKY      GO IF RPL SPECIFIED
         IDAERMAC 2,RPL                 ISSUE ERROR MNOTE
&ERR     SETB  1                        INDICATE ERROR
.ROKY    ANOP
         AIF   ('&MARK' NE '').MOKY     GO IF MARK SPECIFIED
         IDAERMAC 2,MARK                ISSUE ERROR MNOTE
&ERR     SETB  1                        INDICATE ERROR
         AGO   .MARKB                   GO AROUND MARK ANALYSIS
.MOKY    ANOP
         AIF   ('&MARK' NE 'OUT').MOUTB  GO IF NOT MARKING FOR OUTPUT
&CODE    SETA  0*256                    INDICATE MARK FOR OUTPUT
         AGO   .MARKB                   GO AROUND MARK ANALYSIS
.MOUTB   ANOP
         AIF   ('&MARK' NE 'RLS').MRLSB  GO IF NOT MARKING FOR RELEASE
&CODE    SETA  4*256                    INDICATE MARK FOR RELEASE
         AGO   .MARKB                   GO AROUND MARK ANALYSIS
.MRLSB   ANOP
         IDAERMAC 3,MARK,&MARK          ISSUE ERROR MNOTE
&ERR     SETB  1                        INDICATE ERROR
.MARKB   ANOP
         AIF   (&ERR).END               GO IF ANY ERRORS
         AIF   ('&NAME' EQ '').NAMEB    GO IF NO NAME
&NAME    DS    0H
.NAMEB   ANOP
         AIF   ('&RPL'(1,1) EQ '(').RPLR  GO IF RPL SPECIFIED AS REG
         LA    1,&RPL                   LOAD RPL ADDRESS
         AGO   .AREG                    GO AROUND REG CODE
.RPLR    ANOP
         AIF   ('&RPL' EQ '(1)').AREG   GO IF RPL ADDRESS ALREADY IN 1
         LR    1,&RPL(1)                LOAD RPL ADDRESS
.AREG    ANOP
&CODE    SETA  &CODE+14                 INDICATE MARK BUFFER
         LA    0,&CODE                  LOAD CODES
         L     15,24(,1)                LOAD ACB ADDRESS FROM RPL
         L     15,8(,15)                LOAD VSAM INTERFACE RTN ADDR
         BALR  14,15                    LINK TO INTERFACE ROUTINE
.END     ANOP
         MEND
