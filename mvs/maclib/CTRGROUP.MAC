.* MVS 3.8J PUBLIC DOMAIN
         MACRO
&NAME    CTRGROUP &OP1,&OP2,&OP3,&ID=,&SROENAB=,&CTINIT=
         GBLA  &IECPCSW(100),&IECPCTN,&IECCTID,&IECCNO,&IECCTOT
.*             IECPCSW(100) VALUES OF HIGH CNTRS FOR EACH AS
.*             IECPCTN  TOTAL NUMBER OF PULSE COUNTERS DEFINED
.*             IECCTID  VALUE OF ID PREVIOUS CTRGROUP MACRO
.*             IECCNO  VALUE OF CTRNO OF PREVIOUS CTRGROUP MACRO
.*             IECCTOT  TOTAL NUMBER OF COUNTERS DEFINED IN CTRGROUP
         GBLA  &IECNDGP(100),&IECPCTS
.*             IECNDGP(1)  NUMBER OF BYTES COUNTER
.*             IECPCTS  ID VALUE OF LAST ASCTR MACRO
         GBLA  &IECSCNO,&IECCHK
.*             IECSCNO   HIGHEST SCHEDULE REFFERRED TO IN CTRGROUPS
.*             IECCHK   COUNTER TO CHECK 504 LIMIT IN CTR TABLE
         GBLB  &IECSEQ(10),&IECSWW(2),&IECSW(9)
.*             IECSEQ(8)  SEQUENCE INDICATOR
.*             IECSWW(2) ON INDICATES NOT FIRST CTRGROUP MACRO
.*             IECSW(9) ON INDICATES PULSE COUNT
         LCLA  &A,&B,&C,&D,&E,&F
         LCLB  &NOCODE
         LCLC  &CHAR
         ACTR  1000
         AIF   (&IECSWW(2)).SKIPONE
&IECSEQ(8) SETB 1
.SKIPONE AIF   (&IECSW(9)).SKIPTWO
         MNOTE 12,'CTRGROUP MACRO NOT VALID BECAUSE PC=YES NOT CODED INX
               CONFIGUR MACRO'
         MEXIT
.SKIPTWO AIF  (&IECSEQ(1) AND &IECSEQ(2) AND &IECSEQ(3) AND &IECSEQ(7) X
               AND &IECSEQ(8) AND NOT&IECSEQ(4) AND NOT&IECSEQ(5)).RRR
.SSS     ANOP
&NOCODE  SETB  1
         MNOTE 12,'MACRO SEQUENCE ERROR'
         AGO   .TTT
.RRR     ANOP
         AIF   (&IECSEQ(6)).SSS
.TTT     ANOP
         AIF   (T'&OP1 EQ 'N').CTRCHK
&CHAR    SETC  T'&OP1
&NOCODE  SETB  1
         MNOTE 12,' REQUIRED OPERAND =CTRNO TYPE ATTRIBUTE=&CHAR       X
               IMPROPER'
         AGO   .SROCHK
.CTRCHK  ANOP
&A       SETA  &OP1
         AIF   (&A GE 1 AND &A LE 63).SROCHKK
&NOCODE  SETB  1
         MNOTE 12,'REQUIRED OPERAND =CTRNO HAS INVALID VALUE'
         AGO   .SROCHK
.SROCHKK ANOP
         AIF   (&ID NE &IECCTID).SROCHK
         AIF   (&A GT &IECCNO).SROCHK
&NOCODE  SETB  1
         MNOTE 12,'CTRNO OPERAND IS OUT OF SEQUENCE'
.SROCHK  ANOP
         AIF   (T'&OP2 EQ 'N' OR T'&OP2 EQ 'O').SROLGN
&CHAR    SETC  T'&OP2
&NOCODE  SETB  1
         MNOTE 12,'SRO OPERAND HAS INVALID TYPE ATTRIBUTE = &CHAR'
         AGO   .CTTCHK
.SROLGN  AIF   (T'&OP2 EQ 'O').CTTCHK
         AIF   (&OP2 GE 0 AND &OP2 LE 15).CTTCHK1
&NOCODE  SETB  1
         MNOTE 12,'SRO OPERAND NOT IN VALID RANGE OF 0 TO 15'
         AGO   .CTTCHK
.CTTCHK1 ANOP
         AIF   (&OP2 LE &IECSCNO).CTTCHK
&IECSCNO SETA  &OP2
.CTTCHK  ANOP
         AIF   (T'&OP3 EQ 'N' OR T'&OP3 EQ 'O').CTTLNG
&CHAR    SETC  T'&OP3
&NOCODE  SETB  1
         MNOTE 12,'CTTEST OPERAND HAS TYPE ATTRIBUTE = &CHAR'
         AGO   .SROECHK
.CTTLNG  ANOP
         AIF   (T'&OP3 EQ 'O').SROECHK
        AIF   (&OP3 GE 0 AND &OP3 LE 15).SROECHK
&NOCODE  SETB  1
         MNOTE 12,'CTTEST OPERAND NOT IN VALID RANGE OF 0 TO 15'
         AGO   .SROECHK
.SROECK  ANOP
         AIF   (&OP3 LE &IECSCNO).SROECHK
&IECSCNO SETA  &OP3
.SROECHK ANOP
         AIF   (T'&SROENAB EQ 'O').CTINCHK
.SROE    AIF   ('&SROENAB' EQ 'YES' OR '&SROENAB' EQ 'NO').CTINCHK
.ERR     ANOP
&NOCODE  SETB  1
         MNOTE 12,'SROENAB=&SROENAB INVALID'
         AGO   .HERE
.CTINCHK ANOP
         AIF   (T'&OP2 NE 'O').THIS
         AIF   ('&SROENAB' EQ 'YES').ERR2
         AGO   .HERE
.THIS    ANOP
&A       SETA  &OP2
         AIF   (&A EQ 0 AND '&SROENAB' EQ 'YES').ERR2
.HERE    ANOP
         AIF   (T'&CTINIT EQ 'O').IDCHK
.CTI     AIF   ('&CTINIT' EQ 'NULL' OR '&CTINIT' EQ 'NCT' OR '&CTINIT' X
               EQ 'UNASP').LOGCHK
.ERR1    ANOP
&NOCODE  SETB  1
         MNOTE 12,'CTINIT =&CTINIT INVALID'
         AGO   .IDCHK
.ERR2    ANOP
&NOCODE  SETB  1
         MNOTE 12,'SROENAB=YES CAN NOT BE CODED IF SRO OPERAND IS 0 OR X
               OMITTED'
         AGO   .HERE
.LOGCHK  ANOP
         AIF   (T'&OP3 NE 'O').WHA
         AIF   ('&CTINIT' EQ 'NCT' OR '&CTINIT' EQ 'UNASP').ERR3
         AGO   .IDCHK
.WHA     ANOP
&A       SETA  &OP3
         AIF   (&A EQ 0 AND '&CTINIT' EQ 'NCT').ERR3
         AIF   (&A EQ 0 AND '&CTINIT' EQ 'UNASP').ERR3
         AGO   .IDCHK
.ERR3    ANOP
&NOCODE  SETB  1
         MNOTE 12,'CTINIT=NULL OR CTINIT=UNASP CAN NOT BE CODED IF CTTEX
               ST OPERAND IS 0 OR OMITTED'
         AGO   .IDCHK
.IDCHK   ANOP
         AIF   (&IECCHK LE 504).IDCKK
         MNOTE 12,'NUMBER OF COUNTERS DEFINED BY CTRGROUP MACROS       X
               EXCEEDS 504'
&NOCODE  SETB  1
.IDCKK   ANOP
         AIF   (T'&ID NE 'O').IDCHK2
.IDCHK1  ANOP
&CHAR    SETC  T'&ID
&NOCODE  SETB  1
         MNOTE 12,'ID OPERAND HAS INVALID TYPE ATTRIBUTE = &CHAR'
         AGO   .CODECHK
.IDCHK2  AIF   (T'&ID NE 'N').IDCHK1
.IDVAL   AIF   (&ID GE 0 AND &ID LE 99).NOTHLOP
&NOCODE  SETB  1
         MNOTE 12,'ID OPERAND IS NOT IN VALID RANGE OF 0 TO 99'
         AGO   .CODECHK
.NOTHLOP ANOP
         AIF   (&ID LT &IECCTID).ERR8
.OMIT    ANOP
         AIF   ((T'&OP2 EQ 'O' OR '&OP2' EQ '0') AND (T'&OP3 EQ 'O' OR X
               '&OP3' EQ '0')).ERR6
         AGO   .VERIFY
.ERR8    ANOP
&NOCODE  SETB  1
         MNOTE 12,'ID OPERAND IS OUT OF SEQUENCE'
         AGO   .OMIT
.ERR6    ANOP
&C       SETA  &ID+1
&D       SETA  &IECPCSW(&C)
&E       SETA  &OP1
&C       SETA  &D-&E
         AIF   (&D EQ &E).DECR
.ERR7    ANOP
&NOCODE  SETB  1
         MNOTE 12,'NEITHER SRO NOR CTTEST SPECIFIED,MACRO IGNORED'
         AGO   .NAMLOOP
.DECR    ANOP
&D       SETA  &E-&IECCNO
&IECPCTN SETA  &IECPCTN-&D
         AGO   .ERR7
.VERIFY  ANOP
&C       SETA  &ID+1
&D       SETA  &IECPCSW(&C)
&E       SETA  &OP1
         AIF   ((&D-&E) GE 0).NAMLOOP
&NOCODE  SETB  1
         MNOTE 12,'CTRNO=&OP1 IS HIGHER THAN HIGHCTR OPERAND OF ASCTR  X
               MACRO WITH ID=&ID'
         AGO   .NAMLOOP
.NAMLOOP ANOP
         AIF   (&ID EQ &IECCTID).HSETUP
&IECCNO  SETA  0
&B       SETA  0
.HSETUP  ANOP
&C       SETA  &ID+1
&D       SETA  &IECPCSW(&C)
.CODECHK ANOP
         AIF   (&NOCODE).NOGEN
         AIF   (&IECSWW(2)).LCTRG
&IECSWW(2) SETB 1
&A       SETA  (2*&IECPCTN)
&A       SETA  &A+61440
         DC    AL2(&A)
         SPACE 1
         SPACE 1
*                       COUNTER TABLE
         SPACE 1
         SPACE 1
IECCTRGR EQU   *
.LCTRG   ANOP
&E       SETA  &OP1
&F       SETA  2*(&D-&E)
&A       SETA  &E-&IECCNO
         AIF   (&A EQ 1).REGENT
&A       SETA  &A-1
&B       SETA  &A
&A       SETA  2*&A
&A       SETA  &A+16384
         DC    AL2(&A)
.LOOP3   ANOP
         AIF   (&B EQ 1).REGENT
         DC    AL2(16384)
&B       SETA  &B-1
         AGO   .LOOP3
.REGENT  ANOP
&IECCNO  SETA  &OP1
&IECCTID SETA  &ID
         AIF   (T'&OP2 NE 'O').SPEC
&A       SETA  0
         AGO   .EOG
.SPEC    ANOP
&A       SETA  &OP2
.EOG     ANOP
&C       SETA  &ID+1
&D       SETA  &IECPCSW(&C)
&E       SETA  &OP1
         AIF   (&D NE &E).EOT
&A       SETA  &A+32
&IECCTOT SETA  &IECCTOT+&E
.EOT     ANOP
         AIF   (&IECCTOT NE &IECPCTN).ENA
&A       SETA  &A+16
.ENA     ANOP
         AIF   ('&SROENAB' NE 'YES').FIN
&A       SETA  &A+128
.FIN     ANOP
         DC    YL1(&A)
         AIF   (T'&OP3 NE 'O').SET1
&A       SETA  0
         AGO   .SET0
.SET1    ANOP
&A       SETA  &OP3
.SET0    ANOP
         AIF   ('&CTINIT' NE 'NCT').UNAS
&A       SETA  &A+64
&A       SETA  &A+128
.UNAS    ANOP
         AIF   ('&CTINIT' NE 'UNASP').IT
&A       SETA  &A+32
&A       SETA  &A+128
.IT      ANOP
         DC    YL1(&A)
         AIF   (&IECCTOT NE &IECPCTN).DONE
&A       SETA  12288
         DC    AL2(&A)
&IECNDGP(1) SETA &IECNDGP(1)+2*&IECPCTS+2*&IECCTOT+8
         AGO   .DONE
.NOGEN   ANOP
         MNOTE 12,'TEXT GENERATION SUPPRESSED'
.DONE    ANOP
         MEND
