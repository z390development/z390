.* MVS 3.8J PUBLIC DOMAIN
         MACRO
&NAME    GREADR &DECB,&TYPE,&DCB,&AREA,&MF=
         LCLA  &WCODE                                            A26234
&WCODE   SETA  0
.*4141,29400,35000                                                 1170
.* A45000,A177000-179000                                      LI A47179
.*       THIS MACRO REWRITEN FOR RELEASE 19               A25397,A26234
         AIF   (T'&TYPE EQ 'O').ABORT1  IF NO TYPE GET OUT    LI A47179
         AIF   (T'&DECB EQ 'O' AND '&MF' EQ 'E').ERRORA          A26234
         AIF   ('&MF' EQ 'E').BUILD                              A26234
.CONT1   AIF   (T'&DECB EQ 'O').CONT10                           A26234
         AIF   ('&DECB'(1,1) EQ '(').BUILD1                      A26234
.CONT10  AIF   (T'&DCB NE 'O').CONT2                             A26234
         AIF   ('&MF' EQ 'L').COO                                A26234
         IHBERMAC   6                                            A26234
&WCODE   SETA  1                                                 A26234
         AGO   .CONT2                                            A26234
.COO     IHBERMAC   178,DCB,0                                    A26234
.CONT2   AIF   (T'&DECB NE 'O').CONT3                            A26234
         IHBERMAC   7                                            A26234
&WCODE   SETA  1                                                 A26234
.CONT3   AIF   (T'&AREA NE 'O').AR                               A26234
         IHBERMAC   178,AREA,0                                   A26234
.AR      AIF   ('&TYPE' EQ 'DEV' OR '&TYPE' EQ 'MIP' OR '&TYPE' EQ 'XYPC
               ' OR '&TYPE' EQ 'SEN').GON                        A26234
         IHBERMAC   178,TYPE,0                                   A26234
         MEXIT
.ABORT1  IHBERMAC  25                                         LI A47179
         MEXIT                                                LI A47179
.ERRORA  IHBERMAC   7                                            A26234
         MEXIT
.GON     AIF   (&WCODE NE 1).DONE
         AIF   ('&MF' EQ 'L').GON1
         AIF   (T'&MF NE 'O').WRNG
.GON1    MEXIT
.ERRORB  IHBERMAC   7                                            A26234
&WCODE   SETA  1                                                 A26234
         AGO   .CONT10                                           A26234
.BUILD1  AIF   ('&MF' EQ 'L').ERRORB                             A26234
.BUILD   ANOP
&NAME    SR    15,15                    CLEAR REGISTER
         AIF   ('&DECB'(1,1) EQ '(').RFORM                       A26234
         LA    1,&DECB                  PARAMETER LIST
         AGO   .RFORM1                                           A26234
.RFORM   AIF   ('&DECB' EQ '(1)').RFORM1                         A26234
         LA    1,0&DECB                 PARAMETER LIST           A26234
.RFORM1  AIF   (T'&TYPE EQ 'O').BDCB                             A26234
.BTYPE1  AIF   ('&TYPE' NE 'MIP').BTYPE2
         ST    15,4(0,1)                CLEAR TYPE AREA IN DECB
         MVI   4(1),X'08'               MIP TYPE TO DECB
         AGO   .BDCB
.BTYPE2  AIF   ('&TYPE' NE 'XYP').BTYPE3
         ST    15,4(0,1)                CLEAR TYPE AREA IN DECB
         MVI   4(1),X'10'               XYP TYPE TO DECB
         AGO   .BDCB
.BTYPE3  AIF   ('&TYPE' NE 'DEV').BTYPE4
         ST    15,4(0,1)                CLEAR TYPE AREA IN DECB
         MVI   5(1),X'18'               DEV AND SENSE TYPE TO DECB
         AGO   .BDCB
.BTYPE4  AIF   ('&TYPE' NE 'SEN').BTYPE5                         A25397
         ST    15,4(0,1)                CLEAR TYPE AREA IN DECB  A25397
         MVI   4(1),X'18'               SENSE TYPE TO DECB       A25397
         AGO   .BDCB                                             A25397
.BTYPE5  IHBERMAC   178,TYPE,N/C                                 A26234
.BDCB    AIF   (T'&DCB EQ 'O').BAREA                             A26234
         AIF   ('&DCB'(1,1) EQ '(').BDCB1                        A26234
         LA    15,&DCB                  DCB ADDRESS
         ST    15,8(0,1)                DCB ADDRESS IN DECB
         AGO   .BAREA
.BDCB1   LA    15,0&DCB                 DCB ADDRESS              A26234
         ST    15,8(0,1)                DCB ADDRESS IN DECB
.BAREA   AIF   (T'&AREA EQ 'O').BEXCP                            A26234
         AIF   ('&AREA'(1,1) EQ '(').BAREA1                      A26234
         LA    15,&AREA                 AREA ADDRESS
         ST    15,12(0,1)               AREA ADDRESS IN DECB
         AGO   .BEXCP
.BAREA1  LA    15,0&AREA                AREA ADDRESS             A26234
         ST    15,12(0,1)               AREA ADDRESS IN DECB
.BEXCP   XC    0(4,1),0(1)              CLEAR ECB                A26234
         L     15,8(0,1)                DCB ADDRESS
         L     15,48(0,15)              GIOCR ADDRESS
         CALL  (15)
         MEXIT
.WRNG    IHBERMAC   54                                           A26234
         MEXIT
.DONE    AIF   ('&MF' EQ 'L').PARM
         AIF   (T'&MF NE 'O').WRNG
         CNOP  0,4                                               A26234
&NAME    BAL   1,*+36                   PARAMETER LIST REGISTER  A26234
         AGO   .PARM1
.PARM    ANOP
&NAME    DS    0F
.PARM1   ANOP
&DECB    DC    F'0'
.TMIP    AIF   ('&TYPE' NE 'MIP').TXYP
         DC    BL2'0000100000000000'
.NDEV    DC    H'0'                                              A26234
         AIF   (T'&DCB EQ 'O').CONTR                             A26234
         AIF   ('&DCB'(1,1) EQ '(').CONTR                        A26234
         DC    A(&DCB)
.CONTR1  AIF   (T'&AREA EQ 'O').NAREA                            A26234
         AIF   ('&AREA'(1,1) EQ '(').NAREA                       A26234
         DC    A(&AREA)
         AGO   .FILL                                             A26234
.NAREA   DC    F'0'                                              A26234
.FILL    DC    F'0'                                              A26234
         DC    F'0'
         DC    F'0'
         DC    F'0'
         AIF   ('&MF' EQ 'L').GON1                               A26234
         AIF   (T'&AREA EQ 'O').FINE2                            A26234
         AIF   ('&AREA'(1,1) NE '(').FINE2                       A26234
         ST    &AREA(1),12(1)           PLACE AREA ADDR. IN LIST A26234
.FINE2   AIF   (T'&DCB EQ 'O').FINE1                             A26234
         AIF   ('&DCB'(1,1) NE '(').FINE1                        A26234
         ST    &DCB(1),8(1)             PLACE DCB ADDR. IN LIST  A26234
         L     15,48&DCB                GET GIOCR'S ENTRY POINT  A26234
         AGO   .FINE                                             A26234
.FINE1   L     15,&DCB+48               GET GIOCR'S ENTRY POINT  A26234
.FINE    XC    0(4,1),0(1)              CLEAR ECB                A26234
         CALL  (15)                                              A26234
         MEXIT
.CONTR   DC    F'0'                                              A26234
         AGO   .CONTR1
.TXYP    AIF   ('&TYPE' NE 'XYP').TDEV
         DC    BL2'0001000000000000'
         AGO   .NDEV
.TDEV    AIF   ('&TYPE' NE 'DEV').TSEN                           A25397
         DC    BL2'0000000000011000'
         AGO   .NDEV
.TSEN    AIF   ('&TYPE' NE 'SEN').NOTYPE                         A25397
         DC    BL2'0001100000000000'                             A25397
         AGO   .NDEV                                             A25397
.NOTYPE  DC    H'0'                                              A26234
         AGO   .NDEV
         MEND
