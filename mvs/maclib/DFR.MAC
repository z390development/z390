.* MVS 3.8J PUBLIC DOMAIN
         MACRO
&NAME    DFR   &NATNHP=NO,                                             X
               &FONT=,                                                 X
               &BCHSER=,                                               X
               &BCH=,                                                  X
               &ERASE=NO,                                              X
               &REJECT=,                                               X
               &EDCHAR=,                                               X
               &CHRSET=
         GBLA  &IHBFNT
         GBLB  &IHBDFC,&IHBNAT
         LCLA  &SERBCH,&A1,&B1,&C1,&CTR,&TEMP,&ERS,&CHST
         LCLB  &OMTD
         LCLC  &RJT
&IHBDFC  SETB  1                    INIT VALID DFR SWITCH
.* STATEMENTS .DD100 TO .DD150 CHECK SPECIFICATION OF FONT KEYWORD
.DD100   AIF   ('&FONT' NE '').DD110     FONT KEYWORD SPECIFIED
.DD105   MNOTE 12,'***  IHB393  INVALID OR OMITTED FONT OPERAND SPECIFIX
               ED. NO FORMAT RECORD GENERATED'
&IHBDFC  SETB  0                    INDICATE INVALID DFR
         AGO   .DD150               SKIP FONT CHECK
.DD110   ANOP
&IHBFNT  SETA  1
         AIF   ('&FONT' EQ 'MRKA').DD150
&IHBFNT  SETA  2
         AIF   ('&FONT' EQ 'MRKB').DD150
&IHBFNT  SETA  9
         AIF   ('&FONT' EQ 'NUMA').DD150
&IHBFNT  SETA  10
         AIF   ('&FONT' EQ 'ANA1').DD150
&IHBFNT  SETA  11
         AIF   ('&FONT' EQ 'ANA2').DD150
&IHBFNT  SETA  5
         AIF   ('&FONT' EQ 'NUMB').DD150
&IHBFNT  SETA  6
         AIF   ('&FONT' EQ 'ANB1').DD150
&IHBFNT  SETA  12
         AIF   ('&FONT' EQ 'NHP1').DD150
&IHBFNT  SETA  14
         AIF   ('&FONT' EQ 'NHP2').DD150
&IHBFNT  SETA  13
         AIF   ('&FONT' NE 'GOTH').DD105
.* STATEMENTS .DD150 TO .DD200 CHECK BCHSER AND BCH KEYWORDS
.DD150   AIF   ('&BCHSER' EQ '').DD180     BCHSER SPECIFIED
&TEMP    SETA  1                           INDICATE BCHSER SPECIFIED
         AIF   ('&BCHSER' GE '1' AND '&BCHSER' LE '3').DD175
         MNOTE 4,'***  IHB394  INVALID ''BCHSER'' OPERAND. ASSUME NO NUX
               MBERING'
         AGO   .DD180                      SKIP SETTING
.DD175   ANOP
&SERBCH  SETA  &BCHSER*16                  SETUP FOR DC GENERATION
.DD180   AIF   ('&BCH' EQ '').DD200        BCH SPECIFIED
         AIF   (&TEMP EQ 1).DD185   BCHSER SPECIFIED, YES BOTH SPCFD
         AIF   ('&BCH' GE '1' AND '&BCH' LE '3').DD190
.DD185   MNOTE 4,'***  IHB395  INVALID ''BCH'' OPERAND, ASSUME NO NUMBEX
               RING'
&SERBCH  SETA  0                    SETUP FOR DC GENERATION
         AGO   .DD200               CONTINUE MAIN FLOW
.DD190   ANOP
&SERBCH  SETA  &BCH*16+64           SETUP FOR DC GENERATION
.DD200   AIF   (&IHBDFC EQ 0).DD210 VALID DFR, YES GEN DC FOR FR LENGTH
&NAME    DC    AL2(IHBEND-IHBBEG)  FORMAT RECORD LENGTH
.* STATEMENTS .DD210 TO .DD260 CHECK REJECT KEYWORD
.DD210   AIF   ('&REJECT' EQ '').DD250  REJECT SPECIFIED
         AIF   (K'&REJECT NE 1).DD220   REJECT EQ 1 CHAR
&RJT     SETC  '''&REJECT'''            YES,SETUP FOR DC GENERATION
         AGO   .DD235                   GO TO GEN REJECT DC
.DD220   AIF   (K'&REJECT EQ 2).DD240   REJECT EQ 2 CHARS
         AIF   (K'&REJECT EQ 4 AND ('&REJECT'(2,2) EQ '''''' OR        X
               '&REJECT'(2,2) EQ '&&')).DD230  CHK SPECIAL CASES
         AIF   (K'&REJECT NE 3 OR '&REJECT'(1,1) NE '''' OR            X
               '&REJECT'(3,1) NE '''').DD240   ENCLOSING QUOTES
.DD230   ANOP
&RJT     SETC  '&REJECT'             SETUP FOR DC GENERATION
.DD235   AIF   (&IHBDFC EQ 0).DD260  VALID DFR, YES GEN DC
IHBBEG   DC    C&RJT REJECT CHARACTER CODE
         AGO   .DD260                CONTINUE MAIN FLOW
.DD240   MNOTE 4,'***  IHB396  REJECT CHARACTER INVALID. ''3F'' USED'
.DD250   AIF   (&IHBDFC EQ 0).DD260  VALID DFR, YES GEN DEFAULT REJECT
IHBBEG   DC    AL1(63) REJECT CHARACTER CODE
.* STATEMENTS .DD260 THRU .DD270 CHECK ERASE KEYWORD
.DD260   AIF   ('&ERASE' EQ '' OR '&ERASE' EQ 'NO').DD290 VALID ERASE
         AIF   ('&ERASE' NE 'YES').DD270    VALID ERASE
&ERS     SETA  128                          SETUP FOR DC  GENERATION
         AGO   .DD290                       CONTINUE MAIN FLOW
.DD270   MNOTE 4,'***  IHB397  INVALID ERASE SPECIFIED. ''NO'' ASSUMED'
.* STATEMENTS .DD290 THRU .DD300 CHECK CHRSET KEYWORD
.DD290   AIF   ('&CHRSET' EQ '').DD310   CHRSET SPECIFIED
         AIF   ('&CHRSET' LT '0' OR '&CHRSET' GT '5').DD300
&CHST    SETA  &CHRSET                SETUP FOR DC GENERATION
         AGO   .DD310                 CONTINUE MAIN FLOW
.DD300   MNOTE 4,'***  IHB398  INVALID CHRSET SPECIFIED. ''0'' ASSUMED'
.DD310   AIF   (&IHBDFC EQ 0).DD315   VALID DFR,YES GEN DC S
         DC    AL1(IHBFONT) FONT SELECT
         DC    AL1(&ERS+&SERBCH+&CHST) G/C ERASE, B/S NO., CHRSET
.* STATEMENTS .DD315 THRU .DD317 CHECK NATNHP KEYWORD
.DD315   ANOP
&IHBNAT  SETB  0                           INITIALIZE FOR DC GEN
         AIF   ('&NATNHP' EQ '' OR '&NATNHP' EQ 'NO').DD317
         AIF   ('&NATNHP' EQ 'YES').DD316  VALID NATNHP
         MNOTE 4,'***  IHB399  INVALID NATNHP SPECIFIED. ''NO'' ASSUMEDX
               '
         AGO   .DD317                      CONTINUE MAIN FLOW
.DD316   ANOP
&IHBNAT  SETB  1                 SETUP FOR DC GENERATION
.* STATEMENTS .DD317 THRU .DD400 CHECK EDCHAR KEYWORD
.DD317   ANOP
&A1      SETA  1
&B1      SETA  N'&EDCHAR         FIND NUMBER OF EDCHAR OPERANDS
         AIF   (&B1 LE 6).DD320  MORE THAN 6 THEN ERROR
         MNOTE 4,'***  IHB400  MORE THAN 6 EDCHAR''S SPECIFIED. FIRST 6X
                USED'
&B1      SETA  6                 DEFAULT TO 6 OPERANDS
.DD320   AIF   ('&EDCHAR(&A1)' EQ '').DD370 SUBOPERAND SPECIFIED
         AIF   (&OMTD EQ 0).DD330  NEXT MNOTE GEND BEFORE
         MNOTE 4,'***  IHB401  INVALID EDCHAR SUBOPERAND. SUBOPERAND IGX
               NORED'
&OMTD    SETB  0                 INDICATE MESSAGE GENERATED
.DD330   AIF   (K'&EDCHAR(&A1) NE 1).DD340 SUBOPERAND EQ 1 CHAR
         AIF   (&IHBDFC EQ 0).DD350        IF VALID DFR THEN GEN DC
         DC    CL1'&EDCHAR(&A1)' EDIT CHARACTER
         AGO   .DD350            GO CHECK NEXT EDCHAR
.DD340   AIF   (K'&EDCHAR(&A1) EQ 2).DD360  EDCHAR EQ 2 CHARS
         AIF   (K'&EDCHAR(&A1) EQ 4 AND ('&EDCHAR(&A1)'(2,2) EQ '''''' X
               OR '&EDCHAR(&A1)'(2,2) EQ '&&')).DD345 SPECIAL CASE CHK
         AIF   (K'&EDCHAR(&A1) NE 3 OR '&EDCHAR(&A1)'(1,1) NE '''' OR  X
               '&EDCHAR(&A1)'(3,1) NE '''').DD360 ENCLOSING QUOTES
.DD345   AIF   (&IHBDFC EQ 0).DD350  IF VALID DFR THEN GEN DC
         DC    CL1&EDCHAR(&A1) EDIT CHARACTER
.DD350   ANOP
&C1      SETA  &C1+1                 INCREMENT CTR OF GEND EDCHARS
         AGO   .DD380                CONTINUE EDCHAR CHECK
.DD360   MNOTE 4,'***  IHB401  INVALID EDCHAR SUBOPERAND. SUBOPERAND IGX
               NORED'
         AGO   .DD380                CONTINUE EDCHAR CHECK
.DD370   ANOP
&OMTD    SETB  1                     SET OMITTED INDICATOR
.DD380   ANOP
&A1      SETA  &A1+1                  INCR NO OF EDCHARS CHECKED
         AIF   (&A1 LE &B1).DD320     ALL OPERANDS CHECKED
         AIF   (&IHBDFC EQ 0).DD400   IF NOT VALID DFR THEN EXIT
.DD390   AIF   (&C1 GE 6).DD400       IF 6 EDCHARS GEND THEN EXIT
&C1      SETA  &C1+1                  GEN REMAINING DC S TO TOTAL 6
         DC    AL1(0) UNUSED EDIT CHARACTER
         AGO .DD390
.DD400   ANOP
         MEXIT
         MEND

