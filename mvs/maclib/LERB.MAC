.* MVS 3.8J PUBLIC DOMAIN
         MACRO
&NAME    LERB  &NLINES,&OP2
.*130300400200,00540270                                            000A
         LCLA  &LOOP,&N,&M ARITHMETIC COUNTERS AND INDICES
         LCLA  &DEFAULT(4) ARITHMETIC DEFAULT VALUES OF THRESHOLD
         LCLA  &THRESH(4) ARITHMETIC CURRENT VALUES OF THRESHOLD
         LCLC  &LC       CHARACTER SCRATCH VARIABLE
         AIF   (T'&NAME NE 'O').NAMEIN
         MNOTE 12,'***  IHB076  MACRO NAME FIELD BLANK-NAME REQUIRED'
         MEXIT
.*
.NAMEIN  AIF   (N'&SYSLIST GT 0).NSLOK
         MNOTE 12,'***  IHB004  REQUIRED OPERAND(S) NOT SPECIFIED'
         MEXIT
.*
.NSLOK   AIF   (T'&NLINES EQ 'N').NLTOK
&LC      SETC  T'&NLINES PICK UP TYPE FOR MNOTE
         MNOTE 12,'***  IHB002  INVALID NLINES OPERAND SPECIFIED-&NLINEX
               S'                                                  000A
         MEXIT
.*
.NLTOK   ANOP
&LOOP    SETA  &NLINES CONVERT ABSEXP TO VALUE
         AIF   (&LOOP GE 1 AND &LOOP LE 255).LVOK
         MNOTE 12,'***  IHB002  INVALID NLINES OPERAND SPECIFIED-&NLINEX
               S'                                                  000A
         MEXIT
.LVOK    ANOP
&DEFAULT(1) SETA 255
&DEFAULT(2) SETA 10
&DEFAULT(3) SETA 5
&DEFAULT(4) SETA 5
&THRESH(1) SETA &DEFAULT(1)
&THRESH(2) SETA &DEFAULT(2)
&THRESH(3) SETA &DEFAULT(3)
&THRESH(4) SETA &DEFAULT(4)
&N       SETA  1 PRIME OUTER SYSLIST INDEX
&NAME    DC    0F'0' ALIGN TABLE AND ATTACH NAME
         SPACE 1
.*
.* HEAD OF OUTER SYSLIST LOOP
.*
.OUTLOOP ANOP
&N       SETA  &N+1 INCREMENT OUTER INDEX
&M       SETA  0    CLEAR INNER INDEX
.*
.* HEAD OF INNER SYSLIST LOOP
.*
.INLOOP  ANOP
&M       SETA  &M+1 INCREMENT INNER INDEX
         AIF   (T'&SYSLIST(&N,&M) EQ 'O').TYPO
         AIF   (T'&SYSLIST(&N,&M) EQ 'N').TYPN
&LC      SETC  T'&SYSLIST(&N,&M) PICK UP TYPE FOR MNOTE
         MNOTE 12,'***  IHB002  INVALID OPERAND(&N,&M) OPERAND SPECIFIEX
               D-&SYSLIST(&N,&M)'
         AGO   .SELDFLT
.*
.TYPO    AIF   (&THRESH(&M) LE &THRESH(1)).TESTM
         MNOTE 12,'***  IHB002  INVALID UNCODED OPERAND(&N,&M) OPERAND X
               SPECIFIED-&THRESH(&M)'
         AGO   .SELDFLT
.*
.TYPN    ANOP
&THRESH(&M) SETA &SYSLIST(&N,&M) CONVERT SUB-OPERAND TO VALUE
         AIF   (&THRESH(&M) GE 1 AND &THRESH(&M) LE 255).VALOK
         MNOTE 12,'***  IHB002  INVALID OPERAND(&N,&M) OPERAND SPECIFIEX
               D-&SYSLIST(&N,&M)'
         AGO   .CKMEQ1
.*
.VALOK   AIF   (&THRESH(&M) LE &THRESH(1)).TESTM
         MNOTE 12,'***  IHB002  INVALID OPERAND(&N,&M) OPERAND SPECIFIEX
               D-&THRESH(&M)'
         AGO   .SELDFLT
.*
.CKMEQ1  AIF   (&M EQ 1).DFLT
.SELDFLT AIF   (&DEFAULT(&M) LE &THRESH(1)).DFLT
         MNOTE *,'CURRENT TRNSMCT=&THRESH(1) USED.'
&THRESH(&M) SETA &THRESH(1)
         AGO   .TESTM
.*
.DFLT    MNOTE *,'DEFAULT=&DEFAULT(&M) USED.'
&THRESH(&M) SETA &DEFAULT(&M)
.*
.TESTM   AIF   (&M LT 4).INLOOP GO BACK TO CHECK NEXT SUB-OPERAND
*        ACCUMULATORS
         DC    F'0' TRANSMISSIONS
         DC    H'0' DATA CHECKS
         DC    H'0' INTERVENTIONS
         DC    H'0' NONTEXT TIMEOUTS
*        COUNTERS
         DC    X'0' TRANSMISSIONS
         DC    X'0' DATA CHECKS
         DC    X'0' INTERVENTIONS
         DC    X'0' NONTEXT TIMEOUTS
*        THRESHOLDS
         DC    YL1(&THRESH(1)) TRANSMISSIONS
         DC    YL1(&THRESH(2)) DATA CHECKS
         DC    YL1(&THRESH(3)) INTERVENTIONS
         DC    YL1(&THRESH(4)) NONTEXT TIMEOUTS
*        RESERVED
         DC    XL2'0'
         SPACE 1
.*
&LOOP    SETA  &LOOP-1
         AIF   (&LOOP GT 0).OUTLOOP
         MEND

