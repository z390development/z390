.* MVS 3.8J PUBLIC DOMAIN
         MACRO
&NAME    CAMLST &PAR1,&PAR2,&PAR3,&PAR4,&OPT1,&OPT2,&OPT3,&DSCBTTR=,   *
               &DUMMY=
         LCLA  &B1,&B2,&B3,&CTR
         LCLB  &P(17),&OP(3),&D(4),&NOGO                            TSO
         AIF   ('&PAR1' EQ '').E1
         AIF   ('&PAR2' EQ '').E2
.R1      ANOP
&P(1)    SETB  ('&PAR1' EQ 'CAT' OR '&PAR1' EQ 'CATBX')             TSO
&P(2)    SETB  ('&PAR1' EQ 'RECAT')
&P(3)    SETB  ('&PAR1' EQ 'UNCAT' OR '&PAR1' EQ 'UCATDX')          TSO
&P(4)    SETB  ('&PAR1' EQ 'BLDX' OR '&PAR1' EQ 'CATBX')            TSO
&P(5)    SETB  ('&PAR1' EQ 'BLDG')
&P(6)    SETB  ('&PAR1' EQ 'BLDA')
&P(7)    SETB  ('&PAR1' EQ 'LNKX')
&P(8)    SETB  ('&PAR1' EQ 'DLTX' OR '&PAR1' EQ 'UCATDX')           TSO
&P(9)    SETB  ('&PAR1' EQ 'DLTA')
&P(10)   SETB  ('&PAR1' EQ 'DRPX')
&P(11)   SETB  ('&PAR1' EQ 'NAME')
&P(12)   SETB  ('&PAR1' EQ 'BLOCK')
&P(13)   SETB  ('&PAR1' EQ 'SEARCH')
&P(14)   SETB  ('&PAR1' EQ 'SEEK')
&P(15)   SETB  ('&PAR1' EQ 'SCRATCH')
&P(16)   SETB  ('&PAR1' EQ 'RENAME')
&P(17)   SETB  ('&DSCBTTR' NE'')
&CTR     SETA  &P(1)+&P(2)+&P(3)+&P(4)+&P(5)+&P(6)+&P(7)+&P(8)+&P(9)
&CTR     SETA  &CTR+&P(10)+&P(11)+&P(12)+&P(13)+&P(14)+&P(15)+&P(16)
         AIF   (&CTR EQ 0).E3                                       TSO
&D(1)    SETB  ('&PAR3' NE '')
&D(2)    SETB  ('&PAR4' NE '')
         AIF   (NOT &D(1) AND (&P(13) OR &P(14) OR &P(16))).E4
.R3      AIF   (&D(1) AND &P(15)).E5
         AIF   (NOT &D(2) AND (&P(1) OR &P(2) OR &P(6) OR &P(7) OR &P(1X
               1) OR &P(12) OR &P(13) OR &P(14) OR &P(15) OR &P(16))).E
         AIF   (&P(1) AND &P(4)).R2                                 TSO
         AIF   (&D(2) AND (&P(3) OR &P(8) OR &P(9) OR &P(10))).E6
         AIF   (&D(2) AND (&P(3) OR &P(4) OR &P(5) OR &P(8) OR &P(9) ORX
               &P(10))).E6
.R2      AIF   ('&OPT1' EQ '').CONTA
         AIF   ('&OPT1' NE 'DELETE').E7
&OP(1)   SETB  1
         AIF   (NOT &P(5)).E7
.CONTA   AIF   ('&OPT2' EQ '').CONTB
&OP(2)   SETB  ('&OPT2' EQ 'EMPTY')
&OP(3)   SETB  ('&OPT2' EQ 'OVRD')
         AIF   (&OP(2)+&OP(3)  NE 1).E8
         AIF   (NOT &P(5) AND &OP(2) OR(NOT &P(15) AND &OP(3))).E8
.CONTB   AIF   ('&OPT3' EQ '' AND &P(5)).E10
         AIF   ('&OPT3' NE '' AND NOT &P(5)).E11
.CONTB1  AIF   (&P(17) AND NOT (&P(1) OR &P(2))).E12
         AIF   (&NOGO EQ 1).END
&B1      SETA  128*&D(1)+64*&D(2)+32*&P(1)+20*&P(2)+8*&P(3)+2*&P(12)
&D(4)    SETB  (&P(3) OR &P(8))                                     TSO
&B1      SETA  &B1+4*(&P(11)+&P(5)+&P(6)+&P(7)+&D(4)+&P(9)+&P(10))  TSO
&D(3)    SETB  (&P(1) OR &P(4))                                     TSO
&B1      SETA  &B1+4*&D(3)+&P(13)+&P(15)+&P(16)                     TSO
&B2      SETA  128*&P(14)+64*&P(4)+32*&P(5)+16*&P(6)+8*&P(7)+4*&P(8)+2*X
               &P(17)+&P(9)
&B3      SETA  128*&P(10)+64*&P(15)+32*&P(16)+64*&OP(1)+16*&OP(3)+8*&OPX
               (2)
&NAME    DS    0F                                ALIGN ON FULL WORD
         DC    AL1(&B1)                          THREE BYTES OF FLAGS
         DC    AL1(&B2)                          INDICATING THE FUNC-
         DC    AL1(&B3)                          TION TO BE PERFORMED
         AIF   (&P(5)).CONTC
         DC    AL1(0)                            NO OPTION THREE
         AGO   .CONTD
.CONTC   DC    AL1(&OPT3)                        NO. OF ENTRIES
.CONTD   DC    A(&PAR2)                          PARAMETER TWO
         AIF   (&D(1)).CONTE
         DC    A(0)                              PARAM. THREE OMMITTED
         AGO   .CONTF
.CONTE   DC    A(&PAR3)                          PARAMETER THREE
.CONTF   AIF   (NOT &D(2)).END
         DC    A(&PAR4)                          PARAMETER FOUR
         AIF   (NOT &P(17)).END
         DC    A(&DSCBTTR)                       PARAMETER FIVE
         MEXIT
.E       IHBERMAC 5                     PAR4 MISSING
         MEXIT
.E1      IHBERMAC 2                     PAR1 MISSING
         MEXIT
.E2      IHBERMAC 3                     PAR2 MISSING
&NOGO    SETB  1
         AGO   .R1
.E3      IHBERMAC 36,,&PAR1             PAR1 INVALID
         MEXIT
.E4      IHBERMAC 4                     PAR3 MISSING
&NOGO    SETB  1
         AGO   .R3
         MEXIT
.E5      IHBERMAC 60,,&PAR3             PAR3 W/SCRATCH
         MEXIT
.E6      IHBERMAC 39,,&PAR4             PAR4 NOT ALLOWED
&NOGO    SETB  1
         AGO   .R2
.E7      IHBERMAC 50,,&OPT1             OPTN1 INVALID
&NOGO    SETB  1
         AGO   .CONTA
.E8      IHBERMAC 51,,&OPT2             OPTN2 INVALID
&NOGO    SETB  1
         AGO   .CONTB
.E10     IHBERMAC 52                    OPTN3 MISSING
         MEXIT
.E11     IHBERMAC 53,,&OPT3             OPTN3 INVALID
&NOGO    SETB  1
         AGO  .CONTB1
.E12     IHBERMAC 54,,DSCBTTR           DSCBTTR NOT ALLOWED.
.END     MEND
