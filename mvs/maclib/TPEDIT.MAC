.* MVS 3.8J PUBLIC DOMAIN
         MACRO
&NAME    TPEDIT &EDIT=EDITD,&RECFM=V,&ERROPT=IGNORE,&VERCHK=NOCHK,     X
               &REPLACE=X'19',&BUFFER=NO,&MINLN=
         LCLA  &A
         LCLB  &SW(6),&B
         LCLC  &C
&SW(1)   SETB  ('&EDIT' EQ 'EDITR')
         AIF   ('&EDIT' EQ 'EDITD' OR '&EDIT' EQ 'EDITR').EDITOK
&SW(1)   SETB  0
         MNOTE 4,'EDIT=&EDIT INCORRECTLY SPECIFIED,DEFAULT ASSUMED'
.EDITOK  ANOP
&SW(2)   SETB  ('&RECFM' EQ 'V')
         AIF   ('&RECFM' EQ 'V' OR '&RECFM' EQ 'U').RECFMOK
&SW(2)   SETB  0
         MNOTE 4,'RECFM=&RECFM INCORRECTLY SPECIFIED,DEFAULT ASSUMED'
.RECFMOK ANOP
&SW(3)   SETB  ('&ERROPT' EQ 'IGNORE')
&SW(4)   SETB  ('&VERCHK' EQ 'VOKCHK')
         AIF   ('&VERCHK' EQ 'NOCHK' OR ('&VERCHK' EQ 'VOKCHK' AND NOT X
               &SW(3))).VOK
&SW(4)   SETB  0
         MNOTE 4,'VERCHK=&VERCHK INCORRECTLY SPECIFIED,DEFAULT ASSUMED'
.VOK     ANOP
&SW(5)   SETB  ('&BUFFER' EQ 'YES')
         AIF   ('&BUFFER' EQ 'NO' OR '&BUFFER' EQ 'YES').BUFOK
&SW(5)   SETB  0
         MNOTE 4,'BUFFER=&BUFFER INCORRECTLY SPECIFIED,DEFAULT ASSUMED'
.BUFOK   AIF   (T'&REPLACE EQ 'N').TYPOK
&SW(6)   SETB  1
&C       SETC  T'&REPLACE
         MNOTE 4,'REPLACE=&REPLACE TYPE ATTRIBUTE=&C IMPROPER,         X
               DEFAULT ASSUMED'
         AGO   .REPOK
.TYPOK   ANOP
&A       SETA  &REPLACE
&B       SETB  (&A EQ 0 OR &A EQ 17 OR &A EQ 18 OR &A EQ 24 OR &A EQ   X
               29 OR &A EQ 30 OR &A EQ 60 OR &A EQ 113 OR &A EQ 114)
         AIF   (&B).TEST
&B       SETB  (&A EQ 115 OR &A EQ 116 OR &A EQ 117 OR &A EQ 118 OR &A X
               EQ 119 OR &A EQ 120)
.TEST    AIF   (NOT&B).REPOK
         MNOTE 4,'WARNING - REPLACE CHARACTER SPECIFIED IS ONE OF THE  X
               SPECIAL PURPOSE 50-MDI CODES'
.REPOK   AIF   (T'&MINLN EQ 'N' OR T'&MINLN EQ 'U').TYPMNOK
&C       SETC  T'&MINLN
&A       SETA  0
         MNOTE 4,'MINLN TYPE ATTRIBUTE=&C IMPROPER'
         AGO   .BY1
.TYPMNOK ANOP
&A       SETA  &MINLN
.BY1     ANOP
         CNOP  0,4
&NAME    BAL   0,IECA&SYSNDX
            DC B'&SW(1)&SW(2)&SW(4)&SW(5)&SW(3)000' MACRO OPTION IND
         AIF   (&SW(6)).DCX19
         DC    &REPLACE *          REPLACE CHARACTER
*  IF THE ABOVE STATEMENT IS IN ERROR THE REPLACE OPERAND WAS
*   INCORRECTLY CODED
         AGO   .X19
.DCX19   DC    X'19' *             REPLACE CHARACTER
.X19     DC    H'&A' *             MINIMUM RECORD LENGTH
         AIF   (&SW(3)).V0
         DC    V(&ERROPT)          USER ERROR EXIT ROUTINE
         AGO   .BY
.V0      DC    A(0) *              NULL USER ERROR EXIT ROUTINE
.BY      ANOP
IECC&SYSNDX DC V(IECTEDIT)         EDIT LOGIC MODULE
IECA&SYSNDX L  15,IECC&SYSNDX
         BALR  14,15 *             LINK TO EDIT LOGIC
         MEND
