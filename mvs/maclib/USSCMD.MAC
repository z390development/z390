.* MVS 3.8J PUBLIC DOMAIN
* /* START OF SPECIFICATIONS ****
*
*01*  MODULE-NAME = USSCMD
*
*01*  DESCRIPTIVE-NAME = USS ALTERNATE COMMAND DEFINITION MACRO
*
*
*01*  STATUS = RELEASE 2
*
*01*  FUNCTION = THIS MACRO IS USED TO GENERATE A VPB (CMD PROCESSING
*     BLOCK) WITH INFO REQUIRED FOR AN ALTERNATE COMMAND
*     THE BLOCK CONTAINS 4 POINTERS, CHAIN, CHARACTER STRING,
*     REPLACEMENT STRING, 1ST PARM PROCESSING BLOCK(OPTIONAL)
*
*02*    CHARACTER-CODE-DEPENDENCIES = NONE
*
*02*    DEPENDENCIES= NONE
*
*02*    RESTRICTIONS = NONE
*
*02*    REGISTER-CONVENTIONS = NONE
*
*02*    PATCH-LABEL = NONE
*
*01*  MODULE-TYPE = MACRO
*
*02*    PROCESSOR = ASSEM-370R
*
*02*    MACRO-SIZE = 35 STATEMENTS
*
*02*    ATTRIBUTES = LIST-EXEC
*
*01*  ENTRY-POINT = NA
*
*01*  EXIT-NORMAL = NA
*
*01*  EXIT-ERROR = NA
*
*01*  EXTERNAL-REFERENCES = NONE
*
*01*  TABLES = VPB (USS COMMAND PROCESSING BLOCK)
*
*01*  INNER-MACROS = USSEND
*
*01*  SYSTEM-MACROS = NONE
*
*01*  CHANGE-ACTIVITY = AS-FOLLOWS
*                       NEW MACRO - FIRST RELEASE   32  DOS/VS
*                                                   04  OS/VS1
*                                                   03  OS/VS2
*            APAR OZ26263  ADDITIONAL VALIDITY CHECKS        @ZA26263
**** END OF SPECIFICATIONS ***/
         MACRO
&LABEL   USSCMD &CMD=,&REP=,&FORMAT=PL1
         GBLC  &Q                 USSEND OUTPUT VARIABLE
         GBLC  &T                 USSEND OUTPUT VARIABLE
         GBLC  &N                 STATEMENT LABEL PREFIX
         GBLB  &B                 SWITCH FOR USSCMD SPECIFIED
         GBLB  &Z                 SWITCH FOR USSTAB SPECIFIED
         LCLC  &R                 INTERNAL LABEL VARIABLE
         LCLC  &F                 INTERNAL FORMAT VARIABLE
         AIF   (&Z EQ 1).SQ005
         SPACE 1
         MNOTE 4,'USSTAB MACRO NOT SPECIFIED'
.SQ005   ANOP
         AIF   ('&CMD' NE '').SQ010
         SPACE 1
         MNOTE 4,'&N.001 CMD= PARAMETER OMITTED'
.SQ010   ANOP
&F       SETC  '80'               SET FOR FORMAT=BAL
         AIF   ('&FORMAT' EQ 'BAL').SQ020
&F       SETC  '00'               SET FOR FORMAT=PL1
         AIF   ('&FORMAT' EQ 'PL1').SQ020
         SPACE 1
         MNOTE 4,'&N.000 FORMAT= PARAMETER INVALID'
.SQ020   USSEND INC=P             INCREMENT USSPARM COUNTER
&R       SETC  '&Q'               SAVE USSPARM COUNTER
         AIF   (&B EQ 0).SQ030
         SPACE 1
&N.P&T   EQU   0                  END USSPARM BLOCK QUEUE
.SQ030   SPACE 1
&B       SETB  1                  INDIC USSCMD SPECIFIED
&LABEL   DC    0F'0'              ALIGN ON FULLWORD BOUNDARY
         USSEND INC=C             INCREMENT USSCMD COUNTER
&N.C&T   DC    A(&N.C&Q)          ADDR NEXT USSCMD BLOCK
         DC    X'&F',AL3(&N.V&T)  ADDR VERB CHAR STRING
         DC    A(&N.R&T)          ADDR REPLACEMENT STRING
         DC    A(&N.P&R)          ADDR FIRST USSPARM BLOCK
&N.V&T   USSEND GEN=&CMD,NAME=&N.W&T
&N.R&T   USSEND GEN=&REP,NAME=&N.S&T,OMIT=&N.V&T
         SPACE 1
         MEND

