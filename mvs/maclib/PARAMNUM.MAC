.* MVS 3.8J PUBLIC DOMAIN
         MACRO
&NAME    PARAMNUM   &PLN=,&PARMLST=
         GBLA  &IECPARN,&IECNDGP(100)
.*             IECPARN  PARAMETER LIST NUMBER OF LAST MACRO
.*             IECNDGP(1)   TOTAL BYTES COUNTER
         GBLB  &IECSW(9),&IECSEQ(10),&IECSQ(7),&IECGDSW
.*             IECSQ(3)  SEQUENCE INDICATOR FOR PARAMNUM MACRO
.*             IECGDSW   INDICATES GDU=YES CODED IN CONFIGUR MACRO
         LCLA  &A
         LCLB  &NOCODE
         LCLC  &CHAR
         ACTR  1000
               AIF  (&IECSEQ(1) AND &IECSEQ(2) AND &IECSQ(1) AND &IECSQX
               (2) AND NOT &IECSEQ(4) AND NOT &IECSEQ(5)).SEQ1
         AGO   .SEQ2
.SEQ1    ANOP
         AIF   (NOT &IECSEQ(6) AND NOT &IECSEQ(10) AND NOT &IECSQ(4)   X
               AND NOT &IECSQ(5) AND NOT &IECSQ(6)).SEQOK
.SEQ2    ANOP
&NOCODE  SETB  1
         MNOTE 12,'MACRO SEQUENCE ERROR'
.SEQOK   ANOP
         AIF   (&IECSQ(7)).SEQ2
         AIF   (&IECGDSW).SWOK
&NOCODE  SETB  1
         MNOTE 12,'GDU=YES MUST BE CODED IN CONFIGUR MACRO'
.SWOK    ANOP
         AIF   (T'&PLN NE 'O').PLN1
&NOCODE  SETB  1
         MNOTE 12,'PLN OPERAND CAN NOT BE OMITTED'
         AGO   .PARLST1
.PLN1    ANOP
         AIF   (T'&PLN EQ 'N').PLN2
&NOCODE  SETB  1
         MNOTE 12,'PLN OPERAND HAS INVALID TYPE ATTRIBUTE'
         AGO   .PARLST1
.PLN2    ANOP
         AIF   (&PLN GE 1 AND &PLN LE 127).PLN3
&NOCODE  SETB  1
         MNOTE 12,'PLN OPERAND NOT IN VALID RANGE OF 1 TO 127'
         AGO   .PARLST1
.PLN3    ANOP
         AIF   (&IECSQ(3)).PLN4
         AIF   (&PLN GT &IECPARN).PARLST1
.PLN4    ANOP
         AIF   (&PLN GT &IECPARN).PARLST1
&NOCODE  SETB  1
         MNOTE 12,'PLN OPERAND MUST BE GREATER THAN PLN OPERAND OF     X
               PREVIOUS MACRO'
.PARLST1 ANOP
         AIF   (&PLN EQ 0).PARLST3
&A       SETA  &IECPARN+1
         AIF   (&PLN EQ &A).PARLST3
&NOCODE  SETB  1
         MNOTE 12,'PLN OPERAND MUST BE ONE GREATER THAN PLN OF         X
               PREVIOUS PARAMNUM MACRO'
.PARLST3 ANOP
         AIF   (T'&PARMLST NE 'O').PARLST2
&NOCODE  SETB  1
         MNOTE 12,'PARMLST OPERAND CAN NOT BE OMITTED'
         AGO   .GEN
.PARLST2 ANOP
         AIF   (T'&PARMLST EQ 'M').GEN
&NOCODE  SETB  1
         MNOTE 12,'PARMLST OPERAND HAS INVALID TYPE ATTRIBUTE'
.GEN     ANOP
         AIF   (&NOCODE).NOGEN
         AIF   (&IECSQ(3)).NONAME
IECPARLN EQU   *    GDU PARAMETER LIST NUMBER TABLE
         DS    0H
.NONAME  ANOP
&A       SETA  &IECPARN+1
.LOOP    AIF   (&PLN EQ &A).GEN1
         DC    YL2(0)
&IECNDGP(1) SETA &IECNDGP(1)+2
&A       SETA  &A+1
         AGO   .LOOP
.GEN1    ANOP
&IECPARN SETA  &PLN
&CHAR    SETC  '&PARMLST'
         DC    AL2(&CHAR-IEC15TAB)
&IECNDGP(1) SETA &IECNDGP(1)+2
&IECSQ(3) SETB 1
         AGO   .MEND
.NOGEN   ANOP
         MNOTE 12,'TEXT GENERATION SUPPRESSED'
.MEND    ANOP
         MEND

