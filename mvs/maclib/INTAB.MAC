.* MVS 3.8J PUBLIC DOMAIN
         MACRO
&NAME    INTAB
.* GLOBAL CMZELOG IS USED TO CONTROL THE ORDER OF THE MACROS
.* LOCAL  ERRBIT  IS A GENERAL ERROR SWITCH
.* LOCAL  INT1     IS A GENERAL STRING INDEX
.* LOCAL  ERRCHAR IS A GENERAL ERROR CHARACTER INDICATOR
         GBLB  &CMZELOG(2)
         LCLB  &ERRBIT
         LCLA  &IN1
         LCLC  &ERRCHAR
         AGO   .ONW00
* /* START OF SPECIFICATIONS ****
*
*01*  MODULE-NAME = INTAB
*
*01*  DESCRIPTIVE-NAME = BEGIN LOGON CHARACTERISTICS TABLE
*
*
*01*  STATUS = RELEASE 1
*
*01*  FUNCTION = CREATES A CSECT CARD FOR THE NAME OF A LOGON
*     CHARACTERISTICS TABLE
*
*01*  NOTES = THE FOLLOWING MNOTES ARE PRODUCED:
* 8,'NO STARTS,CSECTS,OR DSECTS ALLOWED IN THIS ASSEMBLY'
* 8,'NAME WAS NOT SPECIFIED'
* 8,'NAME ''&NAME'' IS LONGER THAN EIGHT CHARACTERS'
* 8,'''&ERRCHAR'' IS INVALID CHARACTER IN NAME ''&NAME'''
*
*02*    CHARACTER-CODE-DEPENDENCIES = EBCDIC
*
*02*    DEPENDENCIES = NONE
*
*02*    RESTRICTIONS = VTAM MAPPING MACRO
*
*02*    REGISTER-CONVENTIONS = NONE, LIST FORM
*
*02*    PATCH-LABEL = NONE
*
*01*  MODULE-TYPE = MACRO
*
*02*    PROCESSOR = ASSEMBLER
*
*02*    MACRO-SIZE = 50 STATEMENTS
*
*02*    ATTRIBUTES = LIST
*
*01*  ENTRY-POINT = NA
*
*01*  EXIT-NORMAL = NA
*
*01*  EXIT-ERROR = NA
*
*01*  EXTERNAL-REFERENCES = AS-FOLLOWS
*
*02*    ROUTINES = NONE
*
*02*    DATA-SETS = NONE
*
*02*    DATA-AREA = GLOBAL CMZELOG  IS USED TO PASS INFORMATION BETWEEN
*       INTAB, LOGCHAR, AND ENDINTAB MACROS.  THE FIELD IS TWO BITS
*       USED AS SEQUENCING BITS.
*
*02*    CONTROL-BLOCKS-SYSTEM = REQ: COMMENTS:,
*                            COMMENTS: ENTER NONE OR LIST SYSTEM
*                                      CONTROL BLOCKS WITH FIELDS
*                                      REFERENCED OR SET, THIS
*                                      SECTION MAY BE EXPANDED;
*
*02*    CONTROL-BLOCKS-ATCAM = REQ: CHOOSE: NONE|
*                                        ISTATCVT=SPCATCVT|
*                                        ISTACB=SPCACB|
*                                        ISTACDEB=SPCACDEB|
*                                        ISTRPL=SPCRPL|
*                                        ISTPREFX=SPCPREFX|
*                                        ISTQAB=SPCQAB|
*                                        ISTDFNOD=SPCDFNOD|
*                                        ISTEXLST=SPCEXLST|
*                                        ISTLDO=SPCLDO|
*                                        ISTDNCB=SPCDNCB|
*                                        ISTRPLH=SPCRPLH|
*                                        ISTRDT=SPCRDT|
*                                        ISTBTU=SPCBTU|
*                                        ISTDVT=SPCDVT|
*                                        ISTAPB=SPCAPB|
*                                        ISTICB=SPCICB|
*                                        ISTICEX=SPCICEX|
*                                        ISTFSB=SPCFSB|
*                                        ISTFDB=SPCFDB,
*                           COMMENTS: ENTER NAME OF CONTROL BLOCK
*                                     USED, COPY THIS SECTION IF
*                                     MORE THAN ONE;
*
*01*  TABLES = NONE
*
*01*  INNER-MACROS = NONE
*
*01*  SYSTEM-MACROS = NONE
*
*01*  CHANGE-ACTIVITY = NONE
*
**** END OF SPECIFICATIONS ***/
*/*INTAB: CHART (FMODE) */
*/* HEADER
*/*PROGRAM NAME:
*/*DATE:
*/*PROGRAMMER:                                                      */
*/*1A3 E INTAB */
*/*1B3 P GLOBAL: CMZELOG(2), LOCALS: ERRBIT, IN1, ERRCHAR */
*/*ONW00:1C3 D (NO,ERR0010:LT,YES,) IS CMZELOG(1) = 0 */
*/*1D3 D (NO,ERR0010:LT,YES,) IS CMZELOG(2) = 0 */
*/*1E3 P SET CMZELOG(1)= 1 */
*/*ONW10:1F3 D (NO,ERR0020:RT,YES,) IS SYSECT = NULL */
*/*%1G3:1G3 $ (,ONW20) */
*/*ONW20:2B2 D (NO,ERR0030:LT,YES,) IS THERE A NAME SPECIFIED */
*/*ONW30:2C2 P SET IN1 = IN1 + 1 */
*/*2D2 D (YES,ONW40:LT,NO,) IS IN1 > LENGTH OF NAME */
*/*2E2 D (YES,ERR0040:BT,NO,:RT) IS IN1 > 8 */
*/*2F4 D (NO,:BT,INVL,ERR0050:LT,YES,ONW30:RT) IS NAME(IN1) FROM A-Z */
*/*2G4 D (YES,ERR0050:LT,NO,) IS IN1 = 1 */
*/*2H4 D (NO,ERR0050:BT,YES,ONW30:RT) IS NAME(IN1) FROM 0-9 */
*/*ONW40:2G2 D (YES,ONW50:LT,NO,) WAS ERRBIT SET */
*/*2H2 P GENERATE CSECT CARD */
*/*ONW50:2J2 R MEXIT */
*/*ERR0010:1E2 P (,%1G3) MNOTE ERROR, ERRBIT=1, CMZELOG = B'01' */
*/*ERR0020:1F4 P (,%1G3) MNOTE ERROR, ERRBIT=1 */
*/*ERR0030:2C1 P (,ONW40) MNOTE ERROR, ERRBIT=1 */
*/*ERR0040:2F2 P (,ONW40) MNOTE ERROR, ERRBIT=1 */
*/*ERR0050:2J4 P (,ONW30:RT) MNOTE ERROR, ERRBIT=1 */
*/*INTAB: END */
.* IF BOTH GLOBAL BITS ARE NOT OFF, THE INTAB MACRO WAS CALLED
.* IN IN AN IMPROPER SEQUENCE OF MACRO CALLS. WHEN A ERROR IN
.* SEQUENCING IS FOUND, THE BITS ARE SET TO 0,1.
.ONW00   AIF   (&CMZELOG(1) NE 0 ).ERR0010
         AIF   (&CMZELOG(2) NE 0 ).ERR0010
.* IF BOTH BITS ARE OFF, SET THE FIRST ONE ON TO INDICATE INTAB CALLED
&CMZELOG(1) SETB 1
.* IF A CSECT, DSECT, OR START CARD WAS USED, ISSUE ERROR MESSAGE
.ONW10   AIF   ('&SYSECT' NE '' ).ERR0020
.* IF A NAME WAS NOT SPECIFIED, ISSUE ERROR MESSAGE
.ONW20   AIF   ('&NAME' EQ '' ).ERR0030
.* CHECK THAT THE NAME IS LESS TAHN EIGHT CHARACTERS AND VALID EBCDIC
.ONW30   ANOP
&IN1     SETA  &IN1+1                  POINT TO NEXT CHARACTER
         AIF   (&IN1 GT K'&NAME ).ONW40
         AIF   (&IN1 GT 8 ).ERR0040
         AIF   ('&NAME'(&IN1,1) LT 'A' ).ERR0050
         AIF   ('&NAME'(&IN1,1) LE 'I' ).ONW30
         AIF   ('&NAME'(&IN1,1) LT 'J' ).ERR0050
         AIF   ('&NAME'(&IN1,1) LE 'R' ).ONW30
         AIF   ('&NAME'(&IN1,1) LT 'S' ).ERR0050
         AIF   ('&NAME'(&IN1,1) LE 'Z' ).ONW30
         AIF   (&IN1 EQ 1 ).ERR0050
         AIF   ('&NAME'(&IN1,1) LT '0' ).ERR0050
         AIF   ('&NAME'(&IN1,1) GT '9' ).ERR0050
         AGO   .ONW30
.* IF THE ERROR BIT IS SET, SKIP CSECT CARD
.ONW40   AIF   (&ERRBIT EQ 1).ONW50
&NAME    CSECT
.ONW50   ANOP
         MEXIT
.ERR0010 MNOTE 8,'CARDS OUT OF SEQUENCE'
&ERRBIT  SETB  1
&CMZELOG(1) SETB 0
&CMZELOG(2) SETB 1
         AGO   .ONW20
.ERR0020 MNOTE 8,'NO STARTS,CSECTS,OR DSECTS ALLOWED IN THIS ASSEMBLY'
&ERRBIT  SETB  1
         AGO   .ONW20
.ERR0030 MNOTE 8,'NAME WAS NOT SPECIFIED'
&ERRBIT  SETB  1
         AGO   .ONW40
.ERR0040 MNOTE 8,'NAME ''&NAME'' IS LONGER THAN EIGHT CHARACTERS'
&ERRBIT  SETB  1
         AGO   .ONW40
.ERR0050 ANOP
&ERRCHAR SETC  '&NAME'(&IN1,1)
         MNOTE 8,'''&ERRCHAR'' IS INVALID CHARACTER IN NAME ''&NAME'''
&ERRBIT  SETB  1
         AGO   .ONW30
         MEND
