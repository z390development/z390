.* MVS 3.8J PUBLIC DOMAIN
         MACRO
&NAME    CTRLIST &DEVCOD=,&CTRADR=,&CTTEST=,&CTROP=,&CTRRD=,&MSG=
         GBLA  &IECNDGP(100)
.*             IECNDGP(1) NUMBER OF BYTES COUNTER
         GBLB  &IECSEQ(10),&IECSW(9),&IECTXT,&IECAS,&IECCL
.*             IECSEQ(10)  SEQUENCE INDICATOR
.*             IECSW(9) ON INDICATES PULSE COUNT
.*             IECCL  INDICATES CTRLIST MACRO IS CODED
.*             IECAS  INDICATES ASLIST MACRO IS CODED
         GBLB  &IECDE
.*             IECDE   INDICATES DEULIST MACRO CODED
         LCLA  &B
         LCLB  &NOCODE,&CBSW(8)
         LCLC  &CHAR
         AIF   (&IECSW(9)).SEQCHK
&NOCODE  SETB  1
         MNOTE 12,'CTRLIST MACRO NOT VALID BECAUSE PC=YES NOT CODED IN X
               CONFIGUR MACRO'
         MEXIT
.SEQCHK  ANOP
&IECSW(2) SETB 0
     AIF (&IECSEQ(1) AND &IECSEQ(2) AND &IECSEQ(3) AND &IECSEQ(4)).RRR
&NOCODE  SETB  1
         MNOTE 12,'MACRO SEQUENCE ERROR'
.RRR     ANOP
         AIF   (&IECDE).SVT
&NOCODE  SETB  1
         MNOTE 12,'DEULIST MACRO MUST PRECEDE CTRLIST MACRO'
.SVT     ANOP
         AIF   (NOT&IECCL).SEQOK
         MNOTE 12,'CTRLIST MACRO ALREADY CODED FOR THIS TRANSACTION'
&NOCODE  SETB  1
.SEQOK   ANOP
&IECCL   SETB  1
         AIF   (T'&DEVCOD NE 'O').DEV1
&NOCODE  SETB  1
         MNOTE 12,'DEVCOD OPERAND WAS OMITTED'
         AGO   .ADR1
.DEV1    AIF   ('&DEVCOD' EQ 'B' OR '&DEVCOD' EQ 'C' OR '&DEVCOD' EQ   X
               'M').ADR1
&NOCODE  SETB  1
         MNOTE 12,'DEVCOD OPERAND IS INVALID'
.ADR1    ANOP
         AIF   (T'&CTRADR NE 'O').RRD1
&NOCODE  SETB  1
         MNOTE 12,'CTRADR OPERAND IS OMITTED'
         AGO   .RRD
.RRD1    ANOP
         AIF   ('&CTRADR' EQ 'EXP' OR '&CTRADR' EQ 'IMP').RRD
&NOCODE  SETB  1
         MNOTE 12,'CTRADR OPERAND IS INVALID'
.RRD     ANOP
         AIF   (T'&CTRRD NE 'O').TEST1
&NOCODE  SETB  1
         MNOTE 12,'CTRRD OPERAND IS OMITTED'
         AGO   .TEST
.TEST1   ANOP
         AIF   ('&CTRRD' EQ 'SINGLE' OR '&CTRRD' EQ 'GROUP').TEST
&NOCODE  SETB  1
         MNOTE 12,'CTRRD OPERAND IS INVALID'
.TEST    ANOP
         AIF   (T'&CTTEST NE 'O').OP1
&NOCODE  SETB  1
         MNOTE 12,'CTTEST OPERAND IS OMITTED'
         AGO   .OP
.OP1     ANOP
         AIF   ('&CTTEST' EQ 'RESET' OR '&CTTEST' EQ 'SETNCT' OR '&CTTEX
               ST' EQ 'SETUNAS' OR '&CTTEST' EQ 'NULL').OP
&NOCODE  SETB  1
         MNOTE 12,' CTTEST OPERAND IS INVALID'
.OP      ANOP
         AIF   (T'&CTROP NE 'O').OPCHK
&NOCODE  SETB  1
         MNOTE 12,'CTROP OPERAND WAS OMITTED'
         AGO   .GEN
.OPCHK   ANOP
         AIF   ('&CTROP' EQ 'RDRESID').GEN
         AIF   ('&CTROP' EQ 'READ' OR '&CTROP' EQ 'SET' OR '&CTROP' EQ X
               'READSET' OR '&CTROP' EQ 'READRST').GEN
         AIF   ('&CTROP' EQ 'NULL').GEN
&NOCODE  SETB  1
         MNOTE 12,'CTROP OPERAND IS INVALID'
.GEN     AIF   (T'&MSG EQ 'O').GENCODE
         AIF   (T'&MSG NE 'O' AND &IECTXT).CKMRMSG
         MNOTE 3,'MSG OPERAND ILLEGALLY CODED'
&IECSW(2) SETB 1
         AGO   .GENCODE
.CKMRMSG AIF   ('&MSG'(1,1) EQ '''' AND '&MSG'(K'&MSG,1) EQ '''').MFOK
         MNOTE 3,'MSG OPERAND IN INCORRECT FORMAT'
&IECSW(2) SETB 1
         AGO   .GENCODE
.MFOK    ANOP
&B       SETA  K'&MSG-2
         AIF   (&B GE 0 AND &B LE 127).GENCODE
         MNOTE 3,'MESSAGE LENGTH EXCEEDS 127'
&IECSW(2) SETB 1
.GENCODE ANOP
         AIF   (&IECSW(8)).WRONG5
.CHK1    ANOP
         AIF   ('&CTRRD' EQ 'GROUP' AND '&CTROP' EQ 'SET').WRONG1
.CHK2    ANOP
         AIF   ('&CTRRD' EQ 'GROUP' AND '&CTROP' EQ 'READSET').WRONG2
.CHK3    ANOP
         AIF   ('&CTRRD' EQ 'GROUP' AND '&CTROP' EQ 'READRST').WRONG3
.CHK4    ANOP
         AIF   ('&CTRRD' EQ 'GROUP' AND '&CTRADR' EQ 'IMP').WRONG4
.CHK5    ANOP
         AIF   (NOT&IECAS).NOWRG
         MNOTE 12,'CTRLIST MACRO CAN NOT FOLLOW AN ASLIST MACRO'
&NOCODE  SETB  1
         AGO   .NOWRG
.WRONG1  ANOP
&NOCODE  SETB  1
         MNOTE 12,'CTRRD=GROUP AND CTROP=SET NOT VALID COMBINATION'
         AGO   .CHK2
.WRONG2  ANOP
&NOCODE  SETB  1
         MNOTE 12,'CTRRD=GROUP AND CTROP=READSET NOT VALID COMBINATION'
         AGO   .CHK3
.WRONG3  ANOP
&NOCODE  SETB  1
         MNOTE 12,'CTRRD=GROUP AND CTROP=READRST NOT VALID COMBINATION'
         AGO   .CHK4
.WRONG4  ANOP
&NOCODE  SETB  1
         MNOTE 12,'CTRRD=GROUP AND CTRADR=IMP NOT VALID COMBINATION'
         AGO   .CHK5
.WRONG5  ANOP
         MNOTE 12,'MSG OPERAND CODED IN PREVIOUS MACRO'
&NOCODE  SETB  1
         AGO   .CHK1
.NOWRG   ANOP
         AIF   (&NOCODE).NOGEN
         SPACE 1
*              COUNTER APPENDAGE
         SPACE 1
         DC    X'20'
         DC    X'00'
         DC    X'00'
         AIF   ('&DEVCOD' EQ 'M').MANUAL
         DC    X'01'      DISPLACEMENT BYTE
         AGO   .CB
.MANUAL  ANOP
         DC    X'05'    DISPLACEMENT BYTE
.CB      ANOP
         AIF   ('&CTTEST' EQ 'NULL').CB1
         AIF   ('&CTTEST' NE 'SETNCT').CB2
&CBSW(8) SETB 1
         AGO   .CB1
.CB2     AIF   ('&CTTEST' NE 'SETUNAS').CB3
&CBSW(7) SETB  1
         AGO   .CB1
.CB3     ANOP
&CBSW(8) SETB  1
&CBSW(7) SETB  1
.CB1     ANOP
         AIF   ('&CTRADR' NE 'IMP').CB4
&CBSW(6) SETB  1
.CB4     ANOP
         AIF   ('&CTROP' NE 'SET').CB5
&CBSW(3) SETB  1
.CB5     ANOP
         AIF   ('&CTRRD' EQ 'GROUP').CB6
         AIF   ('&CTROP' NE 'READ').CB7
&CBSW(1) SETB  1
.CB7     ANOP
         AIF   ('&CTROP' NE 'RDRESID').CB8
&CBSW(1) SETB  1
&CBSW(3) SETB  1
.CB8     ANOP
         AIF   ('&CTROP' NE 'READRST').CB9
&CBSW(1) SETB  1
&CBSW(2) SETB 1
.CB9     ANOP
         AIF   ('&CTROP' NE 'READSET').CBEND
&CBSW(1) SETB  1
&CBSW(2) SETB  1
&CBSW(3) SETB  1
.CB6     ANOP
         AIF   ('&CTROP' EQ 'READ' AND '&CTRRD' EQ 'GROUP').CB11
         AIF   ('&CTROP' EQ 'RDRESID' AND '&CTRRD' EQ 'GROUP').CB12
         AGO   .CBEND
.CB11    ANOP
&CBSW(1) SETB  1
&CBSW(5) SETB  1
         AGO   .CBEND
.CB12    ANOP
&CBSW(1) SETB  1
&CBSW(2) SETB  1
&CBSW(5) SETB  1
.CBEND   ANOP
         DC    B'&CBSW(1)&CBSW(2)&CBSW(3)&CBSW(4)&CBSW(5)&CBSW(6)&CBSW(X
               7)&CBSW(8)'       CONTROL BYTE
&B       SETA  0
&IECAS   SETB  1
&IECDE   SETB  1
         AIF   (T'&MSG EQ 'O' OR &IECSW(2)).ADD
         AIF   (NOT &IECSW(8)).YOYO
         MNOTE 3,'MSG OPERAND CANNOT BE CODED IN DEULIST MACRO IF A    X
               CTRLIST MACRO FOLLOWS'
.YOYO    ANOP
&IECSW(8) SETB 1
         DC    YL1(&IECNDGP(4))  END OF LIST BYTE
&B       SETA  K'&MSG-2
         DC    YL1(&B)   MESSAGE LENGTH
         DC    C&MSG
&B       SETA  &B+1
.ADD     ANOP
&IECNDGP(1) SETA &IECNDGP(1)+5+&B
         AGO   .MEND
.NOGEN   ANOP
         MNOTE 12,'TEXT GENERATION SUPPRESSED'
.MEND    MEND

