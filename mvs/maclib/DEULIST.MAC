.* MVS 3.8J PUBLIC DOMAIN
         MACRO
&NAME    DEULIST &DIGIT=,&MSG=,&LENGTH=,&MODULUS=,&DIGIT2=
         GBLA  &IECNDGP(100)
.*             IECNDGP(3)  COUNTER FOR NUMBER OF DEULIST MACROS
         GBLB  &IECSW(9),&IECTXT,&IECSEQ(10)                      12094
.*             IECSW(5)  INDICATES NOT FIRST MACRO
.*             IECSW(8) SET WHEN MSG TXT APPEARS.INDICATES TO FOLLOWING
.*               TRLIST OR STEND MACRO THAT AN END OF LIST BYTE
.*             GENERATED BY THIS MACRO
.*             IECSEQ(5)  SEQUENCE INDICATOR
         GBLB  &IECDE                                             21094
.*             IECDE   INDICATES DEULIST MACRO CODED              21094
         GBLB  &IECAS                                             21094
.*             IECAS    INDICATES ASLIST MACRO CODED              21094
         GBLB  &IECM10,&IECM11
.*             IECM10  INDICATES MODULUS 10 CHECKING IN TRLIST
.*             IECM11  INDICATES MODULUS 11 CHECKING IN TRLIST
         LCLA  &A,&B,&C
         LCLB  &NCODE
         LCLB  &SW(4)
         LCLC  &CHAR
&IECSEQ(5) SETB 1
&IECDE   SETB 1                                                   21094
         AIF   (&IECSEQ(1) AND &IECSEQ(2) AND &IECSEQ(3) AND &IECSEQ(4)-
               AND &IECSEQ(5) AND NOT&IECSEQ(6)).SEQOK
.J       ANOP                                                     21094
         MNOTE 12,'MACRO SEQUENCE ERROR'
         MEXIT
.SEQOK   ANOP
         AIF   (&IECAS).J                                         21094
&IECNDGP(3) SETA &IECNDGP(3)+1
         AIF   (&IECNDGP(3) LE 16).NOK
         MNOTE 12,'NO OF DEULIST MACROS EXCEEDS 16'
         MEXIT
.NOK     AIF   (T'&LENGTH EQ 'O').NOK1
         AIF   (T'&LENGTH EQ 'N').LTYPOK1
&SW(3)   SETB  1
&CHAR    SETC  T'&LENGTH
         MNOTE 12,'LENGTH TYPE ATTRIBUTE = &CHAR IMPROPER'
         AGO   .NOK1
.LTYPOK1 ANOP
&A       SETA  &LENGTH
         AIF   (&A LE 18).NOK1
&SW(3)   SETB  1
         MNOTE 3,'LENGTH = &A EXCEEDS MAXIMUM ALLOWED'
.NOK1    AIF   (T'&DIGIT EQ 'O').CKMSG
         AIF   (T'&DIGIT(1) EQ 'N' AND T'&DIGIT(2) EQ 'N').TYPOK
&SW(1)   SETB  1
&CHAR    SETC  T'&DIGIT
         MNOTE 12,'DIGIT TYPE ATTRIBUTE = &CHAR IMPROPER'
         AGO   .CKMSG
.TYPOK   ANOP
&A       SETA  &DIGIT(1)
         AIF   (&A GE 1 AND &A LE 15).VALOK
&SW(1)   SETB  1
         MNOTE 3,'DIGIT CHECK POSITION = &A INVALID'
.VALOK   ANOP
&A       SETA  &DIGIT(2)
         AIF   (&A LE 9).CKMSG
&SW(1)   SETB  1
         MNOTE 3,'DIGIT VALUE TO BE CHECKED=&A INVALID'
.CKMSG   AIF   (T'&MSG EQ 'O').GENCODE
         AIF   (T'&MSG NE 'O' AND &IECTXT).CKMRMSG
         MNOTE 3,'MSG OPERAND ILLEGALLY CODED'
&SW(2)   SETB  1
         AGO   .GENCODE
.CKMRMSG AIF   ('&MSG'(1,1) EQ '''' AND '&MSG'(K'&MSG,1) EQ '''').MFOK
         MNOTE 3,'MSG OPERAND IN INCORRECT FORMAT,OPERAND IGNORED'
&SW(2)   SETB  1
         AGO   .GENCODE
.MFOK    ANOP
&B       SETA  K'&MSG-2
         AIF   (&B GE 0 AND &B LE 127).GENCODE
         MNOTE 3,'MESSAGE LENGTH EXCEEDS 127,OPERAND IGNORED'
&SW(2)   SETB  1
.GENCODE ANOP
         AIF   (T'&MODULUS EQ 'O').MOD6
         AIF   (T'&MODULUS(1) EQ 'N' AND T'&MODULUS(2) EQ 'N').MOD1
&SW(1)   SETB  1
         MNOTE 3,'MODULUS = &MODULUS INVALID'
         AGO   .MOD3
.MOD1    ANOP
&A       SETA  &MODULUS(1)
         AIF   (&A GE 2 AND &A LE 16).MOD2
&SW(1)   SETB  1
         MNOTE 3,'MODULUS POSITION = &A INVALID'
.MOD2    ANOP
&A       SETA  &MODULUS(2)
         AIF   (&A GE 1 AND &A LE 15).MOD3
&SW(1)   SETB  1
         MNOTE 3,'MODULUS LENGTH = &A INVALID'
.MOD3    ANOP
         AIF   (T'&MODULUS EQ 'O').MOD6
         AIF   (T'&DIGIT EQ 'O').MOD4
&NCODE   SETB  1
         MNOTE 12,' MODULUS AND DIGIT OPERANDS MUTUALLY EXCLUSIVE'
.MOD4    ANOP
         AIF   (&IECSW(1)).MOD5
&NCODE   SETB  1
         MNOTE 12,'MODULUS OPERAND NOT VALID UNLESS CORE=32 IN CONFIGURX
               MACRO'
.MOD5    ANOP
         AIF   (&IECM10 OR &IECM11).MOD6
&NCODE   SETB  1
         MNOTE 12,'MODULUS OPERAND REQUIRES THAT EITHER DEMOD10=YES OR X
               DEMOD11=YES BE CODED IN TRLIST MACRO'
.MOD6    ANOP
         AIF   (T'&DIGIT EQ 'O').MOD7
         AIF   (&IECM10 OR &IECM11).ERROR1
         AGO   .MOD7
.ERROR1  ANOP
&NCODE   SETB  1
         MNOTE 12,'DIGIT OPERAND CAN NOT BE CODED IF DEMOD10=YES OR    X
               DEMOD11=YES IN TRLIST MACRO'
.MOD7    ANOP
         AIF   (T'&DIGIT2 EQ 'O').D2
         AIF   (T'&DIGIT2(1) EQ 'N' AND T'&DIGIT2(2) EQ 'N').D3
&SW(4)   SETB  1
&CHAR    SETC  T'&DIGIT2
         MNOTE 3,'DIGIT2 TYPE ATTRIBUTE = &CHAR IMPROPER'
         AGO   .D2
.D3      ANOP
&A       SETA  &DIGIT2(1)
         AIF   (&A GE 1 AND &A LE 15).D4
&SW(4)   SETB  1
         MNOTE 3,'DIGIT2 CHECK POSITION = &A INVALID'
.D4      ANOP
&A       SETA  &DIGIT2(2)
         AIF   (&A LE 9 AND &A GE 0).D2
&SW(4)   SETB  1
         MNOTE 3,'DIGIT2 VALUE TO BE CHECKED = &A INVALID'
.D2      ANOP
&A       SETA  0
&B       SETA  0
         AIF   (NOT &IECSW(5)).SKIP
&C       SETA  X'C0'
.SKIP    DC    YL1(&C)
         AIF   (T'&DIGIT EQ 'O' OR &SW(1)).NOSET
&A       SETA  &DIGIT(1)
&B       SETA  &DIGIT(2)
.NOSET   ANOP
         AIF   (T'&MODULUS EQ 'O' OR &SW(1)).NOSET1
&A       SETA  &MODULUS(1)-1
&B       SETA  &MODULUS(2)
.NOSET1  ANOP
         DC    YL1(&A*16+&B) DIGIT OR MODULUS POSITION AND VALUE
&A       SETA  0
         AIF   (T'&LENGTH EQ 'O' OR &SW(3)).SETNOT
&A       SETA  &LENGTH
.SETNOT  DC    YL1(&A)  LENGTH
         DC    X'00'
&A       SETA  0
&B       SETA  0
         AIF   (T'&DIGIT2 EQ 'O' OR &SW(4)).SKIP1
&A       SETA  &DIGIT2(1)
&B       SETA  &DIGIT2(2)
.SKIP1   ANOP
         DC    YL1(&A*16+&B) SECOND DIGIT POSITION AND VALUE OR NULL
&B       SETA  0
         AIF   (T'&MSG EQ 'O' OR &SW(2)).ADD
&IECSW(8) SETB 1
         DC    YL1(&IECNDGP(4))  END OF LIST BYTE
&B       SETA  K'&MSG-2
         DC    YL1(&B)  MESSAGE LENGTH
         DC    C&MSG
&B       SETA  &B+1
&IECTXT  SETB  0  RESET TO PREVENT DEULIST EXPANSION FOLLOWING MSG TXT
.ADD     ANOP
&IECNDGP(1) SETA &IECNDGP(1)+5+&B
&IECSW(5) SETB 1
.MEND    MEND

