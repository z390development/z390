.* MVS 3.8J PUBLIC DOMAIN
*      INCLUDES  ZTR 00626
* /* START OF SPECIFICATIONS ****
*
*01*  MODULE-NAME = DVT
*
*01*  DESCRIPTIVE-NAME = USER MACRO TO GENERATE SKELETEL DVT NAME
*
*
*01*  STATUS = RELEASE 1
*
*01*  FUNCTION = INVOKED BY USER IN FOLLOWING MANNER
*&LABEL  DVT
*   MACRO GENERATES FOLLOWING ASSEMBLER CODE
*        DS    0H
*LABEL   DC    CL8'LABEL'
*        XL4'00',CL4'RDVT',XL8'00'
*
*01*  NOTES = &LABEL IS SKELETEL DVT NAME
*
*            FOLLOWING IS LIST OF MNOTES USED:
*
*        MNOTE 8,'NO LABEL PRESENT ON DVT MACRO'
*        MNOTE 8,'NO NAMED CSECT - USE NAMED CSECT OR STRTMODE MACRO'
*        MNOTE *,'USING &SYSECT AS MODE NAME'
*        MNOTE *,'CONSECUTIVE DVT MACROS ISSUED - PRIOR OVERLAYED'
*
*02*    CHARACTER-CODE-DEPENDENCIES = NONE
*
*02*    DEPENDENCIES = STRTMODE MACRO, FUNCT MACRO, SKELETEL DVT READ
*       MODULE (ISTOCCSD)
*
*02*    RESTRICTIONS = NONE
*
*02*    REGISTER-CONVENTIONS = NONE
*
*02*    PATCH-LABEL = NONE
*
*01*  MODULE-TYPE = MACRO
*
*02*    PROCESSOR = ASSEMBLER
*
*02*    MACRO-SIZE = 26 STATEMENTS
*
*02*    ATTRIBUTES = LIST
*
*01*  ENTRY-POINT = NA
*
*01*  EXIT-NORMAL = NA
*
*01*  EXIT-ERROR = NA
*
*01*  EXTERNAL-REFERENCES = NONE
*
*01*  TABLES = NONE
*
*01*  INNER-MACROS = NONE
*
*01*  SYSTEM-MACROS = NONE
*
*01*  CHANGE-ACTIVITY = DCR  1K0266
*
**** END OF SPECIFICATIONS ***/
         MACRO
&LABEL   DVT
         GBLA  &CNT                    FUNCTION COUNTER
         GBLC  &A                      SYSECT SAVE
         GBLB  &B,&C,&M                DVT FUNCT AND MODE
         AIF   ('&LABEL' NE '').A1
         MNOTE 8,'NO LABEL PRESENT ON DVT MACRO'
         MEXIT
.A1      AIF   (&M EQ 1).A2
         AIF   ('&SYSECT' NE '').A15
         MNOTE 8,'NO NAMED CSECT - USE NAMED CSECT OR STRTMODE MACRO'
         MEXIT
.A15     MNOTE *,'USING &SYSECT AS MODE NAME'
.A2      AIF   (&B EQ 0 OR &C EQ 1).A3
         MNOTE *,'CONSECUTIVE DVT MACROS ISSUED - PRIOR OVERLAYED'
         ORG   *-24
&CNT     SETA  &CNT-1
.A3      ANOP
&CNT     SETA  &CNT+1
         DS    0F
&LABEL   DC    CL8'&LABEL'
         DC    XL4'00',CL4'RDVT'
         DC    XL8'00'
         DC    F'0'                    FOR DCR 3440.1
&B       SETB  1                       SET DVT ON
&C       SETB  0                       SET FUNCT OFF
         MEND

