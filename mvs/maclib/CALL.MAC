.* MVS 3.8J PUBLIC DOMAIN
         MACRO
&NAME    CALL  &ENTRY,&OPRNDS,&VLPARA,&ID=,&MF=I
         GBLB  &IHBSWA,&IHBSWB
         GBLC  &IHBNO
         LCLC  &GNAME
&IHBNO   SETC  '309'
&GNAME   SETC  'IHB'.'&SYSNDX'
&IHBSWA  SETB  ('&VLPARA' EQ 'VL')
&IHBSWB  SETB  ('&ENTRY' EQ '(15)')
         AIF   ('&VLPARA' NE '' AND '&VLPARA' NE 'VL').ERROR4  @ZA33014
         AIF   ('&MF' EQ 'L' AND '&ENTRY' NE '').ERROR1
         AIF   ('&MF' EQ 'L' AND '&ID' NE '').ERROR2
         AIF   ('&MF' NE 'L' AND '&ENTRY' EQ '').ERROR3
         AIF   ('&MF' EQ 'L' ).CONTC
         AIF   (&IHBSWB).CONTCC
         CNOP  0,4
&NAME    B     *+8                               BRANCH AROUND VCON
&GNAME.B DC    V(&ENTRY)                         ENTRY POINT ADDRESS
.CONTC   AIF   ('&OPRNDS' EQ '' AND '&MF(1)' NE 'E').CONTB
.CONTA   IHBOPLST &ENTRY,&OPRNDS,&NAME,MF=&MF
.CONTB   AIF   ('&MF' EQ 'L').EXIT
         AIF   (&IHBSWB).CONTD
         L     15,&GNAME.B                       LOAD 15 WITH ENTRY ADR
.CONTD   BALR  14,15                             BRANCH TO ENTRY POINT
         AIF   ('&ID' EQ '').EXIT
         DC    X'4700'                           NOP INSTRUCTION WITH
         DC    AL2(&ID)                          ID IN LAST TWO BYTES
.EXIT    MEXIT
.CONTCC  ANOP
&NAME    DS    0H
         AGO   .CONTC
.ERROR1  IHBERMAC 73,&IHBNO,&ENTRY          ENTRY  W/ MF=L
         MEXIT
.ERROR2  IHBERMAC 74,&IHBNO,&ID             ID W/ MF=L
         MEXIT
.ERROR3  IHBERMAC 26,&IHBNO                 ENTRY SYMBOL MISSING
         MEXIT
.ERROR4  IHBERMAC 1014,THIRD                INVALID THIRD PARM @ZA33014
         MEND
