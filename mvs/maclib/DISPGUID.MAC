.* MVS 3.8J PUBLIC DOMAIN
         MACRO
&NAME    DISPGUID &SUPPRES=,&DISPMSG=
         GBLA  &IECNDGP(100)
.*             IECNDGP(1)     NUMBER OF BYTES COUNTER
         GBLB  &IECSW(9),&IECSEQ(10),&IECSQ(7),&IECGDSW
.*             IECSQ(5)   SEQUENCE INDICATOR
.*             IECGDSW  ON INDICATES GDU=YES CODED IN CONFIGUR MACRO
         LCLA  &B
         LCLB  &NOCODE,&A
.*             &A   ON INDICATES SUPPRES=YES IS CODED
         ACTR  500
         AIF   (&IECSEQ(1) AND &IECSEQ(2) AND &IECSQ(1) AND &IECSQ(2)  X
               AND &IECSQ(3) AND &IECSQ(4) AND NOT &IECSQ(6)).SEQ
.SEQ1    ANOP
&NOCODE  SETB  1
         MNOTE 12,'MACRO SEQUENCE ERROR'
         AGO   .NAME
.SEQ     AIF   (&IECSEQ(4) OR &IECSEQ(5) OR &IECSEQ(10) OR &IECSEQ(6)).X
               SEQ1
.NAME    ANOP
         AIF   ('&NAME' NE '').GDUYES
&NOCODE  SETB  1
         MNOTE 12,'NAME FIELD REQUIRED'
.GDUYES  AIF   (&IECGDSW).LEN
&NOCODE  SETB  1
         MNOTE 12,'DISPGUID MACRO NOT VALID UNLESS GDU=YES IN CONFIGUR X
               MACRO'
.LEN     ANOP
         AIF   (T'&SUPPRES EQ 'O').DIS1
         AIF   ('&SUPPRES' EQ 'NO').DIS
         AIF   ('&SUPPRES' EQ  'YES').DIS1
         MNOTE 3,'SUPPRES OPERAND INVALID,OPERAND IGNORED'
.DIS1    ANOP
&A       SETB  1
.DIS     ANOP
         AIF   (T'&DISPMSG NE 'O').DIS2
&NOCODE  SETB  1
         MNOTE 12,'DISPMSG OPERAND CAN NOT BE OMITTED'
         AGO   .CKGEN
.DIS2    ANOP
         AIF   ('&DISPMSG'(1,1) EQ '''' AND '&DISPMSG'(K'&DISPMSG,1)   X
               EQ '''').DIS3
&NOCODE  SETB  1
         MNOTE 12,'DISPMSG OPERAND IN INCORRECT FORMAT'
         AGO   .CKGEN
.DIS3    ANOP
         AIF   (K'&DISPMSG GE 3).DIS4
&NOCODE  SETB  1
         MNOTE 12,'DISPMSG OPERAND OF DISPGUID MACRO IS INVALID'
.DIS4    ANOP
         AIF   (K'&DISPMSG LE 18).DIS5
&NOCODE  SETB  1
         MNOTE 12,'MORE THAN 16 TEXT CHARACTERS SPECIFIED'
.DIS5    ANOP
.CKGEN   ANOP
         AIF   (NOT &NOCODE).GENCODE
         MNOTE 12,'TEXT GENERATION SUPPRESSED'
         AGO   .MEND
.GENCODE ANOP
&NAME    EQU   *
&B       SETA  K'&DISPMSG-2
         DC    YL1(X'80'*&A+&B)
         DC    C&DISPMSG
&B       SETA  &B+1
&IECNDGP(1) SETA &IECNDGP(1)+&B
.MEND    MEND
