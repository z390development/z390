.* MVS 3.8J PUBLIC DOMAIN
         MACRO
&NAME    AS    &ID=,&ASGROUP=,&DEGROUP=
         GBLA  &IECNDGP(100),&IECCNTR
.*             IECCNTR   MAINTAINS CURRENT VALUE OF ID PARAMETER
.*             IECNDGP(100)  GLOBAL SAVE CELLS FOR NUMBER OF DEUS PER
.*                           AS. CERTAIN ONES REUSED AFTER DEU TABLE
.*                           AND DEU-SEQ TABLE GENERATED BY FIRST
.*                           TGROUP MACRO
         GBLB  &IECSW(9),&IECSEQ(10)                              21094
.*             IECSW(1)  INDICATES CORE SIZE ON = 32K SET BY CONFIG
.*             IECSW(2)  ON INDICATES NOT FIRST AS MACRO SET BY AS
.*             IECSW(7)   INTERNAL SW USED BY AS MACROS
.*             IECSEQ(2)  SEQUENCE INDICATOR
         GBLC  &IECDGRP(100)
         LCLA  &A,&B,&C ARITH SCRATCH
         LCLB  &NCODE
         LCLC  &CHAR,&CHARS    CHARACTER SCRATCH
         ACTR  1000                                               21094
         AIF   (&IECSW(2)).BY
&IECSEQ(2) SETB 1
.BY      AIF   (&IECSEQ(1) AND &IECSEQ(2) AND NOT&IECSEQ(3) AND NOT&IEC-
               SEQ(4) AND NOT&IECSEQ(5) AND NOT&IECSEQ(6)).SEQOK
&NCODE   SETB  1
         MNOTE 12,'MACRO SEQUENCE ERROR'
.SEQOK   AIF   (T'&ID EQ 'N').IDTYPOK
&CHAR    SETC  T'&ID
&NCODE   SETB  1
         MNOTE 12,'REQUIRED OPERAND=ID TYPE ATTRIBUTE=&CHAR IMPROPER'
.IDTYPOK AIF   (&ID+1 GT &IECCNTR AND &ID LE 63 AND NOT &IECSW(1)).IDOK
         AIF   (&ID+1 GT &IECCNTR AND &ID LE 99 AND &IECSW(1)).IDOK
&NCODE   SETB  1
         MNOTE 12,'ID NOT IN ALLOWED RANGE OR IS OUT OF SEQUENCE'
         AGO   .IDOK2
.IDOK    AIF   (NOT (&ID EQ &IECCNTR AND &IECSW(2))).IDOK2
&NCODE   SETB  1
&CHAR    SETC  '&ID'
         MNOTE 12,'DUPLICATE ID = &CHAR'
.IDOK2   AIF   (T'&ASGROUP EQ 'O' OR T'&ASGROUP EQ 'M').ASTYPOK
&NCODE   SETB  1
&CHAR    SETC  T'&ASGROUP
         MNOTE 12,'ASGROUP TYPE ATTRIBUTE=&CHAR IMPROPER'
.ASTYPOK AIF ((T'&DEGROUP EQ 'O') OR (N'&DEGROUP EQ 2 AND T'&DEGROUP(1)X
               EQ 'M' AND T'&DEGROUP(2) EQ 'N')).DETYPOK
&NCODE   SETB  1
&CHAR    SETC  T'&DEGROUP(1)
&CHARS   SETC  T'&DEGROUP(2)
         MNOTE 12,'DEGROUP TYPE ATTRIBUTE=&CHAR,&CHARS MAY BE IMPROPER X
               OR NUMBER OF SUBOPERANDS NOT 2'
.DETYPOK ANOP                                                     21094
         AIF   (N'&DEGROUP EQ 0).DEOK                             21094
&A       SETA  &DEGROUP(2)                                        21094
         AIF   (&A LE 32).DEOK                                    21094
         MNOTE 12,'NUMBER OF DEUS SPECIFIED IS GREATER THAN 32'   21094
&NCODE   SETB  1                                                  21094
.DEOK    ANOP                                                     21094
         AIF   (&IECSW(2)).SKIP                                   21094
         SPACE 1
*                  AREA STATION TABLE
         SPACE 1
IECASTBL EQU   *
.SKIP    ANOP
         AIF   (&NCODE).NOGEN
&CHARS   SETC  '&ASGROUP'
&C       SETA  18
&A       SETA  &IECCNTR+1
         AIF   (&ID EQ &A AND &ID EQ 1 AND NOT &IECSW(2)).NUTS
.LOOP    AIF   (&ID EQ &A OR &ID EQ 0).OUT
         AIF   (&IECCNTR NE 0 OR &IECSW(7)).PASS
.NUTS    ANOP
&IECCNTR SETA 1
&A       SETA  0
.PASS    ANOP
&CHAR    SETC  '&A'
         MNOTE *,'ID=&CHAR OMITTED'
         DC    YL1(0)
&A       SETA  &A+1
         AGO   .LOOP
.OUT     ANOP
&IECSW(7) SETB 1
&IECCNTR SETA  &ID
         AIF   ('&ASGROUP' NE '').DC
&C       SETA  0
&CHARS   SETC  'IECFTGRP'
.DC      DC    YL1((((&CHARS-IECFTGRP)+&C)/18)*4)
         AIF   ('&DEGROUP' EQ '').MEND
&A       SETA  &ID+1
&IECDGRP(&A) SETC '&DEGROUP(1)'
&IECNDGP(&A) SETA &DEGROUP(2)
         AGO   .MEND
.NOGEN   MNOTE 12,'TEXT GENERATION SUPRESSED'
.MEND    ANOP
&IECSW(2) SETB 1
         MEND

