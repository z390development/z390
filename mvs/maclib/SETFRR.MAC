.* MVS 3.8J PUBLIC DOMAIN
         MACRO
&NAME    SETFRR    &OPT,&FRRAD=,&PARMAD=,&WRKREGS=,&RELATED=,&CLEAR=
*    MACDATE    75295
.*
.**********************************************************************
.*                                                                    *
.*     THE SETFRR MACRO EXPANSION GENERATES CODE WHICH MANIPULATES    *
.*     A STACK.                                                       *
.*   THERE  ARE TWO STACKS WHICH CAN BE MANIPULATED VIA THE SETFRR    *
.*   MACRO                                                            *
.*         1. A STACK FOR FRRS  RUNNING IN SUPERVISOR CONTROL MODE    *
.*         2. A STACK FOR FRRS RUNNING IN NON-SUPERVISOR CONTROL MODE *
.*                                                                    *
.*   THE HEADER FORMAT FOR EACH OF THESE STACKS IS AS FOLLOWS         *
.*                                                                    *
.*      NON-SUPERVISOR CONTROL STACK HEADER FORMAT                    *
.*                                                                    *
.*       ***********************************************              *
.*       * ADDRESS OF STACK - 32 BYTES                 *              *
.*       ***********************************************              *
.*       *    ADDRESS OF LAST ENTRY IN THE STACK       *              *
.*       ***********************************************              *
.*       *  LENGTH OF EACH ENTRY IN THE STACK  (32)    *              *
.*       ***********************************************              *
.*       *ADDRESS OF CURRENT FRR ADDRESS IN THE STACK  *              *
.*       *(INITIALIZED TO THE ADDRESS OF STACK - 32)   *              *
.*       ***********************************************              *
.*                                                                    *
.*      SUPERVISOR CONTROL STACK HEADER FORMAT                        *
.*                                                                    *
.*       ***********************************************              *
.*       * ADDRESS OF STACK                            *              *
.*       ***********************************************              *
.*       *    ADDRESS OF LAST ENTRY IN THE STACK       *              *
.*       ***********************************************              *
.*       *  LENGTH OF EACH ENTRY IN THE STACK  (32)    *              *
.*       ***********************************************              *
.*       *ADDRESS OF CURRENT FRR ADDRESS IN THE STACK  *              *
.*       *(INITIALIZED TO THE ADDRESS OF THE STACK )   *              *
.*       ***********************************************              *
.*                                                                    *
.* PRIOR TO ISSUING SETFRR THE USER MUST HAVE PERFORMED THE FOLLOWING *
.*     1.  ESTABLISHED ADDRESSABILITY TO THE PSA(THE FIRST            *
.*         INSTRUCTION GENERATED BY THE SETFRR EXPANSION REFERENCES   *
.*         THE PSACSTK FIELD OF THE PSA)                              *
.*                                                                    *
.*     2.  HAVE ISSUED THE FRR STACK MAPPING MACRO(IHAFRRS)           *
.*                                                                    *
.**********************************************************************
      LCLC    &R1
      LCLC    &R2
      LCLC    &R3
      LCLC    &R4
.*    VERIFY VALID OPTION SPECIFIED FOR CLEAR KEYWORD
.C1   AIF    ('&CLEAR' EQ '' OR '&CLEAR' EQ 'Y' OR '&CLEAR' EQ 'N').C2
      AIF    ('&CLEAR' EQ 'YES' OR '&CLEAR' EQ 'NO').C2
      MNOTE  *,'INVALID CLEAR OPTION, CLEAR=YES ASSUMED'
         AGO      .C2
.C4      MNOTE      8,'** PARMAD MUST BE SPECIFIED WITH CLEAR OPTION'
         MEXIT
.C2      AIF       ('&CLEAR' NE '' AND '&PARMAD' EQ '').C4
         AIF       ('&WRKREGS' EQ '').MSG1            CHECK TO SEE IF
.*                                                    WORK REGISTERS
.*                                                    ARE SPECIFIED
         AIF       ('&WRKREGS'(1,1) NE '(' OR N'&WRKREGS NE 2).MSG2
.*                                                    IF &WRKREGS IS
.*                                                    SPECIFIED BUT
.*                                                    INVALIDLY ISSUE
.*                                                    ERROR MESSAGE.
.*    SET UP LOCAL CHARACTER FOR USE IN EXPANSIONS
&R1    SETC    '&WRKREGS(1)'
&R2    SETC    '&WRKREGS(2)'
&R3    SETC    '&PARMAD(1)'
&R4    SETC    '&FRRAD(1)'
.*                                                    OTHERWISE CHECK
.*                                                    &OPT FOR A VALID
.*                                                    OPTION
         AIF       ('&OPT' EQ 'A' OR '&OPT' EQ 'R').ADREP
         AIF       ('&OPT' EQ 'D' OR '&OPT' EQ 'P').EXPAND
         AIF       ('&OPT' EQ 'F').FLUSH
         MNOTE     12,'INVALID OPTION &OPT SPECIFIED'
.*                                                    MESSAGE STATING
.*                                                    INVALID OPTION
         AGO       .END                               STOP EXPANSION
.ADREP   AIF       ('&FRRAD' EQ '').MSG3              ISSUE MESSAGE IF
.*                                                    &FRRAD  IS NOT
.*                                                    SPECIFIED FOR
.*                                                    THE ADD/REPLACE
.*                                                    OPTION
         AGO       .EXPAND1
.EXPAND  ANOP                                         BEGIN EXPANSION
         AIF       ('&FRRAD' NE '' AND '&PARMAD' NE '').WARN
         AIF       ('&FRRAD' NE '' OR '&PARMAD' NE '').WARN
         AGO       .EXPAND1
.WARN    MNOTE     *,'FRRAD AND/OR PARMAD SPECIFIED FOR DELETE/PURGE'
         MNOTE     *,'FRRAD AND/OR PARMAD IGNORED-DELETE/PURGE HONORED'
.EXPAND1 ANOP
&NAME    L     &R1,PSACSTK-PSA(0,0)         ADDRESS OF CURRENT STACK
         AIF       ('&OPT' EQ 'P').PURGE              GO EXPAND FOR A
.*                                                    PURGE OF THE FRR
.*                                                    STACK
         AIF       ('&OPT' EQ 'A').ADD   GO TO ADD ROUTINE
         L     &R2,FRRSCURR-FRRS(0,&R1)     ADDR OF CURRENT ELEMENT
         CL    &R2,FRRSEMP-FRRS(0,&R1)      IS THE FRR STACK EMPTY
         AIF       ('&OPT' EQ 'R').BRANCH             IF OPTION IS
.*                                                    REPLACE EXPAND
.*                                                    AT .BRANCH
         BE    *+12                         YES, DO NOT DELETE ENTRY
         SL    &R2,FRRSELEN-FRRS(0,&R1)     DECREMENT CURRENT ADDRESS
         AGO       .UPDATE
.ADD     LA    &R2,32(0,0)                  LENGTH OF FRR STACK ENTRY
         AL    &R2,FRRSCURR-FRRS(0,&R1)     COMPUTE NEXT ENTRY ADDRESS
         CL    &R2,FRRSLAST-FRRS(0,&R1)     WILL STACK OVERFLOW
         BH    PSALSFCC-PSA(0,0)            YES, ISSUE 07D ABEND
         AGO       .COMMON1
.BRANCH  BNE   *+12                         NO, SKIP UPDATE
.COMMON  AL    &R2,FRRSELEN-FRRS(0,&R1)     COMPUTE NEXT ENTRY ADDR
.COMMON1 ST    &R2,FRRSCURR-FRRS(0,&R1)     UPDATE CURRENT ENTRY ADDR
         AIF        ('&FRRAD'(1,1) EQ '(').STFRR
         AIF        (T'&FRRAD EQ 'N').MSG4
         L     &R1,&FRRAD                   LOAD FRR ENTRY ADDRESS
         ST    &R1,FRRSFRRA-FRRSENTR(0,&R2) ADD ADDRESS TO THE STACK
         AGO         .PMCHK
.STFRR   ST    &R4,FRRSFRRA-FRRSENTR(0,&R2) ADD ADDRESS TO STACK
.PMCHK   AIF    ('&PARMAD' EQ '' ).END
.*                             DETERMINE IF PARMS ARE SPECIFIED
         AIF        ('&PARMAD'(1,1) EQ '(').LPARM
         AIF        (T'&PARMAD EQ 'N').MSG4
         AIF     ('&OPT' EQ 'R' AND '&CLEAR' EQ 'Y').C5
         AIF     ('&OPT' EQ 'R' AND '&CLEAR' EQ 'YES').C5
         AIF       ('&OPT' NE 'A').NOZERO1            IS THIS THE
.*                                                    ADD OPTION
.*                                                   YES DO FOLLOWING
         AIF   ('&CLEAR' EQ 'N' OR '&CLEAR' EQ 'NO').NOZERO1
.C5      XC    FRRSPARM-FRRSENTR(24,&R2),FRRSPARM-FRRSENTR(&R2)
*                                           ZERO FRR PARM AREA
.NOZERO1 LA    &R2,FRRSPARM-FRRSENTR(0,&R2) RETURN PARM AREA
         ST    &R2,&PARMAD                  ADDRESS TO USER
         AGO        .END
.LPARM   ANOP
         AIF      ('&OPT' EQ 'R' AND '&CLEAR' EQ 'Y').C6
         AIF      ('&OPT' EQ 'R' AND '&CLEAR' EQ 'YES').C6
         AIF       ('&OPT' NE 'A').NOZERO2            IS THIS THE
.*                                                    ADD OPTION
.*                                                   YES DO FOLLOWING
         AIF   ('&CLEAR' EQ 'N' OR '&CLEAR' EQ 'NO').NOZERO2
.C6      XC    FRRSPARM-FRRSENTR(24,&R2),FRRSPARM-FRRSENTR(&R2)
*                                           ZERO FRR PARM AREA
.NOZERO2 LA    &R3,FRRSPARM-FRRSENTR(0,&R2) ADDRESS OF PARM AREA
         AGO       .END
.PURGE   L     &R2,FRRSEMP-FRRS(0,&R1)      LOAD EMPTY STACK ADDRESS
.UPDATE  ST    &R2,FRRSCURR-FRRS(0,&R1)     UPDATE CURRENT STACK ENTRY
         AGO       .END
.FLUSH   ANOP
&NAME    L     &R1,PSASTAK-PSA(0,0)               NORMAL STACK PTR
         ST    &R1,PSASTAK-PSA+FRRSCURR-FRRS(0,0) PURGE STACK
         MVI   PSASTAK-PSA+FRRSRTMW-FRRS(0),X'00' ZERO RECURSION BYTE
         AGO       .END
.MSG1    MNOTE     12,'WORK REGISTERS NOT SPECIFIED'
         AGO       .END
.MSG2    MNOTE     8,'WORK REGISTERS SPECIFIED INCORRECTLY'
         MNOTE     8,'SPECIFY AS FOLLOWS - WRKREGS=(REGISTER,REGISTER)'
         AGO       .END
.MSG3    MNOTE     8,'FRR ADDRESS NOT SPECIFIED FOR ADD/REPLACE'
         AGO        .END
.MSG4    MNOTE  8,'ADDRESS SPECIFIED FOR FRRAD/PARMAD IS SELF-DEFINING'
.END     MEND

