.* MVS 3.8J PUBLIC DOMAIN
* %/*  THIS MAKES THE BAL VERSION LOOK LIKE A PL/S MACRO COMMENT
* /* MACCOMP Y-1 SC1C5/PZD81                                         */
* /* MACSTAT Y-1 72237/015157                                        */
* /* MACDATE Y-1 72237                                               */
* /*
         MACRO
&NAME    DOM   &MSG=,&MSGLIST=,&REPLY=
.**********************************************************************
.*                                                                    *
.*  THIS MACRO WAS REWRITTEN FOR OS/VS2 RELEASE2.                     *
.*                                                                    *
.*  VERSION 01   DATE  6/1/72                                         *
.*                                                                    *
.*  TESTS ARE DOCUMENTED BY EXPLAINING WHAT IS BEING TESTED.          *
.*  THE COMMENT AFTER THE AIF (FALSE PATH) WILL EXPLAIN WHAT CONDITION*
.*  WAS FOUND. THE COMMENT BEFORE THE TRUE TARGET WILL EXPLAIN THE    *
.*  MEANING OF THE TRUE CONDITION.                                    *
.**********************************************************************
         LCLA  &RPYINDC        REPLY OPERAND INDICATOR
&RPYINDC SETA  0              INIT TO REPLY OPERAND NOT PRESENT
.*  CHECK IF REPLY KEYWORD IS PRESENT.
         AIF   (T'&REPLY EQ 'O').DOML2
.* REPLY OPERAND IS PRESENT.  CHECK IF IT IS 'YES'.
         AIF   ('&REPLY' EQ 'YES').DOML1
.* REPLY OPERAND IS IN ERROR
         MNOTE 4,'*** REPLY OPERAND NOT YES. REPLY KEYWORD IGNORED.'
         AGO   .DOML2
.DOML1   ANOP                 REPLY OPERAND WAS 'YES' BRANCH PT
&RPYINDC SETA  4                        SHOW THAT REPLY = YES USED
.*  CHECK IF THERE IS AN ERROR IN MSG-MSGLIST KEYWORDS
.DOML2   AIF   ((T'&MSG NE 'O' AND T'&MSGLIST EQ 'O' ) OR              X
               (T'&MSG EQ 'O' AND T'&MSGLIST  NE 'O')).DOML3
.*  THERE IS AN ERROR IN MSG, MSGLIST USAGE. ISSUE ERROR MESSAGE
.* CHECK IF BOTH MSG AND MSGLIST ARE PRESENT.
         AIF   (T'&MSG NE 'O' AND T'&MSGLIST NE 'O').DOML2A
.* ERROR DUE TO BOTH KEYWORDS MISSING.
         IHBERMAC   24
         MEXIT
.DOML2A  ANOP                ERROR DUE TO BOTH PRESENT
         MNOTE 12,'*** BOTH MSG AND MSGLIST SPECIFIED.  USE JUST ONE.'
         MEXIT
.*  A VALID KEYWORD IS SPECIFIED. CHECK IF IT IS MSG KEYWORD.
.DOML3   AIF   (T'&MSG EQ 'O').DOML7    PROCESS MSGLIST IF TEST TRUE
.* YES, MSG SPECIFIED. IS OPERAND CORRECT
         AIF   ('&MSG'(1,1) EQ '(' AND '&MSG(1)' GE '1' AND            X
               '&MSG(1)' LE '12').DOML4
.* NO, OPERAND IS INCORRECT
         IHBERMAC   18
         MEXIT
.* YES, OPERAND IS CORRECT. CHECK IF REGISTER 1.
.DOML4   AIF   ('&MSG(1)' NE '1').DOML5
.* YES, REGISTER IS 1. CHECK IF &NAME WAS SPECIFIED.
         AIF   (T'&NAME EQ 'O').DOML6
.* &NAME WAS SPECIFIED, PUT OUT A LABEL
&NAME    DS    0H
         AGO   .DOML6
.* NO, REGISTER 1 NOT SPECIFIED. NOVE ADDRESS TO REGISTER 1.
.DOML5   ANOP                 REGISTER IS 2 - 12. MOVE IN TO REG 1
&NAME    LR    1,&MSG(1)      .MOVE MSG ID TO REGISTER 1
.DOML6   ANOP
         LA    0,&RPYINDC.(0,0) .LOAD MSG/REPLY INDICATOR        YM1995
         SVC   87             .ISSUE DOM SVC
         MEXIT
.*  VALID KEYWORD WAS MSGLIST.
.DOML7   ANOP
&RPYINDC SETA  &RPYINDC+8     INCREMENT INDICATOR BY 8 FOR MSGLIST.
.*  CHECK IF OPERAND IS AN ADDRESS NAME
         AIF   ('&MSGLIST'(1,1) EQ '(').DOML9
.*  OPERAND IS A NON PARENTHSIZED NAME. THEREFORE IT IS AN ADDR NAME.
&NAME    LA    1,&MSGLIST     .PUT ADDRESS OF ID LIST IN REGISTER 1
         AGO   .DOML12        LOOP AROUND FALSE PART OF LAST TEST
.*  OPERAND IS PARENTHESIZED REGISTER. CHECK IF IN RANGE 1 TO 12.
.DOML9   AIF   ('&MSGLIST(1)' GE '1' AND '&MSGLIST(1)' LE '12').DOML10
.*  NO, INVALID REGISTER SPECIFIED. ISSUE ERROR MESSAGE
         IHBERMAC 18
         MEXIT
.*   A VALID REGISTER WAS SPECIFIED. SEE IF IT IS REGISTER 1
.DOML10  AIF   ('&MSGLIST(1)' NE '1').DOML11
.*   YES IT IS REGISTER 1. PUT OUT LABEL IF PRESENT
         AIF   (T'&NAME EQ 'O').DOML12
.*   LABEL IS PRESENT
&NAME    DS    0H
         AGO   .DOML12
.*   NO, NOT REGISTER 1. PUT OUT LR INSTRUCTION.
.DOML11  ANOP
&NAME    LR    1,&MSGLIST(1)  .MOVE LIST ADDRESS TO REGISTER 1
.DOML12  ANOP                 PUT OUT REST OF MSGLIST
.*  CHECK IF REPLY KEYWORD SPECIFIED
         AIF   (&RPYINDC EQ 8).DOML13
.*  REPLY WAS SPECIFIED
         LA    0,&RPYINDC.(0,0) .LOAD MSGLIST/REPLY INDICATOR    YM1995
         AGO   .DOML14
.DOML13  ANOP
         LNR   0,1            .INDICATE MSGLIST ONLY
.DOML14  ANOP
         SVC   87             .ISSUE DOM SVC
         MEXIT
         MEND
**/
*DOM:  MACRO KEYS( MSG,      /* SINGLE MESSAGE ID IS PASSED          */
*                  MSGLIST,  /* A LIST OF UP TO 60 IDS IS PASSED     */
*                  REPLY);   /* DELETE WTORS AS WELL AS WTOS         */
*        ANS('?'||MACLABEL||'DOMP '||MACLIST||MACKEYS||';');
*%END;

