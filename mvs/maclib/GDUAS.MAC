.* MVS 3.8J PUBLIC DOMAIN
         MACRO
&NAME    GDUAS &ID=,&GDUNUMB=
         GBLA  &IECGNUM,&IECGDAS,&IECCNTR
.*             IECGNUM   TOTAL NUMBER OF GUIDANCE DISPLAY UNITS
.*             IECGDAS   VALUE OF ID OPERAND OF LAST GDUAS MACRO
.*             IECCNTR   VALUE OF ID OPERAND OF LAST AS MACRO
         GBLB  &IECSW(9),&IECSEQ(10),&IECSQ(7),&IECGDSW
.*             IECGDSW   INDICATES GDU=YES IN CONFIGUR MACRO
.*             IECSQ(1) SEQUENCE INDICATOR FOR GDUAS MACRO
         LCLA  &A
         LCLB  &NOCODE
         LCLC  &CHAR
         ACTR  1000
         AIF   (&IECSEQ(1) AND &IECSEQ(2) AND NOT&IECSEQ(3) AND NOT &IEC
               CSEQ(4) AND NOT&IECSEQ(5) AND NOT&IECSEQ(6)).SEQOK
.SEQERR  ANOP
&NOCODE  SETB  1
         MNOTE 12,'MACRO SEQUENCE ERROR'
         AGO   .SEQDON
.SEQOK   ANOP
         AIF   (NOT&IECSEQ(7) AND NOT&IECSEQ(8) AND NOT&IECSEQ(9) AND  X
               NOT&IECSEQ(10) AND NOT&IECSQ(2) AND NOT&IECSQ(3)).SQ1
         AGO   .SEQERR
.SQ1     ANOP
         AIF   (NOT&IECSQ(4) AND NOT&IECSQ(5) AND NOT&IECSQ(6)).SEQDON
         AGO   .SEQERR
.SEQDON  ANOP
         AIF   (&IECGDSW).GDUOK
&NOCODE  SETB  1
         MNOTE 12,'GDU=YES MUST BE CODED IN CONFIGUR MACRO'
.GDUOK   ANOP
         AIF   (T'&ID NE 'O').ID1
&NOCODE  SETB  1
         MNOTE 12,'ID OPERAND CANNOT BE OMITTED'
         AGO   .NUM4
.ID1     ANOP
         AIF   (T'&ID EQ 'N').ID2
&NOCODE  SETB  1
         MNOTE 12,'ID OPERAND HAS INVALID TYPE ATTRIBUTE'
         AGO   .NUM4
.ID2     ANOP
         AIF   (&ID GE 0 AND &ID LE 99).ID3
&NOCODE  SETB  1
         MNOTE 12,'ID OPERAND IS NOT IN RANGE OF 0 TO 99'
.ID3     ANOP
         AIF   (&IECSQ(1)).ID4
         AIF   (&ID EQ 0).NUM
.ID4     ANOP
         AIF   (&ID GT &IECGDAS).NUM
&NOCODE  SETB  1
         MNOTE 12,'ID OPERAND MUST BE GREATER THAN ID OF PREV   MACRO'
.NUM     ANOP
         AIF   (&ID LE &IECCNTR).NUM4
&NOCODE  SETB  1
         MNOTE 12,'ID OPERAND CAN NOT BE HIGHER THAN HIGHEST ID OPERANDX
               IN AS MACROS'
.NUM4    ANOP
         AIF   (T'&GDUNUMB NE 'O').NUM1
&NOCODE  SETB  1
         MNOTE 12,'GDUNUMB OPERAND CANNOT BE OMITTED'
         AGO   .GEN
.NUM1    ANOP
         AIF   (T'&GDUNUMB EQ 'U').NUM7
         AIF   (T'&GDUNUMB EQ 'N').NUM2
.NUM7    ANOP
&NOCODE  SETB  1
         MMOTE 12,' GDUNUMB OPERAND HAS INVALID TYPE ATTRIBUTE'
         AGO   .GEN
.NUM2    ANOP
         AIF   (&GDUNUMB GE 1 AND &GDUNUMB LE 16).GEN
&NOCODE  SETB  1
         MNOTE 12,'GDUNUMB OPERAND NOT IN VALID RANGE OF 1 TO 16'
.GEN     ANOP
         AIF   (K'&GDUNUMB EQ 1 OR K'&GDUNUMB EQ 2).GENN1
&NOCODE  SETB  1
         MNOTE 12,'GDUNUMB OPERAND HAS INVALID NUMBER OF CHARACTERS'
.GENN1   ANOP
         AIF   (&NOCODE).NOGEN
         AIF   (&IECSQ(1)).NONAME
IECGDUAS EQU   *   GUIDANCE DISPLAY UNIT AREA STATION TABLE
.NONAME  ANOP
&A       SETA  &IECGDAS+1
         AIF   (&ID EQ &A AND &ID EQ 1 AND NOT &IECSQ(1)).GEN1
.LOOP    AIF   (&ID EQ &A OR &ID EQ 0).GEN2
         AIF   (&IECGDAS NE 0 OR &IECSQ(1)).GEN3
.GEN1    ANOP
&IECGDAS SETA 1
&A       SETA  0
.GEN3    ANOP
         DC    YL1(0)
&A       SETA  &A+1
         AGO   .LOOP
.GEN2    ANOP
&IECGDAS SETA  &ID
&IECSQ(1) SETB 1
         AIF   (&IECGNUM LE 256).GEN5
         MNOTE 3,'TOTAL NUMBER OF GUIDANCE DISPLAY UNITS EXCEEDS 256,  X
               GDUNUMB OPERAND IGNORED'
         DC    YL1(0)
         AGO   .MEND
.GEN5    ANOP
         DC    YL1(&IECGNUM)
&IECGNUM SETA  &IECGNUM+&GDUNUMB
         AGO   .MEND
.NOGEN   ANOP
         MNOTE 12,'TEXT GENERATION SUPPRESSED'
.MEND    ANOP
         MEND

