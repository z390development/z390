.* MVS 3.8J PUBLIC DOMAIN
         MACRO
&ANYNM   TSTCOM  &TDCBA,&TDEVT,&TMF
         GBLB  &QUITSW
.*  FUNCTION - TEST DCB,DEVT,AND MF    PARAMETERS FOR VALID SPEC
.*           - CAUSE THE PARAMETER LIST TO BEGIN ON WORD BOUNDARY
.*             FOR THE STANDARD AND LIST FORMS.
.*       TEST MF  PARAMETER
         AIF   ('&TMF' EQ '' OR '&TMF' EQ 'L' OR '&TMF(1)' EQ 'E').NXTTX
               ST1     TEST FOR STD LIST OR EXECUTE FORMS
         IHBERMAC 035,,&TMF,,,0    INVALID MACRO FORM MNOTE
&QUITSW  SETB  1                   SET MACRO TERMINATION FLAG
         AGO   .NXTS1A             GO TEST DCB OPERAND
.NXTTST1 AIF  ('&TMF' EQ '' OR '&TMF' EQ 'L').NXTS1A    STD/LIST FORM
         AIF   ('&TMF(2)' NE '' AND '&TMF(2)' NE '(0)').NXTS1A
         IHBERMAC 035,,&TMF,,,0    INVALID MACRO FORM MNOTE
&QUITSW  SETB  1                   SET MACRO TERMINATION FLAG
.*       TEST DCB ADDRESS
.NXTS1A  ANOP
         AIF   ('&TDCBA' NE '' OR '&TMF' EQ 'L' OR '&TMF(1)' EQ 'E').NXX
               TTST2         TEST IF DCB SPECIFIED FOR STANDARD FM
         IHBERMAC  006     ISSUE MNOTE DCB REQUIRED NOT SPECIFIED
&QUITSW  SETB  1                   SET MACRO TERMINATION FLAG
         AGO   .NXTTST3            GO TEST DEVT OPERAND
.NXTTST2 AIF   ('&TDCBA' EQ '').NXTTST3 DCB PARAMETER PRESENT
         AIF   ('&TMF' EQ 'L').NXTS1B  TEST FOR MF=L
         AIF   ('&TDCBA' NE '(1)' AND '&TDCBA' NE '(0)').NXTTST3
.*                           TEST IF DCB SPEC IN REG 0 OR 1,
         IHBERMAC  252,,&TDCBA     INVALID DCB OPERAND
&QUITSW  SETB  1                   SET MACRO TERMINATION FLAG
         AGO   .NXTTST3                GO TEST DEVT PARAMETER
.NXTS1B  AIF   ('&TDCBA'(1,1) NE '(').NXTTST3 VALID FORM MF=L
         IHBERMAC  069                 INVALID REG NOTATION WITH MF=L
&QUITSW  SETB  1                   SET MACRO TERMINATION FLAG
.*       TEST DEVT PARAMETER
.NXTTST3 AIF   ('&TDEVT' NE '').NXTTST4  TEST IF DEVT SPEC REQ PARM
         IHBERMAC  260
         AGO   .SETQUIT                  GO SET QUIT SWITCH ON
.*       AT THIS TIME 3886 AND SENECA ARE ONLY DEVICES SUPPORTED
.NXTTST4 AIF   ('&TDEVT' EQ '3886' OR '&TDEVT' EQ '3890').NXTTST5
         IHBERMAC  253,,&TDEVT     INVALID DEVICE TYPE
         AGO   .SETQUIT                  GO SET QUIT SWITCH ON
.NXTTST5 AIF   (&QUITSW).FINI      TEST QUIT SWITCH
.*       FORCE WORD BOUNDARY ALIGNMENT FOR LIST AND STANDARD FORMS
.ALIGN   AIF   ('&TMF' EQ 'L').LISTALN    TEST FOR LIST FM ALIGN DS
         CNOP  0,4
         MEXIT
.LISTALN ANOP
         DS    0F
         MEXIT
.SETQUIT ANOP
&QUITSW  SETB  1
.FINI    MEND
