.* MVS 3.8J PUBLIC DOMAIN
         MACRO
&NAME    NIL   &BYTE,&MASK,&REF=,&WREGS=(0,1,2)
.*             AND IMMEDIATE LOCKED
.*             04/26/72, LEVEL=1
.*
.*       THIS MACRO IS USED IN A MULTIPROCESSING ENVIRONMENT TO
.*       PROVIDE A LOCK ON A WORD CONTAINING THE BYTE WHICH IS THE
.*       TARGET OF AN NI INSTRUCTION.
.*
.*       &BYTE      SYMBOL NAMING THE BYTE TO WHICH THE AND IMMEDIATE
.*                  FUNCTION IS TO BE APPLIED.
.*       &MASK      A ONE-BYTE SELF-DEFINING TERM OR SYMBOL EQUATED
.*                  TO SUCH A TERM WHICH SPECIFIES A MASK AS IN
.*                  IMMEDIATE OPERAND OF NI INSTRUCTION.
.*       &REF       LABEL OF A FULL WORD IN STORAGE.  ADDRESS OF REF
.*                  LABEL MUST BE LESS THAN OR EQUAL TO THAT OF BYTE
.*                  (FIRST OPERAND).  LABEL MUST BE ADDRESSABLE WHEN
.*                  THE MACRO IS ISSUED, AND USING MUST HAVE BEEN
.*                  ISSUED ON A FULL-WORD LABEL.
.* NOTE  &BYTE, &MASK AND &REF MUST BE SPECIFIED ON MACRO CALL.
.*       &WREGS     DECIMAL NUMBERS OR SYMBOLS EQUATED TO DECIMAL
.*                  NUMBERS OF THREE WORK REGISTERS TO BE USED BY THIS
.*                  MACRO.  REGISTERS 0, 1 AND 2 ARE DEFAULT VALUES.
.*                  THE SECOND WORK REGISTER MUST NOT BE 0 BECAUSE IT
.*                  IS USED IN AN EXECUTE INSTRUCTION.
         LCLC  &XREGS(3)      FOR WREGS VALUES
         AIF   ('&BYTE' NE '').NIL10    BRANCH IF BYTE SPECIFIED
         IHBERMAC 1006,BYTE   BYTE REQUIRED BUT NOT SPECIFIED
         MEXIT
.NIL10   AIF   ('&MASK' NE '').NIL20    BRANCH IF MASK SPECIFIED
         IHBERMAC 1006,MASK   MASK REQUIRED BUT NOT SPECIFIED
         MEXIT
.NIL20   AIF   ('&REF' NE '').NIL30     BRANCH IF REF SPECIFIED
         IHBERMAC 1006,REF    REF REQUIRED BUT NOT SPECIFIED
         MEXIT
.NIL30   AIF   ('&WREGS(1)' NE '').NIL35 BRANCH IF FIRST REG SPECIFIED
&XREGS(1) SETC '0'            USE REGISTER 0 AS DEFAULT
         AGO   .NIL40
.NIL35   ANOP
&XREGS(1) SETC '&WREGS(1)'
.NIL40   AIF   ('&WREGS(2)' NE '').NIL45 BRANCH IF SECOND REG SPECIFIED
&XREGS(2) SETC '1'            USE REGISTER 1 AS DEFAULT
         AGO   .NIL50
.NIL45   ANOP
&XREGS(2) SETC '&WREGS(2)'
.NIL50   AIF   ('&WREGS(3)' NE '').NIL55 BRANCH IF THIRD REG SPECIFIED
&XREGS(3) SETC '2'            USE REGISTER 2 AS DEFAULT
         AGO   .NIL60
.NIL55   ANOP
&XREGS(3) SETC '&WREGS(3)'
.NIL60   ANOP  ,              ALL OPERANDS SPECIFIED
&NAME    B     *+10           BRANCH TO LA INSTRUCTION
         DC    2AL1(&MASK)    HALF WORD OF MASK BYTES
         ICM   &XREGS(3),0,*-2 INSERT MASK BYTE IN REGISTER
         LA    &XREGS(2),1(0,0)  LOAD ONE IN REGISTER FOR USE IN
*                             SETTING UP MASK REGISTER           YM1995
         LNR   &XREGS(3),&XREGS(2) SET REGISTER TO ALL ONES
         SLL   &XREGS(2),3-((&BYTE-&REF)-(((&BYTE-&REF)/4)*4))(0)
*                             SHIFT THE ONE TO PROPER POSITION FOR
*                             4-BIT MASK FOR ICM INSTRUCTION     YM1995
         EX    &XREGS(2),*-14 EXECUTE ICM INSTRUCTION TO PUT MASK BYTE
*                             IN PROPER POSITION IN REGISTER WITH
*                             ALL ONES
         L     &XREGS(1),&REF+(((&BYTE-&REF)/4)*4)
*                             LOAD WORD CONTAINING BYTE TO BE SET
         LR    &XREGS(2),&XREGS(1) LOAD WORD CONTAINING BYTE TO BE SET
         NR    &XREGS(2),&XREGS(3) AND MASK INTO BYTE IN REGISTER
*                             USE COMPARE AND SWAP TO STORE UPDATED
*                             REGISTER IN STORAGE IF STORAGE WORD WAS
*                             NOT MODIFIED
         CS    &XREGS(1),&XREGS(2),&REF+(((&BYTE-&REF)/4)*4)
         BNE   *-8            DO AGAIN FROM LR IF WORD WAS MODIFIED
         MEND

