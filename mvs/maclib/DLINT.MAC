.* MVS 3.8J PUBLIC DOMAIN
         MACRO
&NAME    DLINT &LFR=,&LINBEG=,&IMAGE=,&NOSCAN=,&FREND=,                X
               &FLD1=,&EDIT1=,&FLD2=,&EDIT2=,&FLD3=,&EDIT3=,           X
               &FLD4=,&EDIT4=,&FLD5=,&EDIT5=,&FLD6=,&EDIT6=,           X
               &FLD7=,&EDIT7=,&FLD8=,&EDIT8=,&FLD9=,&EDIT9=,           X
               &FLD10=,&EDIT10=,&FLD11=,&EDIT11=,&FLD12=,&EDIT12=,     X
               &FLD13=,&EDIT13=,&FLD14=,&EDIT14=
         GBLA  &IHBEND,&IHBI3,&IHBNSC(51),&IHBSIZ,&IHBFLN,&IHBFEA(14)
         GBLB  &IHBLFR(64),&IHBDFC,&IHBIMG,&IHBFLD,&IHBDEL,&IHBER1
         GBLB  &IHBFT(6),&IHBNAT
         LCLA  &I1,&I2,&I4,&HOLD,&LMARTN,&LMARDG,&I8,&FRNPO
         LCLB  &B1,&B2,&B3,&B4,&NFNT1,&NFNT2
         ACTR  9999
         AIF   (&IHBDFC EQ 1).DL010     VALID DFR PRECEDE THIS MACRO
         MNOTE 12,'***  IHB367  DFR MACRO INVALIDLY SPECIFIED. DLINT IGX
               NORED'
         AGO   .END                     EXIT MACRO
.* STATEMENTS .DL010 THRU .DL040 CHECK LFR KEYWORD
.DL010   AIF   ('&LFR' NE '').DL030     LFR KEYWORD SPECIFIED,NO ERR
.DL020   MNOTE 8,'***  IHB368  LFR PARAMETER INVALIDLY SPECIFIED. DLINTX
                IGNORED'
         AGO   .END                     EXIT MACRO
.DL030   AIF   (T'&LFR NE 'N').DL020    LFR EQ A NUMBER
         AIF   (&LFR GT 63).DL020       LFR VALID NUMBER
&FRNPO   SETA  &LFR+1                   SET INDEX EQ LFR NUMBER + 1
         AIF   (&IHBLFR(&FRNPO) EQ 0).DL040 LFR NUM ON PREVIOUS DLINT
         MNOTE 8,'***  IHB369  LFR=&LFR ON PREVIOUS DLINT. THIS DLINT IX
               GNORED'
         AGO   .END                     EXIT MACRO
.DL040   ANOP
&IHBLFR(&FRNPO) SETB 1                  IND LFR NO USED
.* STATEMENTS .DL050 TO .DL070 CHECK LINBEG KEYWORD
         AIF   ('&LINBEG' NE '').DL060 LINBEG SPECIFIED,NO ERR
.DL050   MNOTE 8,'***  IHB370  LINBEG PARAMETER INVALID OR NOT SPECIFIEX
               D. DLINT IGNORED'
         AGO   .END                    EXIT MACRO
.DL060   AIF   (T'&LINBEG NE 'N').DL050 LINBEG NUMERIC,YES VALID
         AIF   (&LINBEG LT 4 OR &LINBEG GT 85).DL050 VALID NUMERIC
&IHBEND  SETA  &LINBEG                 INIT FIELD END TO LINBEG
.* STATEMENTS .DL070 TO .DL150 CHECK NOSCAN KEYWORD AND BUILDS A GLOBAL
.*    TABLE OF NOSCAN FIELD ENDS
&IHBI3   SETA  0                       INIT NO OF NOSCAN FIELDS TO 0
         AIF   ('&NOSCAN' EQ '').DL150 NOSCAN KEYWORD SPECIFIED
.DL070   ANOP
&I2      SETA  &I2+1                   INCR INDEX OF NOSCAN OPERAND
         AIF   (T'&NOSCAN(&I2) EQ 'O').DL120 I2ND OPERAND OMITTED
         AIF   (T'&NOSCAN(&I2) EQ 'N').DL090 I2ND OPERAND NUMERIC
.DL080   MNOTE 8,'***  IHB371  NOSCAN OPERAND &I2 INVALID. OPERAND &I2 X
               IGNORED'
         AGO   .DL120                   GOTO CHECK NEXT SUBOPERAND
.DL090   AIF   (&NOSCAN(&I2) LE &IHBEND OR &NOSCAN(&I2) GT 88).DL080
&IHBI3   SETA  &IHBI3+1                 INCR OF VALID NOSCAN FIELDS
&I4      SETA  0                        INIT INDEX TO NOSCAN TABLE
.DL100   ANOP
&I4      SETA  &I4+1                    INCR INDEX TO NOSCAN TABLE
         AIF   (&NOSCAN(&I2) NE &IHBNSC(&I4)).DL110 FIELD SPCFD PREVS
&IHBI3   SETA  &IHBI3-1                 YES,DECR CTR OF VALID FIELDS
         AGO   .DL080                   GOTO ISSUE MESSAGE
.DL110   AIF   (&I4 LT &IHBI3).DL100    TABLE ALL CHKED FOR DUPS
&IHBNSC(&IHBI3) SETA &NOSCAN(&I2)       YES,PUT NOSCAN FIELD IN TABLE
.DL120   AIF   (N'&NOSCAN GT &I2).DL070 ALL NOSCAN ENTRIES CHECKED
.*     STATEMENTS .DL120 TO .DL150 REORDER TABLE ENTRIES
         AIF   (&IHBI3 LE 1).DL150      0 OR 1 VALID NOSCAN ONLY
&I4      SETA  1                        INIT INDEX TO FIRST ENTRY
&I2      SETA  2                        INIT INDEX TO 2ND ENTRY
.DL130   AIF   (&IHBNSC(&I4) LT &IHBNSC(&I2)).DL140 ENTRY OUT OF SEQ
&HOLD    SETA  &IHBNSC(&I4)             *
&IHBNSC(&I4) SETA &IHBNSC(&I2)          * SWITCH TABLE ENTRIES
&IHBNSC(&I2) SETA &HOLD                 *
.DL140   ANOP
&I2      SETA  &I2+1                    INCR SECOND INDEX
         AIF   (&I2 LE &IHBI3).DL130    INNER LEVEL SORT COMPLETE
&I4      SETA  &I4+1                    INCR FIRST INDEX
&I2      SETA  &I4+1                    SETUP 2ND INDEX
         AIF   (&I4 LT &IHBI3).DL130    TABLE ALL SORTED,YES CONT PROC
.DL150   ANOP
.* STATEMENTS .DL150 TO .DL170 CHECK IMAGE KEYWORD
&IHBIMG  SETB  0                       INIT SWITCH TO IMAGE=NO
         AIF   ('&IMAGE' EQ '').DL170  IMAGE SPECIFIED
         AIF   ('&IMAGE' EQ 'NO').DL170   * VALID IMAGE EQUAL
         AIF   ('&IMAGE' NE 'YES').DL160  *
&IHBIMG  SETB  1                       SET SWITCH TO IMAGE=YES
         AGO   .DL170                  CONTINUE PROCESSING
.DL160   MNOTE 4,'***  IHB372  IMAGE PARAMETER INVALID. NO ASSUMED'
.DL170   ANOP
.* STATEMENTS .DL170 TO .DL180 CHECK FOR FLD SPCFCTN,IF NONE ZERO NOSCN
&B1      SETB  (('&FLD1' NE '') OR ('&FLD2' NE '') OR                  X
                ('&FLD3' NE '') OR ('&FLD4' NE ''))
&B2      SETB  (('&FLD5' NE '') OR ('&FLD6' NE '') OR                  X
                ('&FLD7' NE '') OR ('&FLD8' NE ''))
&B3      SETB  (('&FLD9' NE '') OR ('&FLD10' NE '') OR                 X
               ('&FLD11' NE '') OR ('&FLD12' NE ''))
&B4      SETB  (('&FLD13' NE '') OR ('&FLD14' NE ''))
         AIF   (&B1 OR &B2 OR &B3 OR &B4).DL180 AT LEAST 1 FLD SPCFD
         MNOTE 8,'***  IHB373  NO FLDX PARAMETER SPECIFIED. DLINT IGNORX
               ED'
&I4      SETA  0                       INIT NOSCAN TAB INDEX
.DL175   AIF   (&I4 EQ &IHBI3).DL250   ALL ENTRIES ZEROED,YES EXIT MAC
&I4      SETA  &I4+1                   INCR TABLE INDEX
&IHBNSC(&I4) SETA 0                    ZERO TABLE ENTRY
         AGO   .DL175                  GOTO CHECK TABLE ALL ZEROED
.DL180   ANOP
.* STATEMENTS .DL180 TO .DL187 GENERATE LINE INFORMATION RECORD
&IHBSIZ  SETA  &IHBSIZ+4               INCR SIZE OF FR RCD GENERATED
         AIF   (&IHBSIZ GT 212).DL183  MAX RECORD GEND ALREADY
&NAME    DC    AL1(&LFR)      *** LINE INFORMATION RECORD ***
&HOLD    SETA  &IHBEND-4              *
&LMARTN  SETA  &HOLD/10               * SETUP LINE BEGIN
&LMARDG  SETA  &HOLD-(10*&LMARTN)     *
         DC    AL1(&LMARTN*16+&LMARDG) LEFT MARGIN
         DC    AL1(&IHBIMG*128)        IMAGE
         AGO   .DL187                  GOTO CHECK FLD AND EDIT KEYWORDS
.DL183   ANOP
&IHBER1  SETB  1                       INDICATE MAX FR SIZE GEND
.DL187   ANOP
&IHBDEL  SETB  0                       INIT FIELD DELIMETER SWITCH
&IHBFLD  SETB  0                       INIT VALID FIELD SWITCH
&IHBFLN  SETA  0                       INIT TOTAL OF FIELD LNGTHS EQ 0
.* THE DLINTIN MACRO CALLS CHECK THE FLD AND EDIT KEYWORDS
         DLINTIN 0,&FLD1,&FLD2,&FLD3,&FLD4,&FLD5,&FLD6,&FLD7,          X
               &FLD8,&FLD9,&FLD10,&FLD11,&FLD12,&FLD13,&FLD14
         DLINTIN 1,&FLD1,&EDIT1
         DLINTIN 2,&FLD2,&EDIT2
         DLINTIN 3,&FLD3,&EDIT3
         DLINTIN 4,&FLD4,&EDIT4
         DLINTIN 5,&FLD5,&EDIT5
         DLINTIN 6,&FLD6,&EDIT6
         DLINTIN 7,&FLD7,&EDIT7
         DLINTIN 8,&FLD8,&EDIT8
         DLINTIN 9,&FLD9,&EDIT9
         DLINTIN 10,&FLD10,&EDIT10
         DLINTIN 11,&FLD11,&EDIT11
         DLINTIN 12,&FLD12,&EDIT12
         DLINTIN 13,&FLD13,&EDIT13
         DLINTIN 14,&FLD14,&EDIT14
&I1      SETA  1                       *
.DL189   ANOP                          * ZERO OUT FIELD END TABLE
&IHBFEA(&I1) SETA 0                    *
&I1      SETA  &I1+1                   *
         AIF   (&I1 LT 15).DL189       *
         AIF   (&IHBFLD EQ 1).DL190    AT LEAST 1 VALID FIELD
         MNOTE 12,'***  IHB374  NO VALID FIELDS FOUND FOR LINE. INVALIDX
                FORMAT RECORD GENERATED'
.DL190   AIF   (&IHBDEL EQ 0).DL220    LAST FIELD CHAR DELIMITED,
         MNOTE 12,'***  IHB375  LAST FIELD CHARACTER DELIMITED. INVALIDX
                FORMAT RECORD GENERATED'
.* STATEMENTS .DL220 TO .DL245 CHECK FOR INVALID NOSCAN AND GENERATE
.*    NOSCANS AT END OF LINE
.DL220   AIF   (&IHBI3 EQ 0).DL245     ANY VALID NOSCANS,NO CONT PROC
&I8      SETA  0                       SET TABLE INDEX TO 0
.DL230   ANOP
&I8      SETA  &I8+1                   INCR TABLE INDEX
         AIF   (&IHBNSC(&I8) EQ 0).DL240  NOSCAN ALREADY GEND
         AIF   (&IHBNSC(&I8) GT &IHBEND).DL232  NOSCAN AT END OF LINE
         MNOTE 4,'***  IHB377  NOSCAN FIELD END &IHBNSC(&I8) NOT NEEDEDX
               . IGNORED'
         AGO   .DL234                  GOTO CHECK NEXT TABLE ENTRY
.DL232   ANOP
&IHBSIZ  SETA  &IHBSIZ+4               INCR FR SIZE
         AIF   (&IHBSIZ GT 212).DL233  MAX SIZE RECORD ALREADY GEND
&HOLD    SETA  &IHBNSC(&I8)-4          *
&LMARTN  SETA  &HOLD/10                * SETUP BCD FOR FIELD END
&LMARDG  SETA  &HOLD-(10*&LMARTN)      *
         DC    AL1(&LMARTN*16+&LMARDG)  *** NOSCAN FIELD ***
         DC    AL1(128)
         DC    AL1(0)
         DC    AL1(0)
         AGO   .DL234                  GO SET TAB  ENTRY TO ZERO
.DL233   ANOP
&IHBER1  SETB  1                       IND MAX FR SIZE REACHED
.DL234   ANOP
&IHBNSC(&I8) SETA 0                    SET TABLE ENTRY TO ZERO
.DL240   AIF   (&I8 LT &IHBI3).DL230   MORE ENTRIES TO CHECK
.DL245   AIF   (&IHBER1 EQ 0).DL248    MAX FR GEND,YES ISSUE MSG
         MNOTE 12,'***  IHB376  MAXIMUM FORMAT RECORD REACHED. LINES ANX
               D FIELDS MAY BE IGNORED'
.DL248   ANOP
         DC    X'FF'                      *** END OF LINE           ***
.* STATEMENTS .DL250 TO .END CHECK FREND AND GEN EQU FOR FONT
.DL250   AIF   ('&FREND' EQ '').END        FREND SPECIFIED
         AIF   ('&FREND' NE 'YES').DL260   FREND SPECIFIED EQ YES
         AIF   (&IHBFT(6) NE 1).DL255      FONT GRP 6 SPCFD PREVIOUS
         AIF   (&IHBFT(2) EQ 1).DL255      FONT GRP 2 SPCFD PREVIOUS
&IHBFT(1) SETB 1                           IND FONT GRP 1
.DL255   ANOP
&NFNT1   SETB  (&IHBFT(5) AND &IHBNAT)        SET ON FOR NATNHP=YES
&NFNT2   SETB  (&IHBFT(5) AND NOT(&IHBNAT))   SET ON FOR NATNHP=NO
IHBFONT  EQU   &IHBFT(1)*128+&IHBFT(2)*64+&IHBFT(3)*32+&IHBFT(4)*16+&NFX
               NT2*8+&NFNT1*4
         DC    X'FF'              *** END OF FORMAT RECORD          ***
IHBEND   EQU   *
         AGO   .END               EXIT MACRO
.DL260   AIF   ('&FREND' EQ 'NO').END   FREND VALID,NO ISSUE MSG
         MNOTE 8,'***  IHB378  FREND PARAMETER INVALIDLY SPECIFIED. NO X
               ASSUMED'
.END     MEXIT
         MEND
