*********************************************************************
* Copyright 2006 Automated Software Tools Corporation               *
* This source code is part of z390 assembler/emulator package       *
* The z390 package is distributed under GNU general public license  *
* Author - Don Higgins                                              *
*********************************************************************
*
* RUN DEMOGUI5 WITH GUAM OPTION TO DEMO TN3270 APPL WITH 3 FIELDS
*
* DEMOGUI6 PROMPS FOR NAME, ADDR, ZIP AND USES SEPARATE AREA OF 
* SAME SCREEN TO DISPLAY ANY FIELD THAT WAS UPDATED.           
*********************************************************************
* 02/27/06 NEW DEMO TO TEST TAB AND READ FOR MULTILPE FIELDS  
* 03/02/06 RPI 220 TEXT SF TEXT OVERWRITE, TEXT WCC X'04' BELL
* 03/13/06 RPI 220 USE ESC,WRT_EW TO CLEAR, REMOVE WCC_CKD    
* 03/15/06 RPI 224 CORRECT SBA ADDR FOR IC TO FIRST INPUT FIELD AND
*          USE RA COMMAND WITH NULLS TO CLEAR ERROR MESSAGE LINE    
*********************************************************************
DEMOGUI4 SUBENTRY
         GUAM  WINDOW,TITLE,'DEMOGUI6 TN3270 MULT FIELD TEST'
REFRESH  EQU   *
         TPUT  TITLE,LTITLE,FULLSCR      CLEAR SCREEN AND SHOW TITLE
LOOP     EQU   *
         TPUT  PROMPT,LPROMPT,FULLSCR    PROMPT FOR 3 FIELD INPUT
         TGET  INPUT,LINPUT,ASIS         READ AID OR ANY FIELD UPDT
         CLI   INPUT,CLEAR               CLEAR SCREEN ON CLEAR (CTRL-C)
         BE    REFRESH
         CLI   INPUT,PF1                 DISPLAY HELP SCREEN
         BE    HELP
         CLI   INPUT,PF3                 EXIT ON PF3
         BE    EXIT
         CLI   INPUT,ENTER               UPDATE CHANGED FIELDS ON ENTER
         BE    UPDATE
         UNPK  SHOW_HEX(3),INPUT(2)
         TR    SHOW_HEX(2),HEXTAB-240
         MVI   SHOW_HEX+2,C' '
         TPUT  SHOW_AID,LSHOW_AID,FULLSCR SHOW HEX AID AND IGNORE 
         B     LOOP
EXIT     SUBEXIT
HELP     EQU   * DISPLAY HELP SCREEN AND WAIT FOR ENTER TO REFRESH
         TPUT  SHOW_HELP,LSHOW_HELP,FULLSCR
         TGET  INPUT,LINPUT,ASIS
         B     REFRESH
UPDATE   EQU   * UPDATE LOWER SCREEN FROM MODIFIED INPUT FIELDS
         LTR   R1,R1 DSHX
         LA    R3,INPUT(R1)  R3 = END OF INPUT + 1
         LA    R4,INPUT+3    FIRST FIELD AFTER AID AND CURSOR SBA
         CLR   R4,R3         IS THERE AN UPDATED FIELD
         BNL   ERROR_NOFIELD SHOW NO FIELDS UPDATED ERROR
NEXT_FIELD EQU   *
         CLR   R4,R3         ANY MORE MODIFIED FIELDS IN INPUT
         BNL   LOOP          NO GO PROMPT AGAIN
         CLC   0(3,R4),NAME_KEY
         BE    UPDATE_NAME
         CLC   0(3,R4),ADDR_KEY
         BE    UPDATE_ADDR
         CLC   0(3,R4),ZIP_KEY
         BE    UPDATE_ZIP
         B     ERROR_BADFIELD      NO SBA MATCH, SHOW BAD SBA ERROR
UPDATE_NAME EQU *
         MVC   DNAME,3(R4)
         LA    R4,3+L'DNAME(R4)
         TPUT  SHOW_NAME,LSHOW_NAME,FULLSCR
         B     NEXT_FIELD
UPDATE_ADDR EQU *
         MVC   DADDR,3(R4)
         LA    R4,3+L'DADDR(R4)
         TPUT  SHOW_ADDR,LSHOW_ADDR,FULLSCR
         B     NEXT_FIELD
UPDATE_ZIP EQU *
         MVC   DZIP,3(R4)
         LA    R4,3+L'DZIP(R4)
         TPUT  SHOW_ZIP,LSHOW_ZIP,FULLSCR
         B     NEXT_FIELD
ERROR_NOFIELD EQU   *
         TPUT  SHOW_NOFIELD,LSHOW_NOFIELD,FULLSCR
         B     LOOP
ERROR_BADFIELD EQU   *
         TPUT  SHOW_BADFIELD,LSHOW_BADFIELD,FULLSCR
         B     LOOP
         PRINT DATA
TITLE    TN3270 ESC,WRT_EW,WCC_KD,(SBA,1,10),(SF,ATT_PA)
         DC    C'DEMOGUI6 UPDATE NAME, ADDR, AND/OR ZIP (PF1=HELP PF3=EX
               XIT)'
         TN3270 (SBA,15,10),(SF,ATT_PA,C'PRESS F1 FOR HELP')
LTITLE   EQU   *-TITLE
PROMPT   TN3270 WCC_KD
         TN3270 (SBA,3,10),(SF,ATT_PA)
         DC    C'ENTER NAME'
         TN3270 (SF,ATT_UA)
NAME_FIELD DC  40C'_'
         TN3270 (SF,ATT_PA)
         TN3270 (SBA,5,10),(SF,ATT_PA)
         DC    C'ENTER ADDR'
         TN3270 (SF,ATT_UA)
ADDR_FIELD DC  40C'_'
         TN3270 (SF,ATT_PA)
         TN3270 (SBA,7,10),(SF,ATT_PA)
         DC    C'ENTER  ZIP'
         TN3270 (SF,ATT_UN)
ZIP_FIELD DC   5C'_'
         TN3270 (SF,ATT_PA),(SBA,3,22),IC
LPROMPT  EQU   *-PROMPT
NAME_KEY TN3270 (SBA,3,22)
ADDR_KEY TN3270 (SBA,5,22)
ZIP_KEY  TN3270 (SBA,7,22)
INPUT    DS    XL200
LINPUT   EQU   *-INPUT
SHOW_NAME TN3270 WCC_KD,(SBA,9,10),(SF,ATT_PA)
         DC    C'NAME'
         TN3270 (SF,ATT_PA)
DNAME    DC    CL40' '
         TN3270 (SBA,15,10),(RA,15,80,0)
LSHOW_NAME EQU   *-SHOW_NAME
SHOW_ADDR TN3270 WCC_KD,(SBA,11,10),(SF,ATT_PA)
         DC    C'ADDR'
         TN3270 (SF,ATT_PA)
DADDR    DC    CL40' '
         TN3270 (SBA,15,10),(RA,15,80,0)
LSHOW_ADDR EQU   *-SHOW_ADDR
SHOW_ZIP TN3270 WCC_KD,(SBA,13,10),(SF,ATT_PA)
         DC    C'ZIP '
         TN3270 (SF,ATT_PA)
DZIP     DC    CL5' '
         TN3270 (SBA,15,10),(RA,15,80,0)
LSHOW_ZIP EQU   *-SHOW_ZIP
SHOW_AID TN3270 WCC_BKD,(SBA,15,10),(SF,ATT_PA)
         DC    C'NO ACTION FOR AID KEY HEX '
         TN3270 (SF,ATT_PA)
SHOW_HEX DC     C'HH ' EXTRA SPACE FOR UNPACK
         TN3270 (RA,15,80,0),(SF,ATT_PA)
LSHOW_AID EQU   *-SHOW_AID
HEXTAB   DC    C'0123456789ABCDEF'
SHOW_NOFIELD TN3270 WCC_BKD,(SBA,15,10),(SF,ATT_PA)
         DC    C'NO FIELDS CHANGED'
         TN3270 (RA,15,80,0),(SF,ATT_PA)
LSHOW_NOFIELD EQU   *-SHOW_NOFIELD
SHOW_BADFIELD TN3270 WCC_BKD,(SBA,15,10),(SF,ATT_PA)
         DC    C'FIELD COMMAND AND/OR SBA NOT FOUND'
         TN3270 (RA,15,80,0),(SF,ATT_PA)
LSHOW_BADFIELD EQU   *-SHOW_BADFIELD
SHOW_HELP TN3270 ESC,WRT_EW,WCC_KD
         TN3270 (SBA,2,10),(SF,ATT_PA)
         DC     C'DEMOGUI6 TEST INTERACTIVE TN3270 WITH 3 FIELDS'
         TN3270 (SBA,3,10),(SF,ATT_PA)
         DC     C'PRESS ENTER OR ANY AID KEY TO RETURN TO MAIN SCREEN'
         TN3270 (SBA,4,10),(SF,ATT_PA)
         DC     C'TAB CURSOR TO FIELD YOU WISH TO CHANGE'
         TN3270 (SBA,5,10),(SF,ATT_PA)
         DC     C'ENTER DATA IN ONE OR MORE FIELDS AND PRESS ENTER'
         TN3270 (SBA,6,10),(SF,ATT_PA)
         DC     C'THE ZIP FIELD IS NUMERIC SO ALPHA IS IGNORED'
         TN3270 (SBA,7,10),(SF,ATT_PA)
         DC     C'USE BACK SPACE WITHIN FIELD TO CORRECT FIELD'
         TN3270 (SBA,8,10),(SF,ATT_PA)
         DC     C'USE CLEAR (CTRL-C) TO REFRESH SCREEN'
         TN3270 (SBA,9,10),(SF,ATT_PA)
         DC     C'USE PF3 TO EXIT APPLICATION FROM MAIN SCREEN'
         TN3270 (SBA,10,10),(SF,ATT_PA)
         DC     C'ANY UNUSED AID KEY SUCH AS PF4-PF24 SHOWS HEX VALUE'
         TN3270 (SBA,11,10),(SF,ATT_PA)
         DC     C'SUBMIT RPI IF YOU FIND ANY ERRORS VIA WWW.Z390.ORG'
LSHOW_HELP EQU *-SHOW_HELP
         TN3270 SYMBOLS DEFINE TN3290 CONTROL AND ATTRIBUTE CODES
         GUAM   SYMBOLS DEFINE GUI MCS, SCREEN, AND GRAPH CODES
         EQUREGS
         END
