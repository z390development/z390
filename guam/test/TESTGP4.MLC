TESTGP#  CSECT
         YREGS
         STM   R14,R12,12(R13)
         LR    R12,R15
         USING TESTGP#,R12
         LA    R2,SAVE
         ST    R13,4(,R2)
         ST    R2,8(,R13)
         LR    R13,R2
         LA    R1,SCREEN1
         LA    R0,SCREEN1L
         TPUT  (1),(0),FULLSCR
         TGET  INBUFF,INBUFFL,ASIS,WAIT
         UNPK  AIDSHOW(3),INBUFF(2)
         TR    AIDSHOW(2),HEX-C'0'
         MVI   AIDSHOW+2,C'<'
         LA    R1,SCREEN2
         LA    R0,SCREEN2L
         TPUT  (1),(0),FULLSCR
         STIMER WAIT,BINTVL=ENDWAIT
         L     R13,4(,R13)
         LM    R14,R12,12(R13)
         SR    R15,R15
         BR    R14
SAVE     DC    18F'0'
ENDWAIT  DC    F'500'                GIVE 5 SECS TO EXAMINE SCREEN
* SCREEN1: WCC:SOUND_BELL+UNLOCK_KB+RESET_MDT
*          SBA TO LOCATION ZERO
*          REPEAT TO LOCATION ZERO X'5C'=EBCDIC ASTERISK
*          INSERT THE CURSOR (LEAVE IT AT TOP LEFT CORNER)
*          START FIELD LOW_INTENSITY+PROTECTED
*          (THIS SHOULD PUT ATTRIBUTE BYTE WHERE CURSOR WAS PUT)
SCREEN1  DC    X'C71140403C40405C131DF0'
SCREEN1L EQU   *-SCREEN1
* SCREEN2: WCC:SOUND_BELL+UNLOCK_KB+RESET_MDT
*          SBA TO LINE 10 COLUMN 20
*          DISPLAY 20 EBCDIC CHARACTERS STARTING THERE
*          NOTE: CURSOR IS NOT MOVED BY SCREEN2 DATA STREAM
SCREEN2  DC    X'C7114BE3'           4BE3 = LINE 10 COLUMN 20
         DC    C'>>>>INPUT-AID='
AIDSHOW  DC    C'??'
         DC    C'<<<<'
SCREEN2L EQU   *-SCREEN2
INBUFF   DC    XL80'00'
INBUFFL  EQU   *-INBUFF
HEX      DC    CL16'0123456789ABCDEF'
         END
