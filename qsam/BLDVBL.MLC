*********************************************************************
* z390 - Mainframe assembler emulator and run-time engine
* Copyright (C) 2021 z390 Assembler LLC
*
* This file is part of z390.
*
* z390 is free software; you can redistribute it and/or modify
* it under the terms of the GNU General Public License as published by
* the Free Software Foundation; either version 2 of the License, or
* (at your option) any later version.
* z390 is distributed in the hope that it will be useful,
* but WITHOUT ANY WARRANTY; without even the implied warranty of
* MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
* GNU General Public License for more details.
*
* You should have received a copy of the GNU General Public License 
* along with this program; if not, see https://www.gnu.org/licenses.
*********************************************************************
* Author - Don Higgins                                              *
* Date   - 09/30/05                                                 *
* 09/16/2021 DSH COPYRIGHT, BLDVBL
*********************************************************************
BLDVBL   SUBENTRY
         WTO   'BLDVBL CREATE RECFM=VB FILE WITH LBI'
         OPEN  (SYSUT2,(OUTPUT),SNAPOUT,(OUTPUT))
         LA    R4,1
         LA    R5,10
LOOP     EQU   *
         LA    R8,RECORD+4
         LR    R9,R5
         LA    R10,0
         LR    R11,R4
         O     R11,=X'000000F0' 
         SLL   R11,24
         MVCL  R8,R10 FILL WITH EBCIDC RECORD #
         LA    R6,4(R5)
         LA    R7,RECORD(R6)
         O     R6,=X'80000000'  SET LBI HIGH BIT
         ST    R6,LLLL
         PUT   SYSUT2,RECORD
         SAVE  (R14,R12)
         SNAP   STORAGE=(RECORD,(R7)),PDATA=,ID=(R4),DCB=SNAPOUT
         RESTORE (R14,R12)
         LA    R4,1(R4) 
         MH    R5,=H'10'
         C     R5,=F'100000'
         BNH   LOOP
EOF      CLOSE (SYSUT2,,SNAPOUT)
         WTO   'BLDVBL ENDED OK'
         SUBEXIT 
ABEND    ABEND 111,DUMP
         LTORG          
SYSUT2   DCB   DSORG=PS,DDNAME=SYSUT2,MACRF=PM,RECFM=VB,BLKSIZE=100008,X
               DCBE=SYSUT2E
SYSUT2E  DCBE  SYNAD=ABEND,BLKSIZE=1  (1=LBI LARGE BLK BWD)
SNAPOUT  DCB   DDNAME=SNAPOUT,DSORG=PS,RECFM=VBA,BLKSIZE=882,          X
               LRECL=125,MACRF=W
RECORD   DS    0XL100004
LLLL     DC    XL4'00'
         DC    100000C'X'
         DCBD
         EQUREGS
         END
