.**********************************************************************
.* Copyright 2006 Automated Software Tools Corporation                *
.* This source code is part of z390 assembler/emulator package        *
.* The z390 package is distributed under GNU general public license   *
.* Author - Don Higgins                                               *
.**********************************************************************
         MACRO
         CICS_CANCEL
         GBLB  &PARMS_100C
         LCLB  &EDFOFF,&NOEDF,&NOHAND
         LCLC  &REQID,&RESP,&RESP2
&NP      SETA  N'&SYSLIST
&IP      SETA  0
.PLOOP   ANOP
&IP      SETA  &IP+1
         AIF   (&IP GT &NP).ELOOP
&PARM    SETC  '&SYSLIST(&IP)'
         AIF   ('&PARM'(1,6) EQ 'REQID(').REQID
         AIF   ('&PARM'(1,6) EQ 'EDFOFF').EDFOFF
         AIF   ('&PARM'(1,5) EQ 'NOEDF').NOEDF
         AIF   ('&PARM'(1,8) EQ 'NOHANDLE').NOHAND
         AIF   ('&PARM'(1,5) EQ 'RESP(').RESP
         AIF   ('&PARM'(1,6) EQ 'RESP2(').RESP2
         MNOTE 12,'BAD PARM &PARM'
         AGO   .PLOOP
.*
.REQID   ANOP
&REQID   SETC  '&PARM'(7,K'&PARM-7)
         AGO   .PLOOP
.*
.EDFOFF  ANOP
&EDFOFF  SETB  1
         AGO   .PLOOP
.*
.NOEDF   ANOP
&NOEDF   SETB  1
         AGO   .PLOOP
.*
.NOHAND  ANOP
&NOHAND  SETB  1
         AGO   .PLOOP
.*
.RESP    ANOP
&RESP    SETC  '&PARM'(6,K'&PARM-6)
&NOHAND  SETB  1
         AGO   .PLOOP
.*
.RESP2   ANOP
&RESP2   SETC  '&PARM'(7,K'&PARM-7)
         AGO   .PLOOP
.*
.ELOOP   ANOP
         GEN_XC P100C,P100CEND-P100C,P100C
         GEN_MVC P100CFN,,=X'100C' SET FUNCTION CODE
.* PROCESS REQID
         AIF   ('&REQID' EQ '').MISRQID
         AIF   ('&REQID'(1,1) EQ '''').REQSTRG
.* REQID FROM LABEL
         LARL  R15,P100CREQ       ADDRESS TARGET
         MVC   0(8,R15),&REQID    MOVE REQID FROM LABEL
         AGO   .DOCALL
.*
.REQSTRG ANOP
.* REQID AS STRING
&REQLEN  SETA  K'&REQID
         AIF   (&REQLEN LT 3 OR &REQLEN GT 10).BADREQ
         GEN_MVC P100CREQ,,=CL8&REQID SET REQID AS STRING
.DOCALL  ANOP
         GEN_ST DFHEIBR,P100CEIB  SET EIB ADDRESS
         GEN_ST TCTTEAR,P100CTCT  SET TCTTE ADDRESS
         AIF   (&SYSEDF AND NOT &NOEDF)
         GEN_ST R13,P100CEDS      SET DSA ADDRESS FOR EDF
         L     R0,DFHEILEN        DSA LENGTH
         GEN_STH R0,P100CEDL      SET DSA LENGTH FOR EDF
         LA    R0,DFHEIUSR        DSA USER AREA
         SR    R0,R13             R0=CICS DSA LENGTH
         GEN_STH R0,P100CEDC      SET CICS DSA LENGTH FOR EDF
         AELSE
         GEN_MVI P100CNED,X'FF'   SET NOEDF
         AEND
         AIF   (NOT &EDFOFF).DUNEOF
         GEN_MVI P100CEOF,X'FF'   SET EDF OFF
.DUNEOF  ANOP
         AIF   (NOT &NOHAND).DUNNOH
         GEN_MVI P100CNOH,X'FF'   SET NOHANDLE
.DUNNOH  ANOP
         AIF   ('&RESP' EQ '').NORESP
         LA    R0,&RESP           ADDRESS OF RESP
         GEN_ST R0,P100CRSP       SAVE IT
.NORESP  ANOP
         AIF   ('&RESP2' EQ '').NORESP2
         LA    R0,&RESP2          ADDRESS OF RESP2
         GEN_ST R0,P100CRS2       SAVE IT
.NORESP2 ANOP
         LARL  R0,P100CEDF_&SYSNDX ADDRESS ORIGIN FOR CEDF
         GEN_ST R0,P100CEDF       SAVE IT
         L     R15,TCTTELCL       R15=LCL MODULE INDEXER
         LARL  R1,=A(P100C)       R1=LCL MODULE PARAMETER LIST
P100CEDF_&SYSNDX EQU *
         BAKR  0,R15              STACK REGS AND GO
.*
         GEN_CLI P100CNOH,X'FF'   NOHANDLE ?
         JE    P100CBYP_&SYSNDX   EXIT IF IT IS
         OC    EIBRESP,EIBRESP    ANY BAD RESPONSE ?
         JZ    P100CBYP_&SYSNDX   EXIT IF NONE
* INVREQ, NOTFND
         DC    AL3(0),C'ABEND'    MARKER FOR Z390KCP ESTAE
         DC    AL4(P100CBYP_&SYSNDX) ENTRYPOINT FOR IGNORE CONDITION
*
         AIF   (&PARMS_100C).BYP100C
&PARMS_100C SETB 1
         P100C
*
.BYP100C ANOP
P100CBYP_&SYSNDX DS 0H
         MEXIT
.*
.MISRQID MNOTE 12,'REQID IS MANDATORY'
         MEXIT
.*
.BADREQ  MNOTE 12,'INVALID REQID'
         MEXIT
         MEND
