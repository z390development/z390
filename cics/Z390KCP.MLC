***********************************************************************
* Copyright 2006 Automated Software Tools Corporation                 *
* This source code is part of z390 assembler/emulator package         *
* The z390 package is distributed under GNU general public license    *
* Author - Don Higgins                                                *
***********************************************************************
* 05/07/06 RPI 308 ADD INITIAL EXEC CICS SUPPORT COMMAND PROCESSOR    *
* 06/17/08 RPI 845 CHANGE ESTAE ROUTINES TO USE NEW SDWA              *
* 11/11/08 RPI 946 CORRECTIONS TO MESSAGES TO PREVENT 6'S DISPLAYED   *
***********************************************************************
Z390KCP  SUBENTRY BASES=(R13,R3,R4)
*
* THE zCICS LOCAL MANAGER
*
* HANDLES ALL LOCAL SERVICES AND COMMS BETWEEN THE TASK AND THE
* GLOBAL MANAGER Z390CICS
*
* THIS IS A SINGLE TASK SYSTEM, CONTROL BLOCKS ARE GETMAINED HERE
* WE DON'T NEED THE ZCVT TO HOLD THE ADDRESSES
*
         L     R5,=A(ZCVT)
         USING IHAZCVT,R5
*
* ESTABLISH THE ABEND AND HANDLE CONDITION ROUTINE
*
         ESTAE APPABEND,CT
*
* GETMAIN TCTTE AND CLEAR IT
* THE TCTTE IS NEVER FREED, IT GOES WHEN THE THREAD IS TERMINATED
*
         GETMAIN R,LV=TCTTELEN
         LR    TCTTEAR,R1         TCTTE BASE
         USING DFHTCTTE,TCTTEAR
         XC    DFHTCTTE(TCTTELEN),DFHTCTTE CLEAR TCTTE
         MVC   TCTTEHED,=CL8'DFHTCTTE' SET HEADER
         MVC   TCTTEQFN,=PL3'1'   SET SEQ TERM FILE NUMBER
         MVC   TCTTEABC,=8C' '    SET ASSIGN ABCODE DEFAULT
         MVC   TCTTEINV,=8C' '    SET ASSIGN INVOKINGPROG DEFAULT
         MVC   TCTTERET,=8C' '    SET ASSIGN RETURNPROG DEFAULT
*
* THE LKA CARRIES THE DSA ADDRESS FOR EACH LINK LEVEL (TCTTELNK)
* IT'S USED TO DETERMINE WHERE WE GO BACK TO AT PROGRAM END
*
* SPACE FOR 25 LINK-LEVELS IS GETMAINED.
* NO CODING CHECKS WILL BE DONE IF THIS IS EXCEEDED.
*
* GETMAIN LKA AND CLEAR IT
* THE LKA IS NEVER FREED, IT GOES WHEN THE THREAD IS TERMINATED
*
         GETMAIN R,LV=100,A=TCTTELKA
         XC    0(100,R1),0(R1)    CLEAR LKA
         ST    R13,0(R1)          LINK-LEVEL 0 IS HERE
*
* Z390CICS PASSES TERMID AND OPTIONAL INITIAL TRANSID VIA PARM()
* TRANSID WILL BE ---- IF NONE
*
* TYPES OF PARM():
*    LTERMTRANK OR LTERM----K   LOCAL TERMINAL SUPPORT
*                               DATA PASSED BY Z390CICS
*
*    R                          REMOTE TERMINAL SUPPORT
*
         SR    R6,R6              CLEAR R6
         ICM   R6,3,ZCVTPARM      R6=PARM LENGTH
         BZ    PARMERR            ERROR IF ZERO
         CH    R6,=H'10'          MAX 10 BYTES
         BH    PARMERR            ERROR IF HIGHER
         MVC   TCTTETI,=8C' '     CLEAR TERMID
*
* OPEN TCPIO CLIENT
*
         TCPIO OPEN,HOST=*,PORT=3900 OPEN CLIENT PORT
         LTR   R15,R15            CHECK RC ?
         BNZ   TCPIOERR           EXIT IF BAD
         CLI   ZCVTPARM+2,C'L'    LOCAL AND Z390CICS PARM ?
         BE    Z390PARM           EXIT IF IT IS
         CLI   ZCVTPARM+2,C'!'    NON-TERM AND Z390CICS PARM ?
         BE    Z390PARM           EXIT IF IT IS
         B     OPENRMTE           EXIT IF REMOTE
*
Z390PARM EQU   *
* LOCAL TERMINAL INITIATED FROM Z390CICS
* NON-TERMINAL INITIATED FROM Z390CICS (GBL10FF)
         MVC   TCTTETI,ZCVTPARM+3 SET TERMID
         MVC   TCTTEINT,ZCVTPARM+7 SET INITIAL TRANSID
         MVC   INIDATE,ZCVTPARM+11 SET DATE FORMAT
         DROP  R5
         CLC   TCTTETI,=C'SQ01'   SEQUENTIAL TERMINAL ?
         BNE   OPEN3270           EXIT IF NOT
* SET UP SEQUENTIAL TERMINAL PATH AND FILENAME
         GETENV SEQENV            GET Z390SEQ ENV VARIABLE
         LA    R9,SEQFILE         R9=PATHNAME ADDRESS
Z390SEQL EQU   *
         MVC   0(1,R9),0(R2)      MOVE DSN A BYTE AT A TIME
         AHI   R2,1               BUMP SOURCE
         AHI   R9,1               BUMP DESTINATION
         CLI   0(R2),X'00'        END OF DSN ?
         BNE   Z390SEQL           LOOP IF NOT
         MVC   0(12,R9),=C'SEQI0001.TXT'
         ST    R9,TCTTEQFL        SAVE FILENAME ADDRESS
         LARL  R9,SEQRCVE         RECEIVE DCB
         ST    R9,TCTTEQDI        SAVE IT
         LARL  R9,SEQSEND         SEND DCB
         ST    R9,TCTTEQDO        SAVE IT
* OPEN OF SEQRCVE IS HANDLED IN EXEC CICS RECEIVE
         L     R9,TCTTEQFL        R9=FILENAME ADDRESS
         MVI   3(R9),C'O'         SET TO SEQO0001.TXT
         LARL  R1,SEQSEND         SEND DCB
         OPEN  ((R1),OUTPUT)      OPEN SEND FILE
         B     OPEN3270           EXIT
*
OPENRMTE EQU   *
* REMOTE, MUST ASK FOR TERMID/TRANSID
         LA    R6,KCPREQTM        MESSAGE TO SEND
         BAL   R9,TCPSEND         SEND IT
         BAL   R9,TCPRCVE         RECEIVE TCPIO RESPONSE
         CLC   KCPRCFN,=C'04FE'   CHECK IT'S RIGHT
         BE    TERMERR            EXIT IF REQUEST FAILED
         CLC   KCPRCFN,=C'04FF'   CHECK IT'S RIGHT
         BNE   TCPIOERR           EXIT IF NOT
         MVC   TCTTETI,KCPRCTRM   SAVE REQUESTED TERMID
         MVC   TCTTEINT,KCPRCTRN  SET INITIAL TRANSID
         MVC   INIDATE,KCPRCDAT   SET DATE FORMAT
OPEN3270 EQU   *
*
* OPEN 3270 WINDOW
*
         MVC   TERM_TITLE+9(4),TCTTETI    TERMID INTO TITLE
         GUAM  WINDOW,TITLE,TERM_TITLE    SHOW WHICH TERMINAL WE ARE
         GUAM  WINDOW,LOC,0,0             POSITION WINDOW TOP LEFT
         GUAM  WINDOW,VIEW,SCREEN,24,80,0 RESET SCREEN TO 24 X 80
*
* LOAD PCT, WE DON'T DELETE IT, IT'S TOO POPULAR
*
         LOAD  EP=DFHPCT,LOADPT=TCTTEPCT
*
* LOAD Z390LCL - LCL MODULE INDEXER
*
         LOAD  EP=Z390LCL,LOADPT=TCTTELCL
*
* LOAD LCLCEDF - EDF PROCESSOR
*
         LOAD  EP=LCLCEDF,LOADPT=TCTTEEDA
*
* TELL Z390CICS WE ARE UP AND RUNNING
*
         MVC   KCPUPTRM,TCTTETI   SET TERMID
         LA    R6,KCPUP           MESSAGE TO SEND
         BAL   R9,TCPSEND         SEND IT
*
LOCAL_MANAGER EQU *
*
* GETMAIN AND INITIALIZE EIB
* THE EIB WILL BE FREED AT TASK END
*
         GETMAIN R,LV=EIBLENG
         LR    DFHEIBR,R1         EIB BASE
         USING DFHEIBLK,DFHEIBR
         XC    DFHEIBLK(EIBLENG),DFHEIBLK CLEAR EIB
         MVC   EIBHEAD,=CL8'DFHEIB' SET HEADER
         MVC   EIBTRMID,TCTTETI   SET TERMID
         TIME  DEC                GET TIME/DATE
         ST    R0,TIMEDEC         SAVE TIME AS HHMMSSTT
         MVO   EIBTIME,TIMEDEC(3) MOVE AS PACKED
         OI    EIBTIME+3,X'0C'    SET +VE SIGN
         ST    R1,EIBDATE         SET DATE AS CCYYDDD+
         MVC   EIBDTFRM,INIDATE   SET DATE FORMAT
         MVC   EIBPROG,=8C' '     INITIALISE EIBPROG
         OC    TCTTECA,TCTTECA    ANY RETURN COMMAREA ?
         BZ    INITTSET           EXIT IF NONE
         MVC   EIBCALEN,TCTTECAL  SET COMMAREA LENGTH
*
* INTERNAL RECEIVE FOR NEXT TRANSID
* TRANSID MAY HAVE PRECEDING SBA
*
INITTSET EQU   *
         CLI   TCTTEINT,C'-'      ANY INITIAL TRANSID ?
         BE    INITGO             EXIT IF NOT
         MVC   INITBUFF(3),=X'7D40C4' FAKE ENTER,SBA(1,5)
         MVC   INITBUFF+3(4),TCTTEINT SET INITIAL TRANSID
         MVI   KCPGOTYP,C'A'      SET INITIAL TRANSID TYPE
         MVC   TCTTEINT,=4C'-'    NEVER AGAIN
         XC    P0402TGR,P0402TGR  FAKE A GOOD RECEIVE
         B     INITFAKE           EXIT
*
INITGO   EQU   *
         CLI   TCTTEIMM,X'FF'     RETURN IMMEDIATE SET ?
         BNE   NOIMM              EXIT IF NOT
* RETURN IMMEDIATE
         MVI   TCTTEIMM,X'00'     RESET RETURN IMMEDIATE FLAG
         MVC   INITBUFF(3),=X'7D40C4' FAKE ENTER,SBA(1,5)
         MVC   INITBUFF+3(4),TCTTETC SET RETURN TRANSID
         MVI   KCPGOTYP,C'C'      SET RETURN TRANSID TYPE
         XC    P0402TGR,P0402TGR  FAKE A GOOD RECEIVE
         B     INITFAKE           EXIT
*
NOIMM    EQU   *
         LA    R6,INITBUFF        AREA TO CLEAR
         LA    R7,L'INITBUFF      LENGTH TO CLEAR
         LR    R8,R6              AREA TO CLEAR
         SR    R9,R9              CLEAR SECOND LENGTH
         MVCLE R6,R8,X'00'        CLEAR INITBUFF
*
INITRCVE EQU   *
* PROCESS ANY UNSOLICITED SENDS
*    10FF  EXPIRED ICE
*    FE04  SHUTDOWN REQUEST
         TCPIO RECEIVE,NOWAIT,MSG=KCPRCVE,LMSG=200,PORT=3900
         CHI   R15,4              ANYTHING RECEIVED ?
         BE    INITCRCV           EXIT IF NOT
         LTR   R15,R15            CHECK RC ?
         BNZ   TCPIOERR           EXIT IF BAD
         CLC   KCPRCFN,=C'FE04'   SHUTDOWN REQUEST ?
         BE    SHUTKCP            EXIT IF IT IS
         CLC   KCPRCFN,=C'10FF'   EXPIRED ICE ?
         BNE   ICABEND1           EXIT IF NOT
* EXPIRED ICE, WE HAVE A TRANSID TO IC START
         MVC   INITBUFF(3),=X'7D40C4' FAKE ENTER,SBA(1,5)
         MVC   INITBUFF+3(4),KCPRCTRN SET TRANSID
         MVC   EIBREQID,KCPRCRQD  SET REQID
         MVC   KCPGOREQ,KCPRCRQD  SET REQID
         XC    P0402TGR,P0402TGR  FAKE A GOOD RECEIVE
         MVI   KCPGOTYP,C'D'      SET IC START-NO DATA TYPE
         CLI   KCPRCRTV,X'FF'     ANY TS Q DATA ?
         BNE   INITFAKE           EXIT IF NOT
         MVI   KCPGOTYP,C'E'      SET IC START-DATA TYPE
         B     INITFAKE           EXIT
*
ICABEND1 DC    H'0'               INVALID UNSOL
*
INITCRCV EQU   *
         EXEC  CICS RECEIVE INTO(INITBUFF) LENGTH(INITLEN)             X
               NOHANDLE NOWAIT
*
* THE TGET RETURN CODE IS IN P0402TGR
*
INITFAKE EQU  *
* BUFFER HAS AID(1), CURSOR POS(2), DATA
         CLFHSI P0402TGR,4        ANYTHING RECEIVED ?
         BNE   INITPROC           EXIT IF WE DID
* WE DIDN'T RECEIVE ANYTHING FROM THE SCREEN
         STIMER WAIT,BINTVL=INITDLAY WAIT 0.1 SECOND
         B     INITRCVE           EXIT TO RETRY
INITDLAY DC    F'10'              DELAY OF 0.1 SECOND
*
* PROCESS WHAT WE RECEIVED
*
INITPROC EQU   *
         LA    R6,INITBUFF        R6=INITIAL TIOA ADDRESS
         ST    R6,TCTTEDA         SET IN TCTTE
         MVC   EIBAID,INITBUFF    SET AID
* PROCESS CLEAR KEY, PASS IT IF WE HAVE A PRESET TRANSID
         OC    TCTTETC,TCTTETC    ANY PRESET TRANSID ?
         BNZ   NOCLEAR            EXIT IF WE HAVE ONE
         CLI   EIBAID,DFHCLEAR    CLEAR KEY ?
         BNE   NOCLEAR            EXIT IF NOT
         EXEC  CICS SEND CONTROL ERASE FREEKB FRSET CURSOR(0)
         B     INITTSET           EXIT
*
NOCLEAR  EQU   *
* CURSOR POS TO HEX EG. C1C6, ROW 1 COL 71, POS 70
         SR    R6,R6              CLEAR R6
         LR    R7,R6              CLEAR R7
         ICM   R6,8,INITBUFF+1    1ST BYTE TO TOP OF R6  C1..
         SLL   R6,2               SHIFT OUT PARITY BITS  04..
         SRL   R6,26              CONVERT TO 6 BIT VALUE ..01
         ICM   R7,8,INITBUFF+2    2ND BYTE TO TOP OF R7  ..01/C6...
         SLL   R7,2               SHIFT OUT PARITY BITS  ..01/18...
         SRDL  R6,26              COMBINE TO 12-BITS     ..../...46
         STH   R7,EIBCPOSN        SAVE IT
* TRANSID PRECEDENCE:
*    INITIAL TRANSID---ONCE OFF
*    TRANSID FROM RETURN IMMEDIATE (TCTTETC)
*    IC STARTED TRANSID
*    TRANSID FROM RETURN (TCTTETC)
*    TRANSID FROM SCREEN
         CLI   KCPGOTYP,C'D'      IC START-NO DATA ?
         BE    ICSTART            EXIT IF IT IS
         CLI   KCPGOTYP,C'E'      IC START-DATA ?
         BNE   TRYA               EXIT IF NOT
ICSTART  EQU   *
* IC STARTED TASK
* IT MAY BE INTERCEPTING A PSEUDO-CONVERSATIONAL TASK
* SO MUST NOT HARM TCTTETC
* EXEC CICS RETURN (LCL0E08) HAS MODS FOR THIS AS WELL
         MVC   EIBTRNID,INITBUFF+3 SET TRANSID
         OC    EIBTRNID,=8C' '    TRANSID TO UPPER CASE
         B     GOICTRAN           EXIT
*
TRYA     EQU   *
         CLI   KCPGOTYP,C'A'      INITIAL TRANSID ?
         BE    INITTRAN           EXIT IF IT IS
         MVI   KCPGOTYP,C'C'      SET RETURN TRANSID TYPE
         OC    TCTTETC,TCTTETC    ANY PRESET TRANSID ?
         BNZ   GOTRAN             EXIT IF WE HAVE ONE
* TRANSID FROM SCREEN
         MVI   KCPGOTYP,C'B'      SET SCREEN TRANSID TYPE
         CLI   INITBUFF+3,SBA     IS IT AN SBA ?
         BE    TISSBA             EXIT IF IT IS
INITTRAN EQU   *
         MVC   TCTTETC,INITBUFF+3 MOVE TRANSID
         B     GOTRAN             EXIT
*
TISSBA   EQU   *
         MVC   TCTTETC,INITBUFF+6 MOVE TRANSID
GOTRAN   EQU   *
         OC    TCTTETC,=8C' '     TRANSID TO UPPER CASE
         MVC   EIBTRNID,TCTTETC   SET POTENTIAL TRANSID
GOICTRAN EQU   *
         MVC   TCTTETYP,KCPGOTYP  SET THE TRANSID TYPE IN THE TCTTE
*
* SCAN PCT AND DO BLDL/LINK
* SCAN FAILURE ISSUES 'INVALID TRANSID' MSG
* BLDL FAILURE ISSUES APCT ABEND
*
         L     R6,TCTTEPCT        R6=PCT ADDRESS
PCTLOOP  EQU   *
         CLC   0(4,R6),EIBTRNID   FOUND TRANSID ?
         BE    PCTFOUND           EXIT IF IT IS
         AHI   R6,12              BUMP POINTER
         CLI   0(R6),X'FF'        STOPPER ?
         BE    INVTRAN            EXIT IF BAD TRANSID
         B     PCTLOOP            LOOP
*
PCTFOUND EQU   *
         MVC   BLDLNAME,4(R6)     SAVE PROGRAM NAME
         BLDL  0,BLDLLIST         BLDL
         LTR   R15,R15            RC=4 IF ENTRY NOT FOUND
         BNZ   INVPROG            EXIT IF NOT FOUND
*
* THE PROGRAM EXISTS
*
* TELL Z390CICS WE ARE STARTING A TRANSID
* THIS IS ALSO A REQUEST FOR A TASK NUMBER
* IF SHUTDOWN IS IN PROGRESS, THE REQUEST IS REJECTED
*
         MVC   KCPGOTRM,EIBTRMID  SET TERMID
         MVC   KCPGOTRN,EIBTRNID  SET TRANSID
         LA    R6,KCPGO           MESSAGE TO SEND
         BAL   R9,TCPSEND         SEND IT
         MVI   KCPGOTYP,C' '      CLEAR TRANSID TYPE
         BAL   R9,TCPRCVE         RECEIVE TCPIO RESPONSE
         CLC   KCPRCFN,=C'0E00'   CHECK IT'S RIGHT
         BNE   TCPIOERR           ABORT IF IT'S NOT
         PACK  EIBTASKN,KCPRCTSK  SAVE TASK NUMBER
*
* SAVE PASSED COMMAREA INFO IF NOT XCTL COPY
*
         CLI   TCTTECND,X'FF'     XCTL COMMAREA ?
         BE    XCTLCOMM           EXIT IF THERE IS
         MVC   COMMADDR,TCTTECA   SAVE COMMAREA ADDRESS
         MVC   COMMLEN+2(2),TCTTECAL SAVE COMMAREA LENGTH
*
* GO TO THE INITIAL PROGRAM
* THE ONLY COMMAREA WE CAN PASS HERE IS ONE FROM RETURN COMMAREA
XCTLCOMM EQU   *
         XC    TCTTELNK,TCTTELNK  SET LEVEL 0
         CLI   TCTTETYP,C'D'      IC START-NO DATA ?
         BE    ICCHAN             EXIT IF IT IS
         CLI   TCTTETYP,C'E'      IC START-DATA ?
         BNE   DOLINK             EXIT IF NOT
ICCHAN   EQU   *
         GETMAIN R,LV=26          GET CHANNEL AREA
         LR    R2,R1              SET GETMAIN ADDRESS
         USING DFHCHAN,R2
         SR    R0,R0              CLEAR R0
         L     R1,EIBTRNID        R1=TRANSID
         M     R0,EIBTRMID        MULTIPLY BY TERMID
         STM   R0,R1,CHANTSQ      SAVE THE TS Q NAME
         MVHHI CHANTSQ,C'CH'      OVERLAY THE TS Q PREFIX
         MVHHI CHANITEM,1         SET ITEM 1
         EXEC  CICS READQ TS QUEUE(CHANTSQ) INTO(0(R2)) ITEM(CHANITEM) X
               NUMITEMS(CHANNMIT) LENGTH(26) NOHANDLE
         CLC   EIBRESP,DFHRESP(QIDERR) ANY CHANNEL PASSED ?
         BNE   GOTCHAN            EXIT IF IT IS
         FREEMAIN A=(R2),LV=26    FREE CHANNEL
         B     DOLINK             EXIT
*
GOTCHAN  EQU   *
         ST    R2,TCTTECAQ        SET CHANNEL ANCHOR
         MVC   TCTTECHN,CHANNAME  SET CHANNEL NAME
         LA    R5,CHANCONT        R5=CONTAINER ANCHOR POINT
         XC    CHANLINK(6),CHANLINK CLEAR LINK LEVEL/CONTAINER POINTER
         CHHSI CHANNMIT,1         ANY CONTAINERS ?
         BE    DELTSQ             EXIT IF NONE
CONTLOOP EQU   *
* BUILD CONTAINER CHAIN
         GETMAIN R,LV=28          GET CONTAINER PREFIX AREA
         LR    R2,R1              SET GETMAIN ADDRESS
         USING DFHCONT,R2
         LH    R0,CHANITEM        R0=ITEM NUMBER
         AHI   R0,1               +1
         STH   R0,CHANITEM        SAVE NEXT ITEM NUMBER
* GET CONTAINER PREFIX, WILL RAISE LENGERR
         EXEC  CICS READQ TS QUEUE(CHANTSQ) INTO(0(R2)) ITEM(CHANITEM) X
               LENGTH(28) NOHANDLE
         L     R6,CONTLENG        R6=LENGTH OF CONTAINER DATA
         AHI   R6,28              + PREFIX LENGTH
         ST    R6,SAVTSLEN        SAVE LENGTH FOR READQ TS
         FREEMAIN A=(R2),LV=28    FREE CONTAINER PREFIX AREA
         GETMAIN R,LV=(R6)        GET CONTAINER AREA
         LR    R2,R1              SET GETMAIN ADDRESS
* GET CONTAINER
         EXEC  CICS READQ TS QUEUE(CHANTSQ) INTO(0(R2)) ITEM(CHANITEM) X
               FLENGTH(SAVTSLEN)
         XC    CONTADDR(8),CONTADDR CLEAR CHAIN ADDRESS AND SET
         ST    R2,0(R5)           SET CHAIN POINTER
         LA    R5,CONTADDR        SET NEXT POINTER
         CLC   CHANITEM,CHANNMIT  ALL DONE ?
         BNE   CONTLOOP           LOOP TILL WE ARE
DELTSQ   EQU   *
         EXEC  CICS DELETEQ TS QUEUE(CHANTSQ)
DOLINK   EQU   *
         EXEC  CICS LINK PROGRAM(BLDLNAME) COMMAREA(TCTTECA)           X
               LENGTH(TCTTECAL) CHANNEL(TCTTECHN) LEVEL0
*
TASKRETN EQU   *
* WE COME HERE AFTER TASK END OR ABEND
* ENSURE OUR 1ST BASE IS CORRECT
         LR    R13,R3             RE-ESTABLISH
         AHI   R13,-4096                       1ST BASE
* IF WE HAVE A CWA SEND IT BACK
* DEQALL WILL FREE THE LOCK
* FREEMAIN ROUTINE WILL FREE THE LOCAL CWA
         OC    TCTTECWA,TCTTECWA  ANY CWA ?
         BZ    DODQALL            EXIT IF NONE
         LA    R5,CWPREFIX        R5=PREFIX LENGTH
         A     R5,TCTTECWL        + CWA LENGTH
         GETMAIN R,LV=(R5)
         LR    R6,R1              SAVE GETMAIN ADDRESS
         USING DFHCWBLK,R6
         ST    R5,CWATCPIO        SET LENGTH TO SEND
         MVC   CWAFN,=C'FE07'     SET FN
         MVC   CWATERM,TCTTETI    SET TERMINAL ID
         MVC   CWATRAN,EIBTRNID   SET TRANSID
         MVC   CWATASKN,EIBTASKN  SET TASK NUMBER
         MVI   CWARDWRT,X'FF'     SET CWA PUT
         DROP  R6
         LA    R0,CWPREFIX(R1)    SET TARGET ADDRESS
         L     R1,TCTTECWL        SET TARGET LENGTH
         L     R14,TCTTECWA       R14=CWA ADDRESS
         LR    R15,R1             R15=CWA LENGTH
         MVCL  R0,R14             MOVE CWA
         BAL   R9,TCPSEND         SEND IT
         FREEMAIN A=(R6),LV=(R5)
         XC    TCTTECWA,TCTTECWA  CLEAR CWA ADDRESS
DODQALL  EQU   *
*
* FN=12FC  TELL Z390CICS TO DEQALL
*
         MVC   KCPDQTRM,EIBTRMID  SET TERMID
         LA    R6,KCPDQALL        MESSAGE TO SEND
         BAL   R9,TCPSEND         SEND IT
*
* FN=0EFF  TELL Z390CICS THAT TRANSID HAS ENDED
*
         MVC   KCPNDTRN,EIBTRNID  SET TRANSID
         MVC   KCPNDTRM,EIBTRMID  SET TERMID
         MVC   KCPNDTSK,EIBTASKN  SET TASK NUMBER
         LA    R6,KCPEND          MESSAGE TO SEND
         BAL   R9,TCPSEND         SEND IT
         CLI   EIBTRMID,C'!'      NON-TERM ?
         BE    SHUTKCP            EXIT IF IT IS
         CLI   TCTTETS,X'FF'      CEMT S TER OUT REQUEST ?
         BE    SHUTKCP            EXIT IF IT IS
*
* FREE ABEND BLOCK IF ANY
*
         ICM   R1,15,TCTTEABD     ANY ABEND BLOCK ADDRESS ?
         BZ    FREEGTM            EXIT IF NONE
         AHI   R1,-8              R1=ORIGIN ADDRESS
         FREEMAIN LV=ABDLENG*25+8
         XC    TCTTEABD,TCTTEABD  CLEAR ABEND BLOCK ANCHOR
*
* FREE GETMAIN CHAIN
*
FREEGTM  EQU   *
         ICM   R1,15,TCTTESCC     R1=NEXT GETMAIN ADDRESS
         BZ    FREEEIB            EXIT IF NONE
         MVC   TCTTESCC,0(R1)     MOVE NEXT CHAIN ADDRESS
         FREEMAIN LA=4(R1)        FREEMAIN
         B     FREEGTM            LOOP
*
* FREE EIB
*
FREEEIB  EQU   *
         FREEMAIN A=(DFHEIBR),LV=EIBLENG
*
* CLEAR INQUIRE BROWSE TCTTE FIELDS
*
         MVI   TCTTEIQF,X'00'     RESET BROWSE FILE IN PROGRESS
*
* POST RETURN COMMAREA PROCESSING
*    ANY PREVIOUS COMMAREA ?
*       NO  --- EXIT
*       YES --- ANY RETURN COMMAREA ?
*               NO  --- FREEMAIN PREVIOUS COMMAREA
*               YES --- SAME ADDRESS ?
*                          YES --- EXIT
*                          NO  --- FREEMAIN PREVIOUS COMMAREA
*
         OC    COMMADDR,COMMADDR  ANY PREVIOUS COMMAREA SUPPLIED ?
         BZ    FREECHAN           EXIT IF NOT
* THERE IS A PREVIOUS COMMAREA
         OC    TCTTECA,TCTTECA    ANY COMMAREA PASSED ?
         BZ    FREECOMM           EXIT IF NONE
* RETURN WITH COMMAREA
         CLC   TCTTECA,COMMADDR   SAME ADDRESS ?
         BE    FREECHAN           EXIT IF IT IS
FREECOMM EQU   *
* THERE WAS A PREVIOUS COMMAREA, FREEMAIN IT
         FREEMAIN A=COMMADDR,LA=COMMLEN
         XC    COMMADDR,COMMADDR  CLEAR PREVIOUS COMMAREA ADDRESS
FREECHAN EQU   *
* FREE ALL CHANNELS/CONTAINERS EXCEPT THE CHANNEL IN TCTTECHN
         ICM   R9,15,TCTTECAQ     R9=CHANNEL CHAIN ADDRESS
         BZ    FREECHWA           EXIT IF NONE
         USING DFHCHAN,R9
CHANCHLP EQU   *
         CLC   CHANNAME,TCTTECHN  MATCH RETURN CHANNEL ?
         BE    CHANINC            EXIT IF IT DOES
         EXEC  CICS DELETE CONTAINER('*') CHANNEL(CHANNAME)
CHANINC  EQU   *
         ICM   R9,15,CHANADDR     CHAIN TO NEXT
         BNZ   CHANCHLP           LOOP UNTIL CHAIN END
         OC    TCTTECHN,TCTTECHN  ANY PASSED CHANNEL ?
         BNZ   FREECHWA           EXIT IF THERE IS
         XC    TCTTECAQ,TCTTECAQ  CLEAR CHANNEL ANCHOR ADDRESS
FREECHWA EQU   *
*
* FREE ANY STARTBROWSE CHWA CHAIN
*
         ICM   R9,15,TCTTECHW     R9=CHWA CHAIN ADDRESS
         BZ    LOCAL_MANAGER      EXIT IF NONE
         USING DFHCHWA,R9
CHWALOOP EQU   *
         LR    R1,R9              SET FREEMAIN ADDRESS
         L     R9,CHWAADDR        R9=NEXT CHWA
         FREEMAIN LV=40
         LTR   R9,R9              ANY MORE CHWA'S ?
         BNZ   CHWALOOP           LOOP IF THERE ARE
         XC    TCTTECHW,TCTTECHW  CLEAR CHWA ANCHOR ADDRESS
         B     LOCAL_MANAGER      EXIT
*
CHANTSQ  DS    XL8
CHANITEM DS    XL2
CHANNMIT DS    XL2
SAVTSLEN DS    XL4
*
INVTRAN  EQU   *
*
* INVALID TRANSACTION
*
         CLI   TCTTETI,C'!'       NON-TERMINAL ?
         BE    FREEEIB2           EXIT IF IT IS
         LA    R7,AC2001          MSG ADDRESS
         USING MSG,R7
         BAL   R9,TMDTPROC        ERROR MSG TO SCREEN
         XC    TCTTETC,TCTTETC    NO PRESET TRANSID
         EXEC  CICS SEND FROM(AC2001) LENGTH(AC2001L)
         DROP  R7                 DROP MSG BASE
         B     FREEEIB2           EXIT
*
INVPROG  EQU   *
*
* ABEND APCT
*
         CLI   TCTTETI,C'!'       NON-TERMINAL ?
         BE    FREEEIB2           EXIT IF IT IS
         LA    R7,AC2206          MSG ADDRESS
         USING MSG,R7
         MVC   MSGPROG,4(R6)      BAD PROGRAM TO MSG
         MVC   MSGABCDE,=C'APCT'  ABEND CODE TO MSG
         BAL   R9,TMDTPROC        ERROR MSG TO SCREEN
         XC    TCTTETC,TCTTETC    NO PRESET TRANSID
         EXEC  CICS SEND FROM(AC2206) LENGTH(AC2206L)
         DROP  R7                 DROP MSG BASE
* FREE EIB
FREEEIB2 EQU   *
         FREEMAIN A=(DFHEIBR),LV=EIBLENG
         B     LOCAL_MANAGER      EXIT
*
TERMERR  EQU   *
         MVC   KCPXXCDE,=C'MM01'  SET ABEND CODE
         B     ABORT              EXIT
*
PARMERR  EQU   *
         MVC   KCPXXCDE,=C'MM02'  SET ABEND CODE
         B     ABORT              EXIT
*
TCPIOERR EQU   *
         MVC   KCPXXCDE,=C'MM03'  SET ABEND CODE
         B     ABORTIMM           EXIT
*
* ABORT Z390KCP WITH RC=16
*
ABORT    EQU   *
         MVC   KCPXXTRM,TCTTETI   SET TERMID
         MVC   KCPXXTRN,=8C' '    NO TRANSID
         LA    R6,KCPABORT        MESSAGE TO SEND
         BAL   R9,TCPSEND         SEND IT
ABORTIMM EQU   *
         SUBEXIT RC=16
*
* SHUTTERM REQUESTED
* TELL Z390CICS, END Z390KCP
*
SHUTKCP  EQU   *
* FN=12FE  Z390KCP SHUTTERM REQUEST
         GUAM  WINDOW,VIEW,SCREEN CLOSE SCREEN
         MVC   KCPSHTRM,EIBTRMID  SET TERMID
         LA    R6,KCPSHUT         MESSAGE TO SEND
         BAL   R9,TCPSEND         SEND IT
         TCPIO CLOSE,PORT=3900    CLOSE CLIENT
         LTR   R15,R15            CHECK RC ?
         BNZ   TCPIOERR           EXIT IF BAD
         CLC   TCTTETI,=C'SQ01'   SEQUENTIAL TERMINAL ?
         BNE   SHUTIT             EXIT IF NOT
* CLOSE SEQ TERM
         LARL  R1,SEQSEND         SEND DCB
         CLOSE (R1)               CLOSE IT
SHUTIT   EQU   *
         SUBEXIT
*
APPABEND DS    0H
* ESTAE APPLICATION ABEND ROUTINE
* IF HANDLE CONDITION OR HANDLE ABEND ARE ACTIVE,
* TAKE THE CORRECT ACTION
*
* R1 POINTS TO SDWA
* R15 IS BASE
*
* AT THE ABEND POINT WE HAVE THE FOLLOWING MARKERS:
*    1) HANDLE CONDITION INCIDENT
*       AL3(0),C'ABEND',A(IGNORE ADDRESS)
*
*    2) AN EXEC CICS ABEND, REQUEST DUMP XXXX
*       X'0000FE',C'ABEND',C'XXXX'
*
*    3) AN EXEC CICS ABEND, REQUEST NODUMP
*       X'0000FF',C'ABEND',C'????'
*
*    IF NO MARKER RECOGNISED, WE HAVE REALLY ABENDED
*
         USING SDWA,R1            SDWA PASSED TO ESTAE EXIT RPI 845
* SWITCH BASE
         DROP  R3,R4              DROP MAIN BASES 3 AND 4
         USING *,R12
         LR    R12,R15            R12=NEW BASE
         L     R6,SDWAEC1+4       R6=ABEND ADDRESS (EC1 PSW RPI 845)
         MVC   TCTTEPSW,SDWAEC1   MOVE PSW FOR ASSIGN
* ABEND CONDITION HAS ARISEN
         L     R9,SDWAXPAD        GET SDWA PTRS TO EXTENSIONS   RPI 845
         USING SDWAPTRS,R9                                      RPI 845
         L     R9,SDWAXEME        GET SDWA RC4 REGISTER EXTENSION   845
         USING SDWARC4,R9                                       RPI 845
* MOVE THE REGS FOR ASSIGN
         MVC   TCTTEASR(4),SDWAG64+4      MOVE R0
         MVC   TCTTEASR+4(4),SDWAG64+12   MOVE R1
         MVC   TCTTEASR+8(4),SDWAG64+20   MOVE R2
         MVC   TCTTEASR+12(4),SDWAG64+28  MOVE R3
         MVC   TCTTEASR+16(4),SDWAG64+36  MOVE R4
         MVC   TCTTEASR+20(4),SDWAG64+44  MOVE R5
         MVC   TCTTEASR+24(4),SDWAG64+52  MOVE R6
         MVC   TCTTEASR+28(4),SDWAG64+60  MOVE R7
         MVC   TCTTEASR+32(4),SDWAG64+68  MOVE R8
         MVC   TCTTEASR+36(4),SDWAG64+76  MOVE R9
         MVC   TCTTEASR+40(4),SDWAG64+84  MOVE R10
         MVC   TCTTEASR+44(4),SDWAG64+92  MOVE R11
         MVC   TCTTEASR+48(4),SDWAG64+100 MOVE R12
         MVC   TCTTEASR+52(4),SDWAG64+108 MOVE R13
         MVC   TCTTEASR+56(4),SDWAG64+116 MOVE R14
         MVC   TCTTEASR+60(4),SDWAG64+124 MOVE R15
         CLC   0(8,R6),APPABCDE   IS IT AN HC MARKER ?
         BNE   APPABORT           NO, IT'S A REAL ABEND
         L     R9,SDWAG64+(8*R13)+4 R9=DSA ADDRESS              RPI 845
         USING DFHEISTG,R9
         ICM   R7,15,DFHEIHCN     ANY HANDLE CONDITION BLOCK
         BZ    APPABBLK           EXIT IF NONE
         USING DFHHCBLK,R7
         DROP  R9                 DROP TEMP DSA BASE
APPHCLP  EQU   *
         OC    HCNCHAIN,HCNCHAIN  IS THERE A CHAIN ?
         BZ    APPHCEND           EXIT IF END OF CHAIN
         L     R7,HCNCHAIN        R7=NEXT CONDITION BLOCK
         B     APPHCLP            LOOP UNTIL END
*
APPHCEND EQU   *
* LAST CONDITION BLOCK LOCATED
         L     R8,EIBRESP         R8=RESP OF CONDITION
         SLL   R8,2               * 4 = INDEX
         LA    R8,12(R7,R8)       R8=12 (PREFIX) + INDEX + COND. BLOCK
         OC    0(4,R8),0(R8)      ANY HANDLE CONDITION ?
         BNZ   APPHANDL           EXIT IF THERE IS
* NO SPECIFIC HANDLE, THIS IS AN ABEND, SO CHECK ERROR
         LA    R8,HCNERROR        R8=HANDLE ERROR ADDRESS
         DROP  R7                 DROP HANDLE BLOCK BASE
         OC    0(4,R8),0(R8)      ANY HANDLE CONDITION ERROR ?
         BZ    APPABBLK           EXIT IF NONE
APPHANDL EQU   *
* GOT A HANDLE/IGNORE CONDITION
         CLC   0(4,R8),=4X'FF'    IS IT IGNORE CONDITION ?
         BNE   APPNTIGN           EXIT IF NOT
* IT'S AN IGNORE, GO TO THE IGNORE ADDRESS (IT MIGHT BE 0)
APPIGNOR EQU   *
         L     R8,SDWAEC1+4       ABEND ADDRESS FROM SDWA PSW RPI 845
         L     R0,8(R8)           SET IGNORE ADDRESS
         LA    R15,4              TELL SVC 3 TO USE R0
         BR    R14                GO TO THE IGNORE ADDRESS
*
APPNTIGN EQU   *
* IT'S A REAL HANDLE, SET THE ADDRESS
         L     R0,0(R8)           SET THE HANDLE ADDRESS
         LA    R15,4              TELL SVC 3 TO USE R0
         BR    R14                GO TO THE HANDLE CONDITION
*
APPABORT EQU   *
* R6 POINTS TO ABEND ADDRESS...
*    X'0000FE',C'ABEND',C'XXXX'  AN EXEC CICS ABEND, REQUEST DUMP XXXX
*    X'0000FF',C'ABEND',C'????'  AN EXEC CICS ABEND, REQUEST NODUMP
*    IF NO MARKER RECOGNISED, WE HAVE REALLY ABENDED
         L     R7,=A(AC2206)      MSG ADDRESS
         USING MSG,R7
         LA    R8,999             SET THE DEFAULT SNAP ID
         MVC   APPABMSG+6(4),=C'ASRA' DEFAULT ABEND CODE FOR SNAP
         MVC   MSGABCDE,=C'ASRA'  DEFAULT ABEND CODE TO MSG
         MVC   TCTTEABC,=C'ASRA'  DEFAULT ABEND CODE FOR ASSIGN
         CLC   0(8,R6),APPREQDM   IS IT AN ABEND REQUEST WITH DUMP ?
         BNE   APPNTRQD           EXIT IF NOT
* EXEC CICS ABEND (DUMP)
         LA    R8,998             SET ABEND REQUEST SNAP ID
         MVC   APPABMSG+6(4),8(R6) SET THE ABCODE REQUESTED FOR SNAP
         MVC   MSGABCDE,8(R6)     ABEND CODE TO MSG
         MVC   TCTTEABC,8(R6)     ABEND CODE FOR ASSIGN
         MVI   TCTTEABP,X'FF'     SET ABDUMP INDICATOR FOR ASSIGN
         XC    TCTTEPSW,TCTTEPSW  NO PSW FOR ASSIGN
         XC    TCTTEASR,TCTTEASR  NO REGS FOR ASSIGN
         B     APPDUMP            EXIT
*
APPNTRQD EQU   *
         CLC   0(8,R6),APPREQND   IS IT AN ABEND REQUEST WITH NO DUMP ?
         BNE   APPDUMP            EXIT IF NOT
* EXEC CICS ABEND NODUMP
         MVC   MSGABCDE,8(R6)     ABEND CODE TO MSG
         MVC   TCTTEABC,=8C' '    DEFAULT ABEND CODE FOR ASSIGN
         XC    TCTTEPSW,TCTTEPSW  NO PSW FOR ASSIGN
         XC    TCTTEASR,TCTTEASR  NO REGS FOR ASSIGN
         DROP  R7                 DROP MSG BASE
         B     APPNODMP           EXIT
*
APPDUMP  EQU   *
* ALWAYS TAKE A DUMP FOR ASRA
* ALWAYS TAKE A DUMP FOR REQUESTED ABEND WITH DUMP
         LR    R7,R1              SAVE ESTAD BASE
         LA    R1,APPABMSG        TEXT FOR SNAP
         SNAP  PDATA=ALL,ID=(R8),TEXT=(R1)
         LR    R1,R7              RESTORE ESTAD BASE
APPNODMP EQU   *
         L     R7,=A(AC2206)      MSG ADDRESS
         USING MSG,R7
         MVC   MSGPROG,EIBPROG    ABENDING PROGRAM TO MSG
         MVC   TCTTEABG,EIBPROG   ABENDING PROGRAM FOR ASSIGN
         CLC   APPABMSG+6(3),=C'ASR' ASR. TYPE ABEND ?
         BE    APPTABDN           EXIT IF IT IS
         XC    TCTTEPSW,TCTTEPSW  NO PSW FOR ASSIGN
         XC    TCTTEASR,TCTTEASR  NO REGS FOR ASSIGN
         DROP  R7                 DROP MSG BASE
         B     APPTABDN           EXIT
*
APPABBLK EQU   *
* CONDITION RAISED BUT NO HANDLE OR IGNORE
* TAKE DUMP WITH CONDITION ABEND CODE
*
* SOME CONDITIONS HAVE IGNORE AS DEFAULT
         CLC   EIBRESP,DFHRESP(EXPIRED) EXPIRED ?
         BE    APPIGNOR           EXIT IF IT IS
         L     R7,=A(AC2206)      MSG ADDRESS
         USING MSG,R7
         MVC   MSGPROG,EIBPROG    ABENDING PROGRAM TO MSG
         MVC   TCTTEABG,EIBPROG   ABENDING PROGRAM FOR ASSIGN
         MVC   MSGABCDE,=4C'?'    DEFAULT ABEND CODE TO MSG
         MVC   TCTTEABC,=8C' '    DEFAULT ABEND CODE FOR ASSIGN
         XC    TCTTEPSW,TCTTEPSW  NO PSW FOR ASSIGN
         XC    TCTTEASR,TCTTEASR  NO REGS FOR ASSIGN
         MVC   APPABMSG+6(4),=4C'?' DEFAULT ABEND CODE TO SNAP TEXT
         LA    R8,APPABTAB        R8=ABEND CODE TABLE
APPTABLP EQU   *
         CLC   EIBRESP,0(R8)      MATCH ?
         BNE   APPTABIN           EXIT IF NOT
         MVC   MSGABCDE,4(R8)     ABEND CODE TO MSG
         MVC   TCTTEABC,4(R8)     ABEND CODE FOR ASSIGN
         MVC   APPABMSG+6(4),4(R8) ABEND CODE TO SNAP TEXT
         DROP  R7                 DROP MSG BASE
         B     APPTABDN           EXIT
*
APPABTAB DS    0F
* REFECT AMENDMENTS IN Z390CICS TRANABD
         DC    F'11',C'AEIK'      TERMIDERR
         DC    F'12',C'AEIL'      FILENOTFOUND
         DC    F'13',C'AEIM'      NOTFND
         DC    F'14',C'AEIN'      DUPREC
         DC    F'15',C'AEIO'      DUPKEY
         DC    F'16',C'AEIP'      INVREQ
         DC    F'17',C'AEIQ'      IOERR
         DC    F'18',C'AEIR'      NOSPACE
         DC    F'19',C'AEIS'      NOTOPEN
         DC    F'20',C'AEIT'      ENDFILE
         DC    F'21',C'AEIU'      ILLOGIC
         DC    F'22',C'AEIV'      LENGERR
         DC    F'26',C'AEIZ'      ITEMERR
         DC    F'27',C'AEI0'      PGMIDERR
         DC    F'28',C'AEI1'      TRANSIDERR
         DC    F'29',C'AEI2'      ENDDATA
         DC    F'31',C'AEI4'      EXPIRED
         DC    F'36',C'AEI9'      MAPFAIL
         DC    F'38',C'AEYB'      INVMPSZ
*                40               OVERFLOW CANNOT ABEND
         DC    F'44',C'AEYH'      QIDERR
*                55               ENQBUSY CANNOT ABEND
         DC    F'56',C'AEYT'      ENVDEFERR
         DC    F'61',C'AEYY'      NOTALLOC
         DC    F'83',C'AEXK'      END
         DC    F'84',C'AEXL'      DISABLED
         DC    F'109',C'AEZI'     ACTIVITYERR
         DC    F'110',C'AEZJ'     CONTAINERERR
         DC    F'112',C'AEZL'     TOKENERR
         DC    F'122',C'AEZV'     CHANNELERR
         DC    X'FF'              STOPPER
*
APPTABIN DS    0H
         AHI   R8,8               BUMP TABLE ADDRESS
         CLI   0(R8),X'FF'        STOPPER ?
         BNE   APPTABLP           LOOP IF NOT
*
* HANDLE ABEND PROCESSING
*
APPTABDN EQU   *
         ICM   R8,15,TCTTEABD     ANY ABEND BLOCK ?
         BZ    APPTERMI           EXIT IF NONE
         LA    R8,24*ABDLENG(R8)  POINT TO 25TH (LAST) ENTRY
         USING ABDDEST,R8
APPHALP  EQU   *
         OC    ABDDEST,ABDDEST    ANY ABEND BLOCK HERE ?
         BZ    APPHADEC           EXIT IF NONE
* FOUND A HANDLE ABEND ENTRY
         OC    ABDACTIV,ABDACTIV  IS IT ACTIVE ?
         BNZ   APPHADEC           EXIT IF NOT
* FOUND AN ACTIVE HANDLE ABEND ENTRY
         MVC   ABDACTIV,=X'FFFF'  MAKE IT INACTIVE TO STOP RECURSION
         CLI   ABDDEST,X'00'      LABEL OR PROGRAM ?
         BNE   APPABPRG           EXIT IF PROGRAM
* ACTIVE HANDLE ABEND LABEL()
* EXIT ESTAE
         ST    R8,APPSAVAB        SAVE ABEND BLOCK ENTRY ADDRESS
         LA    R0,APPHALAB        SET RETURN ADDRESS
         LA    R15,4              TELL SVC 3 TO USE R0
         BR    R14                EXIT TO APPHALAB
*
APPHADEC EQU   *
         C     R8,TCTTEABD        JUST PROCESSED 1ST ENTRY ?
         BE    APPTERMI           YES, NO ACTIVE HANDLE ABEND
         AHI   R8,-ABDLENG        SCAN BACKWARDS
         B     APPHALP            LOOP
*
* GOTO HANDLE ABEND LABEL()
*
APPHALAB EQU   *
         BALR  R7,0               TEMP BASE
         USING *,R7
         L     R8,APPSAVAB        RESTORE ABEND BLOCK ENTRY ADDRESS
* IF THE LINK-LEVEL OF THE ABENDING PROGRAM IS GREATER THAN THAT OF
* THE HANDLE ABEND LABEL, WE MUST SIMULATE A RETURN FOR EACH LEVEL TO
* CLEAN UP.
APPLABCN EQU   *
         CLC   ABDLEVEL,TCTTELNK  IS THE LABEL AT THE SAME LEVEL ?
         BE    APPLVLOK           EXIT IF IT IS
* CLEAN UP HIGHER LEVELS
         L     R1,AZCVT           ADDRESS ZCVT
         USING IHAZCVT,R1
         LA    R1,ZCVTSDWA        RE-ESTABLISH SDWA BASE RPI 845
         USING SDWA,R1
         L     R9,SDWAXPAD        GET SDWA PTRS TO EXTENSIONS   RPI 845
         USING SDWAPTRS,R9                                      RPI 845
         L     R9,SDWAXEME        GET SDWA RC4 REGISTER EXTENSION   845
         USING SDWARC4,R9                                       RPI 845
         L     R13,SDWAG64+(8*R13)+4 RESTORE THE ABENDING PROGRAMS DSA
         DROP  R1
         EXEC  CICS RETURN CLEANER
         B     APPLABCN           LOOP
         DROP  R7
*
APPLVLOK EQU   *
         L     R15,ABDDEST+4      SET LABEL() ADDRESS
         LM    R2,R13,ABDREGS     RESTORE HANDLE ABEND ISSUER REGS
         BR    R15                GO TO LABEL()
AZCVT    DC    A(ZCVT)
*
* NO CONDITION HANDLING
* NO HANDLE ABEND BLOCK OR NO ACTIVE ENTRY
* EXIT ESTAE, WE ARE GOING TO ABEND
*
APPTERMI EQU   *
         BALR  R7,0               TEMP BASE
         USING *,R7
         LA    R0,APPTERMT        SET RETURN ADDRESS
         DROP  R7                 DROP TEMP BASE
         LA    R15,4              TELL SVC 3 TO USE R0
         BR    R14                EXIT TO APPTERMT
*
APPABPRG EQU   *
* ACTIVE HANDLE ABEND PROGRAM()
* EXIT ESTAE
         ST    R8,APPSAVAB        SAVE ABEND BLOCK ENTRY ADDRESS
         LA    R0,APPHAPRG        SET RETURN ADDRESS
         LA    R15,4              TELL SVC 3 TO USE R0
         BR    R14                EXIT TO APPHAPRG
*
APPHAPRG EQU   *
         BALR  R7,0               TEMP BASE
         USING *,R7
         L     R8,APPSAVAB        RESTORE ABEND BLOCK ENTRY ADDRESS
* COPY XCTL PARMS BECAUSE OF ADDRESSING PROBLEMS
         MVC   APPPROG,ABDDEST    COPY PROGRAM NAME
         MVC   APPCOMM,ABDCOMM    COPY COMMAREA ADDRESS
         MVC   APPCALEN,ABDCALEN  COPY COMMAREA LENGTH
         L     R1,=A(ZCVT)        ADDRESS ZCVT
         USING IHAZCVT,R1
         LA    R1,ZCVTSDWA        RE-ESTABLISH ESTAE BASE     RPI 845
         USING SDWA,R1
         L     R9,SDWAXPAD        GET SDWA PTRS TO EXTENSIONS   RPI 845
         USING SDWAPTRS,R9                                      RPI 845
         L     R9,SDWAXEME        GET SDWA RC4 REGISTER EXTENSION   845
         USING SDWARC4,R9                                       RPI 845
         L     R13,SDWAG64+(8*R13)+4 RESTORE THE ABENDING PROGRAMS DSA
         DROP  R1                 DROP ESTAD BASE
         USING DFHEISTG,R13
         OC    APPCOMM,APPCOMM    ANY COMMAREA ?
         BZ    APPXCTL2           EXIT IF NONE
         EXEC  CICS XCTL PROGRAM(APPPROG)                              X
               COMMAREA(APPCOMM) LENGTH(APPCALEN) CHANNEL(TCTTECHN)
*
APPXCTL2 EQU   *
         EXEC  CICS XCTL PROGRAM(APPPROG) CHANNEL(TCTTECHN)
APPSAVAB DS    AL4
         LTORG
         DROP  R7,R13             DROP TEMP BASE AND DSA BASE
*
APPTERMT DS    0H
* NO HANDLES, NOW CLEAN THE TASK AND ABEND
         BALR  R12,0              TEMP BASE 1
         USING *,R12
         CLI   TCTTETI,C'!'       NON-TERMINAL ?
         BE    APPCLEAN           EXIT IF IT IS
         L     R7,=A(AC2206)      MSG ADDRESS
         USING MSG,R7
         BAL   R9,TMDTPROC        ERROR MSG TO SCREEN
         EXEC  CICS SEND FROM(=A(AC2206)) LENGTH(AC2206L2)
         DROP  R7                 DROP MSG BASE
APPCLEAN EQU   *
*
* CLEANER
*
* VERY SIMILAR TO CLEANUP AFTER RETURN
* R8 HOLDS EIBRESP, DON'T DESTROY IT
*
* FREE RETURN COMMAREA
*
         OC    TCTTECA,TCTTECA    ANY RETURN COMMAREA ?
         BZ    CLEANPRT           EXIT IF NONE
         LH    R0,TCTTECAL        R0=GETMAINED COMMAREA LENGTH
         FREEMAIN A=TCTTECA,LV=(R0)
         CLC   TCTTECA,COMMADDR   SAME AS LAST PASSED COMMAREA ?
         BNE   CLEANPRT           EXIT IF NOT
         XC    COMMADDR,COMMADDR  CLEAR LAST PASSED COMMAREA
CLEANPRT EQU   *
*
* CLEAR PRESET TRANSID
*
         XC    TCTTETC,TCTTETC    CLEAR PRESET TRANSID
*
* DON'T NEED TO CLEAN ABEND BLOCK OR GETMAIN CHAIN HERE,
* BETTER DONE AT TASK END
*
         L     R7,TCTTELKA        R7=LKA ADDRESS
         AHI   R7,4               AVOID US
CLEANDLP EQU   *
         USING DFHEISTG,R6
         ICM   R6,15,0(R7)        ANY DSA HERE ?
         BZ    CLEANLKA           EXIT IF NO MORE
* R6=DSA BASE
* ONLY DSA DEPENDANT FREEMAINS HERE
*
* FREE COMMAREA
*
         XC    EIBCALEN,EIBCALEN  CLEAR COMMAREA LENGTH
*
* FREE ALL AID BLOCKS
*
         USING DFHADBLK,R1
         ICM   R1,15,DFHEIAID     ANY AID BLOCK ADDRESSES ?
         BZ    CLEANHCN           EXIT IF NONE
CLEANALP EQU   *
         L     R7,AIDCHAIN        R7=NEXT AID BLOCK IF ANY
         FREEMAIN LV=AIDLENG
         LTR   R1,R7              ANY MORE AID BLOCKS ?
         BNZ   CLEANALP           LOOP IF THERE ARE
*
CLEANHCN EQU   *
         DROP  R1                 DROP AID BLOCK ADDRESS
*
* FREE ALL HANDLE CONDITION BLOCKS
*
         USING DFHHCBLK,R1
         ICM   R1,15,DFHEIHCN     ANY CONDITION BLOCK ADDRESSES ?
         BZ    FREEDSA            EXIT IF NONE
CLEANHLP EQU   *
         L     R7,HCNCHAIN        R7=NEXT CONDITION BLOCK IF ANY
         FREEMAIN LV=HCNLENG
         LTR   R1,R7              ANY MORE CONDITION BLOCKS ?
         BNZ   CLEANHLP           LOOP IF THERE ARE
FREEDSA  EQU   *
*
* FREE EACH DSA
*
         FREEMAIN A=(R6),LV=DFHEILEN
         AHI   R7,4               BUMP LKA
         B     CLEANDLP           LOOP
*
* CLEAR LKA (BUT NOT 1ST ENTRY)
*
CLEANLKA EQU   *
         XC    TCTTECA(6),TCTTECA CLEAR RETURN COMMAREA ADDRESS/LENGTH
         L     R7,TCTTELKA        R7=LKA ADDRESS
         XC    4(96,R7),4(R7)     CLEAR LKA (NOT 1ST ENTRY)
*
* DELETE ANY IC TS Q
*
         OC    EIBREQID,EIBREQID  ANY REQID ?
         BZ    CLEANDUN           EXIT IF NOT
         MVC   APPRESP,EIBRESP    SAVE THE ORIGINAL RESP
         EXEC  CICS DELETEQ TS QUEUE(EIBREQID) NOHANDLE
         MVC   EIBRESP,APPRESP    RESTORE THE ORIGINAL RESP
CLEANDUN EQU   *
*
* FN=0EFE  TELL Z390CICS THAT TRANSID HAS ABENDED
*
         MVC   KCPABTRN,EIBTRNID  SET TRANSID
         MVC   KCPABTRM,EIBTRMID  SET TERMID
         MVC   KCPABTSK,EIBTASKN  SET TASK NUMBER
         MVC   KCPABCDE,AC2206_ABCODE SET ABEND CODE
         MVC   APPABMSG+6(4),KCPABCDE SET ABEND CODE FOR SNAP
         LA    R6,KCPABD          MESSAGE TO SEND
         BAL   R9,TCPSEND         SEND IT
*
* TAKE SNAP AND EXIT TO LABEL AFTER OUR LINK
*
         L     R1,TCTTELKA        R1=LKA ADDRESS
         L     R13,0(R1)          LOAD Z390KCP DSA/SAVE AREA ADDRESS
         L     R0,EIBRESP         SET SNAP ID TO EIBRESP
         LA    R1,APPABMSG        SET SNAP TEXT ADDRESS
         RESTORE (R14,R15)        RESTORE OUR REGS
         RESTORE (R2,R12)         RESTORE OUR REGS
         SNAP  PDATA=ALL,ID=(R0),TEXT=(R1)
         J     TASKRETN           EXIT
*
APPABCDE DC    AL3(0),C'ABEND'    HANDLE CONDITION INCIDENT
APPREQDM DC    X'0000FE',C'ABEND' ABEND WITH DUMP
APPREQND DC    X'0000FF',C'ABEND' ABEND WITHOUT DUMP
*
APPPROG  DS    CL8                COPY OF ABEND PROGRAM
APPCOMM  DS    A                  COPY OF ABEND COMMAREA ADDRESS
APPCALEN DS    H                  COPY OF ABEND COMMAREA LENGTH
APPABMSG DC    C'ABEND XXXX',X'00'
APPRESP  DS    F                  SAVE EIBRESP
AC2206L2 DC    AL2(AC2206Q)
*
KCPABD   DS    0F
* TRANSID ABENDED
         DC    A(KCPABEND-*)
         DC    C'0EFE'
KCPABTRM DS    CL4                TERMID
KCPABTRN DS    CL4                TRANSID
         DS    C                  NO TRANSID TYPE
KCPABTSK DS    PL4                PACKED TASK NUMBER
KCPABCDE DS    CL4                ABEND CODE
KCPABEND EQU   *
*
* DROP HANDLE BLOCK BASE  (R1)
* DROP TEMP DSA BASE      (R6)
* DROP TEMP BASE 1        (R12)
         DROP  R1,R6,R12
*
* SUBROUTINE SHARED BY MULTIPLE BASES
*
TMDTPROC DS    0H
         BALR  R14,0              SET TEMPORARY BASE
         USING *,R14
         USING MSG,R7
         CLI   INIDATE,C'K'       DMY FORMAT ?
         BNE   TMDTNTUK           EXIT IF NOT
         TIME  DEC,TIMEDEC,DATETYPE=DDMMYYYY,LINKAGE=SYSTEM
         MVC   DATEHERE,DATEEDIT  MOVE EDIT WORD
         B     TMDTEDIT
*
TMDTNTUK EQU   *
         CLI   INIDATE,C'S'       MDY FORMAT ?
         BNE   TMDTNTUS           EXIT IF NOT
         TIME  DEC,TIMEDEC,DATETYPE=MMDDYYYY,LINKAGE=SYSTEM
         MVC   DATEHERE,DATEEDIT  MOVE EDIT WORD
         B     TMDTEDIT
*
TMDTNTUS EQU   *
         TIME  DEC,TIMEDEC,DATETYPE=YYYYMMDD,LINKAGE=SYSTEM
         MVC   DATEHERE,DATEEDT2  MOVE EDIT WORD
TMDTEDIT EQU   *
         MVO   TMDTPACK,DATEDEC   PACKED DATE
         ED    DATEHERE,TMDTPACK  EDIT DATE
         MVC   MSGDATE,DATEHERE+2 MOVE DATE TO MSG
         MVO   TMDTPACK,TIMEDEC   PACKED TIME
         MVC   TIMEHERE,TIMEEDIT  MOVE EDIT WORD
         ED    TIMEHERE,TMDTPACK  EDIT TIME
         MVC   MSGTIME,TIMEHERE+2 MOVE TIME TO MSG
         MVC   MSGTRAN,EIBTRNID   MOVE TRANSID TO MSG
         BR    R9                 RETURN
*
TIMEDEC  DS    XL4                SAVE TIME HHMMSSTT
         DS    CL4
DATEDEC  DS    CL4                SAVE DATE DDMMYYYY
TMDTPACK DC    PL5'0'
DATEEDIT DC    X'F02120206120206120202020'
DATEEDT2 DC    X'F02120202020612020612020'
TIMEEDIT DC    X'F02120207A20207A20204B2020'
DATEHERE DS    CL12
TIMEHERE DS    CL13
         LTORG
         DROP  R7,R14             DROP TEMPORARY BASE AND MSG BASE
*
* SUBROUTINES
*
         USING Z390KCP+8,R13      RESTORE ORIGINAL BASE
TCPSEND  DS    0H
         L     R8,0(R6)           R8=MESSAGE LENGTH
         TCPIO SEND,MSG=(R6),LMSG=(R8),PORT=3900
         LTR   R15,R15            CHECK RC ?
         JNZ   TCPIOERR           EXIT IF BAD
         BR    R9                 RETURN
*
TCPRCVE  EQU   *
         LARL  R14,KCPRCVE        RECEIVE AREA
         LA    R15,200            LENGTH
         TCPIO RECEIVE,MSG=(R14),LMSG=(R15),PORT=3900
         LTR   R15,R15            CHECK RC ?
         JNZ   TCPIOERR           EXIT IF BAD
* GET NULL DATA SOMETIMES
         OC    0(4,R14),0(R14)    NULL DATA ?
         JZ    TCPRCVE            TRY AGAIN IF IT IS
         BR    R9                 RETURN
*
* Z390KCP DATA AREAS
*
COMMADDR DC    A(0)               COMMAREA ADDRESS LAST PASSED
COMMLEN  DC    F'0'               COMMAREA LENGTH LAST PASSED
*
INITBUFF DS    CL2000             INITIAL BUFFER
INITLEN  DS    H                  RECEIVED LENGTH
INIDATE  DS    C                  DATE FORMAT: K=DMY  S=MDY  R=YMD
*
BLDLLIST DC    H'1'               BLDL LIST WITH 1 ENTRY
         DC    H'12'              BLDL ENTRY LENGTH
BLDLNAME DC    CL8' '             BLDL ENTRY NAME TO FIND
         DC    XL4'00'
*
SEQENV   DC    C'Z390SEQ',X'00'   Z390SEQ ENV VARIABLE
SEQFILE  DC    XL80'00'           PATH AND FILE FOR SEQ TERM
*
* MESSAGES
*
TERM_TITLE DC C'TERMINAL TTTT',X'00'
*
AC2001   TN3270 WCC_KD,(SBA,24,1)
         DC    C'DFHAC2001 '
         DS    CL10               DD/MM/YYYY
         DC    C' '
         DS    CL11               HH:MM:SS.SS
         DC    C' TRANSID='
         DS    CL4                TRANSID
         DC    C' IS NOT RECOGNISED'
         DC    CL(80-(*-AC2001))' '
         TN3270 (SBA,1,1),(SF,ATT_UA),IC
AC2001L  DC    AL2(*-AC2001)
*
AC2206   TN3270 WCC_KD,(SBA,24,1)
         DC    C'DFHAC2206 '
         DS    CL10               DD/MM/YYYY
         DC    C' '
         DS    CL11               HH:MM:SS.SS
         DC    C' TRANSID='
         DS    CL4                TRANSID
         DC    C' PROG='
         DS    CL8                PROGRAM
         DC    C' ABENDED '
AC2206_ABCODE DS   CL4            ABEND CODE
         DC    CL(80-(*-AC2206))' '
         TN3270 (SBA,1,1),(SF,ATT_UA),IC
AC2206Q  EQU   *-AC2206
AC2206L  DC    AL2(*-AC2206)
*
* FORMAL MESSAGE REQUESTS AND INFO TO Z390CICS
*
KCPUP    DS    0F
* Z390KCP INITIALIZED
         DC    A(KCPUPEND-*)
         DC    C'12FF'
KCPUPTRM DS    CL4
KCPUPEND EQU   *
*
KCPSHUT  DS    0F
* SHUTTERM REQUEST
*    CEMT S TER OUT
*    CEMT P SHU
         DC    A(KCPSHEND-*)
         DC    C'12FE'
KCPSHTRM DS    CL4
KCPSHEND EQU   *
*
KCPABORT DS    0F
* Z390KCP ABORTED
         DC    A(KCPXXEND-*)
         DC    C'12FD'
KCPXXTRM DS    CL4
KCPXXTRN DS    CL4
KCPXXCDE DS    CL4
KCPXXEND EQU   *
*
KCPREQTM DS    0F
* REQUEST TERMID/INITIAL TRANSID
         DC    A(KCPREQND-*)
         DC    C'04FF'
KCPREQND EQU   *
*
KCPGO    DS    0F
* TRANSID STARTED
         DC    A(KCPGOEND-*)
         DC    C'0E00'
KCPGOTRM DS    CL4                TERMID
KCPGOTRN DS    CL4                TRANSID
KCPGOTYP DS    C                  TYPE OF TRANSID START
*                                 REFLECT THIS IN THE TCTTE
*                                    A = .INI INITIAL TRANSID
*                                    B = SCREEN INPUT
*                                    C = RETURN TRANSID()
*                                    D = IC START-NO DATA
*                                    E = IC START-DATA
KCPGOREQ DS    CL8                REQID
KCPGOEND EQU   *
*
KCPEND   DS    0F
* TRANSID ENDED
         DC    A(KCPNDEND-*)
         DC    C'0EFF'
KCPNDTRM DS    CL4                TERMID
KCPNDTRN DS    CL4                TRANSID
         DS    C                  NO TRANSID TYPE
KCPNDTSK DS    PL4                PACKED TASK NUMBER
KCPNDEND EQU   *
*
KCPDQALL DS    0F
* DEQALL
         DC    A(KCPDQEND-*)
         DC    C'12FC'
KCPDQTRM DS    CL4                TERMID
         DC    (KCPREFIX-12)X'00' FAKE DFHKCBLK
KCPDQEND EQU   *
*
KCPRCVE  DS    0F
* RECEIVE AREA
         DS    A                  LENGTH
KCPRCFN  DS    CL4                FN
KCPRCTRM DS    CL4                TERMID
KCPRCDTA DS    CL188              DATA
         ORG   KCPRCDTA
* 04FF AREA
KCPRCTRN DS    CL4                INITIAL TRANSID
KCPRCDAT DS    C                  DATE FORMAT
         ORG   KCPRCDTA
* 10FF AREA
         DS    CL4                TRANSID
KCPRCRQD DS    XL8                REQID
KCPRCRTV DS    X                  RETRIEVE INDICATOR
         ORG   KCPRCDTA
* 0E00 AREA
KCPRCTSK DS    CL7                TASK NUMBER
         ORG
*
SEQRCVE  DCB   DSORG=PS,                                               X
               RECFM=F,                                                X
               EODAD=0,                                                X
               DSNAME=SEQFILE,                                         X
               MACRF=GM,                                               X
               BLKSIZE=1,                                              X
               LRECL=1,                                                X
               SYNAD=0
*
SEQSEND  DCB   DSORG=PS,                                               X
               RECFM=F,                                                X
               DSNAME=SEQFILE,                                         X
               MACRF=PM,                                               X
               BLKSIZE=2000,                                           X
               LRECL=2000
*
         LTORG
*
* CICS MESSAGE STRUCTURE
*
MSG      DSECT
         DS    CL4                WCC/SBA
         DS    CL10               MSG ID
MSGDATE  DS    CL10               DD/MM/YYYY
         DS    C                  SPACE
MSGTIME  DS    CL11               HH:MM:SS.SS
         DS    CL9                ' TRANSID='
MSGTRAN  DS    CL4                TRANSID
         DS    CL6                ' PROG='
MSGPROG  DS    CL8                PROGRAM
         DS    CL9                ' ABENDED '
MSGABCDE DS    CL4                ABEND CODE
*
         DFHEIBLK                 EIB
         DFHTCTTE                 TCTTE
         DFHEISTG                 DSA FOR ABEND ROUTINE
         DFHADBLK                 HANDLE AID BLOCK
         DFHHCBLK                 HANDLE CONDITION BLOCK
         DFHABBLK                 HANDLE ABEND BLOCK
         DFHKCBLK                 TASK CONTROL BLOCK
         DFHCWBLK                 CWA PREFIX AREA
         DFHCHAN                  CHANNEL/CONTAINER STRUCTURES
         DFHCHWA                  STARTBROWSE CONTAINER BLOCK
*
         TN3270 SYMBOLS           DEFINE 3270 CODES
         GUAM   SYMBOLS           DEFINE GUAM GUI DIALOG WINDOW OPTIONS
         COPY   DFHAID            AID KEYS
         ZCVTD  ,                 ZCVT
         IHASDWA ,                SDWA CONTROL BLOCK FOR ESTAE EXIT
         EQUREGS
* R3  BASE 2
* R4  BASE 3
* R5  ZCVT (TEMP)
* R9  LINK REG
* R10 TCTTE
* R11 EIB
* R13 SAVE AREA AND BASE 1
         END
