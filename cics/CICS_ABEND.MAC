.**********************************************************************
.* Copyright 2006 Automated Software Tools Corporation                *
.* This source code is part of z390 assembler/emulator package        *
.* The z390 package is distributed under GNU general public license   *
.* Author - Don Higgins                                               *
.**********************************************************************
.* 04/19/08 RPI 833 ADD STRING QUOTES FOR HLASM COMPATIBILITY         *
.**********************************************************************
         MACRO
         CICS_ABEND
         GBLB  &PARMS_0E0C
         LCLB  &CANCEL,&NODUMP,&NOEDF,&NOHAND
         LCLC  &ABCODE,&RESP,&RESP2
&NP      SETA  N'&SYSLIST
&IP      SETA  0
.PLOOP   ANOP
&IP      SETA  &IP+1
         AIF   (&IP GT &NP).ELOOP
&PARM    SETC  '&SYSLIST(&IP)'
         AIF   ('&PARM'(1,7) EQ 'ABCODE(').ABCODE
         AIF   ('&PARM'(1,6) EQ 'CANCEL').CANCEL
         AIF   ('&PARM'(1,6) EQ 'NODUMP').NODUMP
         AIF   ('&PARM'(1,5) EQ 'NOEDF').NOEDF
         AIF   ('&PARM'(1,8) EQ 'NOHANDLE').NOHAND
         AIF   ('&PARM'(1,5) EQ 'RESP(').RESP
         AIF   ('&PARM'(1,6) EQ 'RESP2(').RESP2
         MNOTE 12,'BAD PARM &PARM'
         AGO   .PLOOP
.*
.ABCODE  ANOP
&ABCODE  SETC  '&PARM'(8,K'&PARM-8)
         AGO   .PLOOP
.*
.CANCEL  ANOP
&CANCEL  SETB  1
         AGO   .PLOOP
.*
.NODUMP  ANOP
&NODUMP  SETB  1
         AGO   .PLOOP
.*
.NOEDF   ANOP
&NOEDF   SETB  1
         AGO   .PLOOP
.*
.NOHAND  ANOP
&NOHAND  SETB  1
         AGO   .PLOOP
.*
.RESP    ANOP
&RESP    SETC  '&PARM'(6,K'&PARM-6)
&NOHAND  SETB  1
         AGO   .PLOOP
.*
.RESP2   ANOP
&RESP2   SETC  '&PARM'(7,K'&PARM-7)
         AGO   .PLOOP
.*
.ELOOP   ANOP
         AIF   (K'&ABCODE NE 0).GOTABCD
&NODUMP  SETB  1                  NO ABCODE, NO DUMP
.GOTABCD ANOP
         GEN_XC P0E0C,P0E0CEND-P0E0C,P0E0C
         GEN_MVC P0E0CFN,,=X'0E0C' SET FUNCTION CODE
         AIF   (K'&ABCODE EQ 0).DEFABCD
         AIF   ('&ABCODE'(1,1) EQ '''').STRING
         AGO   .INDIR
.*
.DEFABCD ANOP
         GEN_MVC CICS_ABEND_&SYSNDX._ABCODE,,=C'????' DEFAULT ABEND COD-
               E
         GEN_MVC P0E0CABC,,=C'????' SET DEFAULT ABCODE
         AGO   .NOSTRING
.*
.INDIR   ANOP
         GEN_MVC CICS_ABEND_&SYSNDX._ABCODE,,&ABCODE MOVE ABEND CODE FR-
               OM LABEL
         GEN_MVC P0E0CABC,,&ABCODE SET ABCODE FROM LABEL
         AGO   .NOSTRING
.*
.STRING  ANOP
         AIF   ('&ABCODE'(2,1) EQ 'A').BADABCD
&ABCDLEN SETA  K'&ABCODE
         AIF   (&ABCDLEN LT 3 OR &ABCDLEN GT 6).INVABCD
         GEN_MVC CICS_ABEND_&SYSNDX._ABCODE,,=CL4&ABCODE MOVE ABEND COD-
               E
         GEN_MVC P0E0CABC,,=CL4&ABCODE SET ABCODE AS STRING
.NOSTRING ANOP
         AIF   (NOT &NODUMP).GODUMP
         MVI   CICS_ABEND_&SYSNDX._NODUMP,X'FF' SET NO DUMP
         GEN_MVI P0E0CNOD,X'FF'   SET NO DUMP
.GODUMP  ANOP
         AIF   (NOT &CANCEL).NOCANCEL
         GEN_MVI P0E0CCAN,X'FF'   SET CANCEL
.NOCANCEL ANOP
         GEN_ST DFHEIBR,P0E0CEIB  SET EIB ADDRESS
         GEN_ST TCTTEAR,P0E0CTCT  SET TCTTE ADDRESS
         AIF   (&SYSEDF AND NOT &NOEDF)
         GEN_ST R13,P0E0CEDS      SET DSA ADDRESS FOR EDF
         L     R0,DFHEILEN        DSA LENGTH
         GEN_STH R0,P0E0CEDL      SET DSA LENGTH FOR EDF
         LA    R0,DFHEIUSR        DSA USER AREA
         SR    R0,R13             R0=CICS DSA LENGTH
         GEN_STH R0,P0E0CEDC      SET CICS DSA LENGTH FOR EDF
         AELSE
         GEN_MVI P0E0CNED,X'FF'   SET NOEDF
         AEND
         AIF   (NOT &NOHAND).DUNNOH
         GEN_MVI P0E0CNOH,X'FF'   SET NOHANDLE
.DUNNOH  ANOP
         AIF   ('&RESP' EQ '').NORESP
         LA    R0,&RESP           ADDRESS OF RESP
         GEN_ST R0,P0E0CRSP       SAVE IT
.NORESP  ANOP
         AIF   ('&RESP2' EQ '').NORESP2
         LA    R0,&RESP2          ADDRESS OF RESP2
         GEN_ST R0,P0E0CRS2       SAVE IT
.NORESP2 ANOP
         LARL  R0,P0E0CEDF_&SYSNDX ADDRESS ORIGIN FOR CEDF
         GEN_ST R0,P0E0CEDF       SAVE IT
         L     R15,TCTTELCL       R15=LCL MODULE INDEXER
         LARL  R1,=A(P0E0C)       R1=LCL MODULE PARAMETER LIST
P0E0CEDF_&SYSNDX EQU *
         BAKR  0,R15              STACK REGS AND GO
.*
.* ABEND HERE
         DC    X'0000'            ABEND POINT
         AIF   (&NODUMP).GENLABL
         DC    X'FE'              FE=TAKE DUMP  FF=NODUMP
         AGO   .LABDONE
.*
.GENLABL ANOP
CICS_ABEND_&SYSNDX._NODUMP DC X'FE' FE=TAKE DUMP  FF=NODUMP
.LABDONE ANOP
         DC    C'ABEND'           MARKER
CICS_ABEND_&SYSNDX._ABCODE DC CL4' ' ABEND CODE IF ANY
*
         AIF   (&PARMS_0E0C).BYP0E0C
&PARMS_0E0C SETB 1
         P0E0C
*
.BYP0E0C ANOP
         MEXIT
.*
.BADABCD MNOTE 12,'ABCODE MUST NOT BEGIN WITH ''A'''
         MEXIT
.*
.INVABCD MNOTE 12,'ABCODE IS INVALID'
         MEXIT
         MEND
