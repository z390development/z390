.**********************************************************************
.* Copyright 2006 Automated Software Tools Corporation                *
.* This source code is part of z390 assembler/emulator package        *
.* The z390 package is distributed under GNU general public license   *
.* Author - Don Higgins                                               *
.**********************************************************************
.* 04/19/08 RPI 833 ADD STRING QUOTES FOR HLASM COMPATIBILITY         *
.**********************************************************************
         MACRO
         CICS_READPREV
.* XRBA AND FLENGTH ARE EXTENSIONS
         GBLB  &PARMS_0610
         GBLB  &ZC_CICS
         LCLB  &RBA,&XRBA,&RRN,&NOEDF,&NOHAND
         LCLC  &FILE,&DATASET,&INTO,&SET,&LENGTH,&FLENGTH,&RIDFLD
         LCLC  &REQID,&KLENGTH,&RESP,&RESP2
.*
&NP      SETA  N'&SYSLIST
&IP      SETA  0
.PLOOP   ANOP
&IP      SETA  &IP+1
         AIF   (&IP GT &NP).ELOOP
&PARM    SETC  '&SYSLIST(&IP)'
.*
         AIF   ('&PARM'(1,5)  EQ 'FILE(').FILE
         AIF   ('&PARM'(1,8)  EQ 'DATASET(').DATASET
         AIF   ('&PARM'(1,5)  EQ 'INTO(').INTO
         AIF   ('&PARM'(1,4)  EQ 'SET(').SET
         AIF   ('&PARM'(1,7)  EQ 'LENGTH(').LENGTH
         AIF   ('&PARM'(1,8)  EQ 'FLENGTH(').FLENGTH
         AIF   ('&PARM'(1,10) EQ 'KEYLENGTH(').KLENGTH
         AIF   ('&PARM'(1,7)  EQ 'RIDFLD(').RIDFLD
         AIF   ('&PARM'(1,6)  EQ 'REQID(').REQID
         AIF   ('&PARM'(1,3)  EQ 'RBA').RBA
         AIF   ('&PARM'(1,4)  EQ 'XRBA').XRBA
         AIF   ('&PARM'(1,3)  EQ 'RRN').RRN
         AIF   ('&PARM'(1,5)  EQ 'NOEDF').NOEDF
         AIF   ('&PARM'(1,8)  EQ 'NOHANDLE').NOHAND
         AIF   ('&PARM'(1,5)  EQ 'RESP(').RESP
         AIF   ('&PARM'(1,6)  EQ 'RESP2(').RESP2
         MNOTE 12,'BAD PARM &PARM'
         AGO   .PLOOP
.*
.FILE    ANOP
&FILE    SETC  '&PARM'(6,K'&PARM-6)
         AGO   .PLOOP
.*
.DATASET ANOP
&DATASET SETC  '&PARM'(9,K'&PARM-9)
         AGO   .PLOOP
.*
.INTO    ANOP
&INTO    SETC '&PARM'(6,K'&PARM-6)
         AGO   .PLOOP
.*
.SET     ANOP
&SET     SETC '&PARM'(5,K'&PARM-5)
         AGO   .PLOOP
.*
.LENGTH  ANOP
&LENGTH  SETC  '&PARM'(8,K'&PARM-8)
         AGO   .PLOOP
.*
.FLENGTH ANOP
&FLENGTH SETC  '&PARM'(9,K'&PARM-9)
         AGO   .PLOOP
.*
.KLENGTH ANOP
&KLENGTH SETC  '&PARM'(11,K'&PARM-11)
         AGO   .PLOOP
.*
.RIDFLD  ANOP
&RIDFLD  SETC  '&PARM'(8,K'&PARM-8)
         AGO   .PLOOP
.*
.REQID   ANOP
&REQID   SETC  '&PARM'(7,K'&PARM-7)
         AGO   .PLOOP
.*
.RBA     ANOP
&RBA     SETB  1
         AGO   .PLOOP
.*
.XRBA    ANOP
&XRBA    SETB  1
         AGO   .PLOOP
.*
.RRN     ANOP
&RRN     SETB  1
         AGO   .PLOOP
.*
.NOEDF   ANOP
&NOEDF   SETB  1
         AGO   .PLOOP
.*
.NOHAND  ANOP
&NOHAND  SETB  1
         AGO   .PLOOP
.*
.RESP    ANOP
&RESP    SETC  '&PARM'(6,K'&PARM-6)
&NOHAND  SETB  1
         AGO   .PLOOP
.*
.RESP2   ANOP
&RESP2   SETC  '&PARM'(7,K'&PARM-7)
         AGO   .PLOOP
.*
.ELOOP   ANOP
         AIF   ('&FILE' EQ '' AND '&DATASET' EQ '').NOFILE
         AIF   ('&FILE' NE '' AND '&DATASET' NE '').BOTHF
         AIF   ('&INTO' EQ '' AND '&SET' EQ '').NOINTST
         AIF   ('&INTO' NE '' AND '&SET' NE '').BOTHINST
         AIF   (T'&INTO EQ 'U' AND '&LENGTH' EQ '' AND                 -
               '&FLENGTH' EQ '').LNTHREQ
         AIF   ('&LENGTH' NE '' AND '&FLENGTH' NE '').BOTHLEN
         AIF   ('&RIDFLD' EQ '').NEEDRID
         AIF   (&RBA AND &XRBA).BADRBA
         AIF   (&RRN AND &RBA).BOTHTYP
         AIF   (&RRN AND &XRBA).BOTHTYP
.*
         GEN_XC P0610,P0610END-P0610,P0610
         GEN_MVC P0610FN,,=X'0610' SET FUNCTION CODE
         LA    R0,&RIDFLD         R0=ADDRESS OF RIDFLD
         GEN_ST R0,P0610ARD       SAVE IT
         LA    R0,L'&RIDFLD       SET NUMERIC RIDFLD LENGTH
         GEN_STC R0,P0610LRD      SAVE IT
.*
         AIF   ('&FILE' EQ '').DODTAST
.* GOT FILE
         AIF   ('&FILE'(1,1) EQ '''').FLSTRING
         LARL  R15,P0610FIL       ADDRESS TARGET
         MVC   0(8,R15),&FILE     SET FILE NAME FROM LABEL
         AGO   .CHKLEN
.*
.FLSTRING ANOP
&FLLEN   SETA  K'&FILE
         AIF   (&FLLEN GT 10).BADFILE
         GEN_MVC P0610FIL,,=CL8&FILE SET FILE NAME AS STRING
         AGO   .CHKLEN
.*
.* GOT DATASET
.DODTAST ANOP
         AIF   ('&DATASET'(1,1) EQ '''').DTSTRING
         LARL  R15,P0610FIL       ADDRESS TARGET
         MVC   0(8,R15),&DATASET  SET DATASET NAME FROM LABEL
         AGO   .CHKLEN
.*
.DTSTRING ANOP
&DTLEN   SETA  K'&DATASET
         AIF   (&DTLEN GT 10).BADFILE
         GEN_MVC P0610FIL,,=CL8&DATASET SET DATASET NAME AS STRING
.*
.CHKLEN  ANOP
         AIF   ('&SET' EQ '').NOSET
         GEN_MVI P0610SET,X'FF'    SET SET INDICATOR
         AGO   .DOLEN
.*
.NOSET   ANOP
         LA    R0,&INTO           INTO ADDRESS
         GEN_ST R0,P0610INT       SAVE IT
.DOLEN   ANOP
         AIF   ('&LENGTH' EQ '').NOLENG
         AIF   (T'&LENGTH NE 'N').LENLABL
         LAY   R0,&LENGTH         SET NUMERIC LENGTH
         GEN_ST R0,P0610LEN       SAVE IT
         AGO   .DUNLEN
.*
.LENLABL ANOP
         AIF   (&ZC_CICS)
         AIF   ('&LENGTH'(1,10) EQ 'LENGTH,OF,')
.* COBOL LENGTH OF EXTRACT
&LENLSL  SETC  '&LENGTH'(11,K'&LENGTH-10)
         LAY   R0,L'&LENLSL       SET NUMERIC LENGTH
         GEN_ST R0,P0610LEN       SAVE IT
         AELSE
.* COBOL LENGTH AS LABEL
         LH    R0,&LENGTH         SET LENGTH FROM LABEL
         GEN_ST R0,P0610LEN        SAVE IT
         LA    R0,&LENGTH         R0=ADDRESS OF LENGTH
         GEN_ST R0,P0610LAD       SAVE IT
         AEND
         AELSE
.* ASM LENGTH AS LABEL
         LH    R0,&LENGTH         R0=LENGTH FROM LABEL
         GEN_ST R0,P0610LEN        SAVE IT
         LA    R0,&LENGTH         R0=ADDRESS OF LENGTH
         GEN_ST R0,P0610LAD       SAVE IT
         AEND
         AGO   .DUNLEN
.*
.NOLENG  ANOP
         AIF   ('&FLENGTH' EQ '').NOLENS
         AIF   (T'&FLENGTH NE 'N').FLENLBL
         GEN_MVC P0610LEN,,=AL4(&FLENGTH) MOVE FLENGTH
         AGO   .DUNLEN
.*
.FLENLBL ANOP
         AIF   (&ZC_CICS)
         AIF   ('&FLENGTH'(1,10) EQ 'LENGTH,OF,')
.* COBOL LENGTH OF EXTRACT
&LENLSL  SETC  '&FLENGTH'(11,K'&FLENGTH-10)
         GEN_MVC P0610LEN,,=AL4(L'&LENLSL) MOVE FLENGTH
         AELSE
.* COBOL LENGTH AS LABEL
         L     R0,&FLENGTH        SET FLENGTH FROM LABEL
         GEN_ST R0,P0610LEN       SAVE IT
         LA    R0,&FLENGTH        R0=ADDRESS OF FLENGTH
         GEN_ST R0,P0610FAD       SAVE IT
         AEND
         AELSE
.* ASM LENGTH AS LABEL
         L     R0,&FLENGTH        R0=FLENGTH FROM LABEL
         GEN_ST R0,P0610LEN       SAVE IT
         LA    R0,&FLENGTH        R0=ADDRESS OF FLENGTH
         GEN_ST R0,P0610FAD       SAVE IT
         AEND
         AGO   .DUNLEN
.*
.NOLENS  ANOP
         LAY   R0,L'&INTO         R0=IMPLIED LENGTH OF INTO
         GEN_ST R0,P0610LEN       SAVE IT
.DUNLEN  ANOP
         AIF   ('&KLENGTH' EQ '').CHKOPT
         AIF   ('&KLENGTH'(2,1) EQ '''').KLENNUM
         AIF   (T'&KLENGTH NE 'N').KLENLAB
.KLENNUM ANOP
         LAY   R0,&KLENGTH        SET NUMERIC KEYLENGTH
         GEN_STH R0,P0610KLN      SAVE IT
         GEN_MVI P0610KIN,X'FF'   SET VALID KEYLENGTH
         AGO   .GENKEY
.*
.KLENLAB ANOP
         AIF   (&ZC_CICS)
         AIF   ('&KLENGTH'(1,10) EQ 'LENGTH,OF,')
.* COBOL LENGTH OF EXTRACT
&LENLSL  SETC  '&KLENGTH'(11,K'&KLENGTH-10)
         LAY   R0,L'&LENLSL       SET NUMERIC KEYLENGTH
         GEN_STH R0,P0610KLN      SAVE IT
         GEN_MVI P0610KIN,X'FF'   SET VALID KEYLENGTH
         AELSE
.* COBOL KEYLENGTH AS LABEL
         LH    R0,&KLENGTH        R0=KEYLENGTH FROM LABEL
         GEN_STH R0,P0610KLN      SAVE IT
         GEN_MVI P0610KIN,X'FF'   SET VALID KEYLENGTH
         AEND
         AELSE
.* ASM KEYLENGTH AS LABEL
         LH    R0,&KLENGTH        R0=KEYLENGTH FROM LABEL
         GEN_STH R0,P0610KLN      SAVE IT
         GEN_MVI P0610KIN,X'FF'   SET VALID KEYLENGTH
         AEND
.GENKEY  ANOP
         LTR   R0,R0              IS IT KEYLENGTH(0) OR -VE ?
         JNP   CICS_READPREV_&SYSNDX._NOKEY EXIT IF IT IS
         CHI   R0,128             GT MAX ALLOWED ?
         JH    CICS_READPREV_&SYSNDX._NOKEY EXIT IF IT IS
         BCTR  R0,0               -1 FOR DYNAMIC LENGTH
         LARL  R1,CICS_READPREV_&SYSNDX._MVCKEY ADDRESS TARGET
         STC   R0,1(R1)           STORE LENGTH
         LARL  R15,P0610RID       ADDRESS TARGET
CICS_READPREV_&SYSNDX._MVCKEY EQU *
         MVC   0(0,R15),&RIDFLD   MOVE RIDFLD
CICS_READPREV_&SYSNDX._NOKEY EQU *
         AGO   .DUNOPT
.*
.CHKOPT  ANOP
         AIF   (NOT &RBA).NORBA
         LARL  R15,P0610RID+4     ADDRESS TARGET
         MVC   0(4,R15),&RIDFLD   MOVE RIDFLD
         GEN_MVI P0610RBA,X'FF'   SET RBA
         AGO   .DUNOPT
.*
.NORBA   ANOP
         AIF   (NOT &XRBA).NOXRBA
         LARL  R15,P0610RID       ADDRESS TARGET
         MVC   0(8,R15),&RIDFLD   MOVE RIDFLD
         GEN_MVI P0610XRB,X'FF'   SET XRBA
         AGO   .DUNOPT
.*
.NOXRBA  ANOP
         AIF   (NOT &RRN).NORRN
         LARL  R15,P0610RID       ADDRESS TARGET
         MVC   0(4,R15),&RIDFLD   MOVE RIDFLD
         GEN_MVI P0610RRN,X'FF'   SET RRN
         AGO   .DUNOPT
.*
.NORRN   ANOP
.* DEFAULT IS FULL RIDFLD
         LARL  R15,P0610RID       ADDRESS TARGET
         LA    R0,L'&RIDFLD       SET NUMERIC RIDFLD LENGTH
         BCTR  R0,0               -1 FOR DYNAMIC LENGTH
         LARL  R1,CICS_READPREV_&SYSNDX._MVCKEY ADDRESS TARGET
         STC   R0,1(R1)           STORE LENGTH
         LARL  R15,P0610RID       ADDRESS TARGET
CICS_READPREV_&SYSNDX._MVCKEY EQU *
         MVC   0(0,R15),&RIDFLD   MOVE RIDFLD
.DUNOPT  ANOP
         AIF   ('&REQID' EQ '').DUNREQ
         AIF   (T'&REQID NE 'N').REQLABL
         GEN_MVC P0610REQ,,=AL2(&REQID) MOVE REQID
         AGO   .DUNREQ
.*
.REQLABL ANOP
         LARL  R15,P0610REQ       ADDRESS TARGET
         MVC   0(2,R15),&REQID    MOVE REQID FROM LABEL
.DUNREQ  ANOP
         GEN_ST DFHEIBR,P0610EIB  SET EIB ADDRESS
         GEN_ST TCTTEAR,P0610TCT  SET TCTTE ADDRESS
         AIF   (&SYSEDF AND NOT &NOEDF)
         GEN_ST R13,P0610EDS      SET DSA ADDRESS FOR EDF
         L     R0,DFHEILEN        DSA LENGTH
         GEN_STH R0,P0610EDL      SET DSA LENGTH FOR EDF
         LA    R0,DFHEIUSR        DSA USER AREA
         SR    R0,R13             R0=CICS DSA LENGTH
         GEN_STH R0,P0610EDC      SET CICS DSA LENGTH FOR EDF
         AELSE
         GEN_MVI P0610NED,X'FF'   SET NOEDF
         AEND
         AIF   (NOT &NOHAND).DUNNOH
         GEN_MVI P0610NOH,X'FF'   SET NOHANDLE
.DUNNOH  ANOP
         AIF   ('&RESP' EQ '').NORESP
         LA    R0,&RESP           ADDRESS OF RESP
         GEN_ST R0,P0610RSP       SAVE IT
.NORESP  ANOP
         AIF   ('&RESP2' EQ '').NORESP2
         LA    R0,&RESP2          ADDRESS OF RESP2
         GEN_ST R0,P0610RS2       SAVE IT
.NORESP2 ANOP
         LARL  R0,P0610EDF_&SYSNDX ADDRESS ORIGIN FOR CEDF
         GEN_ST R0,P0610EDF       SAVE IT
         L     R15,TCTTELCL       R15=LCL MODULE INDEXER
         LARL  R1,=A(P0610)       R1=LCL MODULE PARAMETER LIST
P0610EDF_&SYSNDX DS 0H
         BAKR  0,R15              STACK REGS AND GO
.*
         AIF   ('&SET' EQ '').NOSET2
         AIF   (&ZC_CICS)
         AIF   ('&SET'(1,11) EQ 'ADDRESS,OF,')
.* COBOL LINKAGE-SECTION LABEL, EXTRACT AND TRANSFORM
&SETLSL  SETC  '&SET'(12,K'&SET-11)
         LARL  R1,P0610STA        ADDRESS SOURCE
         MVC   ZC_&SETLSL._PTR(4),0(R1) SET ADDRESS IN L-S POINTER
         AELSE
.* COBOL STANDARD POINTER
         GEN_MVC &SET,4,P0610STA  SET ADDRESS IN POINTER
         AEND
         AELSE
.* ASM GENERAL REGISTER
         LRL   &SET,P0610STA      LOAD THE SET REGISTER
         AEND
.NOSET2  ANOP
         GEN_CLI P0610NOH,X'FF'   NOHANDLE ?
         JE    P0610BYP_&SYSNDX   EXIT IF IT IS
         OC    EIBRESP,EIBRESP    ANY BAD RESPONSE ?
         JZ    P0610BYP_&SYSNDX   EXIT IF NONE
* INVREQ, FILENOTFOUND, LENGERR, NOTFND, NOTOPEN, DISABLED, ENDFILE
* ILLOGIC
         DC    AL3(0),C'ABEND'    MARKER FOR Z390KCP ESTAE
         DC    AL4(P0610BYP_&SYSNDX) ENTRYPOINT FOR IGNORE CONDITION
*
         AIF   (&PARMS_0610).BYP0610
&PARMS_0610 SETB 1
         P0610
*
.BYP0610 ANOP
P0610BYP_&SYSNDX DS 0H
         MEXIT
.*
.NOFILE  MNOTE 12,'FILE OR DATASET MUST BE SPECIFIED'
         MEXIT
.*
.BOTHF   MNOTE 12,'BOTH FILE AND DATASET ARE SPECIFIED'
         MEXIT
.*
.BADFILE MNOTE 12,'INVALID FILE OR DATASET'
         MEXIT
.*
.NOINTST MNOTE 12,'INTO OR SET MUST BE SPECIFIED'
         MEXIT
.*
.BOTHINST MNOTE 12,'BOTH INTO AND SET ARE SPECIFIED'
         MEXIT
.*
.LNTHREQ MNOTE 12,'THIS TYPE OF INTO REQUIRES LENGTH/FLENGTH'
         MEXIT
.*
.BOTHLEN MNOTE 12,'BOTH LENGTH AND FLENGTH ARE SPECIFIED'
         MEXIT
.*
.NEEDRID MNOTE 12,'RIDFLD IS MANDATORY'
         MEXIT
.*
.BADRBA  MNOTE 12,'BOTH RBA AND XRBA ARE SPECIFIED'
         MEXIT
.*
.BOTHTYP MNOTE 12,'BOTH RRN AND (X)RBA ARE SPECIFIED'
         MEXIT
         MEND
