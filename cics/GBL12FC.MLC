***********************************************************************
* Copyright 2006 Automated Software Tools Corporation                 *
* This source code is part of z390 assembler/emulator package         *
* The z390 package is distributed under GNU general public license    *
* Author - Don Higgins                                                *
***********************************************************************
GBL12FC  SUBENTRY
*
* FN=12FC  DEQALL
*
*         +0/DSCT +4/DSECT +8/IND   +12      +16
* R1  >>> WTOMSG, REQDSECT,QEAANCHR,TRCEZ390,THRDCNTL
* R14 = RETURN ADDRESS
         LR    R3,R1              SAVE PARM POINTER
         L     R4,12(R3)          ADDRESS OF TRCEZ390
         MVC   TRCEZ390,0(R4)     MOVE IT
         L     R9,0(R3)           WTODSECT ADDRESS
         USING WTODSECT,R9
         L     R6,4(R3)           ADDRESS RECEIVED DATA
         USING DFHKCBLK,R6
         MVC   WTOFN,KCPFN        SET FN
         MVC   WTOTERM,KCPTERM    SET TERMID
         MVC   WTOTRAN,=4C' '     NO TRANSID
         MVI   WTOTYPE,C' '       NO TRANSID TYPE
         MVC   WTOTASKN,=7C' '    NO TASK NUMBER
         MVC   WTODESC,=CL45'<<<Z390KCP DEQALL     REQUEST'
         MVI   CICSTIND,C'I'      SET INTERNAL TYPE
         BAL   R11,CICSTRCE       TRACE IT
KCRESCAN EQU   *
         L     R4,8(R3)           ADDRESS OF QEAANCHR ADDRESS
         LR    R8,R4              R8=CHAIN FROM ANCHOR
         ICM   R7,15,0(R4)        R7=QEA CHAIN
         BZ    KCGOOD             EXIT IF NONE
         USING DFHQEADS,R7
* QEA CHAIN EXISTS
* SCAN THE QEA CHAIN FOR ANY MATCHING TERMID
KCEXLOOP EQU   *
         CLC   KCPTERM,=C'SQ01'   SEQUENTIAL TERMINAL ?
         BNE   KCNOTSEQ           EXIT IF NOT
         CLI   QEATERM,X'FA'      SAME TERMID SUFFIX ?
         BNE   KCCHAIN            EXIT IF NOT
         B     KCTSTDEQ           EXIT
*
KCNOTSEQ EQU   *
         CLC   QEATERM,KCPTERM+3  SAME TERMID SUFFIX ?
         BNE   KCCHAIN            EXIT IF NOT
KCTSTDEQ EQU   *
* FOUND AN ENQ FOR THIS TERMID
         OC    QEAWAIT(THRDNUM),QEAWAIT ANY TASKS WAITING FOR DEQ ?
         BZ    KCUNCHN            EXIT IF NONE
* WE ARE GOING TO RESUME ALL WAITING TASKS AND THEN DEQ
         LA    R5,THRDNUM         R5=NO. OF TERMINALS
         LA    R10,QEAWAIT        ADDRESS WAIT INDICATORS
         SR    R12,R12            CLEAR R12
         MVC   WTODESC,=CL45'>>>Z390KCP DEQALL     RESUME TTTT'
         MVC   WTODESC+29(3),KCPTERM MOVE TERMID PREFIX
         L     R11,16(R3)         THRDCNTL ADDRESS
         USING THRDDSCT,R11
KCLOOP   EQU   *
         CLI   0(R10),X'FF'       TERMID SUSPENDED ?
         BNE   KCINCR             EXIT IF NOT
         LA    R15,KCPREFIX       R15=LENGTH TO SEND
         L     R2,THRDCONN        R2=CONNID
         TCPIO SEND,MSG=DFHKCBLK,LMSG=(R15),CONN=(R2),PORT=3900
         LTR   R15,R15            CHECK RC ?
         BNZ   ABEND780           EXIT IF BAD
         STC   R12,WTODESC+32     MOVE TERMID SUFFIX
         CLI   WTODESC+32,THRDSEQ SEQUENTIAL TERMINAL ?
         BNE   KCNOTSQ2           EXIT IF NOT
         MVC   WTODESC+29(4),=C'SQ01' MOVE SEQUENTIAL TERMID
         B     KCSETRUN           EXIT
*
KCNOTSQ2 EQU   *
         OI    WTODESC+32,X'F0'   TERMID SUFFIX TO CHAR
KCSETRUN EQU   *
         MVI   THRDIND,ST_RUN     SET RUNNING STATUS
         DROP  R11
         ST    R11,KCSAVR11       SAVE R11
         BAL   R11,CICSTRCE       TRACE IT
         L     R11,KCSAVR11       RESTORE R11
         MVC   WTODESC+29(3),KCPTERM RESET TERMID PREFIX
KCINCR   EQU   *
         AHI   R10,1              BUMP WAIT INDICATOR POINTER
         AHI   R12,1              BUMP TERMID SUFFIX
         AHI   R11,THRDLEN        BUMP THREAD CONTROL AREA
         BCT   R5,KCLOOP          LOOP
* ALL TASKS RESUMED (IF ANY)
KCUNCHN  EQU   *
* REPAIR THE CHAIN AND FREEMAIN THE QEA
         MVC   0(4,R8),QEACHNF    REMAKE THE CHAIN
         FREEMAIN LV=QEALEN,A=(R7) FREE QEA
         B     KCRESCAN           EXIT
KCSAVR11 DS    AL4                SAVE R11
*
KCCHAIN  EQU   *
         LR    R8,R7              R8=CHAINED FROM ADDRESS
         ICM   R7,15,QEACHNF      R7=QEA CHAIN
         BNZ   KCEXLOOP           LOOP IF MORE QEAS
KCGOOD   EQU   *
         XC    KCPRETCD,KCPRETCD  CLEAR RETURN CODE
         MVC   WTODESC,=CL45'>>>Z390KCP DEQALL     COMPLETED'
         BAL   R11,CICSTRCE       TRACE IT
         SUBEXIT
*
* SUBROUTINES
*
* MODIFIED CICSTRCE
CICSTRCE DS    0H
         CLI   TRCEZ390,C'N'      NO TRACING ?
         BER   R11                RETURN IF IT IS
         CLI   TRCEZ390,C'Y'      ALL TRACING ?
         BE    CICSTRGO           TRACE IF IT IS
         CLC   TRCEZ390,CICSTIND  TRACE TYPE=TRACE REQUEST ?
         BNER  R11                RETURN IF NOT
CICSTRGO EQU   *
         TIME  DEC,TIME,LINKAGE=SYSTEM GET TIME
         MVO   TIMEPACK,TIME      MOVE TO PACKED FIELD
         MVC   TIMEHERE(13),TIMEEDIT MOVE EDIT WORD
         ED    TIMEHERE(13),TIMEPACK EDIT TIME
         MVC   WTOTIME,TIMEHERE   SET TIME
         WTO   MF=(E,WTOMSG)      TRACE IT
         BR    R11                RETURN
*
ABEND780 ABEND 780,DUMP           TCPIO SEND FAILED
*
CICSTIND DS    C                  TRACE TYPE INDICATOR
TRCEZ390 DS    C                  Z390 TRACE OPTION
*
* TIME CONVERSION FIELDS
*
TIME     DS    CL4                HHMMSSTT
         DS    CL8                SPACE FOR DATE
TIMEPACK DC    PL5'0'             0HHMMSSTTC
TIMEEDIT DC    X'402021207A20207A20204B2020'
TIMEHERE DS    CL13
*
* WTO AREA DSECT
*
WTODSECT DSECT
WTOMSG   DS    AL4
WTOTIME  DS    CL13               TIME STAMP
         DS    C
WTOFN    DS    CL4                MODULE/FUNCTION REFERENCE (EIBFN)
         DS    C
WTOTERM  DS    CL4                TERMID
         DS    C
WTOTRAN  DS    CL4                TRANSID (OR BLANK)
         DS    C
WTOTYPE  DS    C                  TRANSID TYPE
         DS    C
WTOTASKN DS    CL7                TASK NUMBER
         DS    C
WTODESC  DS    CL45               DESCRIPTION
*
         DFHQEADS                 ENQUEUE ELEMENT BLOCK
         DFHKCBLK                 TASK CONTROL BLOCK
*
         EQUREGS
*
         END
