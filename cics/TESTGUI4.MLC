***********************************************************************
* z390 - Mainframe assembler emulator and run-time engine
* Copyright (C) 2021 z390 Assembler LLC
*
* This file is part of z390.
*
* z390 is free software; you can redistribute it and/or modify
* it under the terms of the GNU General Public License as published by
* the Free Software Foundation; either version 2 of the License, or
* (at your option) any later version.
* z390 is distributed in the hope that it will be useful,
* but WITHOUT ANY WARRANTY; without even the implied warranty of
* MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
* GNU General Public License for more details.
*
* You should have received a copy of the GNU General Public License 
* along with this program; if not, see https://www.gnu.org/licenses.
* Author - Don Higgins                                                *
***********************************************************************
* 05/08/06 RPI 308 CONVERT DEMOGUI4 TO TESTGUI4 EXEC CICS TN3270      *
***********************************************************************
DFHEISTG DSECT
MSG3     TN3270 WCC_KD,(SBA,6,10),(SF,ATT_PA)
REPLY    DC    CL40' '
         TN3270 (SF,ATT_PA)
REPLYL   EQU   *-REPLY
*
TESTGUI4 CSECT
         EXEC  CICS SEND FROM(MSG1) LENGTH(MSG1L)
LOOP     EQU   *
         EXEC  CICS SEND FROM(MSG2) LENGTH(MSG2L)
         XC    INPUT,INPUT        CLEAR INPUT
         EXEC  CICS RECEIVE INTO(INPUT) LENGTH(LENINPUT)
         CLI   INPUT,DFHPF3       EXIT ON PF3
         BE    EXIT
         CLI   INPUT,DFHENTER     IGNORE IF NOT ENTRY WITH TEXT FIELD
         BNE   LOOP
         LH    R1,LENINPUT        INSERTED BY MM
         LA    R2,REPLY
         LA    R3,REPLYL
         LA    R4,INPUT+3+3  FIRST FIELD DATA AFTER (KEY,SBA,SF,SBA)
         LR    R5,R1
         AHI   R5,-6
         ICM   R5,8,=C' '    PAD REPLY WITH SPACES
         MVCL  R2,R4
         MVC   MSG3(MSG3DL),MSG3D  COPY MSG3 TEXT TO DSA
         EXEC  CICS SEND FROM(MSG3) LENGTH(MSG3DLEN)
         CLC   REPLY(3),=C'END'
         BNE   LOOP          EXIT IF REPLY IS END
EXIT     EQU   *
         EXEC  CICS RETURN
*
INPUT    DS    XL100
LENINPUT DS    H                  RECEIVED LENGTH
*
MSG1     TN3270 ESC,WRT_EW,WCC_KD,(SBA,2,10),(SF,ATT_PA)
         DC    C'TESTGUI4 TEST TN3270 GUI INTERFACE'
MSG1L    DC    AL2(*-MSG1)
*
MSG2     TN3270 WCC_KD
         TN3270 (SBA,4,10),(SF,ATT_PA,C'TEXT OR END=')
         TN3270 (SF,ATT_UA,C'???'),(SF,ATT_PA)
         TN3270 (SBA,4,24),IC
MSG2L    DC    AL2(*-MSG2)
*
MSG3D    TN3270 WCC_KD,(SBA,6,10),(SF,ATT_PA)
MSG3DL   EQU   *-MSG3D
MSG3DLEN DC    AL2(REPLYL)
*
         COPY  DFHAID
         TN3270 SYMBOLS
         END
