***********************************************************************
* Copyright 2006 Automated Software Tools Corporation                 *
* This source code is part of z390 assembler/emulator package         *
* The z390 package is distributed under GNU general public license    *
* Author - Don Higgins                                                *
***********************************************************************
LCL9626  CSECT
*
* FN=9626  EXEC CICS STARTBROWSE CONTAINER
*
* R1  >>> P9626 BLOCK POINTER
*
         USING *,R13
         B     12(R15)            BYPASS EYECATCHER
         DC    CL8'LCL9626'
         LR    R13,R15            R13 AS BASE
         LR    R3,R1              SET BASE
         L     R3,0(R3)                    FOR PARMS
         USING (P9626,P9626END),R3
         L     DFHEIBR,P9626EIB   LOAD EIB ADDRESS
         USING DFHEIBLK,DFHEIBR
         L     TCTTEAR,P9626TCT   LOAD TCT ADDRESS
         USING DFHTCTTE,TCTTEAR
         MVC   EIBFN,P9626FN      SET EIBFN
         XC    EIBRCODE,EIBRCODE  CLEAR EIBRCODE
         XC    EIBRESP,EIBRESP    CLEAR EIBRESP
         XC    EIBRESP2,EIBRESP2  CLEAR EIBRESP2
         ICM   R1,15,P9626RSP     R1=RESP ADDRESS
         BZ    NORESP             EXIT IF NONE
         XC    0(4,R1),0(R1)      CLEAR RESP
NORESP   EQU   *
         ICM   R1,15,P9626RS2     R1=RESP2 ADDRESS
         BZ    NORESP2            EXIT IF NONE
         XC    0(4,R1),0(R1)      CLEAR RESP2
NORESP2  EQU   *
         CLI   P9626NED,X'FF'     NOEDF ?
         BE    NOEDFB             EXIT IF IT IS
         CLI   TCTTEEDF,X'FF'     EDF ON ?
         BNE   NOEDFB             EXIT IF NOT
         MVI   TCTTEEBA,X'00'     SET 'BEFORE' INTERCEPT
         MVI   TCTTEEDL,X'00'     SET NORMAL LINKAGE
         L     R15,TCTTEEDA       R15=EDF PROCESSOR ADDRESS
         BAKR  0,R15              STACK REGS AND GO
*
NOEDFB   EQU   *
         CLI   P9626CHN,X'00'     ANY CHANNEL ON STARTBROWSE ?
         BE    ACTIVERR           ERROR IF NONE
* VERIFY CHANNEL NAME
         LA    R15,P9626CHN       R15 POINTS TO CHANNEL NAME
         BAL   R14,VERIFY         VERIFY CHANNEL NAME
         B     CHANOK             EXIT IF OK
         B     CHANERR            EXIT IF BAD
*
CHANOK   EQU   *
* NAME VALID
         ASI   TCTTETKN,1         NEW TOKEN NUMBER
         L     R12,P9626TKN       R12=TOKEN ADDRESS
         MVC   0(4,R12),TCTTETKN  PASS TO REQUESTOR
         LA    R12,TCTTECAQ       R12=CHANNEL ANCHOR POINTER
         ICM   R2,15,TCTTECAQ     R2=CHANNEL CHAIN ADDRESS
         BZ    CHANERR            ERROR IF NONE
         USING DFHCHAN,R2
CHANCHLP EQU   *
* ASSUMING THAT ANY CHANNEL IS UP FOR GRABS
         CLC   CHANNAME,P9626CHN  MATCH STARTBROWSE CHANNEL ?
         BE    CHANFND            EXIT IF IT DOES
         LA    R12,CHANADDR       R12=PREVIOUS POINTER ADDRESS
         ICM   R2,15,CHANADDR     CHAIN TO NEXT
         BNZ   CHANCHLP           LOOP UNTIL CHAIN END
         B     CHANERR            ERROR IF NOT FOUND
*
CHANFND  EQU   *
* CHANNEL FOUND, BUILD CHWA
         LA    R12,TCTTECHW       R12=CHWA ANCHOR POINTER
         ICM   R2,15,TCTTECHW     R2=CHWA CHAIN ADDRESS
         BZ    CHWANEW            EXIT IF NONE
         USING DFHCHWA,R2
CHWALOOP EQU   *
         LA    R12,CHWAADDR       R12=PREVIOUS POINTER ADDRESS
         ICM   R2,15,CHWAADDR     CHAIN TO NEXT
         BNZ   CHWALOOP           LOOP TILL END
*
CHWANEW  EQU   *
         GETMAIN R,LV=40
         LR    R2,R1              SET GETMAIN ADDRESS
         XC    DFHCHWA(40),DFHCHWA CLEAR CHWA
         ST    R2,0(R12)          SAVE NEW CHWAADDR
         MVC   CHWANAME,P9626CHN  SET STARTBROWSE CHANNEL NAME
         MVC   CHWATOKN,TCTTETKN  SET STARTBROWSE TOKEN
         B     RETURN             EXIT
*
CHANERR  EQU   *
* INVOKE CHANNELERR
* NOTE: NO SEPARATE ERROR FOR BAD NAME
         MVI   EIBRCODE,X'DA'     SET EIBRCODE
         MVC   EIBRESP,DFHRESP(CHANNELERR) SET EIBRESP=CHANNELERR
         MVC   EIBRESP2,=F'2'     SET EIBRESP2=CHANNEL NOT FOUND
         B     DORESP             EXIT
*
ACTIVERR EQU   *
* INVOKE ACTIVITYERR
         MVC   EIBRESP,DFHRESP(ACTIVITYERR) SET EIBRESP=ACTIVITYERR
         MVC   EIBRCODE+2(1),EIBRESP+3 SET EIBRCODE
         MVC   EIBRESP2,=F'2'     SET EIBRESP2=NO CHANNEL
DORESP   EQU   *
         ICM   R14,15,P9626RSP    R14=RESP ADDRESS
         BZ    DORESP2            EXIT IF NONE
         MVC   0(4,R14),EIBRESP   SET RESP
DORESP2  EQU   *
         ICM   R14,15,P9626RS2    R14=RESP2 ADDRESS
         BZ    RETURN             EXIT IF NONE
         MVC   0(4,R14),EIBRESP2  SET RESP2
RETURN   EQU   *
         CLI   P9626NED,X'FF'     NOEDF ?
         BE    NOEDFA             EXIT IF IT IS
         CLI   TCTTEEDF,X'FF'     EDF ON ?
         BNE   NOEDFA             EXIT IF NOT
         MVI   TCTTEEBA,X'FF'     SET 'AFTER' INTERCEPT
         MVI   TCTTEEDL,X'00'     SET NORMAL LINKAGE
         L     R15,TCTTEEDA       R15=EDF PROCESSOR ADDRESS
         BAKR  0,R15              STACK REGS AND GO
*
NOEDFA   EQU   *
         PR    ,                  UNSTACK AND RETURN TO MACRO
*
VERIFY   EQU   *
* VALIDATE THE NAME, NO LEADING OR EMBEDDED BLANKS
* TRAILING BLANKS PERMITTED
         LR    R5,R15             R5=START OF NAME
         CLI   0(R15),C' '        BLANK START ?
         BE    4(R14)             ERROR IF IT IS
         LA    R0,16              R0=LENGTH
CHCTCHAR EQU   *
         LA    R2,CHCTTAB         R2 POINTS TO CHAR LIST
CHCTLOOP EQU   *
         CLC   0(1,R15),0(R2)     MATCH TO CHAR ?
         BNE   CHCTCINC           EXIT IF NOT
         AHI   R15,1              BUMP NAME POINTER
         BCT   R0,CHCTCHAR        LOOP THROUGH NAME
         BR    R14                RETURN
*
CHCTCINC EQU   *
         AHI   R2,1               BUMP CHAR LIST POINTER
         CLI   0(R2),X'FF'        STOPPER ?
         BNE   CHCTLOOP           LOOP IF NOT
* FOUND A BAD CHAR
         CLI   0(R15),C' '        IS IT BLANK ?
         BNE   4(R14)             ERROR IF NOT
* FOUND A BLANK, CHECK FOR TRAILING BLANKS
         LA    R2,15(R5)          R2=END OF NAME
         SR    R2,R15             R2=LENGTH OF TRAILING CHARS-1
         EX    R2,CHCTCLC         TEST FOR TRAILING BLANKS
         BNE   4(R14)             ERROR IF NOT ALL BLANK
         BR    R14                RETURN
*
CHCTCLC  CLC   0(0,R15),=15C' '   TEST FOR TRAILING BLANKS
*
CHCTTAB  DC    C'ABCDEFGHIJKLMNOPQRSTUVWXYZ'
         DC    C'abcdefghijklmnopqrstuvwxyz'
         DC    C'0123456789'
         DC    C'$@#/%&&?!:|"=ª,;<>.-_'
         DC    X'FF'              STOPPER
*
         P9626 DSECT
*
         DFHEIBLK                 EIB
         DFHTCTTE                 TCTTE
         DFHCHAN                  CHANNEL/CONTAINER STRUCTURES
         DFHCHWA                  STARTBROWSE CONTAINER BLOCK
*
         EQUREGS REGS=GPR
*
         END
