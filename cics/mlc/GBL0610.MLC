***********************************************************************
* Copyright 2006 Automated Software Tools Corporation                 *
* This source code is part of z390 assembler/emulator package         *
* The z390 package is distributed under GNU general public license    *
* Author - Don Higgins                                                *
***********************************************************************
GBL0610  SUBENTRY
*
* FN=0610  READPREV FILE
*
*         +0/DSCT +4/DSECT +8/IND   +12/IND +16
* R1  >>> WTOMSG, REQDSECT,DFHFCTAD,CONNID, TRCEZ390
* R14 = RETURN ADDRESS
         LR    R3,R1              SAVE PARM POINTER
         L     R4,16(R3)          ADDRESS OF TRCEZ390
         MVC   TRCEZ390,0(R4)     MOVE IT
         L     R9,0(R3)           WTODSECT ADDRESS
         USING WTODSECT,R9
         L     R6,4(R3)           ADDRESS RECEIVED DATA
         USING DFHFCBLK,R6
         L     R4,8(R3)           ADDRESS FCT ADDRESS
         L     R7,0(R4)           ADDRESS FCT
         USING DFHFCTDS,R7
         USING IHAACB,R8
         MVC   WTOTERM,FCPTERM    SET TERMID
         MVC   WTOTRAN,FCPTRAN    SET TRANSID
         MVI   WTOTYPE,C' '       NO TRANSID TYPE
         UNPK  WTOTASKN,FCPTASKN  UNPACK TASK NUMBER
         OI    WTOTASKN+6,X'F0'   CLEAR SIGN
         MVC   WTODESC,=CL45'<<<Z390KCP READPREV   REQUEST'
         MVI   CICSTIND,C'A'      SET APPLICATION TYPE
         BAL   R11,CICSTRCE       TRACE IT
         BAL   R11,FOP1REF        OPEN ON 1ST REFERENCE
         L     R10,ACBVCLRA       R10=VCLR (CLUSTER ENTRY)
         USING IHAVCLR,R10
* R6= REQUEST DATA  (DFHFCBLK)
* R7= FCTTE ADDRESS (DFHFCTDS)
* R8= ACB
* R4= VSWA AND RPL
* R10=VCLR (CLUSTER ENTRY)
         TM    FCTSERV,FCTBRWSE   IS BROWSE ALLOWED ?
         BNO   RDPVIV20           EXIT IF NOT
         ICM   R4,15,FCTVSWA      R4=VSWA ANCHOR
         BZ    RDPVIV41           EXIT IF NO VSWAS
         USING DFHVSWAD,R4
* SCAN VSWAS AND FIND OUR STARTBR ONE
RDPVFLP  EQU   *
         LTR   R4,R4              END OF CHAIN ?
         BZ    RDPVIV41           ERROR IF IT IS
         CLC   VSWTASKN,FCPTASKN  IS IT OURS ?
         BNE   RDPVVCHN           CHAIN IF NOT
         CLI   VSWCTYPE,VSWCBRWS  IS IT BROWSE TYPE ?
         BNE   RDPVVCHN           CHAIN IF NOT
         CLC   VSWREQID,FCPREQID  IS IT OUR REQID ?
         BE    RDPVVSGT           EXIT IF IT IS
RDPVVCHN EQU   *
         L     R4,VSWCHAIN        NEXT ENTRY
         B     RDPVFLP            LOOP
*
RDPVVSGT EQU   *
* FOUND OUR STARTBR VSWA
         USING IHARPL,VSWRPL
         CLI   FCPRDOPT,FCPRDRBA  RBA REQUEST ?
         BNE   RDPVXRBA           EXIT IF NOT
* ESDS/RBA
         MODCB RPL=IHARPL,OPTCD=(ADR,SEQ,BWD),ARG=(S,FCPRBA)
         CLC   FCPRBA,VSWARBA     HAS RBA CHANGED ?
         BE    RDPVNOPT           EXIT IF NOT
         B     RDPVGETR           EXIT
*
RDPVXRBA EQU   *
         CLI   FCPRDOPT,FCPRDXRB  XRBA REQUEST ?
         BNE   RDPVRRN            EXIT IF NOT
* ESDS/XRBA
         MODCB RPL=IHARPL,OPTCD=(ADR,SEQ,BWD,XRBA),ARG=(S,FCPXRBA)
         CLC   FCPXRBA,VSWAXRBA   HAS XRBA CHANGED ?
         BE    RDPVNOPT           EXIT IF NOT
         B     RDPVGETR           EXIT
*
RDPVRRN  EQU   *
         CLI   FCPRDOPT,FCPRDRRN  RRN REQUEST ?
         BNE   RDPVKSD            EXIT IF NOT
* RRDS
         MODCB RPL=IHARPL,OPTCD=(KEY,SEQ,BWD),ARG=(S,FCPRRN)
         CLC   FCPRRN,VSWARRN     HAS RRN CHANGED ?
         BE    RDPVNOPT           EXIT IF NOT
         B     RDPVGETR           EXIT
*
RDPVKSD  EQU   *
* KSDS
* CICS MANUAL: CREATES ERROR FOR GENERIC
* VSAM MANUAL: BWD MEANS GEN/KGE ARE IGNORED AND DEFAULT TO FKS/KEQ
         TM    VSWOPT,VSWGEN      GENERIC ?
         BO    RDPVIV24           ERROR IF IT IS
         MODCB RPL=IHARPL,OPTCD=(KEY,SEQ,FKS,KEQ,BWD),ARG=(S,FCPRID)
         L     R12,ACBVAIXA       R12=PATH ADDRESS OR 0
         USING IHAVAIX,R12
* KEYLENGTH MUST BE = CATALOG KEYLENGTH
         CLI   FCPKEYLN,X'7F'     ANY REQUESTED KEYLENGTH ?
         BE    RDPVKEY            EXIT IF NONE
         LTR   R12,R12            ANY PATH ?
         BZ    RDPVBAS1           EXIT IF NOT
* THIS IS A PATH, USE THE ALTERNATE KEY LENGTH
         CLC   FCPKEYLN,VAIXKLEN+2 VALID AIX KEYLENGTH ?
         BNE   RDPVIV26           ERROR IF NOT
         B     RDPVKEY            EXIT
*
RDPVBAS1 EQU   *
         CLC   FCPKEYLN,VCLRKLEN+2 VALID BASE KEYLENGTH ?
         BNE   RDPVIV26           ERROR IF NOT
RDPVKEY  EQU   *
* ONLY RID CHANGE CAN CAUSE REPOSITION
         L     R2,VCLRKLEN        R2=CATALOG BASE KEYLENGTH
         LTR   R12,R12            ANY PATH ?
         BZ    RDPVBAS2           EXIT IF NOT
         L     R2,VAIXKLEN        R2=CATALOG AIX KEYLENGTH
RDPVBAS2 EQU   *
         BCTR  R2,0               -1 FOR OFFSET
         STC   R2,RDPVCLC+1       STORE LENGTH
RDPVCLC  CLC   FCPRID(0),VSWARID  RIDFLD CHANGED ?
         BE    RDPVNOPT           EXIT IF NOT
*
RDPVGETR EQU   *
         XC    RDPVGETL,RDPVGETL  NO FREEMAIN IF POINT ERROR
         LR    R5,R6              DEFAULT TO CUR FCBLK IF POINT ERROR
         POINT RPL=IHARPL         REPOSITION
         LTR   R15,R15            CHECK RC ?
         BNZ   RDPVFAIL           EXIT IF BAD
RDPVNOPT EQU   *
         L     R5,VCLRLREC        R5=LENGTH
         TM    VCLRFLG1,VCLRVREC  IS IT VARIABLE ?
         BNO   RDPVFIX            EXIT IF NOT
         AHI   R5,4               +4 FOR VARIABLE LENGTH
RDPVFIX  EQU   *
         LA    R1,FCPREFIX(R5)    FC PREFIX LENGTH + MAX RECORD SIZE
         GETMAIN R,LV=(R1)
         ST    R0,RDPVGETL        SAVE LENGTH FOR FREEMAIN
         LR    R5,R1              ADDRESS NEW FC BLOCK
         MVC   0(FCPREFIX,R5),DFHFCBLK MOVE PREFIX INTO NEW BLOCK
         XC    FCPRETCD-DFHFCBLK(4,R5),FCPRETCD-DFHFCBLK(R5) CLEAR RC
         XC    FCPRESP2-DFHFCBLK(4,R5),FCPRESP2-DFHFCBLK(R5) CLEAR RSP2
         MODCB RPL=IHARPL,AREA=(S,FCPREFIX(R5)) READ AREA
         GET   RPL=IHARPL         READ RECORD
         LTR   R15,R15            CHECK RC ?
         BNZ   RDPVFAIL           EXIT IF ERROR
         TM    VCLRFLG1,VCLRVREC  IS IT VARIABLE ?
         BNO   RDPVFIX2           EXIT IF NOT
         MVC   FCPACLEN-DFHFCBLK(4,R5),RPLLREC SAVE ACTUAL LRECL
         B     RDPVLEN            EXIT
*
RDPVFIX2 EQU   *
         MVC   FCPACLEN-DFHFCBLK(4,R5),VCLRLREC SAVE FIXED LRECL
         DROP  R10
RDPVLEN  EQU   *
         L     R1,FCPLEN          R1=LENGTH REQUESTED
         CLC   FCPACLEN-DFHFCBLK(4,R5),FCPLEN IS ACT DATA LENGTH LESS ?
         BH    RDPVLNOK           EXIT IF NOT
         L     R1,FCPACLEN-DFHFCBLK(R5) R1=LENGTH OF DATA
RDPVLNOK EQU   *
         LA    R15,FCPREFIX(R1)   R15=ACTUAL LENGTH SENT
         ST    R15,FCPTCPIO-DFHFCBLK(R5) SAVE IT
* READPREV COMPLETED, PASS DATA TO CLIENT, FREE NEW FCBLK
*   ESDS - SAVE   XRBA IN VSWA FOR CHECKING SKIP SEQUENTIAL
*   RRDS - SAVE FCPRRN IN VSWA FOR CHECKING SKIP SEQUENTIAL
*   KSDS - SAVE FCPRID IN VSWA FOR CHECKING SKIP SEQUENTIAL
*   PASS REQUEST BLOCK BACK WITH MODIFIED RIDFLD
         CLI   FCPRDOPT,FCPRDKEY  FULL KEY REQUEST ?
         BE    RDPVSAVK           EXIT IF IT IS
         CLI   FCPRDOPT,FCPRDGEN  GENERIC KEY REQUEST ?
         BE    RDPVSAVK           EXIT IF IT IS
         CLI   FCPRDOPT,FCPRDRRN  RRN REQUEST ?
         BE    RDPVSAVR           EXIT IF IT IS
* ESDS
         MVC   VSWAXRBA,RPLLXRBA  SAVE XRBA FOR SKIP SEQ
         MVC   FCPXRBA-DFHFCBLK(8,R5),RPLLXRBA RETURN XRBA
         B     RDPVDONE           EXIT
*
RDPVSAVR EQU   *
* RRDS
         L     R8,RPLARG          ADDRESS RETURNED RRN
         MVC   VSWARRN,0(R8)      SAVE RRN FOR SKIP SEQ
         MVC   FCPRRN-DFHFCBLK(4,R5),0(R8) RETURN RRN
         B     RDPVDONE           EXIT
*
RDPVSAVK EQU   *
* KSDS
         L     R8,RPLAREA         ADDRESS RETURNED RID
         MVC   VSWARID,0(R8)      SAVE RID FOR SKIP SEQ
         MVC   FCPRID-DFHFCBLK(128,R5),0(R8) RETURN RID
*
RDPVDONE EQU   *
         MVC   WTODESC,=CL45'<<<Z390KCP READPREV   COMPLETED'
RDPVSEND EQU   *
*        L     R8,CONNID          R8=CONN ID
         L     R8,12(R3)          R8=CONN ID ADDRESS
         L     R8,0(R8)           R8=CONNID
         TCPIO SEND,MSG=(R5),LMSG=0(R5),CONN=(R8),PORT=3900
         LTR   R15,R15            CHECK RC ?
         BNZ   ABEND780           EXIT IF BAD
         OC    RDPVGETL,RDPVGETL  ANY NEW FCBLK ?
         BZ    RDPVTRCE           EXIT IF NOT
         MVC   0(4,R5),RDPVGETL   SET REAL STORAGE LENGTH
         FREEMAIN LA=RDPVGETL,A=(R5)
         B     RDPVTRCE           EXIT
*
RDPVFAIL EQU   *
* READPREV FAILURE PROCESSING
* FILE ERROR HANDLER
         LA    R15,FILEERTB       R15=ERROR TABLE ADDRESS
FILEERLP EQU   *
         CLC   0(4,15),RPLFEEDB   MATCH TO TABLE
         BNE   FILEERIN           EXIT IF NOT
         MVC   FCPRETCD-DFHFCBLK(4,R5),4(R15) MOVE RETURN CODE
         MVC   FCPRESP2-DFHFCBLK(4,R5),8(R15) MOVE RESP2
         MVC   FCPRESP2-DFHFCBLK(1,R5),1(R15) MOVE RPLFEEDB+1
         MVC   FCPRESP2+1-DFHFCBLK(1,R5),3(R15) MOVE RPLFEEDB+3
         B     FILEERDN           EXIT
*
FILEERIN EQU   *
         AHI   R15,20             BUMP TABLE POINTER
         CLI   0(R15),X'FF'       STOPPER ?
         BNE   FILEERLP           LOOP IF NOT
         J     ABEND790           MISSING TABLE ENTRY SHOULD NOT OCCUR
*
FILEERDN EQU   *
         MVC   WTODESC,=CL45'>>>Z390KCP READPREV   FAILED'
         MVC   WTODESC+29(8),12(R15) MOVE CONDITION
         B     RDPVSEND           EXIT
*
RDPVIV20 EQU   *
         MVC   FCPRESP2,=F'20'    SET RESP2 (FUNCTION NOT PERMITTED)
         B     RDPVINVQ           EXIT
*
RDPVIV24 EQU   *
         MVC   FCPRESP2,=F'24'    SET RESP2 (READPREV AND GENERIC)
         B     RDPVINVQ           EXIT
*
RDPVIV26 EQU   *
         MVC   FCPRESP2,=F'26'    SET RESP2 (KL NE CAT KL)
         B     RDPVINVQ           EXIT
*
RDPVIV41 EQU   *
         MVC   FCPRESP2,=F'41'    SET RESP2 (REQID NOT FOUND)
RDPVINVQ EQU   *
         MVC   FCPRETCD,DFHRESP(INVREQ) SET INVREQ
         MVC   WTODESC,=CL45'>>>Z390KCP READPREV   FAILED'
         MVC   WTODESC+29(6),=C'INVREQ'
         LR    R9,R6              SET AS IF NEW DFHFCBLK
*        L     R8,CONNID          R8=CONN ID
         L     R8,12(R3)          R8=CONN ID ADDRESS
         L     R8,0(R8)           R8=CONNID
         LA    R15,FCPREFIX       R15=LENGTH TO SEND
         TCPIO SEND,MSG=(R9),LMSG=(R15),CONN=(R8),PORT=3900
         LTR   R15,R15            CHECK RC ?
         BNZ   ABEND780           EXIT IF BAD
RDPVTRCE EQU   *
         L     R9,0(R3)           WTODSECT ADDRESS
         MVI   CICSTIND,C'A'      SET APPLICATION TYPE
         BAL   R11,CICSTRCE       TRACE IT
         SUBEXIT
*
RDPVGETL DS    F                  SAVE GETMAIN LENGTH
*
* SUBROUTINES
*
* MODIFIED CICSTRCE
CICSTRCE DS    0H
         CLI   TRCEZ390,C'N'      NO TRACING ?
         BER   R11                RETURN IF IT IS
         CLI   TRCEZ390,C'Y'      ALL TRACING ?
         BE    CICSTRGO           TRACE IF IT IS
         CLC   TRCEZ390,CICSTIND  TRACE TYPE=TRACE REQUEST ?
         BNER  R11                RETURN IF NOT
CICSTRGO EQU   *
         TIME  DEC,TIME,LINKAGE=SYSTEM GET TIME
         MVO   TIMEPACK,TIME      MOVE TO PACKED FIELD
         MVC   TIMEHERE(13),TIMEEDIT MOVE EDIT WORD
         ED    TIMEHERE(13),TIMEPACK EDIT TIME
         MVC   WTOTIME,TIMEHERE   SET TIME
         WTO   MF=(E,WTOMSG)      TRACE IT
         BR    R11                RETURN
*
FOP1REF DS     0H
* OPEN ON 1ST REFERENCE
FOP1LOOP EQU   *
* CHECK THE FCT STATUS
         CLC   FCPFILNM,FCTFILNM  MATCH REQUESTED FILENAME TO FCT ?
         BNE   FOP1INCR           EXIT IF NOT MATCHED
* FILE FOUND
         LA    R8,FCTTELEN(R7)    ADDRESS ACB
         TM    FCTFILST,FCTOPEN   IS FILE OPEN ?
         BOR   R11                RETURN IF IT IS
* FILE NOT OPEN
* TEST FILSTAT OPTIONS
*    OPENED,ENABLED   -- FILE OPENED AT START, CANNOT OCCUR HERE
*                     -- BUT TREATED AS OPEN ON 1ST REF
*    OPENED,DISABLED  -- FILE OPENED AT START, CANNOT OCCUR HERE
*                     -- BUT TREATED AS OPEN ON 1ST REF
*                     -- DISABLED STATUS RETURNED
*    OPENED,UNENABLED -- CANNOT OCCUR
*
*    CLOSED,ENABLED   -- FILE OPENED NOW (1ST REF)
*    CLOSED,DISABLED  -- DISABLED STATUS RETURNED
*    CLOSED,UNENABLED -- NOTOPEN STATUS RETURNED
         TM    FCTFILST,FCTOPCLO  FILSTAT=OPENED ?
         BO    FOP1OPEN           EXIT IF IT IS
* FILSTAT=CLOSED
         TM    FCTFILST,FCTENDIS  FILSTAT=ENABLED ?
         BO    FOP1OPEN           EXIT IF IT IS
* FILSTAT=CLOSED AND NOT ENABLED
         TM    FCTFILST,FCTUNENA  FILSTAT=UNENABLED ?
         BO    FOP1NOTO           EXIT IF NOTOPEN
         B     FOP1DISA           EXIT IF DISABLED
*
FOP1INCR EQU   *
         AHI   R7,FCTABLEN        POINT TO NEXT FCT ENTRY
         CLI   0(R7),X'FF'        END OF FCT ?
         BNE   FOP1LOOP           LOOP IF NOT
* FILE NOT FOUND
         B     FOP1FNTF           EXIT IF FILENOTFOUND
*
FOP1OPEN EQU   *
* OPEN ON 1ST REF
* INITIALIZE MESSAGE
         MVC   WTOFN,=C'06FE'     1ST REF FILE OPENS
         MVC   WTOTERM,=4C' '     NO TERMID
         MVC   WTOTRAN,=4C' '     NO PRESET TRANSID
         MVI   WTOTYPE,C' '       NO TRANSID TYPE
         MVC   WTOTASKN,=7C' '    NO TASK NUMBER
         MVC   WTODESC,=CL45'FILE ........ OPEN  FAILED'
         MVC   WTODESC+5(8),FCTFILNM MOVE FILENAME TO MSG
         TM    FCTSERV,FCTDELT+FCTUPDT+FCTADD REASON TO OPEN OUTPUT ?
         BZ    FOP1IN             EXIT IF NONE
* SET ALL BITS WE ARE LIKELY TO USE
*    ADR = ACCESS BY RBA
*    DIR = DIRECT ACCESS
*    KEY = ACCESS BY KEY
*    SEQ = SEQUENTIAL ACCESS
*    IN  = READ ONLY
*    OUT = READ OR WRITE
* OPEN FOR OUTPUT
         MODCB ACB=IHAACB,MACRF=(ADR,DIR,KEY,SEQ,OUT)
         OPEN  (IHAACB,OUTPUT)    OPEN FOR OUTPUT
         LTR   R15,R15            OK ?
         BNZ   FOP1OPER           EXIT IF NOT
         B     FOP1OPOK           EXIT
*
FOP1IN   EQU   *
* OPEN FOR INPUT
         MODCB ACB=IHAACB,MACRF=(ADR,DIR,KEY,SEQ,IN)
         OPEN  (IHAACB,INPUT)     OPEN FOR INPUT
         LTR   R15,R15            OK ?
         BNZ   FOP1OPER           EXIT IF NOT
FOP1OPOK EQU   *
* OPEN SUCCESSFUL, SAY SO
         MVC   WTODESC+20(6),=C'OK    ' SET OK
         MVI   CICSTIND,C'I'      SET INTERNAL TYPE
         ST    R11,FOP1SV11       SAVE RETURN ADDRESS
         BAL   R11,CICSTRCE       TRACE IT
         L     R11,FOP1SV11       RESTORE RETURN ADDRESS
         OI    FCTFILST,FCTOPEN   SET FILE OPEN
         OI    FCTFILST,FCTOPCLO  SET FILSTAT=OPENED
         B     FOP1DIST           EXIT
*
FOP1OPER EQU   *
* OPEN UNSUCCESSFUL, SAY SO
         MVC   WTODESC+20(6),=C'FAILED' SET FAILED
         MVI   CICSTIND,C'I'      SET INTERNAL TYPE
         ST    R11,FOP1SV11       SAVE RETURN ADDRESS
         BAL   R11,CICSTRCE       TRACE IT
         L     R11,FOP1SV11       RESTORE RETURN ADDRESS
         MVI   FCTFILST,X'00'     SET FILE CLOSED,DISABLED
         B     FOP1DISA           EXIT IF DISABLED
*
FOP1DIST EQU   *
         TM    FCTFILST,FCTENDIS  IS FILSTAT=DISABLED ?
         BZ    FOP1DISA           EXIT IF DISABLED
         BR    R11                RETURN
*
FOP1FNTF EQU   *
         MVC   FCPRETCD,DFHRESP(FILENOTFOUND) SET FILENOTFOUND
         MVC   FCPRESP2,=F'1'     SET RESP2
         MVC   WTODESC,=CL45'>>>Z390KCP READPREV   FAILED'
         MVC   WTODESC+29(12),=C'FILENOTFOUND'
         B     FOP1SEND           EXIT
*
FOP1DISA EQU   *
         MVC   FCPRETCD,DFHRESP(DISABLED) SET DISABLED
         MVC   FCPRESP2,=F'50'    SET RESP2
         MVC   WTODESC,=CL45'>>>Z390KCP READPREV   FAILED'
         MVC   WTODESC+29(12),=C'DISABLED'
         B     FOP1SEND           EXIT
*
FOP1NOTO EQU   *
         MVC   FCPRETCD,DFHRESP(NOTOPEN) SET NOTOPEN
         MVC   FCPRESP2,=F'60'    SET RESP2
         MVC   WTODESC,=CL45'>>>Z390KCP READPREV   FAILED'
         MVC   WTODESC+29(7),=C'NOTOPEN'
FOP1SEND EQU   *
         LR    R9,R6              SET AS IF NEW DFHFCBLK
*        L     R8,CONNID          R8=CONN ID
         L     R8,12(R3)          R8=CONN ID ADDRESS
         L     R8,0(R8)           R8=CONNID
         LA    R15,FCPREFIX       R15=LENGTH TO SEND
         TCPIO SEND,MSG=(R9),LMSG=(R15),CONN=(R8),PORT=3900
         LTR   R15,R15            CHECK RC ?
         BNZ   ABEND780           EXIT IF BAD
         L     R9,0(R3)           WTODSECT ADDRESS
         MVI   CICSTIND,C'A'      SET APPLICATION TYPE
         BAL   R11,CICSTRCE       TRACE IT
         SUBEXIT
FOP1SV11 DS    A                  SAVE R11
*
ABEND780 ABEND 780,DUMP           TCPIO SEND FAILED
ABEND790 ABEND 790,DUMP           VSAM FEEDBACK NOT EXPECTED
*
CICSTIND DS    C                  TRACE TYPE INDICATOR
TRCEZ390 DS    C                  Z390 TRACE OPTION
*
         COPY  FILEERTB           RPL FEEDBACK ERROR TABLE
*
* TIME CONVERSION FIELDS
*
TIME     DS    CL4                HHMMSSTT
         DS    CL8                SPACE FOR DATE
TIMEPACK DC    PL5'0'             0HHMMSSTTC
TIMEEDIT DC    X'402021207A20207A20204B2020'
TIMEHERE DS    CL13
*
* WTO AREA DSECT
*
WTODSECT DSECT
WTOMSG   DS    AL4
WTOTIME  DS    CL13               TIME STAMP
         DS    C
WTOFN    DS    CL4                MODULE/FUNCTION REFERENCE (EIBFN)
         DS    C
WTOTERM  DS    CL4                TERMID
         DS    C
WTOTRAN  DS    CL4                TRANSID (OR BLANK)
         DS    C
WTOTYPE  DS    C                  TRANSID TYPE
         DS    C
WTOTASKN DS    CL7                TASK NUMBER
         DS    C
WTODESC  DS    CL45               DESCRIPTION
*
         ACBD  ,                  VSAM ACB
         VCDTD ,                  VSAM CLUSTER DEFINITION TABLE
         DFHFCTDS                 FILE CONTROL TABLE
         DFHFCBLK                 FILE CONTROL TCPIO BLOCK
         DFHVSWAD                 FILE CONTROL VSAM WORK AREA
*
         EQUREGS
*
         END
