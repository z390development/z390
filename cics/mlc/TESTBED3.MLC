***********************************************************************
* Copyright 2006 Automated Software Tools Corporation                 *
* This source code is part of z390 assembler/emulator package         *
* The z390 package is distributed under GNU general public license    *
* Author - Don Higgins                                                *
***********************************************************************
DFHEISTG DSECT       DSA STORAGE
SENDLNS5 DS    XL(LINE5END)
SENDLNS6 DS    XL(LINE6END)
*
TESTBED3 CSECT
*
* TEST BED 3
*
*    XCTL'D FROM TESTBED2 AND THEN TESTBED1 WITH COMMAREA
*    DISPLAYS QUOTES DEPENDING ON COMMAREA ABSENCE OR PRESENCE
*
*    TEST ASSIGN INVOKINGPROG, RETURNPROG
*
         ICM   R4,15,DFHEICAP     ANY COMMAREA ?
         BNZ   TRYCOMM1           EXIT IF THERE IS
* NO COMMAREA
         EXEC  CICS ASSIGN INVOKINGPROG(INVPROG) RETURNPROG(RETPROG)
         CLC   EIBTRNID,=C'BED1'  BED1 ?
         BNE   BEC1               EXIT IF NOT
* BED1
         CLC   INVPROG,=C'TESTBED2' CORRECT ?
         BNE   ABENDINV           ABEND IF NOT
         CLC   RETPROG,=C'TESTBED1' CORRECT ?
         BNE   ABENDRET           ABEND IF NOT
         B     SEND5              EXIT
*
BEC1     EQU   *
         CLC   INVPROG,=C'TESTBEC2' CORRECT ?
         BNE   ABENDINV           ABEND IF NOT
         CLC   RETPROG,=C'TESTBEC1' CORRECT ?
         BNE   ABENDRET           ABEND IF NOT
SEND5    EQU   *
         MVC   SENDLNS5(LINE5END),LINE5 MESSAGE
         EXEC  CICS SEND FROM(SENDLNS5) LENGTH(LINE5LEN)
         B     RETURN             EXIT
*
TRYCOMM1 EQU   *
* WITH COMMAREA
         EXEC  CICS ASSIGN INVOKINGPROG(INVPROG) RETURNPROG(RETPROG)
         CLC   EIBTRNID,=C'BED1'  BED1 ?
         BNE   BEC1_2             EXIT IF NOT
* BED1
         CLC   INVPROG,=C'TESTBED1' CORRECT ?
         BNE   ABENDINV           ABEND IF NOT
         B     TESTRET            EXIT
*
BEC1_2   EQU   *
* BEC1
         CLC   INVPROG,=C'TESTBEC1' CORRECT ?
         BNE   ABENDINV           ABEND IF NOT
TESTRET  EQU   *
         CLC   RETPROG,=8C' '     CORRECT ?
         BNE   ABENDRET           ABEND IF NOT
         CLC   0(6,R4),=C'QUOTE6' IS IT QUOTE 6 ?
         BNE   ABEND              EXIT IF NOT
         MVC   SENDLNS6(LINE6END),LINE6 MESSAGE
         EXEC  CICS SEND FROM(SENDLNS6) LENGTH(LINE6LEN)
RETURN   EQU   *
         EXEC  CICS RETURN
*
ABEND    ABEND 999,DUMP
INVPROG  DS    CL8
RETPROG  DS    CL8
ABENDINV DC    H'0'
ABENDRET DC    H'0'
*
LINE5    EQU   *
         TN3270 WCC_KD
         TN3270 (SBA,20,2)
         DC    CL55'We are such stuff as dreams are made on'
         TN3270 (SBA,21,2)
         DC    CL55'And our little life is rounded with a sleep'
         TN3270 (SBA,22,2)
         DC    CL55'END'
LINE5END EQU   *-LINE5
LINE5LEN DC    AL2(*-LINE5)
*
LINE6    EQU   *
         TN3270 WCC_KD
         TN3270 (SBA,23,2)
         DC    CL55'Brevity is the soul of wit'
         TN3270 (SBA,24,2)
         DC    CL55'END - Task end, now press CLEAR'
LINE6END EQU   *-LINE6
LINE6LEN DC    AL2(*-LINE6)
*
         TN3270 SYMBOLS
         END
