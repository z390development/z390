***********************************************************************
* Copyright 2006 Automated Software Tools Corporation                 *
* This source code is part of z390 assembler/emulator package         *
* The z390 package is distributed under GNU general public license    *
* Author - Don Higgins                                                *
***********************************************************************
GBLFE01  SUBENTRY
*
* FN=FE01  CEMT I SYS REQUESTED
*          ISYSDATA SENT BACK
*
*         +0/DSCT +4/DSECT  +8/IND  +12       +16/IND  +20
* R1  >>> WTOMSG, REQDSECT, CONNID, TRCEZ390, CMD1LEN, CMD0+1
*         +24    +28/IND  +32      +36     +40       +44/IND +48/IND
*         CMD1+4,CICSADDR,TRMPREFX,INIDATE,THREADS+3,LOCALS, REMOTES
*         +52      +56       +60       +64       +68     +72
*         NONTERMS,MAXLOCS+3,MAXREMS+3,MAXNONT+3,INITRAN,CWALENG
*         +76      +80
*         KCPTRPOS,INISEQT
* R14 = RETURN ADDRESS
         LR    R3,R1              SAVE PARM POINTER
         L     R4,12(R3)          TRCEZ390 ADDRESS
         MVC   TRCEZ390,0(R4)     MOVE IT
         L     R5,4(R3)           REQDSECT ADDRESS
         USING REQDSECT,R5
         L     R9,0(R3)           WTODSECT ADDRESS
         USING WTODSECT,R9
         MVC   WTOTERM,REQTERM    SET TERMID
         MVC   WTOTRAN,REQTRAN    SET TRANSID
         MVI   WTOTYPE,C' '       NO TRANSID TYPE
         UNPK  WTOTASKN,REQTASKN  UNPACK TASK NUMBER
         OI    WTOTASKN+6,X'F0'   CLEAR SIGN
         MVC   WTODESC,=CL45'<<<Z390KCP CEMT I SYS REQUEST'
         MVI   CICSTIND,C'A'      SET APPLICATION TYPE
* MODIFIED CICSTRCE
         CLI   TRCEZ390,C'N'      NO TRACING ?
         BE    CICSTRDN           EXIT IF IT IS
         CLI   TRCEZ390,C'Y'      ALL TRACING ?
         BE    CICSTRGO           TRACE IF IT IS
         CLC   TRCEZ390,CICSTIND  TRACE TYPE=TRACE REQUEST ?
         BNE   CICSTRDN           EXIT IF NOT
CICSTRGO EQU   *
         TIME  DEC,TIME,LINKAGE=SYSTEM GET TIME
         MVO   TIMEPACK,TIME      MOVE TO PACKED FIELD
         MVC   TIMEHERE(13),TIMEEDIT MOVE EDIT WORD
         ED    TIMEHERE(13),TIMEPACK EDIT TIME
         MVC   WTOTIME,TIMEHERE   SET TIME
         WTO   MF=(E,WTOMSG)      TRACE IT
         DROP  R9
*
CICSTRDN EQU   *
         MVC   ISYSTERM,REQTERM   PASS BACK TERMID
         L     R4,16(R3)          CMD1LEN ADDRESS
         LH    R6,0(R4)           LENGTH OF ENV VARIABLE
         LTR   R6,R6              ANY JARPATH ?
         BZ    CEMTISCP           EXIT IF NONE
         L     R4,20(R3)          CMD0+1 ADDRESS
         CLI   0(R4),C' '         ANY DRIVE ID ?
         BE    CEMTISNV           EXIT IF NONE
         MVC   ISYSDJRP(2),0(R4)  MOVE DRIVE ID
         L     R4,24(R3)          CMD1+4 ADDRESS
         EX    R6,ISYSMVCJ        MOVE JARPATH AFTER DRIVE ID
         B     CEMTISCP           EXIT
*
CEMTISNV EQU   *
         L     R4,24(R3)          CMD1+4 ADDRESS
         EX    R6,ISYSMVJ2        MOVE JARPATH WITHOUT DRIVE ID
CEMTISCP EQU   *
         L     R4,28(R3)          ADDRESS OF CICSADDR ADDRESS
         ICM   R6,15,0(R4)        R6=ADDRESS OF CICSPATH
         BZ    CEMTNOCP           EXIT IF NONE
         LA    R8,ISYSDCSP        DESTINATION
CEMTCPLP EQU   *
         MVC   0(1,R8),0(R6)      MOVE CICSPATH A BYTE AT A TIME
         AHI   R6,1               BUMP ORIGIN
         AHI   R8,1               BUMP DESTINATION
         CLI   0(R6),X'00'        NULL TERMINATOR ?
         BNE   CEMTCPLP           LOOP IF NOT
CEMTNOCP EQU   *
         L     R4,32(R3)          ADDRESS OF TRMPREFX
         MVC   ISYSDPFX,0(R4)     MOVE TERMID_PREFIX
         L     R4,36(R3)          ADDRESS OF INIDATE
         MVC   ISYSDDTE,0(R4)     MOVE DATE FORMAT
         L     R4,40(R3)          ADDRESS OF THREADS+3
         MVC   ISYSDTMX,0(R4)     MOVE MAXTHREADS
         L     R4,44(R3)          ADDRESS OF LOCALS
         MVC   ISYSDLUS,3(R4)     MOVE LOCAL TERMINALS IN USE
         L     R8,0(R4)           R8=LOCALS IN USE
         L     R6,48(R3)          ADDRESS OF REMOTES
         MVC   ISYSDRUS,3(R6)     MOVE REMOTE TERMINALS IN USE
         A     R8,0(R6)           +REMOTES IN USE
         L     R6,52(R3)          ADDRESS OF NON-TERMS
         MVC   ISYSDNUS,3(R6)     MOVE NON-TERMINALS IN USE
         A     R8,0(R6)           +NON-TERMS IN USE=THREADS IN USE
         STC   R8,ISYSDTUS        SAVE IT
         L     R4,56(R3)          ADDRESS OF MAXLOCS+3
         MVC   ISYSDLCL,0(R4)     MOVE LOCAL_TERMINALS
         L     R4,60(R3)          ADDRESS OF MAXREMS+3
         MVC   ISYSDRMT,0(R4)     MOVE REMOTE_TERMINALS
         L     R4,64(R3)          ADDRESS OF MAXNONT+3
         MVC   ISYSDNON,0(R4)     MOVE NON-TERMINALS
         L     R4,68(R3)          ADDRESS OF INITIAL_TRANSID
         MVC   ISYSDINT,0(R4)     MOVE INITIAL_TRANSID
         L     R4,72(R3)          ADDRESS OF CWASIZE
         MVC   ISYSDCWA,0(R4)     MOVE CWASIZE
         MVC   ISYSDTRL,=C'NO '   ASSUME TRACE_LOCALS=NO
         L     R4,76(R3)          ADDRESS OF KCPTRPOS
         CLI   0(R4),C'L'         IS TRACE SET ?
         BNE   CEMTISTN           EXIT IF NOT
         MVC   ISYSDTRL,=C'YES'   TRACE_LOCALS=YES
CEMTISTN EQU   *
         MVC   ISYSDTRZ,=C'NO '   ASSUME TRACE_Z390CICS=NO
         CLI   TRCEZ390,C'N'      IS IT NO ?
         BE    CEMTISOK           EXIT IF IT IS
         MVC   ISYSDTRZ,=C'INT'   ASSUME TRACE_Z390CICS=INT
         CLI   TRCEZ390,C'I'      IS IT INT ?
         BE    CEMTISOK           EXIT IF IT IS
         MVC   ISYSDTRZ,=C'APP'   ASSUME TRACE_Z390CICS=APP
         CLI   TRCEZ390,C'A'      IS IT APP ?
         BE    CEMTISOK           EXIT IF IT IS
         MVC   ISYSDTRZ,=C'YES'   MUST BE TRACE_Z390CICS=YES
CEMTISOK EQU   *
         MVC   ISYSDSEQ,=C'NO   ' ASSUME SEQ_TERM=NO
         L     R4,80(R3)          ADDRESS OF INISEQT
         CLI   0(R4),C'Y'         IS IT YES ?
         BNE   CEMTISNY           EXIT IF NOT
         MVC   ISYSDSEQ,=C'YES  ' SEQ_TERM=YES
         B     CEMTISNQ           EXIT
*
CEMTISNY EQU   *
         CLI   0(R4),C'T'         IS IT TRACE ?
         BNE   CEMTISNQ           EXIT IF NOT
         MVC   ISYSDSEQ,=C'TRACE' SEQ_TERM=TRACE
CEMTISNQ EQU   *
         L     R8,8(R3)           R8=CONN ID ADDRESS
         L     R8,0(R8)           R8=CONNID
         LA    R6,ISYSLEN         R6=MESSAGE LENGTH
         TCPIO SEND,MSG=ISYSDATA,LMSG=(R6),CONN=(R8),PORT=3900
         LTR   R15,R15            CHECK RC ?
         BNZ   ABEND780           EXIT IF BAD
         SUBEXIT
*
ISYSMVCJ MVC   ISYSDJRP+2(0),0(R4) MOVE JARPATH AFTER DRIVE ID
ISYSMVJ2 MVC   ISYSDJRP(0),0(R4) MOVE JARPATH WITHOUT DRIVE ID
*
ABEND780 ABEND 780,DUMP           TCPIO SEND FAILED
*
CICSTIND DS    C                  TRACE TYPE INDICATOR
TRCEZ390 DS    C                  Z390 TRACE OPTION
*
* TIME CONVERSION FIELDS
*
TIME     DS    CL4                HHMMSSTT
         DS    CL8                SPACE FOR DATE
TIMEPACK DC    PL5'0'             0HHMMSSTTC
TIMEEDIT DC    X'402021207A20207A20204B2020'
TIMEHERE DS    CL13
*
* CEMT I SYS DATA AREA
*
ISYSDATA DS    0F
         DC    A(ISYSLEN)         AREA LENGTH
         DC    C'FE01'            REQUEST CODE
ISYSTERM DS    CL4                TERMID
         DS    CL4                TRANSID
         DS    C                  TRANSID TYPE
         DS    PL4                TASK NO.
ISYSDJRP DC    CL50' '            JARPATH
ISYSDCSP DC    CL50' '            CICSPATH
ISYSDPFX DS    CL3                TERMID_PREFIX
ISYSDDTE DS    C                  DATE FORMAT
ISYSDTMX DS    X                  MAXTHREADS
ISYSDTUS DS    X                  THREADS IN USE
ISYSDLCL DS    X                  LOCAL_TERMINALS
ISYSDLUS DS    X                  LOCAL TERMINALS IN USE
ISYSDRMT DS    X                  REMOTE_TERMINALS
ISYSDRUS DS    X                  REMOTE TERMINALS IN USE
ISYSDNON DS    X                  NON-TERMINALS
ISYSDNUS DS    X                  NON-TERMINALS IN USE
ISYSDINT DS    CL4                INITIAL_TRANSID
ISYSDCWA DS    XL4                CWASIZE
ISYSDTRL DS    CL3                TRACE_LOCALS
ISYSDTRZ DS    CL3                TRACE_Z390CICS
ISYSDSEQ DS    CL5                SEQ_TERM
ISYSLEN  EQU   *-ISYSDATA
*
* RECEIVE MESSAGE AREA
*
REQDSECT DSECT
         DS    AL4                REQUEST LENGTH
         DS    CL4                REQUEST CODE
REQTERM  DS    CL4                TERMINAL ID
REQTRAN  DS    CL4                TRANSID
         DS    C                  TRANSID TYPE
REQTASKN DS    PL4                TASK NO
*
* WTO AREA DSECT
*
WTODSECT DSECT
WTOMSG   DS    AL4
WTOTIME  DS    CL13               TIME STAMP
         DS    C
         DS    CL4                MODULE/FUNCTION REFERENCE (EIBFN)
         DS    C
WTOTERM  DS    CL4                TERMID
         DS    C
WTOTRAN  DS    CL4                TRANSID (OR BLANK)
         DS    C
WTOTYPE  DS    C                  TRANSID TYPE
         DS    C
WTOTASKN DS    CL7                TASK NUMBER
         DS    C
WTODESC  DS    CL45               DESCRIPTION
*
         EQUREGS
*
         END
