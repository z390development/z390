***********************************************************************
* Copyright 2006 Automated Software Tools Corporation                 *
* This source code is part of z390 assembler/emulator package         *
* The z390 package is distributed under GNU general public license    *
* Author - Don Higgins                                                *
***********************************************************************
         DFHEISTG
RECAREA  DS    CL80               RECEIVE AREA
RECLEN   DS    AL2                RECEIVED LENGTH
ACOMM    DS    CL4                COMMAREA
*
TESTBED6 DFHEIENT CODEREG=(R12,R3)
*
* TEST HANDLE ABEND
*
* TEST 1: HANDLE ASRA ABEND BY LABEL
*         PRETEND NOTHING HAPPENED
*
* TEST 2: RAISE PGMIDERR
*         HANDLE AEI0 ABEND BY LABEL
*         PRETEND NOTHING HAPPENED
*
* TEST 3: HANDLE ASRA ABEND BY PROGRAM TESTBED7
*         PRETEND NOTHING HAPPENED
*
* THE FOLLOWING TESTS ARE DRIVEN BY TRANSID BED5
*
* TEST 4: SET HANDLE ABEND LABEL HERE
*         LINK TO TESTBED7 AND ABEND ASRA
*         RETURN IS AFTER OUR LINK
*
* TEST 5: LINK TO TESTBED7 AND HANDLE ABEND AEI0 THERE BY PROGRAM
*         TESTBED8
*         RETURN IS AFTER OUR LINK
*
* TEST 6: EXEC CICS ABEND WITH DUMP HANDLED HERE BY LABEL
*         PRETEND NOTHING HAPPENED
*
         CLC   EIBTRNID,=C'BED5'  SECOND TEST TRANSID ?
         BE    BED5               EXIT IF IT IS
*
         EXEC  CICS SEND FROM(TEST1) LENGTH(TEST1L)
*
* CREATE AND MODIFY HANDLE ABEND BLOCK
         EXEC  CICS HANDLE ABEND LABEL(BED6ASRA)
         EXEC  CICS RECEIVE INTO(RECAREA) LENGTH(RECLEN)
*
* CAUSE ASRA
* THIS IS 'REPLACED' BY SEND AND B AB2
         DC    H'0'
*
AB2      EQU   *
* ASRA HANDLED OK
* MODIFY HANDLE ABEND BLOCK
         EXEC  CICS HANDLE ABEND LABEL(BED6AEI0)
         EXEC  CICS RECEIVE INTO(RECAREA) LENGTH(RECLEN)
*
* CAUSE AEI0
* THIS IS 'REPLACED' BY SEND AND B AB3
         EXEC  CICS LOAD PROGRAM('BADPROG')
*
AB3      EQU   *
* AEI0 HANDLED OK
* MODIFY HANDLE ABEND BLOCK
         EXEC  CICS HANDLE ABEND PROGRAM('TESTBED7')
         EXEC  CICS RECEIVE INTO(RECAREA) LENGTH(RECLEN)
*
* CAUSE ASRA
* THIS ACTS AS THOUGH WE HAD AN XCTL TO TESTBED7 HERE
* THE RETURN IN TESTBED7 ENDS THE TASK NORMALLY
         DC    H'0'
*
BED6ASRA EQU   *
         EXEC  CICS SEND FROM(TEST1R) LENGTH(TEST1RL)
         B     AB2                CONTINUE TESTS
*
BED6AEI0 EQU   *
         EXEC  CICS SEND FROM(TEST2R) LENGTH(TEST2RL)
         B     AB3                CONTINUE TESTS
*
* BED5 TESTS
*
BED5     EQU   *
         EXEC  CICS SEND FROM(TEST4) LENGTH(TEST4L)
         EXEC  CICS HANDLE ABEND LABEL(BED7ASRA)
         EXEC  CICS RECEIVE INTO(RECAREA) LENGTH(RECLEN)
* TESTBED7 WILL ABEND ASRA
         EXEC  CICS LINK PROGRAM('TESTBED7')
*
BED7ASOK EQU   *
* RETURN HERE AFTER HANDLING ASRA ABEND
         EXEC  CICS SEND FROM(TEST4K) LENGTH(TEST4KL)
         EXEC  CICS RECEIVE INTO(RECAREA) LENGTH(RECLEN)
* TESTBED7 WILL ABEND AEI0
         MVC   ACOMM,=C'AEI0'     SET COMMAREA
         EXEC  CICS LINK PROGRAM('TESTBED7') COMMAREA(ACOMM)
*
* RETURN HERE AFTER HANDLING AEI0 ABEND
         EXEC  CICS SEND FROM(TEST5K) LENGTH(TEST5KL)
         EXEC  CICS HANDLE ABEND LABEL(BED6ZZZZ)
         EXEC  CICS RECEIVE INTO(RECAREA) LENGTH(RECLEN)
* TAKE THE DUMP, IGNORE THE ABEND
         EXEC  CICS ABEND ABCODE('ZZZZ')
AB4      EQU   *
         EXEC  CICS RETURN
*
BED7ASRA EQU   *
         EXEC  CICS SEND FROM(TEST4R) LENGTH(TEST4RL)
* NOW GO TO INST AFTER LINK TO TESTBED7
         B     BED7ASOK           EXIT
*
BED6ZZZZ EQU   *
         EXEC  CICS SEND FROM(TEST6R) LENGTH(TEST6RL)
         B     AB4                EXIT
*
TEST1    TN3270 WCC_KD,(SBA,2,2)
         DC    C'TESTBED6 TEST 1:PRESS AID KEY TO HANDLE ASRA ABEND BY X
               LABEL'
TEST1L   DC    AL2(*-TEST1)
*
TEST1R   TN3270 WCC_KD,(SBA,3,2)
         DC    C'TESTBED6 ASRA HANDLED OK BY LABEL'
         TN3270 (SBA,4,2)
         DC    C'TESTBED6 TEST 2:PRESS AID KEY TO HANDLE AEI0 ABEND BY X
               LABEL'
TEST1RL  DC    AL2(*-TEST1R)
*
TEST2R   TN3270 WCC_KD,(SBA,5,2)
         DC    C'TESTBED6 AEI0 HANDLED OK BY LABEL'
         TN3270 (SBA,6,2)
         DC    C'TESTBED6 TEST 3:PRESS AID KEY TO HANDLE ASRA ABEND BY X
               PROGRAM'
TEST2RL  DC    AL2(*-TEST2R)
*
TEST4    TN3270 WCC_KD,(SBA,2,2)
         DC    C'TESTBED6 TEST 4:PRESS AID KEY TO HANDLE ASRA ABEND IN X
               LINKED PROGRAM BY LABEL'
TEST4L   DC    AL2(*-TEST4)
*
TEST4R   TN3270 WCC_KD,(SBA,4,2)
         DC    C'TESTBED6 ASRA IN TESTBED7 HANDLED OK BY LABEL'
TEST4RL  DC    AL2(*-TEST4R)
*
TEST4K   TN3270 WCC_KD,(SBA,5,2)
         DC    C'TESTBED6 HANDLE ABEND RETURNED SUCCESSFULLY'
         TN3270 (SBA,6,2)
         DC    C'TESTBED6 TEST 5:PRESS AID KEY TO HANDLE AEI0 ABEND IN X
               LINKED PROGRAM BY PROGRAM'
TEST4KL  DC    AL2(*-TEST4K)
*
TEST5K   TN3270 WCC_KD,(SBA,9,2)
         DC    C'TESTBED6 HANDLE ABEND RETURNED SUCCESSFULLY'
         TN3270 (SBA,10,2)
         DC    C'TESTBED6 TEST6:PRESS AID KEY TO ABEND, HANDLED BY LABEX
               L'
TEST5KL  DC    AL2(*-TEST5K)
*
TEST6R   TN3270 WCC_KD,(SBA,11,2)
         DC    C'TESTBED6 ABEND ZZZZ HANDLED OK BY LABEL'
         TN3270 (SBA,13,2)
         DC    C'BED5 ENDED OK, NOW PRESS CLEAR'
TEST6RL  DC    AL2(*-TEST6R)
*
         TN3270 SYMBOLS
         END
