***********************************************************************
* Copyright 2006 Automated Software Tools Corporation                 *
* This source code is part of z390 assembler/emulator package         *
* The z390 package is distributed under GNU general public license    *
* Author - Don Higgins                                                *
***********************************************************************
GBLFE06  SUBENTRY
*
* FN=FE06  CEMT I ENQ REQUESTED
*          IENQDATA SENT BACK
*
*         +0/DSCT +4/DSECT  +8/IND   +12/IND +16
* R1  >>> WTOMSG, REQDSECT ,QEAANCHR,CONNID, TRCEZ390
* R14 = RETURN ADDRESS
         LR    R3,R1              SAVE PARM POINTER
         L     R4,16(R3)          TRCEZ390 ADDRESS
         MVC   TRCEZ390,0(R4)     MOVE IT
         L     R5,4(R3)           REQDSECT ADDRESS
         USING REQDSECT,R5
         L     R9,0(R3)           WTODSECT ADDRESS
         USING WTODSECT,R9
         MVC   WTOTERM,REQTERM    SET TERMID
         MVC   WTOTRAN,REQTRAN    SET TRANSID
         MVI   WTOTYPE,C' '       NO TRANSID TYPE
         UNPK  WTOTASKN,REQTASKN  UNPACK TASK NUMBER
         OI    WTOTASKN+6,X'F0'   CLEAR SIGN
         MVC   WTODESC,=CL45'<<<Z390KCP CEMT I ENQ REQUEST'
         MVI   CICSTIND,C'A'      SET APPLICATION TYPE
* MODIFIED CICSTRCE
         CLI   TRCEZ390,C'N'      NO TRACING ?
         BE    CICSTRDN           EXIT IF IT IS
         CLI   TRCEZ390,C'Y'      ALL TRACING ?
         BE    CICSTRGO           TRACE IF IT IS
         CLC   TRCEZ390,CICSTIND  TRACE TYPE=TRACE REQUEST ?
         BNE   CICSTRDN           EXIT IF NOT
CICSTRGO EQU   *
         TIME  DEC,TIME,LINKAGE=SYSTEM GET TIME
         MVO   TIMEPACK,TIME      MOVE TO PACKED FIELD
         MVC   TIMEHERE(13),TIMEEDIT MOVE EDIT WORD
         ED    TIMEHERE(13),TIMEPACK EDIT TIME
         MVC   WTOTIME,TIMEHERE   SET TIME
         WTO   MF=(E,WTOMSG)      TRACE IT
         DROP  R9
*
CICSTRDN DS    0H
* COUNT ENQ'S
         SR    R8,R8              CLEAR R8
         L     R6,8(R3)           ADDRESS QEAANCHR ADDRESS
         USING DFHQEADS,R6
         ICM   R6,15,QEACHNF      R6=QEA CHAIN
         BZ    CEMTEQGO           EXIT IF NONE
CEMTIQEA EQU   *
         AHI   R8,1               COUNT THEM
         ICM   R6,15,QEACHNF      R6=QEA CHAIN
         BNZ   CEMTIQEA           LOOP IF NOT END
CEMTEQGO EQU   *
* GETMAIN AN AREA TO SEND BACK
         MS    R8,=AL4(IENQDLEN)  * LENGTH OF SENT DATA
         AHI   R8,12+1            + PREFIX + STOPPER
         GETMAIN R,LV=(R8),A=CEMTIEGM
         ST    R0,0(R1)           SAVE LENGTH TO SEND
         MVC   4(4,R1),=C'FE06'   SET REQUEST ID
         MVC   8(4,R1),REQTERM    SET TERMID
         LA    R7,12(R1)          ADDRESS SEND AREA
         USING IENQDATA,R7
         L     R6,8(R3)           ADDRESS QEAANCHR ADDRESS
         ICM   R6,15,QEACHNF      R6=QEA CHAIN
         BZ    CEMTIESD           EXIT IF NONE
CEMTIELP EQU   *
         MVC   IENQDRES,QEARES    MOVE RESOURCE
         MVC   IENQDLTH,QEALNTH   MOVE LENGTH
         MVC   IENQDUCT,QEAUCT    MOVE USE COUNT
         MVC   IENQDOWN,QEATERM   MOVE TERMID SUFFIX OF OWNER
         MVC   IENQDWT(THRDLEN),QEAWAIT MOVE WAIT LIST
         AHI   R7,IENQDLEN        POINT TO NEXT SEND ENTRY
         ICM   R6,15,QEACHNF      R6=QEA CHAIN
         BNZ   CEMTIELP           LOOP IF NOT END
CEMTIESD EQU   *
         MVI   IENQDRES,X'FF'     MARK END OF ENQ'S
         L     R8,12(R3)          R8=CONN ID ADDRESS
         L     R8,0(R8)           R8=CONNID
         L     R14,CEMTIEGM       R14=DATA TO SEND
         TCPIO SEND,MSG=(R14),LMSG=0(R14),CONN=(R8),PORT=3900
         LTR   R15,R15            CHECK RC ?
         BNZ   ABEND780           EXIT IF BAD
         FREEMAIN LA=0(R14),A=(R14) FREE FCT SEND AREA
         SUBEXIT
*
ABEND780 ABEND 780,DUMP           TCPIO SEND FAILED
*
CEMTIEGM DS    A
CICSTIND DS    C                  TRACE TYPE INDICATOR
TRCEZ390 DS    C                  Z390 TRACE OPTION
*
* TIME CONVERSION FIELDS
*
TIME     DS    CL4                HHMMSSTT
         DS    CL8                SPACE FOR DATE
TIMEPACK DC    PL5'0'             0HHMMSSTTC
TIMEEDIT DC    X'402021207A20207A20204B2020'
TIMEHERE DS    CL13
*
IENQDATA DSECT
IENQDRES DS    CL30               RESOURCE
IENQDLTH DS    XL2                LENGTH
IENQDUCT DS    XL4                USE COUNT
IENQDOWN DS    C                  OWNER
IENQDWT  DS    CL(THRDNUM)        WAIT LIST
IENQDLEN EQU   *-IENQDATA
*
* RECEIVE MESSAGE AREA
*
REQDSECT DSECT
         DS    AL4                REQUEST LENGTH
         DS    CL4                REQUEST CODE
REQTERM  DS    CL4                TERMINAL ID
REQTRAN  DS    CL4                TRANSID
         DS    C                  TRANSID TYPE
REQTASKN DS    PL4                TASK NO
*
* WTO AREA DSECT
*
WTODSECT DSECT
WTOMSG   DS    AL4
WTOTIME  DS    CL13               TIME STAMP
         DS    C
         DS    CL4                MODULE/FUNCTION REFERENCE (EIBFN)
         DS    C
WTOTERM  DS    CL4                TERMID
         DS    C
WTOTRAN  DS    CL4                TRANSID (OR BLANK)
         DS    C
WTOTYPE  DS    C                  TRANSID TYPE
         DS    C
WTOTASKN DS    CL7                TASK NUMBER
         DS    C
WTODESC  DS    CL45               DESCRIPTION
*
         DFHQEADS                 ENQUEUE ELEMENT BLOCK
*
         EQUREGS
*
         END
