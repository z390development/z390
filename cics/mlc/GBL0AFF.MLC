***********************************************************************
* Copyright 2006 Automated Software Tools Corporation                 *
* This source code is part of z390 assembler/emulator package         *
* The z390 package is distributed under GNU general public license    *
* Author - Don Higgins                                                *
***********************************************************************
GBL0AFF  SUBENTRY
*
* FN=0AFF  CEBR REQUEST QNAMES
*
*         +0/DSCT +4/DSECT +8     +12     +16/IND  +20/IND +24
* R1  >>> WTOMSG, REQDSECT,MSGLEN,CEBRQUS,TSNANCHR,CONNID, TRCEZ390
* R14 = RETURN ADDRESS
         LR    R3,R1              SAVE PARM POINTER
         L     R4,24(R3)          ADDRESS OF TRCEZ390
         MVC   TRCEZ390,0(R4)     MOVE IT
         L     R5,4(R3)           REQDSECT ADDRESS
         USING REQDSECT,R5
         L     R9,0(R3)           WTODSECT ADDRESS
         USING WTODSECT,R9
         MVC   WTOTERM,REQTERM    SET TERMID
         MVC   WTOTRAN,REQTRAN    SET TRANSID
         MVI   WTOTYPE,C' '       NO TRANSID TYPE
         UNPK  WTOTASKN,REQTASKN  UNPACK TASK NUMBER
         OI    WTOTASKN+6,X'F0'   CLEAR SIGN
         MVC   WTODESC,=CL45'<<<Z390KCP CEBR REQUEST QNAMES'
         MVI   CICSTIND,C'A'      SET APPLICATION TYPE
* MODIFIED CICSTRCE
         CLI   TRCEZ390,C'N'      NO TRACING ?
         BE    CICSTRDN           EXIT IF IT IS
         CLI   TRCEZ390,C'Y'      ALL TRACING ?
         BE    CICSTRGO           TRACE IF IT IS
         CLC   TRCEZ390,CICSTIND  TRACE TYPE=TRACE REQUEST ?
         BNE   CICSTRDN           EXIT IF NOT
CICSTRGO EQU   *
         TIME  DEC,TIME,LINKAGE=SYSTEM GET TIME
         MVO   TIMEPACK,TIME      MOVE TO PACKED FIELD
         MVC   TIMEHERE(13),TIMEEDIT MOVE EDIT WORD
         ED    TIMEHERE(13),TIMEPACK EDIT TIME
         MVC   WTOTIME,TIMEHERE   SET TIME
         WTO   MF=(E,WTOMSG)      TRACE IT
         DROP  R9
*
CICSTRDN DS    0H
         USING TSNAMES,R7
         L     R4,16(R3)          ADDRESS OF TSNANCHR ADDRESS
         ICM   R7,15,0(R4)        R7=TS NAME TABLE
         BNZ   CEBRGETS           EXIT IF WE HAVE SOME
         L     R4,12(R3)          ADDRESS CEBRQUS
         XC    0(2,R4),0(R4)      CLEAR NO. OF Q NAMES
         B     CEBRSEND           EXIT
*
CEBRGETS EQU   *
* TS NAME CHAIN EXISTS, COLLECT THE Q NAMES
* SEND THE NO. OF NAMES, AND THEN THE WHOLE TABLE
* TABLE HAS NO DSECT
*    DS CL16  Q NAME
*    DS AL2   NUMITEMS
         LA    R1,60*18           MAX OF 60 NAMES AT 18 BYTES EACH
         GETMAIN R,LV=(R1)
         LR    R6,R1              NAME TABLE ADDRESS
         LR    R9,R1              SAVE ADDRESS FOR FREEMAIN/TCPIO
         SR    R8,R8              CLEAR COUNT
CEBRLOOP EQU   *
         MVC   0(16,R6),TSNQNAME  MOVE Q NAME
         MVC   16(2,R6),TSNNUMIT  MOVE NUMITEMS
         AHI   R6,18              BUMP NAME TABLE
         AHI   R8,1               BUMP COUNT
         CHI   R8,60              COLLECTION LIMIT ?
         BE    CEBRDONE           EXIT IF REACHED
         ICM   R7,15,TSNCHAIN     NEXT TS NAME ENTRY
         BNZ   CEBRLOOP           LOOP TILL DONE
CEBRDONE EQU   *
*        STH   R8,CEBRQUS         SAVE NO. OF Q NAMES
         L     R4,12(R3)          ADDRESS CEBRQUS
         STH   R8,0(R4)           SAVE NO. OF Q NAMES
CEBRSEND EQU   *
         L     R8,20(R3)          R8=CONN ID ADDRESS
         L     R8,0(R8)           R8=CONNID
         L     R7,8(R3)           ADDRESS MSGLEN
* SEND NO. OF Q NAMES
         TCPIO SEND,MSG=(R7),LMSG=14,CONN=(R8),PORT=3900
         LTR   R15,R15            CHECK RC ?
         BNZ   ABEND780           EXIT IF BAD
         L     R4,12(R3)          ADDRESS CEBRQUS
         ICM   R15,3,0(R4)        R15=NO. OF Q NAMES
         BZ    CEBRQDUN           EXIT IF NONE
         MS    R15,=F'18'         * 18
* SEND THE Q NAME TABLE
         TCPIO SEND,MSG=(R9),LMSG=(R15),CONN=(R8),PORT=3900
         LTR   R15,R15            CHECK RC ?
         BNZ   ABEND780           EXIT IF BAD
         LA    R0,60*18           R0=LENGTH
         FREEMAIN LV=(R0),A=(R9)  FREE Q NAME TABLE
CEBRQDUN EQU   *
         SUBEXIT
*
ABEND780 ABEND 780,DUMP           TCPIO SEND FAILED
*
CICSTIND DS    C                  TRACE TYPE INDICATOR
TRCEZ390 DS    C                  Z390 TRACE OPTION
*
* TIME CONVERSION FIELDS
*
TIME     DS    CL4                HHMMSSTT
         DS    CL8                SPACE FOR DATE
TIMEPACK DC    PL5'0'             0HHMMSSTTC
TIMEEDIT DC    X'402021207A20207A20204B2020'
TIMEHERE DS    CL13
*
* RECEIVE MESSAGE AREA
*
REQDSECT DSECT
         DS    AL4                REQUEST LENGTH
         DS    CL4                REQUEST CODE
REQTERM  DS    CL4                TERMINAL ID
REQTRAN  DS    CL4                TRANSID
         DS    C                  TRANSID TYPE
REQTASKN DS    PL4                TASK NO
*
* WTO AREA DSECT
*
WTODSECT DSECT
WTOMSG   DS    AL4
WTOTIME  DS    CL13               TIME STAMP
         DS    C
         DS    CL4                MODULE/FUNCTION REFERENCE (EIBFN)
         DS    C
WTOTERM  DS    CL4                TERMID
         DS    C
WTOTRAN  DS    CL4                TRANSID (OR BLANK)
         DS    C
WTOTYPE  DS    C                  TRANSID TYPE
         DS    C
WTOTASKN DS    CL7                TASK NUMBER
         DS    C
WTODESC  DS    CL45               DESCRIPTION
*
         DFHTSNDS                 TEMPORARY STORAGE NAME TABLE
*
         EQUREGS
*
         END
