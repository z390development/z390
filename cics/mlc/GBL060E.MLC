***********************************************************************
* Copyright 2006 Automated Software Tools Corporation                 *
* This source code is part of z390 assembler/emulator package         *
* The z390 package is distributed under GNU general public license    *
* Author - Don Higgins                                                *
***********************************************************************
GBL060E  SUBENTRY
*
* FN=060E  READNEXT FILE
*
*         +0/DSCT +4/DSECT +8/IND   +12/IND +16
* R1  >>> WTOMSG, REQDSECT,DFHFCTAD,CONNID, TRCEZ390
* R14 = RETURN ADDRESS
         LR    R3,R1              SAVE PARM POINTER
         L     R4,16(R3)          ADDRESS OF TRCEZ390
         MVC   TRCEZ390,0(R4)     MOVE IT
         L     R9,0(R3)           WTODSECT ADDRESS
         USING WTODSECT,R9
         L     R6,4(R3)           ADDRESS RECEIVED DATA
         USING DFHFCBLK,R6
         L     R4,8(R3)           ADDRESS FCT ADDRESS
         L     R7,0(R4)           ADDRESS FCT
         USING DFHFCTDS,R7
         USING IHAACB,R8
         MVC   WTOTERM,FCPTERM    SET TERMID
         MVC   WTOTRAN,FCPTRAN    SET TRANSID
         MVI   WTOTYPE,C' '       NO TRANSID TYPE
         UNPK  WTOTASKN,FCPTASKN  UNPACK TASK NUMBER
         OI    WTOTASKN+6,X'F0'   CLEAR SIGN
         MVC   WTODESC,=CL45'<<<Z390KCP READNEXT   REQUEST'
         MVI   CICSTIND,C'A'      SET APPLICATION TYPE
         BAL   R11,CICSTRCE       TRACE IT
         BAL   R11,FOP1REF        OPEN ON 1ST REFERENCE
         L     R10,ACBVCLRA       R10=VCLR (CLUSTER ENTRY)
         USING IHAVCLR,R10
* R6= REQUEST DATA  (DFHFCBLK)
* R7= FCTTE ADDRESS (DFHFCTDS)
* R8= ACB
* R4= VSWA AND RPL
* R10=VCLR (CLUSTER ENTRY)
         TM    FCTSERV,FCTBRWSE   IS BROWSE ALLOWED ?
         BNO   RDNXIV20           EXIT IF NOT
         ICM   R4,15,FCTVSWA      R4=VSWA ANCHOR
         BZ    RDNXIV34           EXIT IF NO VSWAS
         USING DFHVSWAD,R4
* SCAN VSWAS AND FIND OUR STARTBR ONE
RDNXFLP  EQU   *
         LTR   R4,R4              END OF CHAIN ?
         BZ    RDNXIV34           ERROR IF IT IS
         CLC   VSWTASKN,FCPTASKN  IS IT OURS ?
         BNE   RDNXVCHN           CHAIN IF NOT
         CLI   VSWCTYPE,VSWCBRWS  IS IT BROWSE TYPE ?
         BNE   RDNXVCHN           CHAIN IF NOT
         CLC   VSWREQID,FCPREQID  IS IT OUR REQID ?
         BE    RDNXVSGT           EXIT IF IT IS
RDNXVCHN EQU   *
         L     R4,VSWCHAIN        NEXT ENTRY
         B     RDNXFLP            LOOP
*
RDNXVSGT EQU   *
* FOUND OUR STARTBR VSWA
         USING IHARPL,VSWRPL
         CLI   FCPRDOPT,FCPRDRBA  RBA REQUEST ?
         BNE   RDNXXRBA           EXIT IF NOT
* ESDS/RBA
         MODCB RPL=IHARPL,OPTCD=(ADR,SEQ,FWD),ARG=(S,FCPRBA)
         CLC   FCPRBA,VSWARBA     HAS RBA CHANGED ?
         BE    RDNXNOPT           EXIT IF NOT
         B     RDNXGETR           EXIT
*
RDNXXRBA EQU   *
         CLI   FCPRDOPT,FCPRDXRB  XRBA REQUEST ?
         BNE   RDNXRRN            EXIT IF NOT
* ESDS/XRBA
         MODCB RPL=IHARPL,OPTCD=(ADR,SEQ,FWD,XRBA),ARG=(S,FCPXRBA)
         CLC   FCPXRBA,VSWAXRBA   HAS XRBA CHANGED ?
         BE    RDNXNOPT           EXIT IF NOT
         B     RDNXGETR           EXIT
*
RDNXRRN  EQU   *
         CLI   FCPRDOPT,FCPRDRRN  RRN REQUEST ?
         BNE   RDNXKSD            EXIT IF NOT
* RRDS
         MODCB RPL=IHARPL,OPTCD=(KEY,SEQ,FWD),ARG=(S,FCPRRN)
         CLC   FCPRRN,VSWARRN     HAS RRN CHANGED ?
         BE    RDNXNOPT           EXIT IF NOT
         B     RDNXGETR           EXIT
*
RDNXKSD  EQU   *
* KSDS
         MODCB RPL=IHARPL,OPTCD=(KEY,SEQ,FKS,KEQ,FWD),ARG=(S,FCPRID)
         TM    VSWOPT,VSWGEN      GENERIC ?
         BNO   RDNXGTE            EXIT IF NOT
* KSDS GENERIC
         OI    RPLOPT1,RPLOPT1_GEN SET IN RPL
RDNXGTE  EQU   *
         TM    VSWOPT,VSWGTEQ     GTEQ ?
         BNO   RDNXKEYL           EXIT IF NOT
* KSDS GTEQ
         OI    RPLOPT1,RPLOPT1_KGE SET IN RPL
RDNXKEYL EQU   *
         CLC   FCPKEYLN,VSWKEYLN  HAS KEYLENGTH CHANGED ?
         BE    RDNXKEY            EXIT IF NOT
* KEYLENGTH CHANGED
         L     R12,ACBVAIXA       R12=PATH ADDRESS OR 0
         USING IHAVAIX,R12
         TM    VSWOPT,VSWGEN      GENERIC ?
         BO    RDNXGENK           EXIT IF IT IS
* KEYLENGTH CHANGED AND FULL KEY
*    IF OMITTED, IT IS IGNORED
*    IF KL NE CAT KL RAISE INVREQ/26
         CLI   FCPKEYLN,X'7F'     ANY REQUESTED KEYLENGTH ?
         BE    RDNXKEY            EXIT IF NONE
         LTR   R12,R12            ANY PATH ?
         BZ    RDNXBAS1           EXIT IF NOT
* THIS IS A PATH, USE THE ALTERNATE KEY LENGTH
         CLC   FCPKEYLN,VAIXKLEN+2 IS THE NEW KL=CATALOG AIX KL ?
         BNE   RDNXIV26           ERROR IF NOT
         B     RDNXKEY            EXIT
*
RDNXBAS1 EQU   *
         CLC   FCPKEYLN,VCLRKLEN+2 IS THE NEW KL=CATALOG BASE KL ?
         BNE   RDNXIV26           ERROR IF NOT
         B     RDNXKEY            EXIT
*
RDNXGENK EQU   *
* KEYLENGTH CHANGED AND GENERIC
*    IF OMITTED, IT IS IGNORED
*    IF = CAT KL, IT IS IGNORED
*    IF > CAT KL, RAISE INVREQ/25
*    IF -VE, RAISE INVREQ/42
*    IF ZERO, SET KEYLENGTH=1, FORCE GTEQ
         CLI   FCPKEYLN,X'7F'     ANY REQUESTED KEYLENGTH ?
         BE    RDNXKEY            EXIT IF NONE
         LTR   R12,R12            ANY PATH ?
         BZ    RDNXBAS2           EXIT IF NOT
* THIS IS A PATH, USE THE ALTERNATE KEY LENGTH
         CLC   FCPKEYLN,VAIXKLEN+2 IS THE NEW KL=CATALOG AIX KL ?
         BE    RDNXKEY            EXIT IF IT IS
         BH    RDNXIV25           ERROR IF GREATER
         B     RDNXNEGK           EXIT
         DROP  R12
*
RDNXBAS2 EQU   *
         CLC   FCPKEYLN,VCLRKLEN+2 IS THE NEW KL=CATALOG BASE KL ?
         BE    RDNXKEY            EXIT IF IT IS
         BH    RDNXIV25           ERROR IF GREATER
RDNXNEGK EQU   *
         TM    FCPKEYLN,X'80'     IS IT NEGATIVE ?
         BO    RDNXIV42           ERROR IF IT IS
         OC    FCPKEYLN,FCPKEYLN  KEYLENGTH=0 ?
         BZ    RDNXKEY0           EXIT IF NOT
* KEYLENGTH NOT 0
         MVC   RPLLKEY+2(2),FCPKEYLN SET TO NEW RPL KEYLENGTH
         MVC   VSWKEYLN,FCPKEYLN  SET TO NEW VSWA KEYLENGTH
         B     RDNXGETR           EXIT
*
RDNXKEY0 EQU   *
* KEYLENGTH CHANGED TO ZERO AND GENERIC
* ARG IS ALWAYS X'00' TO GET 1ST RECORD
* KEYLENGTH(1) AND GTEQ FORCED
         XC    FCPRID,FCPRID      SET KEY TO X'00'
         OI    RPLOPT1,RPLOPT1_KGE FORCE GTEQ
         MVC   RPLLKEY,=F'1'      SET KEYLENGTH=1
         MVC   VSWKEYLN,=H'1'     SET VSWA KEYLENGTH=1
         B     RDNXGETR           EXIT
*
RDNXKEY  EQU   *
* SO FAR, NO DETECTED REASON FOR POINT
         L     R2,RPLLKEY         R2=CURRENT KEYLENGTH
         BCTR  R2,0               -1 FOR OFFSET
         STC   R2,RDNXCLC+1       STORE LENGTH
RDNXCLC  EQU   *
         CLC   FCPRID(0),VSWARID  RIDFLD CHANGED ?
         BE    RDNXNOPT           EXIT IF NOT
*
RDNXGETR EQU   *
         XC    RDNXGETL,RDNXGETL  NO FREEMAIN IF POINT ERROR
         LR    R5,R6              DEFAULT TO CUR FCBLK IF POINT ERROR
         POINT RPL=IHARPL         REPOSITION
         LTR   R15,R15            CHECK RC ?
         BNZ   RDNXFAIL           EXIT IF BAD
RDNXNOPT EQU   *
         L     R5,VCLRLREC        R5=LENGTH
         TM    VCLRFLG1,VCLRVREC  IS IT VARIABLE ?
         BNO   RDNXFIX            EXIT IF NOT
         AHI   R5,4               +4 FOR VARIABLE LENGTH
RDNXFIX  EQU   *
         LA    R1,FCPREFIX(R5)    FC PREFIX LENGTH + MAX RECORD SIZE
         GETMAIN R,LV=(R1)
         ST    R0,RDNXGETL        SAVE LENGTH FOR FREEMAIN
         LR    R5,R1              ADDRESS NEW FC BLOCK
         MVC   0(FCPREFIX,R5),DFHFCBLK MOVE PREFIX INTO NEW BLOCK
         XC    FCPRETCD-DFHFCBLK(4,R5),FCPRETCD-DFHFCBLK(R5) CLEAR RC
         XC    FCPRESP2-DFHFCBLK(4,R5),FCPRESP2-DFHFCBLK(R5) CLEAR RSP2
         MODCB RPL=IHARPL,AREA=(S,FCPREFIX(R5)) READ AREA
         GET   RPL=IHARPL         READ RECORD
         LTR   R15,R15            CHECK RC ?
         BNZ   RDNXFAIL           EXIT IF ERROR
         TM    VCLRFLG1,VCLRVREC  IS IT VARIABLE ?
         BNO   RDNXFIX2           EXIT IF NOT
         MVC   FCPACLEN-DFHFCBLK(4,R5),RPLLREC SAVE ACTUAL LRECL
         B     RDNXLEN            EXIT
*
RDNXFIX2 EQU   *
         MVC   FCPACLEN-DFHFCBLK(4,R5),VCLRLREC SAVE FIXED LRECL
         DROP  R10
RDNXLEN  EQU   *
         L     R1,FCPLEN          R1=LENGTH REQUESTED
         CLC   FCPACLEN-DFHFCBLK(4,R5),FCPLEN IS ACT DATA LENGTH LESS ?
         BH    RDNXLNOK           EXIT IF NOT
         L     R1,FCPACLEN-DFHFCBLK(R5) R1=LENGTH OF DATA
RDNXLNOK EQU   *
         LA    R15,FCPREFIX(R1)   R15=ACTUAL LENGTH SENT
         ST    R15,FCPTCPIO-DFHFCBLK(R5) SAVE IT
* READNEXT COMPLETED, PASS DATA TO CLIENT, FREE NEW FCBLK
*   ESDS - SAVE   XRBA IN VSWA FOR CHECKING SKIP SEQUENTIAL
*   RRDS - SAVE FCPRRN IN VSWA FOR CHECKING SKIP SEQUENTIAL
*   KSDS - SAVE FCPRID IN VSWA FOR CHECKING SKIP SEQUENTIAL
*   PASS REQUEST BLOCK BACK WITH MODIFIED RIDFLD
         CLI   FCPRDOPT,FCPRDKEY  FULL KEY REQUEST ?
         BE    RDNXSAVK           EXIT IF IT IS
         CLI   FCPRDOPT,FCPRDGEN  GENERIC KEY REQUEST ?
         BE    RDNXSAVK           EXIT IF IT IS
         CLI   FCPRDOPT,FCPRDRRN  RRN REQUEST ?
         BE    RDNXSAVR           EXIT IF IT IS
* ESDS
         MVC   VSWAXRBA,RPLLXRBA  SAVE XRBA FOR SKIP SEQ
         MVC   FCPXRBA-DFHFCBLK(8,R5),RPLLXRBA RETURN XRBA
         B     RDNXDONE           EXIT
*
RDNXSAVR EQU   *
* RRDS
         L     R8,RPLARG          ADDRESS RETURNED RRN
         MVC   VSWARRN,0(R8)      SAVE RRN FOR SKIP SEQ
         MVC   FCPRRN-DFHFCBLK(4,R5),0(R8) RETURN RRN
         B     RDNXDONE           EXIT
*
RDNXSAVK EQU   *
* KSDS
         L     R8,RPLAREA         ADDRESS RETURNED RID
         MVC   VSWARID,0(R8)      SAVE RID FOR SKIP SEQ
         MVC   FCPRID-DFHFCBLK(128,R5),0(R8) RETURN RID
*
RDNXDONE EQU   *
         MVC   WTODESC,=CL45'<<<Z390KCP READNEXT   COMPLETED'
RDNXSEND EQU   *
*        L     R8,CONNID          R8=CONN ID
         L     R8,12(R3)          R8=CONN ID ADDRESS
         L     R8,0(R8)           R8=CONNID
         TCPIO SEND,MSG=(R5),LMSG=0(R5),CONN=(R8),PORT=3900
         LTR   R15,R15            CHECK RC ?
         BNZ   ABEND780           EXIT IF BAD
         OC    RDNXGETL,RDNXGETL  ANY NEW FCBLK ?
         BZ    RDNXTRCE           EXIT IF NOT
         MVC   0(4,R5),RDNXGETL   SET REAL STORAGE LENGTH
         FREEMAIN LA=RDNXGETL,A=(R5)
         B     RDNXTRCE           EXIT
*
RDNXFAIL EQU   *
* READNEXT FAILURE PROCESSING
* FILE ERROR HANDLER
         LA    R15,FILEERTB       R15=ERROR TABLE ADDRESS
FILEERLP EQU   *
         CLC   0(4,15),RPLFEEDB   MATCH TO TABLE
         BNE   FILEERIN           EXIT IF NOT
         MVC   FCPRETCD-DFHFCBLK(4,R5),4(R15) MOVE RETURN CODE
         MVC   FCPRESP2-DFHFCBLK(4,R5),8(R15) MOVE RESP2
         MVC   FCPRESP2-DFHFCBLK(1,R5),1(R15) MOVE RPLFEEDB+1
         MVC   FCPRESP2+1-DFHFCBLK(1,R5),3(R15) MOVE RPLFEEDB+3
         B     FILEERDN           EXIT
*
FILEERIN EQU   *
         AHI   R15,20             BUMP TABLE POINTER
         CLI   0(R15),X'FF'       STOPPER ?
         BNE   FILEERLP           LOOP IF NOT
         J     ABEND790           MISSING TABLE ENTRY SHOULD NOT OCCUR
*
FILEERDN EQU   *
         MVC   WTODESC,=CL45'>>>Z390KCP READNEXT   FAILED'
         MVC   WTODESC+29(8),12(R15) MOVE CONDITION
         B     RDNXSEND           EXIT
*
RDNXIV20 EQU   *
         MVC   FCPRESP2,=F'20'    SET RESP2 (FUNCTION NOT PERMITTED)
         B     RDNXINVQ           EXIT
*
RDNXIV25 EQU   *
         MVC   FCPRESP2,=F'25'    SET RESP2 (GENERIC AND KL > CAT KL)
         B     RDNXINVQ           EXIT
*
RDNXIV26 EQU   *
         MVC   FCPRESP2,=F'26'    SET RESP2 (FULL KEY AND KL NE CAT KL)
         B     RDNXINVQ           EXIT
*
RDNXIV34 EQU   *
         MVC   FCPRESP2,=F'34'    SET RESP2 (REQID NOT FOUND)
         B     RDNXINVQ           EXIT
*
RDNXIV42 EQU   *
         MVC   FCPRESP2,=F'42'    SET RESP2 (GENERIC AND NEGATIVE KL)
RDNXINVQ EQU   *
         MVC   FCPRETCD,DFHRESP(INVREQ) SET INVREQ
         MVC   WTODESC,=CL45'>>>Z390KCP READNEXT   FAILED'
         MVC   WTODESC+29(6),=C'INVREQ'
         LR    R9,R6              SET AS IF NEW DFHFCBLK
         L     R8,12(R3)          R8=CONN ID ADDRESS
         L     R8,0(R8)           R8=CONNID
         LA    R15,FCPREFIX       R15=LENGTH TO SEND
         TCPIO SEND,MSG=(R9),LMSG=(R15),CONN=(R8),PORT=3900
         LTR   R15,R15            CHECK RC ?
         BNZ   ABEND780           EXIT IF BAD
RDNXTRCE EQU   *
         L     R9,0(R3)           WTODSECT ADDRESS
         MVI   CICSTIND,C'A'      SET APPLICATION TYPE
         BAL   R11,CICSTRCE       TRACE IT
         SUBEXIT
*
RDNXGETL DS    F                  SAVE GETMAIN LENGTH
*
* SUBROUTINES
*
* MODIFIED CICSTRCE
CICSTRCE DS    0H
         CLI   TRCEZ390,C'N'      NO TRACING ?
         BER   R11                RETURN IF IT IS
         CLI   TRCEZ390,C'Y'      ALL TRACING ?
         BE    CICSTRGO           TRACE IF IT IS
         CLC   TRCEZ390,CICSTIND  TRACE TYPE=TRACE REQUEST ?
         BNER  R11                RETURN IF NOT
CICSTRGO EQU   *
         TIME  DEC,TIME,LINKAGE=SYSTEM GET TIME
         MVO   TIMEPACK,TIME      MOVE TO PACKED FIELD
         MVC   TIMEHERE(13),TIMEEDIT MOVE EDIT WORD
         ED    TIMEHERE(13),TIMEPACK EDIT TIME
         MVC   WTOTIME,TIMEHERE   SET TIME
         WTO   MF=(E,WTOMSG)      TRACE IT
         BR    R11                RETURN
*
FOP1REF DS     0H
* OPEN ON 1ST REFERENCE
FOP1LOOP EQU   *
* CHECK THE FCT STATUS
         CLC   FCPFILNM,FCTFILNM  MATCH REQUESTED FILENAME TO FCT ?
         BNE   FOP1INCR           EXIT IF NOT MATCHED
* FILE FOUND
         LA    R8,FCTTELEN(R7)    ADDRESS ACB
         TM    FCTFILST,FCTOPEN   IS FILE OPEN ?
         BOR   R11                RETURN IF IT IS
* FILE NOT OPEN
* TEST FILSTAT OPTIONS
*    OPENED,ENABLED   -- FILE OPENED AT START, CANNOT OCCUR HERE
*                     -- BUT TREATED AS OPEN ON 1ST REF
*    OPENED,DISABLED  -- FILE OPENED AT START, CANNOT OCCUR HERE
*                     -- BUT TREATED AS OPEN ON 1ST REF
*                     -- DISABLED STATUS RETURNED
*    OPENED,UNENABLED -- CANNOT OCCUR
*
*    CLOSED,ENABLED   -- FILE OPENED NOW (1ST REF)
*    CLOSED,DISABLED  -- DISABLED STATUS RETURNED
*    CLOSED,UNENABLED -- NOTOPEN STATUS RETURNED
         TM    FCTFILST,FCTOPCLO  FILSTAT=OPENED ?
         BO    FOP1OPEN           EXIT IF IT IS
* FILSTAT=CLOSED
         TM    FCTFILST,FCTENDIS  FILSTAT=ENABLED ?
         BO    FOP1OPEN           EXIT IF IT IS
* FILSTAT=CLOSED AND NOT ENABLED
         TM    FCTFILST,FCTUNENA  FILSTAT=UNENABLED ?
         BO    FOP1NOTO           EXIT IF NOTOPEN
         B     FOP1DISA           EXIT IF DISABLED
*
FOP1INCR EQU   *
         AHI   R7,FCTABLEN        POINT TO NEXT FCT ENTRY
         CLI   0(R7),X'FF'        END OF FCT ?
         BNE   FOP1LOOP           LOOP IF NOT
* FILE NOT FOUND
         B     FOP1FNTF           EXIT IF FILENOTFOUND
*
FOP1OPEN EQU   *
* OPEN ON 1ST REF
* INITIALIZE MESSAGE
         MVC   WTOFN,=C'06FE'     1ST REF FILE OPENS
         MVC   WTOTERM,=4C' '     NO TERMID
         MVC   WTOTRAN,=4C' '     NO PRESET TRANSID
         MVI   WTOTYPE,C' '       NO TRANSID TYPE
         MVC   WTOTASKN,=7C' '    NO TASK NUMBER
         MVC   WTODESC,=CL45'FILE ........ OPEN  FAILED'
         MVC   WTODESC+5(8),FCTFILNM MOVE FILENAME TO MSG
         TM    FCTSERV,FCTDELT+FCTUPDT+FCTADD REASON TO OPEN OUTPUT ?
         BZ    FOP1IN             EXIT IF NONE
* SET ALL BITS WE ARE LIKELY TO USE
*    ADR = ACCESS BY RBA
*    DIR = DIRECT ACCESS
*    KEY = ACCESS BY KEY
*    SEQ = SEQUENTIAL ACCESS
*    IN  = READ ONLY
*    OUT = READ OR WRITE
* OPEN FOR OUTPUT
         MODCB ACB=IHAACB,MACRF=(ADR,DIR,KEY,SEQ,OUT)
         OPEN  (IHAACB,OUTPUT)    OPEN FOR OUTPUT
         LTR   R15,R15            OK ?
         BNZ   FOP1OPER           EXIT IF NOT
         B     FOP1OPOK           EXIT
*
FOP1IN   EQU   *
* OPEN FOR INPUT
         MODCB ACB=IHAACB,MACRF=(ADR,DIR,KEY,SEQ,IN)
         OPEN  (IHAACB,INPUT)     OPEN FOR INPUT
         LTR   R15,R15            OK ?
         BNZ   FOP1OPER           EXIT IF NOT
FOP1OPOK EQU   *
* OPEN SUCCESSFUL, SAY SO
         MVC   WTODESC+20(6),=C'OK    ' SET OK
         MVI   CICSTIND,C'I'      SET INTERNAL TYPE
         ST    R11,FOP1SV11       SAVE RETURN ADDRESS
         BAL   R11,CICSTRCE       TRACE IT
         L     R11,FOP1SV11       RESTORE RETURN ADDRESS
         OI    FCTFILST,FCTOPEN   SET FILE OPEN
         OI    FCTFILST,FCTOPCLO  SET FILSTAT=OPENED
         B     FOP1DIST           EXIT
*
FOP1OPER EQU   *
* OPEN UNSUCCESSFUL, SAY SO
         MVC   WTODESC+20(6),=C'FAILED' SET FAILED
         MVI   CICSTIND,C'I'      SET INTERNAL TYPE
         ST    R11,FOP1SV11       SAVE RETURN ADDRESS
         BAL   R11,CICSTRCE       TRACE IT
         L     R11,FOP1SV11       RESTORE RETURN ADDRESS
         MVI   FCTFILST,X'00'     SET FILE CLOSED,DISABLED
         B     FOP1DISA           EXIT IF DISABLED
*
FOP1DIST EQU   *
         TM    FCTFILST,FCTENDIS  IS FILSTAT=DISABLED ?
         BZ    FOP1DISA           EXIT IF DISABLED
         BR    R11                RETURN
*
FOP1FNTF EQU   *
         MVC   FCPRETCD,DFHRESP(FILENOTFOUND) SET FILENOTFOUND
         MVC   FCPRESP2,=F'1'     SET RESP2
         MVC   WTODESC,=CL45'>>>Z390KCP READNEXT   FAILED'
         MVC   WTODESC+29(12),=C'FILENOTFOUND'
         B     FOP1SEND           EXIT
*
FOP1DISA EQU   *
         MVC   FCPRETCD,DFHRESP(DISABLED) SET DISABLED
         MVC   FCPRESP2,=F'50'    SET RESP2
         MVC   WTODESC,=CL45'>>>Z390KCP READNEXT   FAILED'
         MVC   WTODESC+29(12),=C'DISABLED'
         B     FOP1SEND           EXIT
*
FOP1NOTO EQU   *
         MVC   FCPRETCD,DFHRESP(NOTOPEN) SET NOTOPEN
         MVC   FCPRESP2,=F'60'    SET RESP2
         MVC   WTODESC,=CL45'>>>Z390KCP READNEXT   FAILED'
         MVC   WTODESC+29(7),=C'NOTOPEN'
FOP1SEND EQU   *
         LR    R9,R6              SET AS IF NEW DFHFCBLK
*        L     R8,CONNID          R8=CONN ID
         L     R8,12(R3)          R8=CONN ID ADDRESS
         L     R8,0(R8)           R8=CONNID
         LA    R15,FCPREFIX       R15=LENGTH TO SEND
         TCPIO SEND,MSG=(R9),LMSG=(R15),CONN=(R8),PORT=3900
         LTR   R15,R15            CHECK RC ?
         BNZ   ABEND780           EXIT IF BAD
         L     R9,0(R3)           WTODSECT ADDRESS
         MVI   CICSTIND,C'A'      SET APPLICATION TYPE
         BAL   R11,CICSTRCE       TRACE IT
         SUBEXIT
FOP1SV11 DS    A                  SAVE R11
*
ABEND780 ABEND 780,DUMP           TCPIO SEND FAILED
ABEND790 ABEND 790,DUMP           VSAM FEEDBACK NOT EXPECTED
*
CICSTIND DS    C                  TRACE TYPE INDICATOR
TRCEZ390 DS    C                  Z390 TRACE OPTION
*
         COPY  FILEERTB           RPL FEEDBACK ERROR TABLE
*
* TIME CONVERSION FIELDS
*
TIME     DS    CL4                HHMMSSTT
         DS    CL8                SPACE FOR DATE
TIMEPACK DC    PL5'0'             0HHMMSSTTC
TIMEEDIT DC    X'402021207A20207A20204B2020'
TIMEHERE DS    CL13
*
* WTO AREA DSECT
*
WTODSECT DSECT
WTOMSG   DS    AL4
WTOTIME  DS    CL13               TIME STAMP
         DS    C
WTOFN    DS    CL4                MODULE/FUNCTION REFERENCE (EIBFN)
         DS    C
WTOTERM  DS    CL4                TERMID
         DS    C
WTOTRAN  DS    CL4                TRANSID (OR BLANK)
         DS    C
WTOTYPE  DS    C                  TRANSID TYPE
         DS    C
WTOTASKN DS    CL7                TASK NUMBER
         DS    C
WTODESC  DS    CL45               DESCRIPTION
*
         ACBD  ,                  VSAM ACB
         VCDTD ,                  VSAM CLUSTER DEFINITION TABLE
         DFHFCTDS                 FILE CONTROL TABLE
         DFHFCBLK                 FILE CONTROL TCPIO BLOCK
         DFHVSWAD                 FILE CONTROL VSAM WORK AREA
*
         EQUREGS
*
         END
