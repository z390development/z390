***********************************************************************
* Copyright 2006 Automated Software Tools Corporation                 *
* This source code is part of z390 assembler/emulator package         *
* The z390 package is distributed under GNU general public license    *
* Author - Don Higgins                                                *
***********************************************************************
GBL0602  SUBENTRY
*
* FN=0602  READ FILE
*
*         +0/DSCT +4/DSECT +8/IND   +12/IND +16
* R1  >>> WTOMSG, REQDSECT,DFHFCTAD,CONNID, TRCEZ390
* R14 = RETURN ADDRESS
         LR    R3,R1              SAVE PARM POINTER
         L     R4,16(R3)          ADDRESS OF TRCEZ390
         MVC   TRCEZ390,0(R4)     MOVE IT
         L     R9,0(R3)           WTODSECT ADDRESS
         USING WTODSECT,R9
         L     R6,4(R3)           ADDRESS RECEIVED DATA
         USING DFHFCBLK,R6
         L     R4,8(R3)           ADDRESS FCT ADDRESS
         L     R7,0(R4)           ADDRESS FCT
         USING DFHFCTDS,R7
         USING IHAACB,R8
         MVC   WTOTERM,FCPTERM    SET TERMID
         MVC   WTOTRAN,FCPTRAN    SET TRANSID
         MVI   WTOTYPE,C' '       NO TRANSID TYPE
         UNPK  WTOTASKN,FCPTASKN  UNPACK TASK NUMBER
         OI    WTOTASKN+6,X'F0'   CLEAR SIGN
         MVC   WTODESC,=CL45'<<<Z390KCP READ       REQUEST'
         MVI   CICSTIND,C'A'      SET APPLICATION TYPE
         BAL   R11,CICSTRCE       TRACE IT
         BAL   R11,FOP1REF        OPEN ON 1ST REFERENCE
* SET UP VSWA FOR REQUEST
* R6= REQUEST DATA  (DFHFCBLK)
* R7= FCTTE ADDRESS (DFHFCTDS)
* R8= ACB
* R4= VSWA AND RPL
* R10=VCLR (CLUSTER ENTRY)
         TM    FCTSERV,FCTREAD    IS READ ALLOWED ?
         BNO   RDFLIV20           EXIT IF NOT
         LA    R5,FCTVSWA         R5=CHAINED FROM ADDRESS
         ICM   R4,15,FCTVSWA      R4=VSWA ANCHOR
         BZ    RDFLNSLT           EXIT IF NO VSWAS
         USING DFHVSWAD,R4
* SCAN TO END OF CHAIN, CREATE A NEW VSWA
RDFLFLP  EQU   *
         LTR   R4,R4              END OF CHAIN ?
         BZ    RDFLNSLT           EXIT IF IT IS
         LR    R5,R4              R5=CHAINED FROM ADDRESS
         L     R4,VSWCHAIN        NEXT ENTRY
         B     RDFLFLP            LOOP
*
RDFLNSLT EQU   *
* LOCATED LAST (OR 1ST) VSWA
* GETMAIN A NEW ONE AND CHAIN
         LA    R1,VSWLEN          R1=VSWA LENGTH
         GETMAIN R,LV=(R1),A=0(R5)
         LR    R4,R1              NEW VSWA
         XC    DFHVSWAD(VSWLEN),DFHVSWAD CLEAR IT
         MVC   VSWTASKN,FCPTASKN  SET TASK NUMBER
         MVC   VSWCTYPE,FCPCTYPE  SET COMMAND TYPE
         USING IHARPL,VSWRPL
         L     R10,ACBVCLRA       R10=VCLR (CLUSTER ENTRY)
         USING IHAVCLR,R10
         CLI   FCPRDOPT,FCPRDXRB  XRBA REQUEST ?
         BE    RDFLXRBA           EXIT IF IT IS
         CLI   FCPRDOPT,FCPRDRRN  RRN REQUEST ?
         BE    RDFLRRN            EXIT IF IT IS
         CLI   FCPRDOPT,FCPRDKEY  FULL KEY REQUEST ?
         BE    RDFLKEY            EXIT IF IT IS
         CLI   FCPRDOPT,FCPRDGEN  GENERIC KEY REQUEST ?
         BE    RDFLKEY            EXIT IF IT IS
         GENCB BLK=RPL,                                                X
               ACB=(R8),                                               X
               OPTCD=(ADR),                                            X
               ARG=(S,FCPRBA),                                         X
               WAREA=IHARPL,                                           X
               LENGTH=RPLEND-IHARPL
         B     RDFLGETR           EXIT
*
RDFLXRBA EQU   *
         GENCB BLK=RPL,                                                X
               ACB=(R8),                                               X
               OPTCD=(ADR,XRBA),                                       X
               ARG=(S,FCPXRBA),                                        X
               WAREA=IHARPL,                                           X
               LENGTH=RPLEND-IHARPL
         B     RDFLGETR           EXIT
*
RDFLRRN  EQU   *
         GENCB BLK=RPL,                                                X
               ACB=(R8),                                               X
               OPTCD=(KEY),                                            X
               ARG=(S,FCPRRN),                                         X
               WAREA=IHARPL,                                           X
               LENGTH=RPLEND-IHARPL
         B     RDFLGETR           EXIT
*
RDFLKEY  EQU   *
         GENCB BLK=RPL,                                                X
               ACB=(R8),                                               X
               OPTCD=(KEY,FKS,KEQ),                                    X
               ARG=(S,FCPRID),                                         X
               WAREA=IHARPL,                                           X
               LENGTH=RPLEND-IHARPL
         MVC   RPLLKEY,VCLRKLEN   DEFAULT TO CATALOG BASE KEYLENGTH
         ICM   R12,15,ACBVAIXA    IS THERE A PATH ?
         BZ    RDFLGEN1           EXIT IF NOT
* THIS IS A PATH, USE THE ALTERNATE KEY LENGTH
         USING IHAVAIX,R12
         MVC   RPLLKEY,VAIXKLEN   DEFAULT TO CATALOG AIX KEYLENGTH
RDFLGEN1 EQU   *
         CLI   FCPRDOPT,FCPRDGEN  GENERIC ?
         BNE   RDFLGTE            EXIT IF NOT
* KSDS GENERIC
         TM    FCPKEYLN,X'80'     IS IT NEGATIVE ?
         BO    RDFLIV42           ERROR IF IT IS
         LTR   R12,R12            IS THERE A PATH ?
         BZ    RDFLBAS2           EXIT IF NOT
* THIS IS A PATH, TEST THE ALTERNATE KEY LENGTH
         CLC   FCPKEYLN,VAIXKLEN+2 VALIDATE AIX GENERIC KEYLENGTH ?
         BNL   RDFLIV25           EXIT IF BAD
         DROP  R12
         B     RDFLGEN2           EXIT
*
RDFLBAS2 EQU   *
         CLC   FCPKEYLN,VCLRKLEN+2 VALIDATE BASE GENERIC KEYLENGTH ?
         BNL   RDFLIV25           EXIT IF BAD
RDFLGEN2 EQU   *
         MVC   RPLLKEY+2(2),FCPKEYLN MOVE REQUESTED GENERIC KEYLENGTH
         OI    RPLOPT1,RPLOPT1_GEN SET IN RPL
RDFLGTE  EQU   *
         CLI   FCPBROPT,FCPBRGTE  GTEQ ?
         BNE   RDFLNTGE           EXIT IF NOT
* KSDS GTEQ
         OI    RPLOPT1,RPLOPT1_KGE SET IN RPL
RDFLNTGE EQU   *
* IF GENERIC OR GTEQ HAS BEEN SPECIFIED WE NEED POINT
         TM    RPLOPT1,RPLOPT1_KGE+RPLOPT1_GEN GTEQ AND/OR GEN ?
         BZ    RDFLGETR           EXIT IF NEITHER
         CLI   FCPKEYLN,X'7F'     ANY REQUESTED KEYLENGTH ?
         BE    RDFLPNT            EXIT IF NONE
         OC    FCPKEYLN,FCPKEYLN  KEYLENGTH=0 ?
         BNZ   RDFLPNT            EXIT IF NOT
* KSDS KEYLENGTH(0) AND (GENERIC AND/OR GTEQ) SPECIFIED
*    ARG IS ALWAYS X'00' TO GET 1ST RECORD
*    GENERIC=KEYLENGTH(1) AND GTEQ FORCED
         XC    FCPRID,FCPRID      SET KEY TO X'00'
         OI    RPLOPT1,RPLOPT1_KGE FORCE GTEQ
         MVC   RPLLKEY,=F'1'      SET KEYLENGTH=1
RDFLPNT  EQU   *
         XC    RDFLGETL,RDFLGETL  NO FREEMAIN IF POINT ERROR
         LR    R5,R6              DEFAULT TO CUR FCBLK IF POINT ERROR
         POINT RPL=IHARPL         POSITION
         LTR   R15,R15            CHECK RC ?
         BNZ   RDFLFAIL           EXIT IF BAD
*
RDFLGETR EQU   *
         L     R5,VCLRLREC        R5=LENGTH
         TM    VCLRFLG1,VCLRVREC  IS IT VARIABLE ?
         BNO   RDFLFIX            EXIT IF NOT
         AHI   R5,4               +4 FOR VARIABLE LENGTH
RDFLFIX  EQU   *
         LA    R1,FCPREFIX(R5)    FC PREFIX LENGTH + MAX RECORD SIZE
         GETMAIN R,LV=(R1)
         ST    R0,RDFLGETL        SAVE LENGTH FOR FREEMAIN
         LR    R5,R1              ADDRESS NEW FC BLOCK
         MVC   0(FCPREFIX,R5),DFHFCBLK MOVE PREFIX INTO NEW BLOCK
         XC    FCPRETCD-DFHFCBLK(4,R5),FCPRETCD-DFHFCBLK(R5) CLEAR RC
         XC    FCPRESP2-DFHFCBLK(4,R5),FCPRESP2-DFHFCBLK(R5) CLEAR RSP2
         MODCB RPL=IHARPL,AREA=(S,FCPREFIX(R5))
         GET   RPL=IHARPL         READ RECORD
         LTR   R15,R15            CHECK RC ?
         BNZ   RDFLFAIL           EXIT IF ERROR
         TM    VCLRFLG1,VCLRVREC  IS IT VARIABLE ?
         BNO   RDFLFIX2           EXIT IF NOT
         MVC   FCPACLEN-DFHFCBLK(4,R5),RPLLREC SAVE ACTUAL LRECL
         B     RDFLLEN            EXIT
*
RDFLFIX2 EQU   *
         MVC   FCPACLEN-DFHFCBLK(4,R5),VCLRLREC SAVE FIXED LRECL
         DROP  R10
RDFLLEN  EQU   *
         L     R1,FCPLEN          R1=LENGTH REQUESTED
         CLC   FCPACLEN-DFHFCBLK(4,R5),FCPLEN IS ACT DATA LENGTH LESS ?
         BH    RDFLLNOK           EXIT IF NOT
         L     R1,FCPACLEN-DFHFCBLK(R5) R1=LENGTH OF DATA
RDFLLNOK EQU   *
         LA    R15,FCPREFIX(R1)   R15=ACTUAL LENGTH SENT
         ST    R15,FCPTCPIO-DFHFCBLK(R5) SAVE IT
         MVC   WTODESC,=CL45'<<<Z390KCP READ       COMPLETED'
RDFLSEND EQU   *
* READ COMPLETED, PASS DATA TO CLIENT, FREE NEW FCBLK, FREE VSWA
         L     R8,12(R3)          R8=CONN ID ADDRESS
         L     R8,0(R8)           R8=CONNID
         TCPIO SEND,MSG=(R5),LMSG=0(R5),CONN=(R8),PORT=3900
         LTR   R15,R15            CHECK RC ?
         BNZ   ABEND780           EXIT IF BAD
         OC    RDFLGETL,RDFLGETL  ANY NEW FCBLK ?
         BZ    RDFLNOFB           EXIT IF NOT
         MVC   0(4,R5),RDFLGETL   SET REAL STORAGE LENGTH
         FREEMAIN LA=RDFLGETL,A=(R5)
RDFLNOFB EQU   *
         LA    R8,FCTVSWA         PREVIOUS CHAIN ADDRESS
         L     R4,FCTVSWA         R4=VSWA CHAIN
RDFLFRVS EQU   *
         CLC   VSWTASKN,FCPTASKN  OUR TASK NUMBER ?
         BNE   RDFLVSNF           EXIT IF NOT
         CLI   VSWCTYPE,VSWCREAD  READ COMMAND ?
         BE    RDFLGOTV           EXIT IF IT IS
RDFLVSNF EQU   *
         LR    R8,R4              PREVIOUS CHAIN ADDRESS
         L     R4,VSWCHAIN        NEXT VSWA
         B     RDFLFRVS           LOOP IF NOT OURS
*
RDFLGOTV EQU   *
* REMAKE THE CHAIN AND FREEMAIN THE VSWA
         MVC   0(4,R8),VSWCHAIN   REMAKE THE CHAIN
         FREEMAIN LV=VSWLEN,A=(R4) FREE VSWA
         MVI   CICSTIND,C'A'      SET APPLICATION TYPE
         BAL   R11,CICSTRCE       TRACE IT
         SUBEXIT
*
RDFLFAIL EQU   *
* READ FAILURE PROCESSING
* FILE ERROR HANDLER
         LA    R15,FILEERTB       R15=ERROR TABLE ADDRESS
FILEERLP EQU   *
         CLC   0(4,15),RPLFEEDB   MATCH TO TABLE
         BNE   FILEERIN           EXIT IF NOT
         MVC   FCPRETCD-DFHFCBLK(4,R5),4(R15) MOVE RETURN CODE
         MVC   FCPRESP2-DFHFCBLK(4,R5),8(R15) MOVE RESP2
         MVC   FCPRESP2-DFHFCBLK(1,R5),1(R15) MOVE RPLFEEDB+1
         MVC   FCPRESP2+1-DFHFCBLK(1,R5),3(R15) MOVE RPLFEEDB+3
         B     FILEERDN           EXIT
*
FILEERIN EQU   *
         AHI   R15,20             BUMP TABLE POINTER
         CLI   0(R15),X'FF'       STOPPER ?
         BNE   FILEERLP           LOOP IF NOT
         J     ABEND790           MISSING TABLE ENTRY SHOULD NOT OCCUR
*
FILEERDN EQU   *
         MVC   WTODESC,=CL45'>>>Z390KCP READ       FAILED'
         MVC   WTODESC+29(8),12(R15) MOVE CONDITION
         B     RDFLSEND           EXIT
*
RDFLIV20 EQU   *
         MVC   FCPRESP2,=F'20'    SET RESP2 (FUNCTION NOT PERMITTED)
         B     RDFLINVQ           EXIT
*
RDFLIV25 EQU   *
         MVC   FCPRESP2,=F'25'    SET RESP2 (KEYLENGTH>=ACTUAL)
         B     RDFLINVQ           EXIT
*
RDFLIV42 EQU   *
         MVC   FCPRESP2,=F'42'    SET RESP2 (KEYLENGTH<0)
RDFLINVQ EQU   *
         MVC   FCPRETCD,DFHRESP(INVREQ) SET INVREQ
         MVC   WTODESC,=CL45'>>>Z390KCP READ       FAILED'
         MVC   WTODESC+29(6),=C'INVREQ'
         LR    R9,R6              SET AS IF NEW DFHFCBLK
         L     R8,12(R3)          R8=CONN ID ADDRESS
         L     R8,0(R8)           R8=CONNID
         LA    R15,FCPREFIX       R15=LENGTH TO SEND
         TCPIO SEND,MSG=(R9),LMSG=(R15),CONN=(R8),PORT=3900
         LTR   R15,R15            CHECK RC ?
         BNZ   ABEND780           EXIT IF BAD
         L     R9,0(R3)           WTODSECT ADDRESS
         MVI   CICSTIND,C'A'      SET APPLICATION TYPE
         BAL   R11,CICSTRCE       TRACE IT
* FREE THE VSWA, ONLY INVREQ/20 DOESN'T CREATE ONE
         OC    FCPRESP2,FCPRESP2  ANY ERROR OCCURRED ?
         BZ    RDFLDONE           EXIT IF NONE
         CLC   FCPRESP2,=F'20'    READ NOT PERMITTED ?
         BE    RDFLDONE           EXIT IF IT IS
         LA    R8,FCTVSWA         PREVIOUS CHAIN ADDRESS
         L     R4,FCTVSWA         R4=VSWA CHAIN
RDFLFRV2 EQU   *
         CLC   VSWTASKN,FCPTASKN  OUR TASK NUMBER ?
         BNE   RDFLVSN2           EXIT IF NOT
         CLI   VSWCTYPE,VSWCREAD  READ COMMAND ?
         BE    RDFLGOT2           EXIT IF IT IS
RDFLVSN2 EQU   *
         LR    R8,R4              PREVIOUS CHAIN ADDRESS
         L     R4,VSWCHAIN        NEXT VSWA
         B     RDFLFRV2           LOOP IF NOT OURS
*
RDFLGOT2 EQU   *
* REMAKE THE CHAIN AND FREEMAIN THE VSWA
         MVC   0(4,R8),VSWCHAIN   REMAKE THE CHAIN
         FREEMAIN LV=VSWLEN,A=(R4) FREE VSWA
RDFLDONE EQU   *
         SUBEXIT
*
RDFLGETL DS    F                  SAVE GETMAIN LENGTH
*
* SUBROUTINES
*
* MODIFIED CICSTRCE
CICSTRCE DS    0H
         CLI   TRCEZ390,C'N'      NO TRACING ?
         BER   R11                RETURN IF IT IS
         CLI   TRCEZ390,C'Y'      ALL TRACING ?
         BE    CICSTRGO           TRACE IF IT IS
         CLC   TRCEZ390,CICSTIND  TRACE TYPE=TRACE REQUEST ?
         BNER  R11                RETURN IF NOT
CICSTRGO EQU   *
         TIME  DEC,TIME,LINKAGE=SYSTEM GET TIME
         MVO   TIMEPACK,TIME      MOVE TO PACKED FIELD
         MVC   TIMEHERE(13),TIMEEDIT MOVE EDIT WORD
         ED    TIMEHERE(13),TIMEPACK EDIT TIME
         MVC   WTOTIME,TIMEHERE   SET TIME
         WTO   MF=(E,WTOMSG)      TRACE IT
         BR    R11                RETURN
*
FOP1REF DS     0H
* OPEN ON 1ST REFERENCE
FOP1LOOP EQU   *
* CHECK THE FCT STATUS
         CLC   FCPFILNM,FCTFILNM  MATCH REQUESTED FILENAME TO FCT ?
         BNE   FOP1INCR           EXIT IF NOT MATCHED
* FILE FOUND
         LA    R8,FCTTELEN(R7)    ADDRESS ACB
         TM    FCTFILST,FCTOPEN   IS FILE OPEN ?
         BOR   R11                RETURN IF IT IS
* FILE NOT OPEN
* TEST FILSTAT OPTIONS
*    OPENED,ENABLED   -- FILE OPENED AT START, CANNOT OCCUR HERE
*                     -- BUT TREATED AS OPEN ON 1ST REF
*    OPENED,DISABLED  -- FILE OPENED AT START, CANNOT OCCUR HERE
*                     -- BUT TREATED AS OPEN ON 1ST REF
*                     -- DISABLED STATUS RETURNED
*    OPENED,UNENABLED -- CANNOT OCCUR
*
*    CLOSED,ENABLED   -- FILE OPENED NOW (1ST REF)
*    CLOSED,DISABLED  -- DISABLED STATUS RETURNED
*    CLOSED,UNENABLED -- NOTOPEN STATUS RETURNED
         TM    FCTFILST,FCTOPCLO  FILSTAT=OPENED ?
         BO    FOP1OPEN           EXIT IF IT IS
* FILSTAT=CLOSED
         TM    FCTFILST,FCTENDIS  FILSTAT=ENABLED ?
         BO    FOP1OPEN           EXIT IF IT IS
* FILSTAT=CLOSED AND NOT ENABLED
         TM    FCTFILST,FCTUNENA  FILSTAT=UNENABLED ?
         BO    FOP1NOTO           EXIT IF NOTOPEN
         B     FOP1DISA           EXIT IF DISABLED
*
FOP1INCR EQU   *
         AHI   R7,FCTABLEN        POINT TO NEXT FCT ENTRY
         CLI   0(R7),X'FF'        END OF FCT ?
         BNE   FOP1LOOP           LOOP IF NOT
* FILE NOT FOUND
         B     FOP1FNTF           EXIT IF FILENOTFOUND
*
FOP1OPEN EQU   *
* OPEN ON 1ST REF
* INITIALIZE MESSAGE
         MVC   WTOFN,=C'06FE'     1ST REF FILE OPENS
         MVC   WTOTERM,=4C' '     NO TERMID
         MVC   WTOTRAN,=4C' '     NO PRESET TRANSID
         MVI   WTOTYPE,C' '       NO TRANSID TYPE
         MVC   WTOTASKN,=7C' '    NO TASK NUMBER
         MVC   WTODESC,=CL45'FILE ........ OPEN  FAILED'
         MVC   WTODESC+5(8),FCTFILNM MOVE FILENAME TO MSG
         TM    FCTSERV,FCTDELT+FCTUPDT+FCTADD REASON TO OPEN OUTPUT ?
         BZ    FOP1IN             EXIT IF NONE
* SET ALL BITS WE ARE LIKELY TO USE
*    ADR = ACCESS BY RBA
*    DIR = DIRECT ACCESS
*    KEY = ACCESS BY KEY
*    SEQ = SEQUENTIAL ACCESS
*    IN  = READ ONLY
*    OUT = READ OR WRITE
* OPEN FOR OUTPUT
         MODCB ACB=IHAACB,MACRF=(ADR,DIR,KEY,SEQ,OUT)
         OPEN  (IHAACB,OUTPUT)    OPEN FOR OUTPUT
         LTR   R15,R15            OK ?
         BNZ   FOP1OPER           EXIT IF NOT
         B     FOP1OPOK           EXIT
*
FOP1IN   EQU   *
* OPEN FOR INPUT
         MODCB ACB=IHAACB,MACRF=(ADR,DIR,KEY,SEQ,IN)
         OPEN  (IHAACB,INPUT)     OPEN FOR INPUT
         LTR   R15,R15            OK ?
         BNZ   FOP1OPER           EXIT IF NOT
FOP1OPOK EQU   *
* OPEN SUCCESSFUL, SAY SO
         MVC   WTODESC+20(6),=C'OK    ' SET OK
         MVI   CICSTIND,C'I'      SET INTERNAL TYPE
         ST    R11,FOP1SV11       SAVE RETURN ADDRESS
         BAL   R11,CICSTRCE       TRACE IT
         L     R11,FOP1SV11       RESTORE RETURN ADDRESS
         OI    FCTFILST,FCTOPEN   SET FILE OPEN
         OI    FCTFILST,FCTOPCLO  SET FILSTAT=OPENED
         B     FOP1DIST           EXIT
*
FOP1OPER EQU   *
* OPEN UNSUCCESSFUL, SAY SO
         MVC   WTODESC+20(6),=C'FAILED' SET FAILED
         MVI   CICSTIND,C'I'      SET INTERNAL TYPE
         ST    R11,FOP1SV11       SAVE RETURN ADDRESS
         BAL   R11,CICSTRCE       TRACE IT
         L     R11,FOP1SV11       RESTORE RETURN ADDRESS
         MVI   FCTFILST,X'00'     SET FILE CLOSED,DISABLED
         B     FOP1DISA           EXIT IF DISABLED
*
FOP1DIST EQU   *
         TM    FCTFILST,FCTENDIS  IS FILSTAT=DISABLED ?
         BZ    FOP1DISA           EXIT IF DISABLED
         BR    R11                RETURN
*
FOP1FNTF EQU   *
         MVC   FCPRETCD,DFHRESP(FILENOTFOUND) SET FILENOTFOUND
         MVC   FCPRESP2,=F'1'     SET RESP2
         MVC   WTODESC,=CL45'>>>Z390KCP READ       FAILED'
         MVC   WTODESC+29(12),=C'FILENOTFOUND'
         B     FOP1SEND           EXIT
*
FOP1DISA EQU   *
         MVC   FCPRETCD,DFHRESP(DISABLED) SET DISABLED
         MVC   FCPRESP2,=F'50'    SET RESP2
         MVC   WTODESC,=CL45'>>>Z390KCP READ       FAILED'
         MVC   WTODESC+29(12),=C'DISABLED'
         B     FOP1SEND           EXIT
*
FOP1NOTO EQU   *
         MVC   FCPRETCD,DFHRESP(NOTOPEN) SET NOTOPEN
         MVC   FCPRESP2,=F'60'    SET RESP2
         MVC   WTODESC,=CL45'>>>Z390KCP READ       FAILED'
         MVC   WTODESC+29(7),=C'NOTOPEN'
FOP1SEND EQU   *
         LR    R9,R6              SET AS IF NEW DFHFCBLK
         L     R8,12(R3)          R8=CONN ID ADDRESS
         L     R8,0(R8)           R8=CONNID
         LA    R15,FCPREFIX       R15=LENGTH TO SEND
         TCPIO SEND,MSG=(R9),LMSG=(R15),CONN=(R8),PORT=3900
         LTR   R15,R15            CHECK RC ?
         BNZ   ABEND780           EXIT IF BAD
         L     R9,0(R3)           WTODSECT ADDRESS
         MVI   CICSTIND,C'A'      SET APPLICATION TYPE
         BAL   R11,CICSTRCE       TRACE IT
         SUBEXIT
FOP1SV11 DS    A                  SAVE R11
*
ABEND780 ABEND 780,DUMP           TCPIO SEND FAILED
ABEND790 ABEND 790,DUMP           VSAM FEEDBACK NOT EXPECTED
*
CICSTIND DS    C                  TRACE TYPE INDICATOR
TRCEZ390 DS    C                  Z390 TRACE OPTION
*
         COPY  FILEERTB           RPL FEEDBACK ERROR TABLE
*
* TIME CONVERSION FIELDS
*
TIME     DS    CL4                HHMMSSTT
         DS    CL8                SPACE FOR DATE
TIMEPACK DC    PL5'0'             0HHMMSSTTC
TIMEEDIT DC    X'402021207A20207A20204B2020'
TIMEHERE DS    CL13
*
* WTO AREA DSECT
*
WTODSECT DSECT
WTOMSG   DS    AL4
WTOTIME  DS    CL13               TIME STAMP
         DS    C
WTOFN    DS    CL4                MODULE/FUNCTION REFERENCE (EIBFN)
         DS    C
WTOTERM  DS    CL4                TERMID
         DS    C
WTOTRAN  DS    CL4                TRANSID (OR BLANK)
         DS    C
WTOTYPE  DS    C                  TRANSID TYPE
         DS    C
WTOTASKN DS    CL7                TASK NUMBER
         DS    C
WTODESC  DS    CL45               DESCRIPTION
*
         ACBD  ,                  VSAM ACB
         VCDTD ,                  VSAM CLUSTER DEFINITION TABLE
         DFHFCTDS                 FILE CONTROL TABLE
         DFHFCBLK                 FILE CONTROL TCPIO BLOCK
         DFHVSWAD                 FILE CONTROL VSAM WORK AREA
*
         EQUREGS
*
         END
