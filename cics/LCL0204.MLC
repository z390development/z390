***********************************************************************
* Copyright 2006 Automated Software Tools Corporation                 *
* This source code is part of z390 assembler/emulator package         *
* The z390 package is distributed under GNU general public license    *
* Author - Don Higgins                                                *
***********************************************************************
LCL0204  CSECT
*
* FN=0204  EXEC CICS HANDLE CONDITION
*
* R1  >>> P0204 BLOCK POINTER
*
         USING *,R13
         B     12(R15)            BYPASS EYECATCHER
         DC    CL8'LCL0204'
         LR    R13,R15            R13 AS BASE
         LR    R3,R1              SET BASE
         L     R3,0(R3)                    FOR PARMS
         USING (P0204,P0204END),R3
         L     DFHEIBR,P0204EIB   LOAD EIB ADDRESS
         USING DFHEIBLK,DFHEIBR
         L     TCTTEAR,P0204TCT   LOAD TCT ADDRESS
         USING DFHTCTTE,TCTTEAR
         MVC   EIBFN,P0204FN      SET EIBFN
         XC    EIBRCODE,EIBRCODE  CLEAR EIBRCODE
         XC    EIBRESP,EIBRESP    CLEAR EIBRESP
         XC    EIBRESP2,EIBRESP2  CLEAR EIBRESP2
         ICM   R1,15,P0204RSP     R1=RESP ADDRESS
         BZ    NORESP             EXIT IF NONE
         XC    0(4,R1),0(R1)      CLEAR RESP
NORESP   EQU   *
         ICM   R1,15,P0204RS2     R1=RESP2 ADDRESS
         BZ    NORESP2            EXIT IF NONE
         XC    0(4,R1),0(R1)      CLEAR RESP2
NORESP2  EQU   *
         CLI   P0204NED,X'FF'     NOEDF ?
         BE    NOEDFB             EXIT IF IT IS
         CLI   TCTTEEDF,X'FF'     EDF ON ?
         BNE   NOEDFB             EXIT IF NOT
         MVI   TCTTEEBA,X'00'     SET 'BEFORE' INTERCEPT
         MVI   TCTTEEDL,X'00'     SET NORMAL LINKAGE
         L     R15,TCTTEEDA       R15=EDF PROCESSOR ADDRESS
         BAKR  0,R15              STACK REGS AND GO
*
NOEDFB   EQU   *
* ONLY THE FIRST CONDITION BLOCK IS ACQUIRED HERE
* PUSH ACQUIRES MORE
         L     R1,P0204HCN        R1=A(DFHEIHCN)
         ICM   R1,15,0(R1)        ANY CONDITION BLOCK ADDRESS ?
         BZ    NOHCN              EXIT IF NONE
         USING DFHHCBLK,R1
HCNLOOP  EQU   *
         LR    R14,R1             R14=LAST CONDITION BLOCK ADDRESS
         ICM   R1,15,HCNCHAIN     R1=NEXT CONDITION BLOCK
         BNZ   HCNLOOP            LOOP
* END OF CONDITION BLOCK CHAIN
         LR    R1,R14             R1=LAST CONDITION BLOCK ADDRESS
         B     GOTHCN             EXIT
*
NOHCN    EQU   *
* GET THE FIRST CONDITION BLOCK
         LA    R1,HCNLENG         R1=CONDITION BLOCK LENGTH
         GETMAIN R,LV=(R1)
         XC    0(256,R1),0(R1)    CLEAR CONDITION BLOCK
         XC    256(HCNLENG-256,R1),256(R1) CLEAR CONDITION BLOCK
         L     R4,P0204HCN        R4=A(DFHEIHCN)
         ST    R1,0(R4)           SAVE CONDITION BLOCK ADDRESS
         MVC   HCNHEAD,=C'DFHHCBLK' SET HEADER
         DROP  R1
GOTHCN   EQU   *
         LA    R4,P0204CND        R4=CONDITION NUMBERS
         LA    R5,P0204ADR        R5=CONDITION ADDRESSES
         LA    R6,30              R6=LOOP LIMIT
SETHCN   EQU   *
         CLI   0(R4),X'00'        END OF LIST ?
         BE    ENDCOND            EXIT IF IT IS
         CLI   0(R5),X'FF'        IS IT CONDITION CANCEL ?
         BNE   HCNLABEL           EXIT IF NOT
* PARM HAS NO LABEL...IT'S A CANCEL
         LB    R7,0(R4)           R7=CONDITION NUMBER
         SLL   R7,2               *4
         AHI   R7,12              +12 (HEADER/CHAIN)
         AR    R7,R1              + HANDLE BLOCK ADDRESS
         XC    0(4,R7),0(R7)      CLEAR ENTRY
         B     HCNINCR            EXIT
*
HCNLABEL EQU   *
* PARM HAS A LABEL
         LB    R7,0(R4)           R7=CONDITION NUMBER
         SLL   R7,2               *4
         AHI   R7,12              +12 (HEADER/CHAIN)
         AR    R7,R1              + CONDITION BLOCK ADDRESS
         MVC   0(4,R7),0(R5)      SET CONDITION LABEL
HCNINCR  EQU   *                  EXIT
         AHI   R4,1               BUMP CONDITION NUMBER POINTER
         AHI   R5,4               BUMP CONDITION ADDRESS POINTER
         BCT   R6,SETHCN          LOOP
ENDCOND  EQU   *
         CLI   P0204NED,X'FF'     NOEDF ?
         BE    NOEDFA             EXIT IF IT IS
         CLI   TCTTEEDF,X'FF'     EDF ON ?
         BNE   NOEDFA             EXIT IF NOT
         MVI   TCTTEEBA,X'FF'     SET 'AFTER' INTERCEPT
         MVI   TCTTEEDL,X'00'     SET NORMAL LINKAGE
         L     R15,TCTTEEDA       R15=EDF PROCESSOR ADDRESS
         BAKR  0,R15              STACK REGS AND GO
*
NOEDFA   EQU   *
         PR    ,                  UNSTACK AND RETURN TO MACRO
*
         P0204 DSECT
*
         DFHEIBLK                 EIB
         DFHTCTTE                 TCTTE
         DFHHCBLK                 HANDLE CONDITION BLOCK
*
         EQUREGS REGS=GPR
* R1  HANDLE CONDITION BLOCK BASE
* R3  PARM POINTER
* R10 TCTTE
* R11 EIB
* R13 BASE 1
*
         END
