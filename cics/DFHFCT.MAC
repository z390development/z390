.**********************************************************************
.* Copyright 2006 Automated Software Tools Corporation                *
.* This source code is part of z390 assembler/emulator package        *
.* The z390 package is distributed under GNU general public license   *
.* Author - Don Higgins                                               *
.**********************************************************************
.* 04/19/08 RPI 833 ADD STRING QUOTES FOR HLASM COMPATIBILITY         *
.* 11/11/08 RPI 946 ADD FCTVSWA AS IT WILL NOW DEFAULT TO X'F6'       *
.**********************************************************************
         MACRO
.* FILE CONTROL TABLE
         DFHFCT &TYPE=,                                                X
               &DATASET=,                                              X
               &FILSTAT=(ENABLED,CLOSED),                              X
               &RECFORM=(VARIABLE,BLOCKED),                            X
               &SERVREQ=
.*
         GBLB  &TYPED,&FINAL
         AIF   (&TYPED).BYPOPSN
END      OPSYN DFHFEND
.BYPOPSN ANOP
.*
         LCLB  &FILOPEN,&FILCLOS,&FILENAB,&FILDISA,&FILUNEN
         LCLB  &RECFIX,&RECVAR
         LCLB  &SVQREAD,&SVQDELT,&SVQUPDT,&SVQADD,&SVQBRWS
.*
         AIF   ('&TYPE' EQ 'DATASET').FILE
         AIF   ('&TYPE' EQ 'FINAL').FINAL
         MNOTE 12,'INVALID TYPE - &TYPE'
         MEXIT
.*
.FILE    ANOP
         AIF   (K'&DATASET EQ 0 OR K'&DATASET GT 8).ERR1
         AIF   (&FINAL).BADTYP
&TYPED   SETB  1
DFHFCT   CSECT
         DS    0D
         DC    CL8'&DATASET'      FCTFILNM
.*
.* FILSTAT PROCESSING
.*
&I       SETA  0
.FILLOOP ANOP
&I       SETA  &I+1
         AIF   (&I GT N'&FILSTAT).CHKFIL
&FILOPEN SETB  ('&FILSTAT(&I)' EQ 'OPENED'    OR &FILOPEN)
&FILCLOS SETB  ('&FILSTAT(&I)' EQ 'CLOSED'    OR &FILCLOS)
&FILENAB SETB  ('&FILSTAT(&I)' EQ 'ENABLED'   OR &FILENAB)
&FILDISA SETB  ('&FILSTAT(&I)' EQ 'DISABLED'  OR &FILDISA)
&FILUNEN SETB  ('&FILSTAT(&I)' EQ 'UNENABLED' OR &FILUNEN)
         AGO   .FILLOOP
.*
.CHKFIL  ANOP
         AIF   (&FILOPEN AND &FILCLOS).FILSERR
         AIF   (&FILENAB AND &FILDISA).FILSERR
         AIF   (&FILENAB AND &FILUNEN).FILSERR
         AIF   (&FILDISA AND &FILUNEN).FILSERR
         AIF   (NOT(&FILOPEN) AND NOT(&FILCLOS)).FILSERR
         AIF   (&FILUNEN AND NOT(&FILCLOS)).FILSERR
         DC    B'&FILOPEN&FILENAB&FILUNEN.00000' FCTFILST
.*
.* RECFORM PROCESSING
.*
&I       SETA  0
.RECLOOP ANOP
&I       SETA  &I+1
         AIF   (&I GT N'&RECFORM).CHKREC
&RECFIX  SETB  ('&RECFORM(&I)' EQ 'FIXED'     OR &RECFIX)
&RECVAR  SETB  ('&RECFORM(&I)' EQ 'VARIABLE'  OR &RECVAR)
         AIF   ('&RECFORM(&I)' EQ 'UNBLOCKED').NOTVSAM
         AGO   .RECLOOP
.*
.CHKREC  ANOP
         AIF   (&RECFIX AND &RECVAR).RECSERR
         DC    B'&RECFIX.0000000' FCTRECFM
.*
.* SERVREQ PROCESSING
.*
&I       SETA  0
.SVQLOOP ANOP
&I       SETA  &I+1
         AIF   (&I GT N'&SERVREQ).CHKSVQ
&SVQREAD SETB  ('&SERVREQ(&I)' EQ 'READ'   OR &SVQREAD)
&SVQDELT SETB  ('&SERVREQ(&I)' EQ 'DELETE' OR &SVQDELT)
&SVQUPDT SETB  ('&SERVREQ(&I)' EQ 'UPDATE' OR &SVQUPDT)
&SVQADD  SETB  ('&SERVREQ(&I)' EQ 'ADD'    OR &SVQADD)
&SVQBRWS SETB  ('&SERVREQ(&I)' EQ 'BROWSE' OR &SVQBRWS)
         AGO   .SVQLOOP
.*
.CHKSVQ  ANOP
.* UPDATE OR BROWSE FORCES READ
         AIF   (&SVQUPDT OR &SVQBRWS).SVQDEFR
         AGO   .GENSVQ
.*
.SVQDEFR ANOP
&SVQREAD SETB  1
.GENSVQ  ANOP
         DC    B'&SVQREAD&SVQDELT&SVQUPDT&SVQADD&SVQBRWS.000' FCTSERV
         DC    AL4(0)             FCTVSWA
.*
         ACB   DDNAME=&DATASET
         MEXIT
.*
.FINAL   ANOP
         AIF   (NOT &TYPED).BADFIN1
         AIF   (&FINAL).BADFIN2
&FINAL   SETB  1
         DS    0D
         DC    X'FF'              STOPPER
         MEXIT
.*
.ERR1    MNOTE 12,'DATASET MISSING OR TOO LARGE'
         MEXIT
.*
.FILSERR MNOTE 12,'FILSTAT HAS INCORRECT OR CONFLICTING PARAMETERS'
         MEXIT
.*
.RECSERR MNOTE 12,'RECFORM HAS INCORRECT OR CONFLICTING PARAMETERS'
         MEXIT
.*
.NOTVSAM MNOTE 12,'RECFORM=(UNBLOCKED) NOT ALLOWED IN VSAM'
         MEXIT
.*
.BADTYP  MNOTE 12,'TYPE=DATASET OCCURS AFTER TYPE=FINAL'
         MEXIT
.*
.BADFIN1 MNOTE 12,'TYPE=FINAL WITHOUT PRECEDING TYPE=DATASET'
         MEXIT
.*
.BADFIN2 MNOTE 12,'MORE THAN ONE TYPE=FINAL'
         MEXIT
         MEND
