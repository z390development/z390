.**********************************************************************
.* Copyright 2006 Automated Software Tools Corporation                *
.* This source code is part of z390 assembler/emulator package        *
.* The z390 package is distributed under GNU general public license   *
.* Author - Don Higgins                                               *
.**********************************************************************
.* 04/19/08 RPI 833 ADD STRING QUOTES FOR HLASM COMPATIBILITY         *
.**********************************************************************
         MACRO
         CICS_POP
         GBLB  &PARMS_020E
         LCLB  &NOEDF,&NOHAND
         LCLC  &TYPE,&RESP,&RESP2
&TYPE    SETC  '&SYSLIST(1)'
         AIF   ('&TYPE' EQ 'HANDLE').GOTHAND
         MNOTE 12,'POP TYPE NOT RECOGNIZED'
         MEXIT
.*
.GOTHAND ANOP
&NP      SETA  N'&SYSLIST
&IP      SETA  1
.PLOOP   ANOP
&IP      SETA  &IP+1
         AIF   (&IP GT &NP).ELOOP
&PARM    SETC  '&SYSLIST(&IP)'
         AIF   ('&PARM'(1,5) EQ 'NOEDF').NOEDF
         AIF   ('&PARM'(1,8) EQ 'NOHANDLE').PLOOP
         AIF   ('&PARM'(1,5) EQ 'RESP(').RESP
         AIF   ('&PARM'(1,6) EQ 'RESP2(').RESP2
         MNOTE 12,'BAD PARM &PARM'
         AGO   .PLOOP
.*
.NOEDF   ANOP
&NOEDF   SETB  1
         AGO   .PLOOP
.*
.NOHAND  ANOP
&NOHAND  SETB  1
         AGO   .PLOOP
.*
.RESP    ANOP
&RESP    SETC  '&PARM'(6,K'&PARM-6)
&NOHAND  SETB  1
         AGO   .PLOOP
.*
.RESP2   ANOP
&RESP2   SETC  '&PARM'(7,K'&PARM-7)
         AGO   .PLOOP
.*
.ELOOP   ANOP
         GEN_XC P020E,P020EEND-P020E,P020E
         GEN_MVC P020EFN,,=X'020E' SET FUNCTION CODE
         GEN_ST DFHEIBR,P020EEIB  SET EIB ADDRESS
         GEN_ST TCTTEAR,P020ETCT  SET TCTTE ADDRESS
         AIF   (&SYSEDF AND NOT &NOEDF)
         GEN_ST R13,P020EEDS      SET DSA ADDRESS FOR EDF
         L     R0,DFHEILEN        DSA LENGTH
         GEN_STH R0,P020EEDL      SET DSA LENGTH FOR EDF
         LA    R0,DFHEIUSR        DSA USER AREA
         SR    R0,R13             R0=CICS DSA LENGTH
         GEN_STH R0,P020EEDC      SET CICS DSA LENGTH FOR EDF
         AELSE
         GEN_MVI P020ENED,X'FF'   SET NOEDF
         AEND
         LA    R0,DFHEIAID        R0=ADDRESS OF DFHEIAID
         GEN_ST R0,P020EAID       SAVE IT
         LA    R0,DFHEIHCN        R1=ADDRESS OF DFHEIHCN
         GEN_ST R0,P020EHCN       SAVE IT
         AIF   (NOT &NOHAND).TRYRESP
         GEN_MVI P020ENOH,X'FF'   SET NOHANDLE
.TRYRESP ANOP
         AIF   ('&RESP' EQ '').NORESP
         LA    R0,&RESP           ADDRESS OF RESP
         GEN_ST R0,P020ERSP       SAVE IT
.NORESP  ANOP
         AIF   ('&RESP2' EQ '').NORESP2
         LA    R0,&RESP2          ADDRESS OF RESP2
         GEN_ST R0,P020ERS2       SAVE IT
.NORESP2 ANOP
         LARL  R0,P020EEDF_&SYSNDX ADDRESS ORIGIN FOR CEDF
         GEN_ST R0,P020EEDF       SAVE IT
         L     R15,TCTTELCL       R15=LCL MODULE INDEXER
         LARL  R1,=A(P020E)       R1=LCL MODULE PARAMETER LIST
P020EEDF_&SYSNDX DS 0H
         BAKR  0,R15              STACK REGS AND GO
.*
         GEN_CLI P020ENOH,X'FF'   NOHANDLE ?
         JE    P020EBYP_&SYSNDX   EXIT IF IT IS
         OC    EIBRESP,EIBRESP    ANY BAD RESPONSE ?
         JZ    P020EBYP_&SYSNDX   EXIT IF NONE
* INVREQ
         DC    AL3(0),C'ABEND'    MARKER FOR Z390KCP ESTAE
         DC    AL4(P020EBYP_&SYSNDX) ENTRYPOINT FOR IGNORE CONDITION
*
         AIF   (&PARMS_020E).BYP020E
&PARMS_020E SETB 1
         P020E
*
.BYP020E ANOP
P020EBYP_&SYSNDX DS 0H
         MEXIT
         MEND
