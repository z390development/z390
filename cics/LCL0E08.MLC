***********************************************************************
* Copyright 2006 Automated Software Tools Corporation                 *
* This source code is part of z390 assembler/emulator package         *
* The z390 package is distributed under GNU general public license    *
* Author - Don Higgins                                                *
***********************************************************************
* WE CANNOT HAVE A CONVENTIONAL ENTRY
* MUST OBEY THE MACRO RULES, NO EXTRA REGS
LCL0E08  CSECT
*
* FN=0E08  EXEC CICS RETURN
*
* R1  >>> P0E08 BLOCK POINTER
* R13 >>> CURRENT DSA
* R14 = RETURN ADDRESS
         USING (P0E08,P0E08END),R1
         USING *,R12
         B     12(R15)            BYPASS EYECATCHER
         DC    CL8'LCL0E08'
         LR    R0,R12             R0=SAVED R12
         LR    R12,R15            SET BASE
         ST    R1,P0E08PTR        SAVE P0E08 PARM POINTER
         ST    R0,SAVER12         SAVE ORIGINAL R12
         ST    R14,SAVER14        SAVE RETURN ADDRESS
         USING DFHEIBLK,DFHEIBR
         USING DFHTCTTE,TCTTEAR
         USING DFHEISTG,R13
         MVC   EIBFN,P0E08FN      SET EIBFN
         XC    EIBRCODE,EIBRCODE  CLEAR EIBRCODE
         XC    EIBRESP,EIBRESP    CLEAR EIBRESP
         XC    EIBRESP2,EIBRESP2  CLEAR EIBRESP2
         ICM   R15,15,P0E08RSP    R15=RESP ADDRESS
         BZ    NORESP             EXIT IF NONE
         XC    0(4,R15),0(R15)    CLEAR RESP
NORESP   EQU   *
         ICM   R15,15,P0E08RS2    R15=RESP2 ADDRESS
         BZ    NORESP2            EXIT IF NONE
         XC    0(4,R15),0(R15)    CLEAR RESP2
NORESP2  EQU   *
         CLI   P0E08NED,X'FF'     NOEDF REQUESTED ?
         BE    NOEDFB             EXIT IF IT IS
         CLI   TCTTEEDF,X'FF'     EDF ON ?
         BNE   NOEDFB             EXIT IF NOT
         MVI   TCTTEEBA,X'00'     SET 'BEFORE' INTERCEPT
         MVI   TCTTEEDL,X'FF'     SET DIRECT LINKAGE
         L     R15,TCTTEEDA       R15=EDF PROCESSOR ADDRESS
         BAKR  0,R15              STACK REGS AND GO
*
         L     R1,P0E08PTR        RESTORE P0E08 PARM POINTER
NOEDFB   EQU   *
* CHECK TRANSID/COMMAREA/CHANNEL/LENGTH/IMMEDIATE, IF ANY OF THOSE ARE
* SPECIFIED WE ARE NOT DONE, OTHERWISE WE ARE
         OC    P0E08TRN(27),P0E08TRN ANY RETURN FIELDS ?
         BZ    RETDONE            EXIT IF NONE
* IT'S NOT JUST EXEC CICS RETURN
* IF LINK-LEVEL NOT ONE, AND OTHER PARMS, THEN IT'S AN ERROR
         CLC   TCTTELNK,=H'1'     ARE WE AT ROOT LEVEL ?
         BE    TISROOT            EXIT IF WE ARE
         B     INVREQ2            EXIT
*
INVREQ1  EQU   *
         MVC   EIBRESP2,=F'1'     SET EIBRESP2=TRANSID AND NON-TERMINAL
         B     INVREQ             EXIT
*
INVREQ2  EQU   *
* INVOKE INVREQ/2
         MVC   EIBRESP2,=F'2'     SET EIBRESP2=NOT AT HIGHEST LEVEL
INVREQ   EQU   *
         MVI   EIBRCODE,X'E0'     SET EIBRCODE
         MVC   EIBRESP,DFHRESP(INVREQ) SET EIBRESP=INVREQ
         B     DORESP             EXIT
*
CHANERR  EQU   *
         MVI   EIBRCODE,X'DA'     SET EIBRCODE
         MVC   EIBRESP,DFHRESP(CHANNELERR) SET EIBRESP=CHANNELERR
         MVC   EIBRESP2,=F'1'     SET EIBRESP2=INVALID CHANNEL NAME
DORESP   EQU   *
         ICM   R15,15,P0E08RSP    R15=RESP ADDRESS
         BZ    DORESP2            EXIT IF NONE
         MVC   0(4,R15),EIBRESP   SET RESP
DORESP2  EQU   *
         ICM   R15,15,P0E08RS2    R15=RESP2 ADDRESS
         BZ    RETURN             EXIT IF NONE
         MVC   0(4,R15),EIBRESP2  SET RESP2
RETURN   EQU   *
         CLI   P0E08NED,X'FF'     NOEDF ?
         BE    NOEDF2             EXIT IF IT IS
         CLI   TCTTEEDF,X'FF'     EDF ON ?
         BNE   NOEDF2             EXIT IF NOT
         MVI   TCTTEEBA,X'FE'     SET 'AFTER' INTERCEPT (RETURN FAIL)
         MVI   TCTTEEDL,X'FF'     SET DIRECT LINKAGE
         L     R15,TCTTEEDA       R15=EDF PROCESSOR ADDRESS
         BAKR  0,R15              STACK REGS AND GO
*
NOEDF2   EQU   *
         L     R12,SAVER12        RESTORE ORIGINAL R12
         BR    R14                RETURN
*
TISROOT  EQU   *
* EXEC CICS RETURN TRANSID/COMMAREA/LENGTH/CHANNEL/IMMEDIATE
         CLI   TCTTETI,C'!'       NON-TERMINAL ?
         BE    INVREQ1            ERROR IF IT IS
         MVC   TCTTEIMM,P0E08IMM  SET IMMEDIATE FLAG (IF ANY)
         MVC   TCTTETC,P0E08TRN   SET TRANSID
         MVC   TCTTENXT,P0E08TRN  SET NEXT TRANSID FOR ASSIGN
         MVC   TCTTECA,P0E08COM   SET COMMAREA ADDRESS (IF ANY)
         MVC   TCTTECAL,P0E08CLN  SET COMMAREA LENGTH (IF ANY)
         XC    TCTTECHN,TCTTECHN  CLEAR ANY PASSED CHANNEL NAME
         OC    P0E08CHN,P0E08CHN  ANY CHANNEL NAME ?
         BZ    ALLDONE            EXIT IF NONE
* WE HAVE A CHANNEL
* VALIDATE THE NAME, NO LEADING OR EMBEDDED BLANKS,
* TRAILING BLANKS PERMITTED
         CLI   P0E08CHN,C' '      BLANK START ?
         BE    CHANERR            ERROR IF IT IS
         LA    R15,P0E08CHN       R15 POINTS TO CHANNEL NAME
         LA    R0,16              R0=LENGTH
CHANCHAR EQU   *
         LA    R2,CHANTAB         R2 POINTS TO CHAR LIST
CHANLOOP EQU   *
         CLC   0(1,R15),0(R2)     MATCH TO CHAR ?
         BNE   CHANCINC           EXIT IF NOT
         AHI   R15,1              BUMP CHANNEL NAME POINTER
         BCT   R0,CHANCHAR        LOOP THROUGH CHANNEL NAME
         B     CHANDONE           EXIT
*
CHANCINC EQU   *
         AHI   R2,1               BUMP CHAR LIST POINTER
         CLI   0(R2),X'FF'        STOPPER ?
         BNE   CHANLOOP           LOOP IF NOT
* FOUND A BAD CHAR
         CLI   0(R15),C' '        IS IT BLANK ?
         BNE   CHANERR            ERROR IF NOT
* FOUND A BLANK, CHECK FOR TRAILING BLANKS
         LA    R2,P0E08CHN+15     R2=END OF CHANNEL NAME
         SR    R2,R15             R2=LENGTH OF TRAILING CHARS-1
         EX    R2,CHANCLC         TEST FOR TRAILING BLANKS
         BNE   CHANERR            ERROR IF NOT ALL BLANK
CHANDONE EQU   *
* CHANNEL NAME VALID, SEARCH THE STRUCTURE
         MVC   TCTTECHN,P0E08CHN  SET PASSED CHANNEL
         LA    R14,TCTTECAQ       R14=CHANNEL ANCHOR POINTER
         ICM   R2,15,TCTTECAQ     R2=CHANNEL CHAIN ADDRESS
         BZ    CHANNTFD           EXIT IF NONE
         USING DFHCHAN,R2
CHANCHLP EQU   *
* ASSUMING THAT ANY CHANNEL IS UP FOR GRABS
         CLC   CHANNAME,TCTTECHN  MATCH CHANNEL ?
         BE    ALLDONE            EXIT IF IT DOES
         LA    R14,CHANADDR       R14=PREVIOUS POINTER ADDRESS
         ICM   R2,15,CHANADDR     CHAIN TO NEXT
         BNZ   CHANCHLP           LOOP UNTIL CHAIN END
CHANNTFD EQU   *
* CHANNEL NOT FOUND, CREATE EMPTY ONE
         GETMAIN R,LV=26
         LR    R2,R1              SET NEW CHANNEL BASE
         XC    DFHCHAN(26),DFHCHAN CLEAR CHANNEL
         MVC   CHANNAME,TCTTECHN  SET CHANNEL NAME
         ST    R1,0(R14)          SAVE NEW CHANADDR
         MVC   CHANLINK,TCTTELNK  SET LINK LEVEL
         XC    CHANCONT,CHANCONT  CLEAR CONTAINER POINTER
         DROP  R1
         B     ALLDONE            EXIT
*
RETDONE  EQU   *
* EXEC CICS RETURN ONLY
* IC STARTED TASKS MUST NOT TOUCH TCTTETC OR COMMAREA FIELDS
         MVC   TCTTENXT,=4C' '    CLEAR NEXT TRANSID FOR ASSIGN
         XC    TCTTECHN,TCTTECHN  CLEAR ANY PASSED CHANNEL NAME
         CLI   TCTTETYP,C'D'      IC STARTED TASK ?
         BE    XCTLCOMM           EXIT IF IT IS
         CLI   TCTTETYP,C'E'      IC STARTED TASK ?
         BE    XCTLCOMM           EXIT IF IT IS
         XC    TCTTETC,TCTTETC    CLEAR PRESET TRANSID
         XC    TCTTECA(6),TCTTECA CLEAR COMMAREA ADDRESS/LENGTH
XCTLCOMM EQU   *
* ANY XCTL COPIED COMMAREA MUST BE FREED NOW
         CLI   TCTTECND,X'FF'     ANY XCTL COMMAREA TO BE FREED ?
         MVI   TCTTECND,X'00'     CLEAR XCTL INDICATOR
         BNE   ALLDONE            EXIT IF NONE
         LH    R0,EIBCALEN        R0=COMMAREA LENGTH
         FREEMAIN A=DFHEICAP,LV=(R0)
         XC    EIBCALEN,EIBCALEN  CLEAR COMMAREA LENGTH
ALLDONE  EQU   *
*
* THIS USED TO BE KNOWN AS THE MACRO DFHEIRET
*
* FREE ALL AID BLOCKS
*
         USING DFHADBLK,R1
         ICM   R1,15,DFHEIAID     ANY AID BLOCK ADDRESSES ?
         BZ    TRYHCN             EXIT IF NONE
AIDLOOP  EQU   *
         L     R14,AIDCHAIN       R14=NEXT AID BLOCK IF ANY
         LA    R0,AIDLENG         R0=AID BLOCK LENGTH
         FREEMAIN R,A=(R1),LV=(R0)
         LTR   R1,R14             ANY MORE AID BLOCKS ?
         BNZ   AIDLOOP            LOOP IF THERE ARE
         DROP  R1
TRYHCN   EQU   *
*
* FREE ALL HANDLE CONDITION BLOCKS
*
         USING DFHHCBLK,R1
         ICM   R1,15,DFHEIHCN     ANY CONDITION BLOCK ADDRESSES ?
         BZ    TRYRTNCM           EXIT IF NONE
HCNLOOP  EQU    *
         L     R14,HCNCHAIN       R14=NEXT CONDITION BLOCK IF ANY
         LA    R0,HCNLENG         R0=CONDITION BLOCK LENGTH
         FREEMAIN R,A=(R1),LV=(R0)
         LTR   R1,R14             ANY MORE CONDITION BLOCKS ?
         BNZ   HCNLOOP            LOOP IF THERE ARE
         DROP  R1                 DROP HANDLE BLOCK BASE
TRYRTNCM EQU   *
*
* IF THE RETURN COMMAREA IS WITHIN THE DSA WE MUST COPY IT NOW
*
         L     R1,TCTTECA         R1=COMMAREA ADDRESS
         LR    R15,R13            R15=DSA ADDRESS
         CR    R1,R15             HIGHER THAN DSA ADDRESS ?
         BL    NOTDSACM           EXIT IF NOT IN DSA
         A     R15,DFHEILEN       R15=BYTE AFTER DSA
         CR    R1,R15             HIGHER THAN DSA+LENGTH ADDRESS ?
         BNL   NOTDSACM           EXIT IF NOT IN DSA
* COPY THE COMMAREA
         LH    R1,TCTTECAL        R1=COMMAREA LENGTH
         L     R14,TCTTECA        R14=COMMAREA ADDRESS FOR MOVE
         GETMAIN R,LV=(R1),A=TCTTECA
         LH    R15,TCTTECAL       R15=COMMAREA LENGTH FOR MOVE
         LR    R1,R0              R1=DESTINATION LENGTH
         L     R0,TCTTECA         R0=DESTINATION ADDRESS
         MVCL  R0,R14             COPY DATA
NOTDSACM EQU   *
*
* FREE THE DSA
*
         FREEMAIN R,A=(R13),LA=DFHEILEN
*
* CLEAR HANDLE ABEND ENTRY IF ANY
*
         ICM   R1,15,TCTTEABD     ANY ABEND BLOCK ?
         BZ    CLRCHN             EXIT IF NONE
         LH    R14,TCTTELNK       LOAD LINK-LEVEL
         AHI   R14,-1             -1 FOR INDEX
         MS    R14,=AL4(ABDLENG)  * ENTRY LENGTH
         AR    R14,R1             R14=CORRECT ABEND BLOCK ENTRY
         USING ABDDEST,R14
         XC    ABDDEST(ABDLENG),ABDDEST CLEAR ABEND ENTRY
         DROP  R14
CLRCHN   EQU   *
* DELETE ALL CHANNELS WITH A LINK-LEVEL EQUAL TO THE CURRENT
* EXCEPT A RETURN CHANNEL
         ICM   R2,15,TCTTECAQ     R2=CHANNEL CHAIN ADDRESS
         BZ    CLRLVL             EXIT IF NONE
CLRCHNLP EQU   *
         CLC   CHANLINK,TCTTELNK  CHANNEL AT OUR LINK-LEVEL ?
         BNE   CLRCHNCH           EXIT IF NOT
         CLC   CHANNAME,TCTTECHN  MATCH RETURN CHANNEL ?
         BE    CLRCHNCH           EXIT IF IT DOES
         EXEC  CICS DELETE CONTAINER('*') CHANNEL(CHANNAME) NOEDF
CLRCHNCH EQU   *
         ICM   R2,15,CHANADDR     CHAIN TO NEXT
         BNZ   CLRCHNLP           LOOP UNTIL CHAIN END
CLRLVL   EQU   *
*
* CLEAR OUR LEVEL
* EXIT TO LAST LINKER
* THIS RETURNS DIRECTLY TO THE CALLED PROGRAM, NOT TO THE LCL MODULE
* AT ROOT LEVEL RETURN IS TO Z390KCP/TASKRETN
*
         LH    R0,TCTTELNK        LOAD LINK-LEVEL
         SLL   R0,2               R0=LINK-LEVEL * 4
         L     R1,TCTTELKA        R1=LKA ADDRESS
         AR    R1,R0              INDEX TO LINK-LEVEL ENTRY
         XC    0(4,R1),0(R1)      CLEAR OUR LEVEL
*
* ADJUST LINK LEVEL
*
         LH    R0,TCTTELNK        LOAD LINK-LEVEL
         AHI   R0,-1              -1
         STH   R0,TCTTELNK        STORE BACK
         AHI   R1,-4              POINT TO PREV LEVEL
         L     R13,0(R1)          R13=PREV DSA ADDRESS
* IF WE HAVE A RETURN WITHOUT CHANNEL, THEN RESTORE THE CURRENT CHANNEL
* NAME FROM THE CALLERS DSA
         L     R1,P0E08PTR        RESTORE P0E08 PARM POINTER
         USING (P0E08,P0E08END),R1
         OC    P0E08CHN,P0E08CHN  ANY CHANNEL NAME ?
         BNZ   GOTCHAN            EXIT IF WE HAVE
         LTR   R0,R0              AT LEVEL 0 ?
         BZ    GOTCHAN            EXIT IF WE ARE
         MVC   TCTTECHN,DFHEICHN  RESTORE CALLERS CHANNEL NAME
GOTCHAN  EQU   *
* PROGRAM/TASK TERMINATION EDF
         CLI   P0E08NED,X'FF'     NOEDF REQUESTED ?
         BE    NOEDFA             EXIT IF IT IS
         CLI   TCTTEEDF,X'FF'     EDF ON ?
         BNE   NOEDFA             EXIT IF NOT
         CLC   EIBPROG,=C'Z390CEDF' IS IT Z390CEDF ?
         BE    NOEDFA             NO EDF IF IT IS
         MVI   TCTTEEBA,X'FF'     SET 'AFTER' INTERCEPT
         MVI   TCTTEEDL,X'FF'     SET DIRECT LINKAGE
         L     R15,TCTTEEDA       R15=EDF PROCESSOR ADDRESS
         BAKR  0,R15              STACK REGS AND GO
*
         L     R1,P0E08PTR        RESTORE P0E08 PARM POINTER
NOEDFA   EQU   *
         L     R14,SAVER14        R14=ORIGINAL RETURN ADDRESS
         CLI   P0E08CNR,X'FF'     IS IT CLEANER RETURN ?
         BER   R14                DIRECT RETURN IF IT IS
         RETURN (R14,R12)
*
CHANCLC  CLC   0(0,R15),=15C' '   TEST FOR TRAILING BLANKS
*
P0E08PTR DS    F                  SAVE P0E08 PARM POINTER
SAVER12  DS    F                  SAVE R12
SAVER14  DS    F                  SAVE R14
CHANTAB  DC    C'ABCDEFGHIJKLMNOPQRSTUVWXYZ'
         DC    C'abcdefghijklmnopqrstuvwxyz'
         DC    C'0123456789'
         DC    C'$@#/%&&?!:|"=ª,;<>.-_'
         DC    X'FF'              STOPPER
*
         P0E08 DSECT
*
         DFHEIBLK                 EIB
         DFHTCTTE                 TCTTE
         DFHCHAN                  CHANNEL/CONTAINER STRUCTURES
         DFHEISTG                 DSA
         DFHADBLK                 HANDLE AID BLOCK
         DFHHCBLK                 HANDLE CONDITION BLOCK
         DFHABBLK                 HANDLE ABEND BLOCK
*
         EQUREGS REGS=GPR
*
         END
