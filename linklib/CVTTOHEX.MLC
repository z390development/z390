*********************************************************************
* z390 - Mainframe assembler emulator and run-time engine
* Copyright (C) 2021 z390 Assembler LLC
*
* This file is part of z390.
*
* z390 is free software; you can redistribute it and/or modify
* it under the terms of the GNU General Public License as published by
* the Free Software Foundation; either version 2 of the License, or
* (at your option) any later version.
* z390 is distributed in the hope that it will be useful,
* but WITHOUT ANY WARRANTY; without even the implied warranty of
* MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
* GNU General Public License for more details.
*
* You should have received a copy of the GNU General Public License 
* along with this program; if not, see https://www.gnu.org/licenses.
*********************************************************************
* Author - Don Higgins                                              *
* Date   - 07/20/06                                                 *
* 07/20/06 RPI 374 - add CVTTOHEX for use in TESTFPC1 
* 05/07/07 RPI 609 - fix DC CL9                         
* 08/12/07 RPI 674 - use single TROT to replace UNPK and TR loop
*********************************************************************
*
*  CALL CVTTOHEX,(IN,OUT,INLEN)
*
*    IN    - ADDR INPUT BYTES
*    OUT   - ADDR OUTPUT HEX EBCDIC BYTES
*    INLEN - COUNT OF BYTES TO CONVERT
*********************************************************************
         COPY  ASMMSP       STRUCTURED PROGRAM MACROS AND EQUS
CVTTOHEX SUBENTRY
         LM    R3,R5,0(R1)  R3=IN, R4=OUT, R5=LENGTH OF INPUT
         LA    R1,TROTTAB 512 BYTE HEX TRANSLATE TABLE FOR TROT
         TROT  R4,R3,1 TRANS ONE TO TWO FROM (R3) TO (R4) NO TESTING
         SUBEXIT
         PRINT DATA
TROTTAB  EQU   *
&HEX(1)  SETC  '0','1','2','3','4','5','6','7','8',                    X
               '9','A','B','C','D','E','F'
&I       SETA  1
&J       SETA  1
.LOOP    ANOP
&CHARS   SETC  '&CHARS&HEX(&I)&HEX(&J)'
&J       SETA  &J+1
         AIF   (&J LE 16).LOOP
         DC    C'&CHARS'
&CHARS   SETC  ''
&J       SETA  1
&I       SETA  &I+1
         AIF   (&I LE 16).LOOP
         LTORG
         EQUREGS
         END
