         TITLE 'RTGENBAT - REGRESSION TEST COMPARISON BAT GENERATOR'
*********************************************************************
* z390 - Mainframe assembler emulator and run-time engine
* Copyright (C) 2021 z390 Assembler LLC
*
* This file is part of z390.
*
* z390 is free software; you can redistribute it and/or modify
* it under the terms of the GNU General Public License as published by
* the Free Software Foundation; either version 2 of the License, or
* (at your option) any later version.
* z390 is distributed in the hope that it will be useful,
* but WITHOUT ANY WARRANTY; without even the implied warranty of
* MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
* GNU General Public License for more details.
*
* You should have received a copy of the GNU General Public License 
* along with this program; if not, see https://www.gnu.org/licenses.
*********************************************************************
* Author - Don Higgins                                              *
* Date   - 08/12/06                                                 *
* 08/12/06 RPI 363 REPACKAGE MVS_MACLIB.ZIP AND Z390_RT.ZIP RT TESTS
* 04/19/08 RPI 833 add SETC quotes for HLASM compatibility               
* 09/05/08 RPI 797 upgrade to use ZSTRMAC structruced programming
* 09/18/08 RPI 911 replace ASELECT>ACASE, APM>ACALL               
* 12/11/08 RPI 962 replace pause for missing file with missing filename
*********************************************************************
*
*  RTGENBAT READS FILE &SYSPARM.DAT AND GENERATES &SYSPARM.PCH
*
*  The input txt file is sorted directory list of regression TEST
*  source and generated files directory plus SAVE directory.  There
*  should be 2 duplicate entries of each file to be compared.  Any
*  file which does not have match will generate:     
*     copy missing %3missing_filename.
*  Matching files will generate:
*     fc %1file %2file > %3file. 
*
*********************************************************************
         :&LAST_FILE SETC '(FILE EMPTY)' PREVIOUS FILE
         :&BREAK   SETB  1 STARTING NEW MATCH
         :&TOT_COMP SETA 0
         :&TOT_ERR  SETA 0
         :&LAST_FILE SETC ''
         :&NEXT_FILE SETC ''
         :&DUP       SETA 0 
         ACALL   READ_NEXT_FILE
         AWHILE ('&NEXT_FILE' NE '')
               ACTR 100
               AIF ('&NEXT_FILE' EQ '&LAST_FILE')
                   :&DUP SETA &DUP+1
                   AIF  (&DUP EQ 2)
                       ACALL COMP_FILES
                   AEND
               AELSE
                   ACALL CHK_DUPS
                   :&LAST_FILE SETC '&NEXT_FILE'
                   :&DUP SETA 1
               AEND
               ACALL READ_NEXT_FILE
         AEND
         ACALL   CHK_DUPS
         PUNCH 'REM TOT COMPARES = &TOT_COMP  TOT ERRORS =&TOT_ERR',   X
               DSNAME='&SYSPARM'
         AIF   (&TOT_ERR GT 0)
               MNOTE *,'RTGENCOM FILE ERRORS FOUND - SEE RTCOMP4.BAT'
         AEND
         PUNCH 'COPY RTGENCMP.BAT %3',                                 X
               DSNAME='&SYSPARM'
.*
.* READ_NEXT_FILE
.*
         AENTRY READ_NEXT_FILE    
         :&NEXT_FILE AREAD DSNAME='&SYSPARM'
         AEND  
.*
.* CHK_DUPS
.*
         AENTRY CHK_DUPS
         AIF   (&DUP NE 2)
               AIF (&DUP GT 0)
                   ACALL MISSING_FILE_MSG
               AEND
         AEND
         AEND
.*
.* MISSING_FILE_MSG
.*
         AENTRY MISSING_FILE_MSG
         :&TOT_ERR SETA &TOT_ERR+1
         :&MSG SETC 'MISSING FILE FOR &LAST_FILE'
         PUNCH 'REM &MSG',DSNAME='&SYSPARM'
         PUNCH 'COPY linklib\MISSING.TXT %3\MISSING_&LAST_FILE',DSNAME=X
               '&SYSPARM'
         MNOTE *,'RTGENBAT &MSG'
         AEND
.*
.* COMP_FILES
.*
         AENTRY COMP_FILES
         :&TOT_COMP SETA &TOT_COMP+1
         PUNCH 'FC %1\&NEXT_FILE %2\&NEXT_FILE > %3\&NEXT_FILE',       X
               DSNAME='&SYSPARM'
         AEND      
         END
