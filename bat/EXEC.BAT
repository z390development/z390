@if /I "%1" == "tron" (echo on) else (echo off)
rem exec execute 390 load module

setlocal
if /I "%1" == "tron" (set z_TraceMode=tron
                      shift /1
              ) else (if /I "%1" == "troff" (set z_TraceMode=troff
                                             shift /1
                                     ) else (set z_TraceMode=)
                      )
set /A z_NestLevel=%z_NestLevel%+1
rem ----- Lvl(%z_NestLevel%) Start %0 %1 %2 %3 %4 %5 %6 %7 %8 %9

if /I %1. == . (set /P z_file=Hit Enter for help or supply name of 390 file to execute: 
        ) else (set z_file=%1)
if /I %z_file%. == . goto help
:chkfile
rem convert Unix path notation to Windows before checking the file
set z_file=%z_file:/=\%
call %~dps0fparts.bat %z_TraceMode% %z_file%
set z_file=%z_fdrv%%z_fpath%%z_fname%
if exist %z_file%%z_fext% goto file_ok
rem if extension specified: error because no default applies
if /I %z_fext%. NEQ . goto notfound
rem try with default extension of 390
set z_fext=.390
if exist %z_file%%z_fext% goto file_ok
:notfound
echo Error: Source file %z_file%%z_fext% does not exist
goto retry

:file_ok
call %~dps0ez390 %z_TraceMode% %z_file%%z_fext% %2 %3 %4 %5 %6 %7 %8 %9
set z_ReturnCode=%ERRORLEVEL%
rem any error or warning message has been issued by ez390

:return
rem ----- Lvl(%z_NestLevel%)  End %0 %1 %2 %3 %4 %5 %6 %7 %8 %9
exit /b %z_ReturnCode%

:help
echo .
echo the EXEC.BAT procedure is intended to execute a single program
echo .
:retry
set z_file=
set /P z_file=Hit Enter for more help or supply name of 390 file to execute: 
if /I %z_file%. NEQ . goto chkfile

call %~dps0help %z_TraceMode%
set z_ReturnCode=16
goto return
