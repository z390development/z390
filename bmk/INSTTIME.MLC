INSTTIME SUBENTRY
*
* AUTHOR:       MELVYN MALTZ
* Z390 VERSION: V1.3.08
* INSTTIME VERSION: 10
*
* CALL MZ390 MUST HAVE "SYSPARM(drive:\pathname)"
* ASSEMBLE WITH OPTION: ASCII
* EXECUTE WITH OPTIONS: ASCII NOTIME MEM(5)
*
* TO DO
*    ADD RC TESTS FOR OUTPUT CFD/CTD
*    TEST IF LD ETC FUNDAMENTAL, TIMES DIFF IF HFP/BFP/DFP
*    CC TESTING
*
         LARL  R3,PARMSIN         ADDRESS PARM I/O AREA
         USING INSTDSCT,R3
         LARL  R8,RESULTS         ADDRESS RESULT I/O AREA
         USING RSLTDSCT,R8
*
         OPEN  (PARMS,INPUT,TEXTOUT,OUTPUT)
         MVC   RSLTTITL(16),VER   MARK RELEASE CHECKED
         BAL   R14,OUTMSG         WRITE AND CLEAR
* CC=0 AND TURN ALL EXCEPTIONS OFF
         SR    R7,R7              R7=0
         SPM   R7                 SET CC=0, ALL EXCEPTIONS OFF
* TIME A BCT
* THE SETUP OF THE 2ND TIME MACRO AND SOME OF THE SVC IS INCLUDED
*
* DEFAULT BCT TIMING (5000000)
         L     R7,BCTSPIN         LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
BCTTIMER BCT   R7,BCTTIMER        DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
*
         LG    R7,TIMEAFT         END TIME
         SG    R7,TIMEBEF                  - BEG TIME
         STG   R7,TIMEBCT         SAVE IT FOR USE
         STG   R7,ORIGBCT         SAVE IT FOR SCALING
* SCALE THE BCT TIME ACCORDINGLY
         SGR   R10,R10            CLEAR R10
         LG    R11,ORIGBCT        ORIGINAL BCT TIME
         MSGF  R11,SPINCNT        * NEW SPIN COUNT
         DSGF  R10,BCTSPIN        / OLD SPIN COUNT
         STG   R11,TIMEBCT        SAVE NEW VALUE
         MVC   RSLTOPCD(9),=C'BCT TIMER' DUMMY THE BCT
         LA    R4,4               INST LENGTH
         LA    R5,BCTTIMER        INST ADDRESS
         BAL   R14,OUTPUT         WRITE THE OUTPUT RECORD
         MVI   BCT1OFF+1,X'00'    BYPASS -BCT TIMING
* GETMAINS FOR STATS OPTION
* HOLDING PL6 PER TIME, 500 SLOTS, CAN EXTEND IF NEEDED
* IF SLOTS MORE THAN 999, CHANGE MEAN SUBROUTINE
         GETMAIN R,LV=3000        TIMES COLLECTION AREA
         ST    R1,STATADR1        SAVE STATS ORIGIN
         ST    R1,STATNXT1        SAVE STATS NEXT SLOT
* HOLDING PL4'L1',PL4'L2' OR PL4'LL',PL4'0'
         GETMAIN R,LV=4000        LENGTHS COLLECTION AREA
         ST    R1,STATADR2        SAVE STATS ORIGIN
         ST    R1,STATNXT2        SAVE STATS NEXT SLOT
* ###    LARL  R1,INSTABND        ABEND INTERCEPT ADDRESS
* ###    ESTAE (1)
*
GET      EQU   *
         GET   PARMS              GET PARM
         CLI   INSTFMAT,C'*'      COMMENT ?
         BE    GET                YES, MISS IT
         CLC   INSTFMAT(11),=C'STATS PRINT' CALC STATS ?
         BNE   NOSTPRNT           EXIT IF NOT
         BAL   R14,MVSD           DO IT
         B     GET                GET ANOTHER PARM
*
NOSTPRNT EQU   *
         CLC   INSTFMAT(9),=C'LINREG/L1' CALC LINEAR REGRESSION ?
         BE    LINGO              EXIT IF IT IS
         CLC   INSTFMAT(9),=C'LINREG/L2' CALC LINEAR REGRESSION ?
         BE    LINGO              EXIT IF IT IS
         CLC   INSTFMAT(9),=C'LINREG/SL' CALC LINEAR REGRESSION ?
         BE    LINGO              EXIT IF IT IS
         CLC   INSTFMAT(9),=C'LINREG/LL' CALC LINEAR REGRESSION ?
         BNE   NOTLINRG           EXIT IF NOT
LINGO    EQU   *
         BAL   R14,LINREG         DO IT
         B     GET                GET ANOTHER PARM
*
NOTLINRG EQU   *
         CLC   INSTFMAT(8),=C'STATS ON' START STATS COLLECTION ?
         BNE   NOTSTTON           EXIT IF NOT
         MVI   STATSIND,X'FF'     SET STATS INDICATOR
         MVC   RSLTTITL(8),INSTFMAT MOVE STAT PARM
         BAL   R14,OUTMSG         WRITE AND CLEAR
         B     GET                GET ANOTHER PARM
*
NOTSTTON EQU   *
         CLC   INSTFMAT(9),=C'STATS OFF' END STATS COLLECTION ?
         BNE   NOTSTTOF           EXIT IF NOT
         MVI   STATSIND,X'00'     RESET STATS INDICATOR
         MVC   STATNXT1,STATADR1  RESET NEXT TIME SLOT ADDRESS
         MVC   STATNXT2,STATADR2  RESET NEXT LENGTHS SLOT ADDRESS
         XC    STATCNT,STATCNT    CLEAR STAT COUNT
         MVC   RSLTTITL(9),INSTFMAT MOVE STAT PARM
         BAL   R14,OUTMSG         WRITE AND CLEAR
         B     GET                GET ANOTHER PARM
*
NOTSTTOF EQU   *
         CLC   INSTFMAT(5),=C'BCTS=' ADJUST BCT SPIN COUNT ?
         BNE   NOTBCTS            EXIT IF NOT
* PROCESS BCTS=
         PACK  DEC+8(8),INSTFMAT+5(7) PACK SPIN COUNT
         CVB   R7,DEC+8           CONVERT TO BINARY
         ST    R7,BCTSPIN         SAVE IN HEX
         MVC   BCTSDEC,DEC+8      AND DECIMAL
* RETIME THE BCT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
BCTRETIM BCT   R7,BCTRETIM        DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
*
         LG    R7,TIMEAFT         END TIME
         SG    R7,TIMEBEF                  - BEG TIME
         STG   R7,ORIGBCT         SAVE IT FOR SCALING
* RESCALE THE BCT TIME
         SGR   R10,R10            CLEAR R10
         LGR   R11,R7             ORIGINAL BCT TIME
         MSGF  R11,SPINCNT        * NEW SPIN COUNT
         DSGF  R10,BCTSPIN        / OLD SPIN COUNT
         STG   R11,TIMEBCT        SAVE NEW VALUE
*
         MVC   RSLTTITL(12),INSTFMAT MOVE SPIN PARM
         BAL   R14,OUTMSG         WRITE AND CLEAR
         MVC   RSLTOPCD(3),=C'BCT' DUMMY THE BCT
         LA    R4,4               INST LENGTH
         LA    R5,BCTRETIM        INST ADDRESS
         MVI   BCT1OFF+1,X'F0'    SET BYPASS OF -BCT TIMING
         BAL   R14,OUTPUT         WRITE THE OUTPUT RECORD
         MVI   BCT1OFF+1,X'00'    ONE OFF BYPASS OF -BCT TIMING
         B     GET                GET ANOTHER PARM
*
NOTBCTS  EQU   *
         CLC   INSTFMAT(5),=C'SPIN=' ADJUST SPIN COUNT ?
         BNE   NOTSPIN            EXIT IF NOT
* PROCESS SPIN=
         PACK  DEC,INSTFMAT+5(7)  PACK SPIN COUNT
         CVBG  R7,DEC             CONVERT TO BINARY
         ST    R7,SPINCNT         SAVE IN HEX
         MVC   SPINDEC,DEC+8      AND DECIMAL
* SCALE THE BCT TIME ACCORDINGLY
         SGR   R10,R10            CLEAR R10
         LG    R11,ORIGBCT        ORIGINAL BCT TIME
         MSGF  R11,SPINCNT        * NEW SPIN COUNT
         DSGF  R10,BCTSPIN        / OLD SPIN COUNT
         STG   R11,TIMEBCT        SAVE NEW VALUE
         MVC   RSLTTITL(12),INSTFMAT MOVE SPIN PARM
         BAL   R14,OUTMSG         WRITE AND CLEAR
         B     GET                GET ANOTHER PARM
*
NOTSPIN  EQU   *
* MUST BE AN INSTRUCTION
         LARL  R2,TYPETABL        ADDRESS TABLE
         USING TYPEDSCT,R2
TYPELOOP EQU   *
         CLC   TYPEFMAT,INSTFMAT  MATCHED FORMAT ?
         BE    FMTOK              YES, GO DO IT
         AHI   R2,8               BUMP TABLE ENTRY
         CLI   TYPEFMAT,X'FF'     END OF TABLE ?
         BNE   TYPELOOP           LOOP IF NOT
         MVC   RSLTTITL(12),INSTFMAT MOVE PART OF INPUT
         MVC   RSLTMICS(12),=C'FORMAT ERROR'
         BAL   R14,OUTMSG         WRITE AND CLEAR
         B     GET                EXIT TO GET ANOTHER TEST
*
FMTOK    EQU   *
         L     R12,TYPEPROC       LOAD PROCESSING ROUTIME ADDRESS
         BR    R12                AND GO THERE
*
EOPARMS  EQU   *
* END OF INPUT
         CLOSE (PARMS,,TEXTOUT)   CLOSE
         SUBEXIT
VER      DC    C'VER='
         DC    CL12'&SYSVER'
         LTORG
***************
* SUBROUTINES *
***************
SRCHOPCD DS    0F
         MVC   RSLTOPCD,INSTOPCD  MOVE THE OPCODE TO OUTPUT
         MVC   RSLTTYPE,INSTTYPE  MOVE THE TYPE TO OUTPUT
         MVC   RSLTSTOR,INSTSTOR  MOVE THE STORE INDICATOR TO OUTPUT
* SEARCH SINGLE BYTE TABLE
         LARL  R7,OPCD1TAB        ADDRESS SINGLE OPCODE TABLE
SRCHLOP1 EQU   *
         CLC   0(6,R7),INSTOPCD   MATCH ?
         BER   R14                YES, RETURN
         AHI   R7,7               BUMP
         CLI   0(R7),X'FF'        END OF TABLE ?
         BNE   SRCHLOP1           LOOP IF NOT
* SEARCH DOUBLE BYTE TABLE
         LARL  R7,OPCD2TAB        ADDRESS DOUBLE OPCODE TABLE
SRCHLOP2 EQU   *
         CLC   0(6,R7),INSTOPCD   MATCH ?
         BER   R14                YES, RETURN
         AHI   R7,8               BUMP
         CLI   0(R7),X'FF'        END OF TABLE ?
         BNE   SRCHLOP2           LOOP IF NOT
         MVC   RSLTMICS(12),=C'OPCODE ERROR'
         BAL   R14,OUTMSG         WRITE AND CLEAR
         B     GET                EXIT TO GET ANOTHER TEST
*
OUTPUT   DS    0F
* INPUT: R2=INSTRUCTION FORMAT
*        R4=INSTRUCTION LENGTH
*        R5=ADDRESS OF INSTRUCTION
*        R6=FOR TYPE=P POINTS TO PACKED FIELD
*        R7=GENERAL WORK
*        R10-11=DUMP CONTENTS FOR SOME INST TYPES
*        F0-F1 =DUMP CONTENTS IN DECIMAL FOR FLOATING POINT
         LG    R7,TIMEAFT         END TIME
         SG    R7,TIMEBEF                  - BEG TIME
BCT1OFF  B     DONTBCT            BYPASS FOR BCT TIME
         SG    R7,TIMEBCT                  - BCT TIME
         SG    R7,TIMEXTRA                 - SPECIAL TIME
         XC    TIMEXTRA,TIMEXTRA  CLEAR SPECIAL TIME
DONTBCT  EQU   *
         CVDG  R7,DEC             INTO DECIMAL
         SRP   DEC,3,0            GET 5.6 DECIMAL
         CLI   BCT1OFF+1,X'F0'    BCT SPIN MODE ?
         BNE   INSMODE            EXIT IF NOT
         DP    DEC,BCTSDEC+4(4)   QQQQQQQQQQQQRRRR
         B     OUTGO              EXIT
INSMODE  EQU   *
         DP    DEC,SPINDEC+4(4)   QQQQQQQQQQQQRRRR
OUTGO    EQU   *
         CLI   STATSIND,X'FF'     IS STATS ON ?
         BNE   OUTNOST            EXIT IF NOT
* STATS MODE
* SAVE TIME
         L     R7,STATNXT1        R7=NEXT STATS SLOT
         MVC   0(6,R7),DEC+6      SAVE IN NEXT SLOT
         AHI   R7,6               BUMP SLOT
         ST    R7,STATNXT1        SAVE NEXT TIME SLOT ADDRESS
         L     R7,STATCNT         CURRENT STAT COUNT
         AHI   R7,1               +1
         ST    R7,STATCNT         SAVE STAT COUNT
* SAVE LENGTHS
*      L1 ONLY ON LM/STM AND SS2 TYPE
*      L2 ONLY ON SS2 TYPE, SRP AND TP ARE EXCEPTIONS
*      LL ON MVC/CLC AND TYPES SS1 AND SVC
         L     R7,STATNXT2        R7=NEXT LENGTHS SLOT
         CLC   INSTOPCD(2),=C'LM' LM INSTS ?
         BE    OUTLL1             EXIT IF IT IS
         CLC   INSTOPCD(3),=C'STM' STM INSTS ?
         BE    OUTLL1             EXIT IF IT IS
         CLC   INSTFMAT(3),=C'SS2' SS2 TYPE ?
         BNE   OUTTRYL2           EXIT IF NOT
OUTLL1   EQU   *
         PACK  DEC2,INSTLL1       PACK L1
         MVC   0(4,R7),DEC2+12    SAVE IN NEXT SLOT
OUTTRYL2 EQU   *
         CLC   INSTFMAT(3),=C'SS2' SS2 TYPE ?
         BNE   OUTTRYLL           EXIT IF NOT
         CLC   INSTOPCD(3),=C'SRP' SRP INST ?
         BE    OUTLENOK           EXIT IF IT IS
         CLC   INSTOPCD(2),=C'TP' TP INST ?
         BE    OUTLENOK           EXIT IF IT IS
         PACK  DEC2,INSTLL2       PACK L2
         MVC   4(4,R7),DEC2+12    SAVE IN NEXT SLOT
         B     OUTLENOK           EXIT
OUTTRYLL EQU   *
         CLC   INSTOPCD(3),=C'MVC' MVC INSTS ?
         BE    OUTLL2             EXIT IF IT IS
         CLC   INSTOPCD(3),=C'CLC' CLC INSTS ?
         BE    OUTLL2             EXIT IF IT IS
         CLC   INSTOPCD(4),=C'CUSE' CUSE INST ?
         BE    OUTLL2             EXIT IF IT IS
         CLC   INSTOPCD(4),=C'CLST' CLST INST ?
         BE    OUTLL2             EXIT IF IT IS
         CLC   INSTOPCD(4),=C'SRST' SRST INST ?
         BE    OUTLL2             EXIT IF IT IS
         CLC   INSTOPCD(2),=C'TR' TR INSTS ?
         BE    OUTLL2             EXIT IF IT IS
         CLC   INSTFMAT(3),=C'SS1' SS1 TYPE ?
         BE    OUTLL2             EXIT IF IT IS
         CLC   INSTFMAT(3),=C'SVC' SVC TYPE ?
         BNE   OUTLENOK           EXIT IF NOT
OUTLL2   EQU   *
         PACK  DEC2,INSTLL        PACK LL
         MVC   0(4,R7),DEC2+12    SAVE IN NEXT SLOT
         CLC   INSTOPCD(4),=C'CUSE' CUSE INST ?
         BNE   OUTLENOK           EXIT IF NOT
         PACK  DEC2,INSTSL        PACK SL
         MVC   4(4,R7),DEC2+12    SAVE IN NEXT SLOT
OUTLENOK EQU   *
         AHI   R7,8               BUMP SLOT
         ST    R7,STATNXT2        SAVE NEXT LENGTHS SLOT ADDRESS
OUTNOST  EQU   *
         MVC   RSLTMICS,=X'4020202021204B202020202020'
         ED    RSLTMICS,DEC+6     EDIT uS
         UNPK  WORK,0(7,R5)       UNPACK INST
         TR    WORK(12),TABLE-C'0' TRANSLATE TO ASCII HEX
         AR    R4,R4              LENGTH DOUBLED
         BCTR  R4,0               -1 FOR EXECUTE
         EX    R4,OUTINST         MOVE THE INST TO RESULT
*
         CLC   INSTOPCD(4),=C'LGDR' LGDR INST ?
         BE    OUTREG64           EXIT IF IT IS
         CLC   INSTOPCD(5),=C'CSDTR' CSDTR INST ?
         BE    OUTREG64           EXIT IF IT IS
         CLC   INSTOPCD(5),=C'CSXTR' CSXTR INST ?
         BE    OUTREG64           EXIT IF IT IS
         CLC   INSTOPCD(5),=C'EEDTR' EEDTR INST ?
         BE    OUTREG64           EXIT IF IT IS
         CLC   INSTOPCD(5),=C'EEXTR' EEXTR INST ?
         BE    OUTREG64           EXIT IF IT IS
         CLC   INSTOPCD(5),=C'ESDTR' ESDTR INST ?
         BE    OUTREG64           EXIT IF IT IS
         CLC   INSTOPCD(5),=C'ESXTR' ESXTR INST ?
         BE    OUTREG64           EXIT IF IT IS
         CLC   INSTOPCD(5),=C'CGDTR' CGDTR INST ?
         BE    OUTREG64           EXIT IF IT IS
         CLC   INSTOPCD(5),=C'CGXTR' CGXTR INST ?
         BE    OUTREG64           EXIT IF IT IS
         CLC   INSTOPCD(5),=C'CUDTR' CUDTR INST ?
         BE    OUTREG64           EXIT IF IT IS
         CLC   INSTOPCD(5),=C'CUXTR' CUXTR INST ?
         BE    OUTREG64           EXIT IF IT IS
         CLI   INSTTYPE+1,C'P'    FLOATING POINT ?
         BE    OUTFP              EXIT IF IT IS
         CLC   INSTFMAT(2),=C'RI' IS IT RI/RIL ?
         BE    OUTREG64           EXIT IF IT IS
         CLC   INSTFMAT(3),=C'RRE' IS IT RRE ?
         BE    OUTREG64           EXIT IF IT IS
         CLC   INSTFMAT(3),=C'RRF' IS IT RRF ?
         BE    OUTREG64           EXIT IF IT IS
         CLC   INSTFMAT(3),=C'RSY' IS IT RSY ?
         BE    OUTREG64           EXIT IF IT IS
         CLC   INSTFMAT(3),=C'RXY' IS IT RXY ?
         BE    OUTREG64           EXIT IF IT IS
         CLC   INSTFMAT(3),=C'SVC' IS IT SVC ?
         BNE   NOT64R             EXIT IF NOT
OUTREG64 EQU   *
* 64-BIT REG, DUMP R10,R11
         STG   R10,REGSAVE        SAVE R10
         UNPK  WORK(9),REGSAVE(5) UNPACK R10 HI
         TR    WORK(8),TABLE-C'0' TRANSLATE TO ASCII HEX
         MVC   RSLTR10G(8),WORK   MOVE TO OUTPUT AREA
         UNPK  WORK(9),REGSAVE+4(5) UNPACK R10 LO
         TR    WORK(8),TABLE-C'0' TRANSLATE TO ASCII HEX
         MVC   RSLTR10G+8(8),WORK MOVE TO OUTPUT AREA
         STG   R11,REGSAVE        SAVE R11
         UNPK  WORK(9),REGSAVE(5) UNPACK R11
         TR    WORK(8),TABLE-C'0' TRANSLATE TO ASCII HEX
         MVC   RSLTR11G(8),WORK   MOVE TO OUTPUT AREA
         UNPK  WORK(9),REGSAVE+4(5) UNPACK R11 LO
         TR    WORK(8),TABLE-C'0' TRANSLATE TO ASCII HEX
         MVC   RSLTR11G+8(8),WORK MOVE TO OUTPUT AREA
         B     OUTMSG             EXIT
*
NOT64R   EQU   *
         CLI   INSTFMAT,C'R'      IS IT RR/RX ?
         BNE   NOTREG             EXIT IF NOT
* 32-BIT REG, DUMP R10,R11
         ST    R10,REGSAVE        SAVE R10
         UNPK  WORK(9),REGSAVE(5) UNPACK R10
         TR    WORK(8),TABLE-C'0' TRANSLATE TO ASCII HEX
         MVC   RSLTR10,WORK       MOVE TO OUTPUT AREA
         ST    R11,REGSAVE        SAVE R11
         UNPK  WORK(9),REGSAVE(5) UNPACK R11
         TR    WORK(8),TABLE-C'0' TRANSLATE TO ASCII HEX
         MVC   RSLTR11,WORK       MOVE TO OUTPUT AREA
         B     OUTMSG             EXIT
*
NOTREG   EQU   *
         CLC   INSTTYPE(2),=C'P ' SS PACKED ?
         BNE   OUTMSG             EXIT IF NOT
* R6 POINTS TO OPERAND 1
         UNPK  WORK(9),0(5,R6)    UNPACK PACKED FIELD PART 1
         TR    WORK(8),TABLE-C'0' TRANSLATE TO ASCII HEX
         MVC   RSLTPAK1,WORK      MOVE TO OUTPUT AREA
         UNPK  WORK(9),4(5,R6)    UNPACK PACKED FIELD PART 2
         TR    WORK(8),TABLE-C'0' TRANSLATE TO ASCII HEX
         MVC   RSLTPAK2,WORK      MOVE TO OUTPUT AREA
         UNPK  WORK(9),8(5,R6)    UNPACK PACKED FIELD PART 3
         TR    WORK(8),TABLE-C'0' TRANSLATE TO ASCII HEX
         MVC   RSLTPAK3,WORK      MOVE TO OUTPUT AREA
         UNPK  WORK(9),12(5,R6)   UNPACK PACKED FIELD PART 4
         TR    WORK(8),TABLE-C'0' TRANSLATE TO ASCII HEX
         MVC   RSLTPAK4,WORK      MOVE TO OUTPUT AREA
         B     OUTMSG             EXIT
*
OUTFP    EQU   *
* DUMP F0 AND F1 IN DECIMAL
* CATER FOR SHORT, LONG, EXTENDED AND HFP, BFP
         CLI   INSTTYPE+2,C'H'    HFP ?
         BNE   OUTFPB             EXIT IF NOT
* FP HFP
         CLI   INSTTYPE,C'E'      SHORT ?
         BNE   OUTFPL             EXIT IF NOT
* FP HFP SHORT
         CTD   CTD_EH,IN=F1,OUT=OUTCTD0 CONVERT TO PRINTABLE FORMAT
         CTD   CTD_EH,IN=F0,OUT=OUTCTD1 CONVERT TO PRINTABLE FORMAT
         B     OUTFPGO2           EXIT
*
OUTFPL   EQU   *
         CLI   INSTTYPE,C'D'      LONG ?
         BNE   OUTFPX             EXIT IF NOT
* FP HFP LONG
         CTD   CTD_DH,IN=F1,OUT=OUTCTD0 CONVERT TO PRINTABLE FORMAT
         CTD   CTD_DH,IN=F0,OUT=OUTCTD1 CONVERT TO PRINTABLE FORMAT
         B     OUTFPGO2           EXIT
*
OUTFPX   EQU   *
         CLI   INSTTYPE,C'X'      EXTENDED ?
         BNE   OUTABORT           ERROR IF NOT
* FP HFP EXTENDED
         CTD   CTD_LH,IN=F1,OUT=OUTCTD0 CONVERT TO PRINTABLE FORMAT
         CTD   CTD_LH,IN=F0,OUT=OUTCTD1 CONVERT TO PRINTABLE FORMAT
         B     OUTFPGO2           EXIT
*
OUTFPB   EQU   *
         CLI   INSTTYPE+2,C'B'    BFP ?
         BNE   OUTFPT             EXIT IF NOT
* FP BFP
         CLI   INSTTYPE,C'E'      SHORT ?
         BNE   OUTFPLB            EXIT IF NOT
* FP BFP SHORT
         CTD   CTD_EB,IN=F1,OUT=OUTCTD0 CONVERT TO PRINTABLE FORMAT
         CTD   CTD_EB,IN=F0,OUT=OUTCTD1 CONVERT TO PRINTABLE FORMAT
         B     OUTFPGO2           EXIT
*
OUTFPLB  EQU   *
         CLI   INSTTYPE,C'D'      LONG ?
         BNE   OUTFPXB            EXIT IF NOT
* FP BFP LONG
         CTD   CTD_DB,IN=F1,OUT=OUTCTD0 CONVERT TO PRINTABLE FORMAT
         CTD   CTD_DB,IN=F0,OUT=OUTCTD1 CONVERT TO PRINTABLE FORMAT
         B     OUTFPGO2           EXIT
*
OUTFPXB  EQU   *
         CLI   INSTTYPE,C'X'      EXTENDED ?
         BNE   OUTABORT           ERROR IF NOT
* FP BFP EXTENDED
         CTD   CTD_LB,IN=F1,OUT=OUTCTD0 CONVERT TO PRINTABLE FORMAT
         CTD   CTD_LB,IN=F0,OUT=OUTCTD1 CONVERT TO PRINTABLE FORMAT
         B     OUTFPGO2           EXIT
*
OUTFPT   EQU   *
* FP DFP
         CLI   INSTTYPE,C'E'      SHORT ?
         BNE   OUTFPDT            EXIT IF NOT
* FP DFP SHORT
         CTD   CTD_ED,IN=F0,OUT=OUTCTD0 CONVERT TO PRINTABLE FORMAT
         CTD   CTD_ED,IN=F1,OUT=OUTCTD1 CONVERT TO PRINTABLE FORMAT
         B     OUTFPGO2           EXIT
*
OUTFPDT  EQU   *
         CLI   INSTTYPE,C'D'      LONG ?
         BNE   OUTFPXT            EXIT IF NOT
* FP DFP LONG
         CTD   CTD_DD,IN=F0,OUT=OUTCTD0 CONVERT TO PRINTABLE FORMAT
         CTD   CTD_DD,IN=F1,OUT=OUTCTD1 CONVERT TO PRINTABLE FORMAT
         B     OUTFPGO2           EXIT
*
OUTFPXT  EQU   *
* FP DFP EXTENDED
         CTD   CTD_LD,IN=F0,OUT=OUTCTD0 CONVERT TO PRINTABLE FORMAT
         CTD   CTD_LD,IN=F1,OUT=OUTCTD1 CONVERT TO PRINTABLE FORMAT
*
OUTFPGO2 EQU   *
* RIGHT ALIGN OUTPUT FIELDS
         LA    R10,16             LENGTH TO MOVE
         LA    R11,OUTCTD1+15     SET FOR BACKSCAN INPUT
         LA    R1,RSLTR10G+15     SET FOR BACKSCAN OUTPUT
OUTALGN1 EQU   *
         CLI   0(R11),C' '        IS CHAR BLANK ?
         BE    OUTDECR1           EXIT IF IT IS
         MVC   0(1,R1),0(R11)     MOVE CHAR
         BCTR  R1,0               OUTPUT POSITION-1
OUTDECR1 EQU   *
         BCTR  R11,0              INPUT POSITION-1
         S     R10,=F'1'          LENGTH-1
         BNZ   OUTALGN1           LOOP
*
         LA    R10,16             LENGTH TO MOVE
         LA    R11,OUTCTD0+15     SET FOR BACKSCAN INPUT
         LA    R1,RSLTR11G+15     SET FOR BACKSCAN OUTPUT
OUTALGN2 EQU   *
         CLI   0(R11),C' '        IS CHAR BLANK ?
         BE    OUTDECR2           EXIT IF IT IS
         MVC   0(1,R1),0(R11)     MOVE CHAR
         BCTR  R1,0               OUTPUT POSITION-1
OUTDECR2 EQU   *
         BCTR  R11,0              INPUT POSITION-1
         S     R10,=F'1'          LENGTH-1
         BNZ   OUTALGN2           LOOP
         B     OUTMSG             EXIT IF DONE
*
OUTABORT ABEND 246
OUTMSG   EQU   *
         PUT   TEXTOUT            WRITE OUTPUT RECORD
         MVI   RESULTS,C' '       BLANK FOR PROPAGATING
         MVC   RESULTS+1(82),RESULTS TRAILING BLANKS ARE STRIPPED
         BR    R14                RETURN
OUTINST  MVC   RSLTOPER(0),WORK
TABLE    DC    C'0123456789ABCDEF'
OUTCTD0  DS    CL45
OUTCTD1  DS    CL45
REGSAVE  DS    D
*
MVSD     DS    0H
* CALCULATE MEAN, VARIANCE AND STD. DEV. FROM CURRENT STORED VALUES
         ST    R14,MVSDR14        STORE OUR R14
         L     R7,STATADR1        START ADDRESS OF VALUES
         ICM   R10,15,STATCNT     LOAD LOOP COUNT
         BZ    MVSDERR            ERROR IF ZERO
         LZDR  F0                 CLEAR F0
MEANLOOP EQU   *
         ZAP   DEC,0(6,R7)        VALUE IN DEC
         CVBG  R11,DEC            TO BINARY
         CDGR  F1,R11             TO LONG FP
         ADR   F0,F1              SUM VALUES
         AHI   R7,6               INCREMENT
         BCT   R10,MEANLOOP       LOOP
*
         L     R10,STATCNT        COUNT
         CDGR  F1,R10             TO LONG FP
         DDR   F0,F1              CALC MEAN
         LDR   F3,F0              SAVE MEAN(Y)
         CGDR  R0,0,F0            TO BINARY
         CVDG  R0,DEC             TO DECIMAL
         MVC   RSLTTITL(4),=C'MEAN' TITLE
         MVC   RSLTTITL+10(11),=X'402021204B202020202020'
         ED    RSLTTITL+10(11),DEC+11 EDIT MEAN uS
         BAL   R14,OUTMSG         PRINT AND CLEAR
* VARIANCE AND STD DEV
         L     R7,STATADR1        START ADDRESS OF VALUES
         L     R10,STATCNT        LOAD LOOP COUNT
         LZDR  F0                 CLEAR F0
MVSDLOOP EQU   *
         ZAP   DEC,0(6,R7)        VALUE IN DEC
         CVBG  R11,DEC            TO BINARY
         CDGR  F1,R11             TO LONG FP
         SDR   F1,F3              VALUE-MEAN
         MDR   F1,F1              (VALUE-MEAN)**2
         ADR   F0,F1              SUM (VALUE-MEAN)**2
         AHI   R7,6               INCREMENT
         BCT   R10,MVSDLOOP       LOOP
*
         L     R10,STATCNT        NO. OF VALUES
         BCTR  R10,0              NO. OF VALUES-1
         CDGR  F1,R10             TO LONG FP
         DDR   F0,F1              CALC VARIANCE
         DD    F0,=DH'1000000'    REDUCE TO 6 DEC POS
         LDR   F2,F0              SAVE FOR STD. DEV.
         CGDR  R0,0,F0            TO BINARY
         CVDG  R0,DEC             TO DECIMAL
         MVC   RSLTTITL(8),=C'VARIANCE' TITLE
         MVC   RSLTTITL+10(11),=X'402021204B202020202020'
         ED    RSLTTITL+10(11),DEC+11 EDIT VARIANCE uS
         BAL   R14,OUTMSG         PRINT AND CLEAR
         DD    F2,=DH'1000000'    REDUCE TO ACTUAL VALUE
         SQDR  F0,F2              CALC STD. DEV.
         MD    F0,=DH'1000000'    MAKE 6 DEC POS
         CGDR  R0,0,F0            TO BINARY
         CVDG  R0,DEC             TO DECIMAL
         MVC   RSLTTITL(9),=C'STD. DEV.' TITLE
         MVC   RSLTTITL+10(11),=X'402021204B202020202020'
         ED    RSLTTITL+10(11),DEC+11 EDIT STD. DEV. uS
         B     MVSDEND            EXIT
*
MVSDERR  EQU   *
         MVC   RSLTTITL+5(16),=C'ERROR, NO VALUES'
MVSDEND  EQU   *
         BAL   R14,OUTMSG         PRINT AND CLEAR
         L     R14,MVSDR14        RESTORE OUR R14
         BR    R14                RETURN
MVSDR14  DS    F
*
LINREG   DS    0H
* CALCULATE LINEAR REGRESSION AND C.C. FROM CURRENT STORED VALUES
* Y=TIME, X=LENGTH TO GIVE Y=aX+b
* ALTHOUGH LINREG CAN BE DONE WITH 2 VALUES, I CHOOSE MIN OF 3
*
* CALCULATE:
*    MEAN(X)
*    MEAN(Y)
*    x=X-MEAN(X)
*    y=Y-MEAN(Y)
*    SUM (xY)
*    SUM (xy)
*    SUM (x**2)
*    SUM (y**2)
         ST    R14,LINRR14        STORE OUR R14
         L     R7,STATADR1        START ADDRESS OF TIME VALUES (Y)
         L     R9,STATADR2        START ADDRESS OF LENGTH VALUES (X)
         L     R10,STATCNT        LOAD LOOP COUNT
         C     R10,=F'3'          3 OR MORE VALUES ?
         BL    LINRERR            ERROR IF NOT
         CDGR  F3,R10             TO LONG FP
         LZDR  F0                 CLEAR F0
         LZDR  F2                 CLEAR F2
         LZDR  F7                 CLEAR F7
         LZDR  F8                 CLEAR F8
         LZDR  F9                 CLEAR F9
         LZDR  F10                CLEAR F10
LINRMEAN EQU   *
* SUM(X) AND SUM(Y)
         ZAP   DEC,0(6,R7)        VALUE IN DEC
         CVBG  R11,DEC            TO BINARY
         CDGR  F1,R11             TO LONG FP
         DD    F1,=DH'1000000'    CORRECT VALUE
         ADR   F0,F1              SUM(Y)
         CLI   INSTFMAT+7,C'S'    SL-USE 2ND ENTRY ?
         BE    LINR1L2            EXIT IF IT IS
         CLI   INSTFMAT+8,C'1'    L1-USE 1ST ENTRY ?
         BE    LINR1L1            EXIT IF IT IS
         CLI   INSTFMAT+8,C'L'    LL-USE 1ST ENTRY ?
         BE    LINR1L1            EXIT IF IT IS
         B     LINR1L2            EXIT IF L2
LINR1L1  EQU   *
         ZAP   DEC,0(4,R9)        VALUE IN DEC
         B     LINRNXT1           EXIT
LINR1L2  EQU   *
         ZAP   DEC,4(4,R9)        VALUE IN DEC
LINRNXT1 EQU   *
         CVBG  R11,DEC            TO BINARY
         CDGR  F1,R11             TO LONG FP
         ADR   F2,F1              SUM(X)
         AHI   R7,6               INCREMENT
         AHI   R9,8               INCREMENT
         BCT   R10,LINRMEAN       LOOP
* F0=SUM(Y)  F2=SUM(X)  F3=n
         DDR   F0,F3              MEAN(Y)
         DDR   F2,F3              MEAN(X)
* F0=MEAN(Y)  F2=MEAN(X)
         L     R7,STATADR1        START ADDRESS OF TIME VALUES (Y)
         L     R9,STATADR2        START ADDRESS OF LENGTH VALUES (X)
         L     R10,STATCNT        LOAD LOOP COUNT
LINRPHS1 EQU   *
         ZAP   DEC,0(6,R7)        VALUE IN DEC
         CVBG  R11,DEC            TO BINARY
         CDGR  F3,R11             TO LONG FP
         DD    F3,=DH'1000000'    CORRECT VALUE
         LDR   F4,F3              F4=Y
         SDR   F4,F0              F4=y=Y-MEAN(Y)
         CLI   INSTFMAT+7,C'S'    SL-USE 2ND ENTRY ?
         BE    LINR2L2            EXIT IF IT IS
         CLI   INSTFMAT+8,C'1'    L1-USE 1ST ENTRY ?
         BE    LINR2L1            EXIT IF IT IS
         CLI   INSTFMAT+8,C'L'    LL-USE 1ST ENTRY ?
         BE    LINR2L1            EXIT IF IT IS
         B     LINR2L2            EXIT IF L2
LINR2L1  EQU   *
         ZAP   DEC,0(4,R9)        VALUE IN DEC
         B     LINRNXT2           EXIT
LINR2L2  EQU   *
         ZAP   DEC,4(4,R9)        VALUE IN DEC
LINRNXT2 EQU   *
         CVBG  R11,DEC            TO BINARY
         CDGR  F5,R11             TO LONG FP
         SDR   F5,F2              F5=x=X-MEAN(X)
* F3=Y  F4=y  F5=x
         LDR   F6,F3              F6=Y
         MDR   F6,F5              F6=xY
         ADR   F7,F6              F7=SUM(xY)
         LDR   F6,F4              F6=y
         MDR   F6,F5              F6=xy
         ADR   F8,F6              F8=SUM(xy)
         MDR   F4,F4              F4=y**2
         ADR   F9,F4              F9=SUM(y**2)
         MDR   F5,F5              F5=x**2
         ADR   F10,F5             F10=SUM(x**2)
         AHI   R7,6               INCREMENT
         AHI   R9,8               INCREMENT
         BCT   R10,LINRPHS1       LOOP
* F0=MEAN(Y) F2=MEAN(X) F7=SUM(xY) F8=SUM(xy)F9=SUM(y**2)F10=SUM(x**2)
* b=SUM(xY)/SUM(x**2)
* a=MEAN(Y)-b*MEAN(X)
         LDR   F1,F7              F1=SUM(xY)
         DDR   F1,F10             F1=b
         LDR   F3,F1              F3=b
         MDR   F3,F2              F3=b*MEAN(X)
         LCDR  F3,F3              F3=-b*MEAN(X)
         LDR   F4,F0              F4=MEAN(Y)
         ADR   F4,F3              F4=a
         MD    F1,=DH'10000000000' ADJUST FOR DEC POS
         MD    F4,=DH'100000000'  ADJUST FOR DEC POS
         CGDR  R0,0,F1            TO BINARY
         CVDG  R0,DEC             TO DECIMAL
         MVC   RSLTTITL(LINRMSLN),LINRMSG MOVE MESSAGE
         MVC   RSLTTITL+45(6),=C'SL    ' AMEND MESSAGE
         CLI   INSTFMAT+7,C'S'    SL ?
         BE    LINROK             EXIT IF IT IS
         MVC   RSLTTITL+45(6),=C'LENGTH' AMEND MESSAGE
         CLI   INSTFMAT+8,C'L'    LENGTH ?
         BE    LINROK             EXIT IF IT IS
         MVC   RSLTTITL+45(6),=C'L1    ' AMEND MESSAGE
         CLI   INSTFMAT+8,C'1'    L1 ?
         BE    LINROK             EXIT IF IT IS
         MVC   RSLTTITL+45(6),=C'L2    ' AMEND MESSAGE
LINROK   EQU   *
         ED    RSLTTITL+27(L'LINRED2),DEC+9 EDIT SLOPE
         TM    DEC+15,X'01'       NEGATIVE ?
         BNO   LINRPOS            EXIT IF NOT
         MVI   RSLTTITL+27,C'-'   MOVE MINUS SIGN
LINRPOS  EQU   *
         CGDR  R0,0,F4            TO BINARY
         CVDG  R0,DEC             TO DECIMAL
         ED    RSLTTITL+10(L'LINRED1),DEC+10 EDIT INTERCEPT
         BAL   R14,OUTMSG         PRINT AND CLEAR
* CORRELATION COEFFICIENT
         MDR   F9,F10             SUM(y**2) * SUM(x**2)
         SQDR  F0,F9              ROOT( SUM(y**2) * SUM(x**2) )
         DDR   F8,F0              SUM(xy) / ROOT( SUM(y**2)*SUM(x**2))
         MD    F8,=DH'1000000'    ADJUST FOR DEC POS
         CGDR  R0,0,F8            TO BINARY
         CVDG  R0,DEC             TO DECIMAL
         MVC   RSLTTITL(21),LINRCC MOVE MESSAGE
         ED    RSLTTITL+L'LINRCC(L'LINRCCED),DEC+11 EDIT CC
         B     LINREND            EXIT
*
LINRERR  EQU   *
         MVC   RSLTTITL+5(21),=C'ERROR, TOO FEW VALUES'
LINREND  EQU   *
         BAL   R14,OUTMSG         PRINT AND CLEAR
         L     R14,LINRR14        RESTORE OUR R14
         BR    R14                RETURN
LINRR14  DS    F
* TIME = 2.12345678 + ( 0.00012345 * LENGTH )
LINRMSG  DC    C'TIME =    '
LINRED1  DC    X'402021204B2020202020202020'
         DC    C' + ('
LINRED2  DC    X'402021204B20202020202020202020'
         DC    C' * LENGTH )'
LINRMSLN EQU   *-LINRMSG
*
LINRCC   DC    C'CC=       '
LINRCCED DC    X'402021204B202020202020'
*
         DS    0H
PARMSIN  DC    CL80' '            PARMS I/O AREA
RESULTS  DC    CL83' '            RESULTS I/O AREA
*
WORK     DS    CL13
SPINCNT  DC    F'0100000'         DEFAULT SPIN COUNT
SPINDEC  DC    PL8'0100000'       DEFAULT SPIN COUNT AS DECIMAL
BCTSPIN  DC    F'5000000'         DEFAULT BCT SPIN COUNT
BCTSDEC  DC    PL8'5000000'       DEFAULT BCT SPIN COUNT AS DECIMAL
TIMEBEF  DS    D                  TIME BEFORE
TIMEAFT  DS    D                  TIME AFTER
TIMEBCT  DS    D                  TIME FOR SPINCNT BCT--ADJUST BY SPIN=
ORIGBCT  DS    D                  TIME FOR SPINCNT BCT
TIMEXTRA DC    FL8'0'             TIME FOR SPINCNT EXTRA INSTS
DEC      DC    PL16'0'
DEC2     DC    PL16'0'
*
* STATS FIELDS
STATADR1 DS    F                  TIMES SAVE AREA ADDRESS
STATNXT1 DS    F                  NEXT TIME SLOT
STATADR2 DS    F                  LENGTHS SAVE AREA ADDRESS
STATNXT2 DS    F                  NEXT LENGTHS SLOT
STATCNT  DS    F                  COUNT OF SAVED ENTRIES
STATSIND DC    X'00'              FF=STATS ON  00=STATS OFF
*
PARMS    DCB   DSNAME=INPUT,      DDNAME                               X
               DSORG=PS,          SEQUENTIAL                           X
               RECFM=FT,          FIXED                                X
               MACRF=GM,          GET                                  X
               EODAD=EOPARMS,                                          X
               LRECL=80,                                               X
               BLKSIZE=80,                                             X
               RECORD=PARMSIN
*
TEXTOUT  DCB   DSNAME=RESULT,     DDNAME                               X
               DSORG=PS,          SEQUENTIAL                           X
               MACRF=PM,          PUT                                  X
               RECFM=FT,          FIXED,XLATE                          X
               LRECL=83,                                               X
               BLKSIZE=83,                                             X
               RECORD=RESULTS
*
INPUT    DC    C"&SYSPARM\INSTTIME.TXT",X'00'
RESULT   DC    C"&SYSPARM\INSTTIME.OUT",X'00'
*
         LTORG
*
* LIMIT OF BASE R13
*
**********
* TABLES *
**********
TYPETABL DS    0F
         DC    CL4'E  ',A(E)      E
         DC    CL4'RI ',A(RI)     RI
         DC    CL4'RIE',A(RIE)    RIE
         DC    CL4'RIL',A(RIL)    RIL
         DC    CL4'RR ',A(RR)     RR
         DC    CL4'RRE',A(RRE)    RRE
         DC    CL4'RRF',A(RRF)    RRF
         DC    CL4'RRR',A(RRR)    RRR
         DC    CL4'RS ',A(RS)     RS
         DC    CL4'RSI',A(RSI)    RSI
         DC    CL4'RSL',A(RSL)    RSL
         DC    CL4'RSY',A(RSY)    RSY
         DC    CL4'RX ',A(RX)     RX
         DC    CL4'RXE',A(RXE)    RXE
         DC    CL4'RXF',A(RXF)    RXF
         DC    CL4'RXY',A(RXY)    RXY
         DC    CL4'S  ',A(S)      S
         DC    CL4'SI ',A(SI)     SI
         DC    CL4'SIY',A(SIY)    SIY
         DC    CL4'SS1',A(SS1)    SS WITH 1 LENGTH
         DC    CL4'SS2',A(SS2)    SS WITH 2 LENGTHS
         DC    CL4'SSF',A(SSF)    SSF
         DC    CL4'SVC',A(SVC)    FUNCTION TIMING
         DC    X'FF'              STOPPER
*
OPCD1TAB DS    0H
* SINGLE BYTE OPCODE TABLE
         DC    CL6'SPM ',X'04'     RR
         DC    CL6'BALR',X'05'     RR
         DC    CL6'BCTR',X'06'     RR
         DC    CL6'BCR ',X'07'     RR
         DC    CL6'BR  ',X'07'     RR--SYNONYM
         DC    CL6'BSM ',X'0B'     RR
         DC    CL6'BASSM',X'0C'    RR
         DC    CL6'BASR',X'0D'     RR
         DC    CL6'MVCL',X'0E'     RR
         DC    CL6'CLCL',X'0F'     RR
         DC    CL6'LPR ',X'10'     RR
         DC    CL6'LNR ',X'11'     RR
         DC    CL6'LTR ',X'12'     RR
         DC    CL6'LCR ',X'13'     RR
         DC    CL6'NR  ',X'14'     RR
         DC    CL6'CLR ',X'15'     RR
         DC    CL6'OR  ',X'16'     RR
         DC    CL6'XR  ',X'17'     RR
         DC    CL6'LR  ',X'18'     RR
         DC    CL6'CR  ',X'19'     RR
         DC    CL6'AR  ',X'1A'     RR
         DC    CL6'SR  ',X'1B'     RR
         DC    CL6'MR  ',X'1C'     RR
         DC    CL6'DR  ',X'1D'     RR
         DC    CL6'ALR ',X'1E'     RR
         DC    CL6'SLR ',X'1F'     RR
         DC    CL6'LPDR',X'20'     RR--FP
         DC    CL6'LNDR',X'21'     RR--FP
         DC    CL6'LTDR',X'22'     RR--FP
         DC    CL6'LCDR',X'23'     RR--FP
         DC    CL6'HDR ',X'24'     RR--FP
         DC    CL6'LRDR',X'25'     RR--FP--SYNONYM
         DC    CL6'LDXR',X'25'     RR--FP
         DC    CL6'MXR ',X'26'     RR--FP
         DC    CL6'MXDR',X'27'     RR--FP
         DC    CL6'LDR ',X'28'     RR--FP
         DC    CL6'CDR ',X'29'     RR--FP
         DC    CL6'ADR ',X'2A'     RR--FP
         DC    CL6'SDR ',X'2B'     RR--FP
         DC    CL6'MDR ',X'2C'     RR--FP
         DC    CL6'DDR ',X'2D'     RR--FP
         DC    CL6'AWR ',X'2E'     RR--FP
         DC    CL6'SWR ',X'2F'     RR--FP
         DC    CL6'LPER',X'30'     RR--FP
         DC    CL6'LNER',X'31'     RR--FP
         DC    CL6'LTER',X'32'     RR--FP
         DC    CL6'LCER',X'33'     RR--FP
         DC    CL6'HER ',X'34'     RR--FP
         DC    CL6'LRER',X'35'     RR--FP--SYNONYM
         DC    CL6'LEDR',X'35'     RR--FP
         DC    CL6'AXR ',X'36'     RR--FP
         DC    CL6'SXR ',X'37'     RR--FP
         DC    CL6'LER ',X'38'     RR--FP
         DC    CL6'CER ',X'39'     RR--FP
         DC    CL6'AER ',X'3A'     RR--FP
         DC    CL6'SER ',X'3B'     RR--FP
         DC    CL6'MER ',X'3C'     RR--FP--SYNONYM
         DC    CL6'MDER',X'3C'     RR--FP
         DC    CL6'DER ',X'3D'     RR--FP
         DC    CL6'AUR ',X'3E'     RR--FP
         DC    CL6'SUR ',X'3F'     RR--FP
         DC    CL6'STH ',X'40'     RX
         DC    CL6'LA  ',X'41'     RX
         DC    CL6'STC ',X'42'     RX
         DC    CL6'IC  ',X'43'     RX
         DC    CL6'EX  ',X'44'     RX
         DC    CL6'BAL ',X'45'     RX
         DC    CL6'BCT ',X'46'     RX
         DC    CL6'BC  ',X'47'     RX
         DC    CL6'B   ',X'47'     RX--SYNONYM
         DC    CL6'LH  ',X'48'     RX
         DC    CL6'CH  ',X'49'     RX
         DC    CL6'AH  ',X'4A'     RX
         DC    CL6'SH  ',X'4B'     RX
         DC    CL6'MH  ',X'4C'     RX
         DC    CL6'BAS ',X'4D'     RX
         DC    CL6'CVD ',X'4E'     RX
         DC    CL6'CVB ',X'4F'     RX
         DC    CL6'ST  ',X'50'     RX
         DC    CL6'N   ',X'54'     RX
         DC    CL6'CL  ',X'55'     RX
         DC    CL6'O   ',X'56'     RX
         DC    CL6'X   ',X'57'     RX
         DC    CL6'L   ',X'58'     RX
         DC    CL6'C   ',X'59'     RX
         DC    CL6'A   ',X'5A'     RX
         DC    CL6'S   ',X'5B'     RX
         DC    CL6'M   ',X'5C'     RX
         DC    CL6'D   ',X'5D'     RX
         DC    CL6'AL  ',X'5E'     RX
         DC    CL6'SL  ',X'5F'     RX
         DC    CL6'STD ',X'60'     RX--FP
         DC    CL6'MXD ',X'67'     RX--FP
         DC    CL6'LD  ',X'68'     RX--FP
         DC    CL6'CD  ',X'69'     RX--FP
         DC    CL6'AD  ',X'6A'     RX--FP
         DC    CL6'SD  ',X'6B'     RX--FP
         DC    CL6'MD  ',X'6C'     RX--FP
         DC    CL6'DD  ',X'6D'     RX--FP
         DC    CL6'AW  ',X'6E'     RX--FP
         DC    CL6'SW  ',X'6F'     RX--FP
         DC    CL6'STE ',X'70'     RX--FP
         DC    CL6'MS  ',X'71'     RX
         DC    CL6'LE  ',X'78'     RX--FP
         DC    CL6'CE  ',X'79'     RX--FP
         DC    CL6'AE  ',X'7A'     RX--FP
         DC    CL6'SE  ',X'7B'     RX--FP
         DC    CL6'ME  ',X'7C'     RX--FP--SYNONYM
         DC    CL6'MDE ',X'7C'     RX--FP
         DC    CL6'DE  ',X'7D'     RX--FP
         DC    CL6'AU  ',X'7E'     RX--FP
         DC    CL6'SU  ',X'7F'     RX--FP
         DC    CL6'BRXH',X'84'     RSI
         DC    CL6'JXH ',X'84'     RSI--SYNONYM
         DC    CL6'BRXLE',X'85'    RSI
         DC    CL6'JXLE ',X'85'    RSI--SYNONYM
         DC    CL6'BXH ',X'86'     RS
         DC    CL6'BXLE',X'87'     RS
         DC    CL6'SRL ',X'88'     RS
         DC    CL6'SLL ',X'89'     RS
         DC    CL6'SRA ',X'8A'     RS
         DC    CL6'SLA ',X'8B'     RS
         DC    CL6'SRDL',X'8C'     RS
         DC    CL6'SLDL',X'8D'     RS
         DC    CL6'SRDA',X'8E'     RS
         DC    CL6'SLDA',X'8F'     RS
         DC    CL6'STM ',X'90'     RS
         DC    CL6'TM  ',X'91'     SI
         DC    CL6'MVI ',X'92'     SI
         DC    CL6'TS  ',X'93'   S/SI  TIMED AS SI
         DC    CL6'NI  ',X'94'     SI
         DC    CL6'CLI ',X'95'     SI
         DC    CL6'OI  ',X'96'     SI
         DC    CL6'XI  ',X'97'     SI
         DC    CL6'LM  ',X'98'     RS
         DC    CL6'MVCLE',X'A8'    RS
         DC    CL6'CLCLE',X'A9'    RS
         DC    CL6'CS  ',X'BA'     RS
         DC    CL6'CDS ',X'BB'     RS
         DC    CL6'CLM ',X'BD'     RS
         DC    CL6'STCM',X'BE'     RS
         DC    CL6'ICM ',X'BF'     RS
         DC    CL6'MVCOS',X'C8'   SSF
         DC    CL6'TRTR ',X'D0'   SS1
         DC    CL6'MVN  ',X'D1'   SS1
         DC    CL6'MVC  ',X'D2'   SS1
         DC    CL6'MVZ  ',X'D3'   SS1
         DC    CL6'NC   ',X'D4'   SS1
         DC    CL6'CLC  ',X'D5'   SS1
         DC    CL6'OC   ',X'D6'   SS1
         DC    CL6'XC   ',X'D7'   SS1
         DC    CL6'TR   ',X'DC'   SS1
         DC    CL6'TRT  ',X'DD'   SS1
         DC    CL6'ED   ',X'DE'   SS1
         DC    CL6'EDMK ',X'DF'   SS1
         DC    CL6'MVCIN',X'E8'   SS1
         DC    CL6'PKA  ',X'E9'   SS1
         DC    CL6'UNPKA',X'EA'   SS1
         DC    CL6'LMD  ',X'EF'   SS2
         DC    CL6'SRP  ',X'F0'   SS2
         DC    CL6'MVO  ',X'F1'   SS2
         DC    CL6'PACK ',X'F2'   SS2
         DC    CL6'UNPK ',X'F3'   SS2
         DC    CL6'ZAP  ',X'F8'   SS2
         DC    CL6'CP   ',X'F9'   SS2
         DC    CL6'AP   ',X'FA'   SS2
         DC    CL6'SP   ',X'FB'   SS2
         DC    CL6'MP   ',X'FC'   SS2
         DC    CL6'DP   ',X'FD'   SS2
         DC    X'FF'              STOPPER
*
OPCD2TAB DS    0H
* DOUBLE BYTE OPCODE TABLE
* THE SECOND BYTE MAYBE A BYTE OR A NIBBLE
         DC    CL6'PR   ',X'01',X'01'   E
         DC    CL6'SCKPF',X'01',X'07'   E
         DC    CL6'TAM  ',X'01',X'0B'   E
         DC    CL6'SAM24',X'01',X'0C'   E
         DC    CL6'SAM31',X'01',X'0D'   E
         DC    CL6'SAM64',X'01',X'0E'   E
*
         DC    CL6'IIHH ',X'A5',X'00'   RI
         DC    CL6'IIHL ',X'A5',X'01'   RI
         DC    CL6'IILH ',X'A5',X'02'   RI
         DC    CL6'IILL ',X'A5',X'03'   RI
         DC    CL6'NIHH ',X'A5',X'04'   RI
         DC    CL6'NIHL ',X'A5',X'05'   RI
         DC    CL6'NILH ',X'A5',X'06'   RI
         DC    CL6'NILL ',X'A5',X'07'   RI
         DC    CL6'OIHH ',X'A5',X'08'   RI
         DC    CL6'OIHL ',X'A5',X'09'   RI
         DC    CL6'OILH ',X'A5',X'0A'   RI
         DC    CL6'OILL ',X'A5',X'0B'   RI
         DC    CL6'LLIHH',X'A5',X'0C'   RI
         DC    CL6'LLIHL',X'A5',X'0D'   RI
         DC    CL6'LLILH',X'A5',X'0E'   RI
         DC    CL6'LLILL',X'A5',X'0F'   RI
*
         DC    CL6'TMLH ',X'A7',X'00'   RI
         DC    CL6'TMH  ',X'A7',X'00'   RI--SYNONYM
         DC    CL6'TMLL ',X'A7',X'01'   RI
         DC    CL6'TML  ',X'A7',X'01'   RI--SYNONYM
         DC    CL6'TMHH ',X'A7',X'02'   RI
         DC    CL6'TMHL ',X'A7',X'03'   RI
         DC    CL6'J    ',X'A7',X'04'   RI--SYNONYM
         DC    CL6'BRC  ',X'A7',X'04'   RI
         DC    CL6'BRU  ',X'A7',X'04'   RI--SYNONYM
         DC    CL6'JAS  ',X'A7',X'05'   RI--SYNONYM
         DC    CL6'BRAS ',X'A7',X'05'   RI
         DC    CL6'JCT  ',X'A7',X'06'   RI--SYNONYM
         DC    CL6'BRCT ',X'A7',X'06'   RI
         DC    CL6'JCTG ',X'A7',X'07'   RI--SYNONYN
         DC    CL6'BRCTG',X'A7',X'07'   RI
         DC    CL6'LHI  ',X'A7',X'08'   RI
         DC    CL6'LGHI ',X'A7',X'09'   RI
         DC    CL6'AHI  ',X'A7',X'0A'   RI
         DC    CL6'AGHI ',X'A7',X'0B'   RI
         DC    CL6'MHI  ',X'A7',X'0C'   RI
         DC    CL6'MGHI ',X'A7',X'0D'   RI
         DC    CL6'CHI  ',X'A7',X'0E'   RI
         DC    CL6'CGHI ',X'A7',X'0F'   RI
*
         DC    CL6'STIDP',X'B2',X'02'   S
         DC    CL6'STCK ',X'B2',X'05'   S
         DC    CL6'STCKC',X'B2',X'07'   S
         DC    CL6'STPT ',X'B2',X'09'   S
         DC    CL6'STPX ',X'B2',X'11'   S
         DC    CL6'STAP ',X'B2',X'12'   S
         DC    CL6'PC   ',X'B2',X'18'   S
         DC    CL6'IPM  ',X'B2',X'22'   RRE
         DC    CL6'DXR  ',X'B2',X'2D'   RRE
         DC    CL6'BAKR ',X'B2',X'40'   RRE
         DC    CL6'SQDR ',X'B2',X'44'   RRE
         DC    CL6'SQER ',X'B2',X'45'   RRE
         DC    CL6'MSR  ',X'B2',X'52'   RRE
         DC    CL6'MVST ',X'B2',X'55'   RRE
         DC    CL6'CUSE ',X'B2',X'57'   RRE
         DC    CL6'CLST ',X'B2',X'5D'   RRE
         DC    CL6'SRST ',X'B2',X'5E'   RRE
         DC    CL6'STCKE',X'B2',X'78'   S
         DC    CL6'STCKF',X'B2',X'7C'   S
         DC    CL6'SRNM ',X'B2',X'99'   S
         DC    CL6'STFPC',X'B2',X'9C'   S
         DC    CL6'LFPC ',X'B2',X'9D'   S
         DC    CL6'TRE  ',X'B2',X'A5'   RRE
         DC    CL6'STFLE',X'B2',X'B0'   S
         DC    CL6'SRNMT',X'B2',X'B9'   S
         DC    CL6'LFAS ',X'B2',X'BD'   S
*
         DC    CL6'LPEBR',X'B3',X'00'   RRE
         DC    CL6'LNEBR',X'B3',X'01'   RRE
         DC    CL6'LTEBR',X'B3',X'02'   RRE
         DC    CL6'LCEBR',X'B3',X'03'   RRE
         DC    CL6'LDEBR',X'B3',X'04'   RRE
         DC    CL6'LXDBR',X'B3',X'05'   RRE
         DC    CL6'LXEBR',X'B3',X'06'   RRE
         DC    CL6'MXDBR',X'B3',X'07'   RRE
         DC    CL6'KEBR ',X'B3',X'08'   RRE
         DC    CL6'CEBR ',X'B3',X'09'   RRE
         DC    CL6'AEBR ',X'B3',X'0A'   RRE
         DC    CL6'SEBR ',X'B3',X'0B'   RRE
         DC    CL6'MDEBR',X'B3',X'0C'   RRE
         DC    CL6'DEBR ',X'B3',X'0D'   RRE
         DC    CL6'MAEBR',X'B3',X'0E'   RRE
         DC    CL6'MSEBR',X'B3',X'0F'   RRE
         DC    CL6'LPDBR',X'B3',X'10'   RRE
         DC    CL6'LNDBR',X'B3',X'11'   RRE
         DC    CL6'LTDBR',X'B3',X'12'   RRE
         DC    CL6'LCDBR',X'B3',X'13'   RRE
         DC    CL6'SQEBR',X'B3',X'14'   RRE
         DC    CL6'SQDBR',X'B3',X'15'   RRE
         DC    CL6'SQXBR',X'B3',X'16'   RRE
         DC    CL6'MEEBR',X'B3',X'17'   RRE
         DC    CL6'KDBR ',X'B3',X'18'   RRE
         DC    CL6'CDBR ',X'B3',X'19'   RRE
         DC    CL6'ADBR ',X'B3',X'1A'   RRE
         DC    CL6'SDBR ',X'B3',X'1B'   RRE
         DC    CL6'MDBR ',X'B3',X'1C'   RRE
         DC    CL6'DDBR ',X'B3',X'1D'   RRE
         DC    CL6'MADBR',X'B3',X'1E'   RRE
         DC    CL6'MSDBR',X'B3',X'1F'   RRE
         DC    CL6'LDER ',X'B3',X'24'   RRE
         DC    CL6'LXDR ',X'B3',X'25'   RRE
         DC    CL6'LXER ',X'B3',X'26'   RRE
         DC    CL6'MAER ',X'B3',X'2E'   RRF
         DC    CL6'MSER ',X'B3',X'2F'   RRF
         DC    CL6'SQXR ',X'B3',X'36'   RRE
         DC    CL6'MEER ',X'B3',X'37'   RRE
         DC    CL6'MAYLR',X'B3',X'38'   RRF
         DC    CL6'MYLR ',X'B3',X'39'   RRF
         DC    CL6'MAYR ',X'B3',X'3A'   RRF
         DC    CL6'MYR  ',X'B3',X'3B'   RRF
         DC    CL6'MAYHR',X'B3',X'3C'   RRF
         DC    CL6'MYHR ',X'B3',X'3D'   RRF
         DC    CL6'MADR ',X'B3',X'3E'   RRF
         DC    CL6'MSDR ',X'B3',X'3F'   RRF
         DC    CL6'LPXBR',X'B3',X'40'   RRE
         DC    CL6'LNXBR',X'B3',X'41'   RRE
         DC    CL6'LTXBR',X'B3',X'42'   RRE
         DC    CL6'LCXBR',X'B3',X'43'   RRE
         DC    CL6'LEDBR',X'B3',X'44'   RRE
         DC    CL6'LDXBR',X'B3',X'45'   RRE
         DC    CL6'LEXBR',X'B3',X'46'   RRE
         DC    CL6'FIXBR',X'B3',X'47'   RRF
         DC    CL6'KXBR ',X'B3',X'48'   RRE
         DC    CL6'CXBR ',X'B3',X'49'   RRE
         DC    CL6'AXBR ',X'B3',X'4A'   RRE
         DC    CL6'SXBR ',X'B3',X'4B'   RRE
         DC    CL6'MXBR ',X'B3',X'4C'   RRE
         DC    CL6'DXBR ',X'B3',X'4D'   RRE
         DC    CL6'TBEDR',X'B3',X'50'   RRF
         DC    CL6'TBDR ',X'B3',X'51'   RRF
         DC    CL6'DIEBR',X'B3',X'53'   RRF
         DC    CL6'FIEBR',X'B3',X'57'   RRF
         DC    CL6'THDER',X'B3',X'58'   RRE
         DC    CL6'THDR ',X'B3',X'59'   RRE
         DC    CL6'DIDBR',X'B3',X'5B'   RRF
         DC    CL6'FIDBR',X'B3',X'5F'   RRF
         DC    CL6'LPXR ',X'B3',X'60'   RRE
         DC    CL6'LNXR ',X'B3',X'61'   RRE
         DC    CL6'LTXR ',X'B3',X'62'   RRE
         DC    CL6'LCXR ',X'B3',X'63'   RRE
         DC    CL6'LXR  ',X'B3',X'65'   RRE
         DC    CL6'LEXR ',X'B3',X'66'   RRE
         DC    CL6'FIXR ',X'B3',X'67'   RRE
         DC    CL6'CXR  ',X'B3',X'69'   RRE
         DC    CL6'LPDFR',X'B3',X'70'   RRE
         DC    CL6'LNDFR',X'B3',X'71'   RRE
         DC    CL6'CPSDR',X'B3',X'72'   RRF
         DC    CL6'LCDFR',X'B3',X'73'   RRE
         DC    CL6'LZER ',X'B3',X'74'   RRE
         DC    CL6'LZDR ',X'B3',X'75'   RRE
         DC    CL6'LZXR ',X'B3',X'76'   RRE
         DC    CL6'FIER ',X'B3',X'77'   RRE
         DC    CL6'FIDR ',X'B3',X'7F'   RRE
         DC    CL6'SFPC ',X'B3',X'84'   RRE
         DC    CL6'SFASR',X'B3',X'85'   RRE
         DC    CL6'CEFBR',X'B3',X'94'   RRE
         DC    CL6'CDFBR',X'B3',X'95'   RRE
         DC    CL6'CXFBR',X'B3',X'96'   RRE
         DC    CL6'CFEBR',X'B3',X'98'   RRF
         DC    CL6'CFDBR',X'B3',X'99'   RRF
         DC    CL6'CFXBR',X'B3',X'9A'   RRF
         DC    CL6'CEGBR',X'B3',X'A4'   RRE
         DC    CL6'CDGBR',X'B3',X'A5'   RRE
         DC    CL6'CXGBR',X'B3',X'A6'   RRE
         DC    CL6'CGEBR',X'B3',X'A8'   RRF
         DC    CL6'CGDBR',X'B3',X'A9'   RRF
         DC    CL6'CGXBR',X'B3',X'AA'   RRF
         DC    CL6'EFPC ',X'B3',X'8C'   RRE
         DC    CL6'CEFR ',X'B3',X'B4'   RRE
         DC    CL6'CDFR ',X'B3',X'B5'   RRE
         DC    CL6'CXFR ',X'B3',X'B6'   RRE
         DC    CL6'CFER ',X'B3',X'B8'   RRF
         DC    CL6'CFDR ',X'B3',X'B9'   RRF
         DC    CL6'CFXR ',X'B3',X'BA'   RRF
         DC    CL6'LDGR ',X'B3',X'C1'   RRE
         DC    CL6'CEGR ',X'B3',X'C4'   RRE
         DC    CL6'CDGR ',X'B3',X'C5'   RRE
         DC    CL6'CXGR ',X'B3',X'C6'   RRE
         DC    CL6'CGER ',X'B3',X'C8'   RRF
         DC    CL6'CGDR ',X'B3',X'C9'   RRF
         DC    CL6'CGXR ',X'B3',X'CA'   RRF
         DC    CL6'LGDR ',X'B3',X'CD'   RRE
         DC    CL6'MDTR ',X'B3',X'D0'   RRR
         DC    CL6'DDTR ',X'B3',X'D1'   RRR
         DC    CL6'ADTR ',X'B3',X'D2'   RRR
         DC    CL6'SDTR ',X'B3',X'D3'   RRR
         DC    CL6'LDETR',X'B3',X'D4'   RRF
         DC    CL6'LEDTR',X'B3',X'D5'   RRF
         DC    CL6'LTDTR',X'B3',X'D6'   RRE
         DC    CL6'FIDTR',X'B3',X'D7'   RRF
         DC    CL6'MXTR ',X'B3',X'D8'   RRR
         DC    CL6'DXTR ',X'B3',X'D9'   RRR
         DC    CL6'AXTR ',X'B3',X'DA'   RRR
         DC    CL6'SXTR ',X'B3',X'DB'   RRR
         DC    CL6'LXDTR',X'B3',X'DC'   RRF
         DC    CL6'LDXTR',X'B3',X'DD'   RRF
         DC    CL6'LTXTR',X'B3',X'DE'   RRE
         DC    CL6'FIXTR',X'B3',X'DF'   RRF
         DC    CL6'KDTR ',X'B3',X'E0'   RRE
         DC    CL6'CGDTR',X'B3',X'E1'   RRE
         DC    CL6'CUDTR',X'B3',X'E2'   RRE
         DC    CL6'CSDTR',X'B3',X'E3'   RRF
         DC    CL6'CDTR ',X'B3',X'E4'   RRE
         DC    CL6'EEDTR',X'B3',X'E5'   RRE
         DC    CL6'ESDTR',X'B3',X'E7'   RRE
         DC    CL6'KXTR ',X'B3',X'E8'   RRE
         DC    CL6'CGXTR',X'B3',X'E9'   RRE
         DC    CL6'CUXTR',X'B3',X'EA'   RRE
         DC    CL6'CSXTR',X'B3',X'EB'   RRF
         DC    CL6'CXTR ',X'B3',X'EC'   RRE
         DC    CL6'EEXTR',X'B3',X'ED'   RRE
         DC    CL6'ESXTR',X'B3',X'EF'   RRE
         DC    CL6'CDGTR',X'B3',X'F1'   RRE
         DC    CL6'CDUTR',X'B3',X'F2'   RRE
         DC    CL6'CDSTR',X'B3',X'F3'   RRE
         DC    CL6'CEDTR',X'B3',X'F4'   RRE
         DC    CL6'QADTR',X'B3',X'F5'   RRF
         DC    CL6'IEDTR',X'B3',X'F6'   RRF
         DC    CL6'RRDTR',X'B3',X'F7'   RRF
         DC    CL6'CXGTR',X'B3',X'F9'   RRE
         DC    CL6'CXUTR',X'B3',X'FA'   RRE
         DC    CL6'CXSTR',X'B3',X'FB'   RRE
         DC    CL6'CEXTR',X'B3',X'FC'   RRE
         DC    CL6'QAXTR',X'B3',X'FD'   RRF
         DC    CL6'IEXTR',X'B3',X'FE'   RRF
         DC    CL6'RRXTR',X'B3',X'FF'   RRF
*
         DC    CL6'LPGR ',X'B9',X'00'   RRE
         DC    CL6'LNGR ',X'B9',X'01'   RRE
         DC    CL6'LTGR ',X'B9',X'02'   RRE
         DC    CL6'LCGR ',X'B9',X'03'   RRE
         DC    CL6'LGR  ',X'B9',X'04'   RRE
         DC    CL6'LGBR ',X'B9',X'06'   RRE
         DC    CL6'LGHR ',X'B9',X'07'   RRE
         DC    CL6'AGR  ',X'B9',X'08'   RRE
         DC    CL6'SGR  ',X'B9',X'09'   RRE
         DC    CL6'ALGR ',X'B9',X'0A'   RRE
         DC    CL6'SLGR ',X'B9',X'0B'   RRE
         DC    CL6'MSGR ',X'B9',X'0C'   RRE
         DC    CL6'DSGR ',X'B9',X'0D'   RRE
         DC    CL6'LRVGR',X'B9',X'0F'   RRE
         DC    CL6'LPGFR',X'B9',X'10'   RRE
         DC    CL6'LNGFR',X'B9',X'11'   RRE
         DC    CL6'LTGFR',X'B9',X'12'   RRE
         DC    CL6'LCGFR',X'B9',X'13'   RRE
         DC    CL6'LGFR ',X'B9',X'14'   RRE
         DC    CL6'LLGFR',X'B9',X'16'   RRE
         DC    CL6'LLGTR',X'B9',X'17'   RRE
         DC    CL6'AGFR ',X'B9',X'18'   RRE
         DC    CL6'SGFR ',X'B9',X'19'   RRE
         DC    CL6'ALGFR',X'B9',X'1A'   RRE
         DC    CL6'SLGFR',X'B9',X'1B'   RRE
         DC    CL6'MSGFR',X'B9',X'1C'   RRE
         DC    CL6'DSGFR',X'B9',X'1D'   RRE
         DC    CL6'LRVR ',X'B9',X'1F'   RRE
         DC    CL6'CGR  ',X'B9',X'20'   RRE
         DC    CL6'CLGR ',X'B9',X'21'   RRE
         DC    CL6'LBR  ',X'B9',X'26'   RRE
         DC    CL6'LHR  ',X'B9',X'27'   RRE
         DC    CL6'CGFR ',X'B9',X'30'   RRE
         DC    CL6'CLGFR',X'B9',X'31'   RRE
         DC    CL6'BCTGR',X'B9',X'46'   RRE
         DC    CL6'NGR  ',X'B9',X'80'   RRE
         DC    CL6'OGR  ',X'B9',X'81'   RRE
         DC    CL6'XGR  ',X'B9',X'82'   RRE
         DC    CL6'FLOGR',X'B9',X'83'   RRE
         DC    CL6'LLGCR',X'B9',X'84'   RRE
         DC    CL6'LLGHR',X'B9',X'85'   RRE
         DC    CL6'MLGR ',X'B9',X'86'   RRE
         DC    CL6'DLGR ',X'B9',X'87'   RRE
         DC    CL6'ALCGR',X'B9',X'88'   RRE
         DC    CL6'SLBGR',X'B9',X'89'   RRE
         DC    CL6'TRTT ',X'B9',X'90'   RRF
         DC    CL6'TRTO ',X'B9',X'91'   RRF
         DC    CL6'TROT ',X'B9',X'92'   RRF
         DC    CL6'TROO ',X'B9',X'93'   RRF
         DC    CL6'LLCR ',X'B9',X'94'   RRE
         DC    CL6'LLHR ',X'B9',X'95'   RRE
         DC    CL6'MLR  ',X'B9',X'96'   RRE
         DC    CL6'DLR  ',X'B9',X'97'   RRE
         DC    CL6'ALCR ',X'B9',X'98'   RRE
         DC    CL6'SLBR ',X'B9',X'99'   RRE
*
         DC    CL6'LARL ',X'C0',X'00'   RIL
         DC    CL6'LGFI ',X'C0',X'01'   RIL
         DC    CL6'JLU  ',X'C0',X'04'   RIL--SYNONYM
         DC    CL6'BRCL ',X'C0',X'04'   RIL
         DC    CL6'BRUL ',X'C0',X'04'   RIL--SYNONYM
         DC    CL6'JASL ',X'C0',X'05'   RIL--SYNONYM
         DC    CL6'BRASL',X'C0',X'05'   RIL
         DC    CL6'XIHF ',X'C0',X'06'   RIL
         DC    CL6'XILF ',X'C0',X'07'   RIL
         DC    CL6'IIHF ',X'C0',X'08'   RIL
         DC    CL6'IILF ',X'C0',X'09'   RIL
         DC    CL6'NIHF ',X'C0',X'0A'   RIL
         DC    CL6'NILF ',X'C0',X'0B'   RIL
         DC    CL6'OIHF ',X'C0',X'0C'   RIL
         DC    CL6'OILF ',X'C0',X'0D'   RIL
         DC    CL6'LLIHF',X'C0',X'0E'   RIL
         DC    CL6'LLILF',X'C0',X'0F'   RIL
*
         DC    CL6'SLGFI',X'C2',X'04'   RIL
         DC    CL6'SLFI ',X'C2',X'05'   RIL
         DC    CL6'AGFI ',X'C2',X'08'   RIL
         DC    CL6'AFI  ',X'C2',X'09'   RIL
         DC    CL6'ALGFI',X'C2',X'0A'   RIL
         DC    CL6'ALFI ',X'C2',X'0B'   RIL
         DC    CL6'CGFI ',X'C2',X'0C'   RIL
         DC    CL6'CFI  ',X'C2',X'0D'   RIL
         DC    CL6'CLGFI',X'C2',X'0E'   RIL
         DC    CL6'CLFI ',X'C2',X'0F'   RIL
*
         DC    CL6'LTG  ',X'E3',X'02'   RXY
         DC    CL6'LG   ',X'E3',X'04'   RXY
         DC    CL6'CVBY ',X'E3',X'06'   RXY
         DC    CL6'AG   ',X'E3',X'08'   RXY
         DC    CL6'SG   ',X'E3',X'09'   RXY
         DC    CL6'ALG  ',X'E3',X'0A'   RXY
         DC    CL6'SLG  ',X'E3',X'0B'   RXY
         DC    CL6'MSG  ',X'E3',X'0C'   RXY
         DC    CL6'DSG  ',X'E3',X'0D'   RXY
         DC    CL6'CVBG ',X'E3',X'0E'   RXY
         DC    CL6'LRVG ',X'E3',X'0F'   RXY
         DC    CL6'LT   ',X'E3',X'12'   RXY
         DC    CL6'LGF  ',X'E3',X'14'   RXY
         DC    CL6'LGH  ',X'E3',X'15'   RXY
         DC    CL6'LLGF ',X'E3',X'16'   RXY
         DC    CL6'LLGT ',X'E3',X'17'   RXY
         DC    CL6'AGF  ',X'E3',X'18'   RXY
         DC    CL6'SGF  ',X'E3',X'19'   RXY
         DC    CL6'ALGF ',X'E3',X'1A'   RXY
         DC    CL6'SLGF ',X'E3',X'1B'   RXY
         DC    CL6'MSGF ',X'E3',X'1C'   RXY
         DC    CL6'DSGF ',X'E3',X'1D'   RXY
         DC    CL6'LRV  ',X'E3',X'1E'   RXY
         DC    CL6'LRVH ',X'E3',X'1F'   RXY
         DC    CL6'CG   ',X'E3',X'20'   RXY
         DC    CL6'CLG  ',X'E3',X'21'   RXY
         DC    CL6'STG  ',X'E3',X'24'   RXY
         DC    CL6'CVDY ',X'E3',X'26'   RXY
         DC    CL6'CVDG ',X'E3',X'2E'   RXY
         DC    CL6'STRVG',X'E3',X'2F'   RXY
         DC    CL6'CGF  ',X'E3',X'30'   RXY
         DC    CL6'CLGF ',X'E3',X'31'   RXY
         DC    CL6'STRV ',X'E3',X'3E'   RXY
         DC    CL6'STRVH',X'E3',X'3F'   RXY
         DC    CL6'BCTG ',X'E3',X'46'   RXY
         DC    CL6'STY  ',X'E3',X'50'   RXY
         DC    CL6'MSY  ',X'E3',X'51'   RXY
         DC    CL6'NY   ',X'E3',X'54'   RXY
         DC    CL6'CLY  ',X'E3',X'55'   RXY
         DC    CL6'OY   ',X'E3',X'56'   RXY
         DC    CL6'XY   ',X'E3',X'57'   RXY
         DC    CL6'LY   ',X'E3',X'58'   RXY
         DC    CL6'CY   ',X'E3',X'59'   RXY
         DC    CL6'AY   ',X'E3',X'5A'   RXY
         DC    CL6'SY   ',X'E3',X'5B'   RXY
         DC    CL6'ALY  ',X'E3',X'5E'   RXY
         DC    CL6'SLY  ',X'E3',X'5F'   RXY
         DC    CL6'STHY ',X'E3',X'70'   RXY
         DC    CL6'LAY  ',X'E3',X'71'   RXY
         DC    CL6'STCY ',X'E3',X'72'   RXY
         DC    CL6'ICY  ',X'E3',X'73'   RXY
         DC    CL6'LB   ',X'E3',X'76'   RXY
         DC    CL6'LGB  ',X'E3',X'77'   RXY
         DC    CL6'LHY  ',X'E3',X'78'   RXY
         DC    CL6'CHY  ',X'E3',X'79'   RXY
         DC    CL6'AHY  ',X'E3',X'7A'   RXY
         DC    CL6'SHY  ',X'E3',X'7B'   RXY
         DC    CL6'NG   ',X'E3',X'80'   RXY
         DC    CL6'OG   ',X'E3',X'81'   RXY
         DC    CL6'XG   ',X'E3',X'82'   RXY
         DC    CL6'MLG  ',X'E3',X'86'   RXY
         DC    CL6'DLG  ',X'E3',X'87'   RXY
         DC    CL6'ALCG ',X'E3',X'88'   RXY
         DC    CL6'SLBG ',X'E3',X'89'   RXY
         DC    CL6'STPQ ',X'E3',X'8E'   RXY
         DC    CL6'LPQ  ',X'E3',X'8F'   RXY
         DC    CL6'LLGC ',X'E3',X'90'   RXY
         DC    CL6'LLGH ',X'E3',X'91'   RXY
         DC    CL6'LLC  ',X'E3',X'94'   RXY
         DC    CL6'LLH  ',X'E3',X'95'   RXY
         DC    CL6'ML   ',X'E3',X'96'   RXY
         DC    CL6'DL   ',X'E3',X'97'   RXY
         DC    CL6'ALC  ',X'E3',X'98'   RXY
         DC    CL6'SLB  ',X'E3',X'99'   RXY
*
         DC    CL6'LMG  ',X'EB',X'04'   RSY
         DC    CL6'SRAG ',X'EB',X'0A'   RSY
         DC    CL6'SLAG ',X'EB',X'0B'   RSY
         DC    CL6'SRLG ',X'EB',X'0C'   RSY
         DC    CL6'SLLG ',X'EB',X'0D'   RSY
         DC    CL6'CSY  ',X'EB',X'14'   RSY
         DC    CL6'RLLG ',X'EB',X'1C'   RSY
         DC    CL6'RLL  ',X'EB',X'1D'   RSY
         DC    CL6'CLMH ',X'EB',X'20'   RSY
         DC    CL6'CLMY ',X'EB',X'21'   RSY
         DC    CL6'STMG ',X'EB',X'24'   RSY
         DC    CL6'STMH ',X'EB',X'26'   RSY
         DC    CL6'STCMH',X'EB',X'2C'   RSY
         DC    CL6'STCMY',X'EB',X'2D'   RSY
         DC    CL6'CSG  ',X'EB',X'30'   RSY
         DC    CL6'CDSY ',X'EB',X'31'   RSY
         DC    CL6'CDSG ',X'EB',X'3E'   RSY
         DC    CL6'BXHG ',X'EB',X'44'   RSY
         DC    CL6'BXLEG',X'EB',X'45'   RSY
         DC    CL6'TMY  ',X'EB',X'51'   SIY
         DC    CL6'MVIY ',X'EB',X'52'   SIY
         DC    CL6'NIY  ',X'EB',X'54'   SIY
         DC    CL6'CLIY ',X'EB',X'55'   SIY
         DC    CL6'OIY  ',X'EB',X'56'   SIY
         DC    CL6'XIY  ',X'EB',X'57'   SIY
         DC    CL6'ICMH ',X'EB',X'80'   RSY
         DC    CL6'ICMY ',X'EB',X'81'   RSY
         DC    CL6'STMY ',X'EB',X'90'   RSY
         DC    CL6'LMH  ',X'EB',X'96'   RSY
         DC    CL6'LMY  ',X'EB',X'98'   RSY
         DC    CL6'TP   ',X'EB',X'C0'   RSL
*
         DC    CL6'BRXHG',X'EC',X'44'   RIE
         DC    CL6'JXHG ',X'EC',X'44'   RIE--SYNONYM
         DC    CL6'BRXLG',X'EC',X'45'   RIE
         DC    CL6'JXLEG',X'EC',X'45'   RIE--SYNONYM
*
         DC    CL6'LDEB ',X'ED',X'04'   RXE
         DC    CL6'LXDB ',X'ED',X'05'   RXE
         DC    CL6'LXEB ',X'ED',X'06'   RXE
         DC    CL6'MXDB ',X'ED',X'07'   RXE
         DC    CL6'KEB  ',X'ED',X'08'   RXE
         DC    CL6'CEB  ',X'ED',X'09'   RXE
         DC    CL6'AEB  ',X'ED',X'0A'   RXE
         DC    CL6'SEB  ',X'ED',X'0B'   RXE
         DC    CL6'MDEB ',X'ED',X'0C'   RXE
         DC    CL6'DEB  ',X'ED',X'0D'   RXE
         DC    CL6'MAEB ',X'ED',X'0E'   RXF
         DC    CL6'MSEB ',X'ED',X'0F'   RXF
         DC    CL6'TCEB ',X'ED',X'10'   RXE
         DC    CL6'TCDB ',X'ED',X'11'   RXE
         DC    CL6'TCXB ',X'ED',X'12'   RXE
         DC    CL6'SQEB ',X'ED',X'14'   RXE
         DC    CL6'SQDB ',X'ED',X'15'   RXE
         DC    CL6'MEEB ',X'ED',X'17'   RXE
         DC    CL6'KDB  ',X'ED',X'18'   RXE
         DC    CL6'CDB  ',X'ED',X'19'   RXE
         DC    CL6'ADB  ',X'ED',X'1A'   RXE
         DC    CL6'SDB  ',X'ED',X'1B'   RXE
         DC    CL6'MDB  ',X'ED',X'1C'   RXE
         DC    CL6'DDB  ',X'ED',X'1D'   RXE
         DC    CL6'MADB ',X'ED',X'1E'   RXF
         DC    CL6'MSDB ',X'ED',X'1F'   RXF
         DC    CL6'LDE  ',X'ED',X'24'   RXE
         DC    CL6'LXD  ',X'ED',X'25'   RXE
         DC    CL6'LXE  ',X'ED',X'26'   RXE
         DC    CL6'MAE  ',X'ED',X'2E'   RXF
         DC    CL6'MSE  ',X'ED',X'2F'   RXF
         DC    CL6'SQE  ',X'ED',X'34'   RXE
         DC    CL6'SQD  ',X'ED',X'35'   RXE
         DC    CL6'MEE  ',X'ED',X'37'   RXE
         DC    CL6'MAYL ',X'ED',X'38'   RXF
         DC    CL6'MYL  ',X'ED',X'39'   RXF
         DC    CL6'MAY  ',X'ED',X'3A'   RXF
         DC    CL6'MY   ',X'ED',X'3B'   RXF
         DC    CL6'MAYH ',X'ED',X'3C'   RXF
         DC    CL6'MYH  ',X'ED',X'3D'   RXF
         DC    CL6'MAD  ',X'ED',X'3E'   RXF
         DC    CL6'MSD  ',X'ED',X'3F'   RXF
         DC    CL6'SLDT ',X'ED',X'40'   RXF
         DC    CL6'SRDT ',X'ED',X'41'   RXF
         DC    CL6'SLXT ',X'ED',X'48'   RXF
         DC    CL6'SRXT ',X'ED',X'49'   RXF
         DC    CL6'TDCET',X'ED',X'50'   RXE
         DC    CL6'TDGET',X'ED',X'51'   RXE
         DC    CL6'TDCDT',X'ED',X'54'   RXE
         DC    CL6'TDGDT',X'ED',X'55'   RXE
         DC    CL6'TDCXT',X'ED',X'58'   RXE
         DC    CL6'TDGXT',X'ED',X'59'   RXE
         DC    CL6'LEY  ',X'ED',X'64'   RXY
         DC    CL6'LDY  ',X'ED',X'65'   RXY
         DC    CL6'STEY ',X'ED',X'66'   RXY
         DC    CL6'STDY ',X'ED',X'67'   RXY
*
         DC    X'FF'                    STOPPER
***********************
* PROCESSING ROUTINES *
***********************
E        DS    0H
* DOUBLE BYTE OPCODE
* PR SCKPF TAM SAM24 SAM31 SAM64
         USING *,R12
         BAL   R14,SRCHOPCD       SET UP OPCODE
         CLC   6(2,R7),=X'0101'   IS IT PR ?
         BE    EPR                EXIT IF IT IS
         MVC   EINST(2),6(R7)     SET THE INST DOUBLE OPCODE
         SAM24 ,                  MAKE SURE WE ARE IN 24-BIT MODE
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
EINST    SAM24 ,                  TESTED INST
         BCT   R7,EINST           DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
         B     EDONE              EXIT
*
EPR      EQU   *
* PR
         SR    R6,R6              PREPARE FOR DIVIDE
         L     R7,SPINCNT         SET LOOP LIMIT
         D     R6,=F'50'          CHECK IF MULTIPLE OF 50
         LTR   R6,R6              ANY REMAINDER ?
         BNZ   EERROR             ERROR IF THERE IS
* TIME 2 BRANCHES AND L,S,ST
* THESE REPLACE THE BCT LOOP
* WE ADD THE BCT TIME, SO IT'S NEUTRALIZED BY THE OUTPUT ROUTINE
         MVC   ESTORE,SPINCNT     SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
EEXTRA   B     EXTRA1             SPECIAL INST (EPCPR)
EXTRA1   EQU   *
         L     R7,ESTORE          LOAD CURRENT LOOP VALUE
         S     R7,=F'1'           -1
         ST    R7,ESTORE          STORE CURRENT LOOP VALUE
         BNZ   EEXTRA             WILL BE UNSTACK LOOP
         TIME  NS,TIMEAFT         AFTER TIME IN nS
*
         LG    R7,TIMEAFT         END TIME
         SG    R7,TIMEBEF                  - BEG TIME
         AG    R7,TIMEBCT                  + BCT TIME
         STG   R7,TIMEXTRA        SAVE IT
*
         MVC   EOUTER,SPINCNT     INITIAL OUTER SPIN LOOP
         XC    EACCUM,EACCUM      CLEAR TIME ACCUMULATOR
EOUTR1   EQU   *
         MVI   EPCPR+1,X'00'      ENSURE NOP
         L     R7,=F'50'          INNER SPIN COUNT
* DO THE STACKING
EPCGO    PC    EPCBCT             TESTED INST
EPCPR    NOP   EPRDECR            USED FOR UNSTACKING
EPCBCT   BCT   R7,EPCGO           DO IT
*
* NOW TIME THE UNSTACKING
* REGISTER RESTORE MEANS WE HAVE TO DO ALL LOOPS THE HARD WAY
         MVC   ESTORE,=F'50'      SET INNER SPIN COUNT
         MVI   EPCPR+1,X'F0'      ENSURE BRANCH
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
EPRGO    PR
EPRDECR  EQU   *
         L     R7,ESTORE          LOAD CURRENT LOOP VALUE
         S     R7,=F'1'           -1
         ST    R7,ESTORE          STORE CURRENT LOOP VALUE
         BNZ   EPRGO              UNSTACK LOOP
         TIME  NS,TIMEAFT         AFTER TIME IN nS
*
         LG    R7,TIMEAFT         END TIME
         SG    R7,TIMEBEF                  - BEG TIME
         LG    R6,EACCUM          SUM OF TIME DIFFS
         AGR   R6,R7              + CURRENT DIFF
         STG   R6,EACCUM          STORE BACK
         L     R7,EOUTER          OUTER SPIN COUNT
         S     R7,=F'50'          -50
         ST    R7,EOUTER          STORE BACK
         BNZ   EOUTR1             OUTER LOOP
* NOW FIDDLE THE TIMES AND POINT AT THE RIGHT INST
         MVC   TIMEAFT,EACCUM     TOTAL TIME DIFFERENCES
         XC    TIMEBEF,TIMEBEF    CLEAR BEFORE TIME
         LA    R5,EPRGO           INST ADDRESS
         B     EDONE2             EXIT
*
EDONE    EQU   *
         LA    R5,EINST           INST ADDRESS
EDONE2   EQU   *
         LA    R4,2               INST LENGTH
         BAL   R14,OUTPUT         WRITE THE OUTPUT RECORD
         B     GET                EXIT TO GET ANOTHER TEST
*
EERROR   EQU   *
         MVC   RSLTMICS(10),=C'SPIN ERROR'
         BAL   R14,OUTMSG         WRITE AND CLEAR
         B     GET                EXIT TO GET ANOTHER TEST
*
ESTORE   DS    F
EOUTER   DS    F                  OUTER LOOP CONTROL
EACCUM   DS    D                  SUM OF TIME DIFFERENCES
         LTORG
*
RR       DS    0H
* SINGLE BYTE OPCODE
* ALR AR CLR CR DR LCR LNR LPR LR LTR MR NR OR SLR SR XR
*
* FLOATING POINT
* LPDR LNDR LTDR LCDR HDR LDXR=LRDR MXR MXDR LDR CDR ADR SDR MDR DDR
* AWR SWR LPER LNER LTER LCER HER LEDR=LRER AXR SXR LER CER AER SER
* MDER=MER DER AUR SUR
*
* BRANCHES
* BALR BCTR BCR BSM BASSM BASR
*
* SPECIALS
* SPM MVCL CLCL
         USING *,R12
         BAL   R14,SRCHOPCD       SET UP OPCODE
         MVC   RRSAVE,RRBCR       SAVE MODIFIED BYTES
         CLI   INSTTYPE+1,C'P'    FLOATING POINT ?
         BE    RRFP               EXIT IF IT IS
         MVC   RRINST(1),6(R7)    SET THE INST
         CLI   RRINST,MVCL        IS IT MVCL ?
         BE    RRMVCL             EXIT IF IT IS
         CLI   RRINST,CLCL        IS IT CLCL ?
         BE    RRMVCL             EXIT IF IT IS
         CLI   RRINST,SPM         IS IT SPM ?
         BE    RRSPM              EXIT IF IT IS
         CLI   INSTTYPE,C'B'      BRANCH TYPE ?
         BE    RRBRANCH           EXIT IF IT IS
         SR    R10,R10            PREPARE FOR MULTIPLY/DIVIDE
         LA    R11,1              JUST SO I KNOW
         LA    R6,1               JUST SO I KNOW
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
RRINST   LR    R10,R6             TESTED INST
         BCT   R7,RRINST          DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
*
         B     RRDONE             EXIT
*
RRBRANCH EQU   *
         L     R15,=X'7FFFFFFF'   FOR BCTR
         MVC   RRBCR(1),6(R7)     SET THE INST--BRANCH
         LA    R10,RRBCRBCT       R2-FIELD
         CLI   INSTTYPE+1,C'0'    CONVERT TO NOP OR ZERO R2-FIELD ?
         BNE   RRNOTB0            EXIT IF NOT
         MVC   RSLTMICS(10),=C'TYPE ERROR' PRESET ERROR MSG
         CLI   RRBCR,BCR          IS IT BCR ?
         BNE   RRNOTBCR           EXIT IF NOT
* BRANCH TO NOPR FOR BCR
         NI    RRBCR+1,X'0F'      MAKE IT NOPR
         MVC   RSLTOPCD(4),=C'NOPR' MAKE IT NOPR
         B     RRNOTB0            EXIT
RRNOTBCR EQU   *
         CLI   RRBCR,BALR         IS IT BALR ?
         BE    RRNOR2             EXIT IF IT IS
         CLI   RRBCR,BCTR         IS IT BCTR ?
         BE    RRNOR2             EXIT IF IT IS
         CLI   RRBCR,BSM          IS IT BSM ?
         BE    RRNOR2             EXIT IF IT IS
         CLI   RRBCR,BASSM        IS IT BASSM ?
         BE    RRNOR2             EXIT IF IT IS
         CLI   RRBCR,BASR         IS IT BASR ?
         BNE   RRTYPERR           ERROR IF NOT
RRNOR2   EQU   *
* ZERO R2-FIELD FOR BALR BCTR BSM BASSM BASR
         NI    RRBCR+1,X'F0'      MAKE IT OPCD R15,0
RRNOTB0  EQU   *
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
* CAN FORM THE FOLLOWING BRANCH TYPES:
*   BR    R10
*   NOPR  R10
*   BALR  R15,R10
*   BALR  R15,0
*   BCTR  R15,R10
*   BCTR  R15,0
*   BSM   R15,R10
*   BSM   R15,0
*   BASSM R15,R10
*   BASSM R15,0
*   BASR  R15,R10
*   BASR  R15,0
RRBCR    BR    R10                TESTED INST
RRBCRBCT BCT   R7,RRBCR           DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
*
         LA    R5,RRBCR           INST ADDRESS
         B     RRDONE2            EXIT
*
RRTYPERR EQU   *
         BAL   R14,OUTMSG         WRITE AND CLEAR
         B     RRDONE3            EXIT
*
RRMVCL   EQU   *
* SPECIAL PROCESS FOR MVCL CLCL
         MVC   RRLONG(1),6(R7)    SET THE INST--LONG
         MVC   RSLTLEN,INSTLL     MOVE LENGTH TO OUTPUT
         ZAP   RRDEC,=P'0'        CLEAR RRDEC
         PACK  RRDEC+12(4),INSTLL-1(7) DECIMAL LENGTH
         CVBG  R1,RRDEC           CONVERT TO HEX
         LR    R5,R1              SET FOR MVCL/CLCL
* VALIDATE LEN
         LTR   R11,R1             SET FOR MVCL/CLCL
         BZ    RRLENERR           EXIT IF IT IS
         GETMAIN R,LV=1000000     'FROM' ADDRESS
         LR    R10,R1             SET FOR MVCL/CLCL
         GETMAIN R,LV=1000000     'TO' ADDRESS
         LR    R4,R1              SET FOR MVCL/CLCL
         STM   R4,R5,RRGET1       SAVE 'TO' REGS
         STM   R10,R11,RRGET2     SAVE 'FROM' REGS
* TIME A 2-REG LM
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
RRLM     LM    R4,R5,RRGET1       SPECIAL INST
         BCT   R7,RRLM            DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
*
         LG    R7,TIMEAFT         END TIME
         SG    R7,TIMEBEF                  - BEG TIME
         SG    R7,TIMEBCT                  - BCT TIME
         AGR   R7,R7              DOUBLE IT (NEED 2 LM'S)
         STG   R7,TIMEXTRA        SAVE IT
*
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
RRXTRA   LM    R4,R5,RRGET1       SPECIAL INST
         LM    R10,R11,RRGET2     SPECIAL INST
* CAN FORM THE FOLLOWING INSTS:
*   MVCL R4,R10
*   CLCL R4,R10
RRLONG   MVCL  R4,R10             TESTED INST
         BCT   R7,RRXTRA          DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
*
         FREEMAIN LV=1000000,A=RRGET1
         FREEMAIN LV=1000000,A=RRGET2
         LA    R5,RRLONG          INST ADDRESS
         B     RRDONE2            EXIT
*
RRSPM    EQU   *
         LA    R10,B'00000011'    CC=0,MASK=0011=NO FP OR DEC OFLO
         SLL   R10,24             SHIFT TO TOP
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
RRSPMGO  SPM   R10                TESTED INST
         BCT   R7,RRSPMGO         DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
*
         LA    R5,RRSPMGO         INST ADDRESS
         B     RRDONE2            EXIT
*
RRFP     EQU   *
* FLOATING POINT
* LPDR LNDR LTDR LCDR HDR LDXR=LRDR LDR CDR ADR SDR AWR SWR LPER LNER
* LTER LCER HER LEDR=LRER AXR SXR LER CER AER SER AUR SUR
*
* SPECIAL PROCESSING
* MXR MXDR
* MDR DDR MDER=MER DER
         LZXR  F0                 F0/F2=0
         LZXR  F1                 F1/F3=0
         CLI   6(R7),MXR          IS IT MXR ?
         BE    RRFPSPEC           EXIT IF IT IS
         CLI   6(R7),MXDR         IS IT MXDR ?
         BE    RRFPSPEC           EXIT IF IT IS
         CLI   6(R7),MDR          IS IT MDR ?
         BE    RRFPSPEC           EXIT IF IT IS
         CLI   6(R7),MDER         IS IT MDER ?
         BE    RRFPSPEC           EXIT IF IT IS
         CLI   6(R7),MER          IS IT MER ?
         BE    RRFPSPEC           EXIT IF IT IS
         CLI   6(R7),DDR          IS IT DDR ?
         BE    RRFPSPEC           EXIT IF IT IS
         CLI   6(R7),DER          IS IT DER ?
         BE    RRFPSPEC           EXIT IF IT IS
         MVC   RRFPINS(1),6(R7)   SET THE INST
* LOADED VALUES ARE EXACT IN ALL SIZES
         LD    F0,=DH'1'          SET F0
         LD    F1,=DH'99'         SET F1
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
RRFPINS  LPDR  F0,F1              TESTED INST
         BCT   R7,RRFPINS         DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
*
         LA    R5,RRFPINS         INST ADDRESS
         B     RRDONE2            EXIT
*
RRFPSPEC EQU   *
* MXR MXDR
* MDR DDR MER=MDER DER
         MVC   RRFPIN2(1),6(R7)   SET THE INST
* LOADED VALUES ARE EXACT IN ALL SIZES
         LD    F1,=DH'99'         SET F1
* TIME AN LD
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
RRLD     LD    F0,=DH'1000'       SPECIAL INST
         BCT   R7,RRLD            DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
*
         LG    R7,TIMEAFT         END TIME
         SG    R7,TIMEBEF                  - BEG TIME
         SG    R7,TIMEBCT                  - BCT TIME
         STG   R7,TIMEXTRA        SAVE IT
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
RRFPSPC  LD    F0,=DH'1000'       SPECIAL INST
RRFPIN2  MDR   F0,F1              TESTED INST
         BCT   R7,RRFPSPC         DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
*
         LA    R5,RRFPIN2         INST ADDRESS
         B     RRDONE2            EXIT
*
RRLENERR EQU   *
         MVC   RSLTMICS+1(12),=C'LENGTH ERROR'
         BAL   R14,OUTMSG         WRITE AND CLEAR
         B     GET                EXIT TO GET ANOTHER TEST
*
RRDONE   EQU   *
         LA    R5,RRINST          INST ADDRESS
RRDONE2  EQU   *
         LA    R4,2               INST LENGTH
         BAL   R14,OUTPUT         WRITE THE OUTPUT RECORD
RRDONE3  EQU   *
         MVC   RRBCR,RRSAVE       RESTORE MODIFIED BYTES
         B     GET                EXIT TO GET ANOTHER TEST
RRDEC    DC    PL16'0'
RRGET1   DS    2F                 SAVE 'TO' REGS
RRGET2   DS    2F                 SAVE 'FROM' REGS
RRSAVE   DS    H
         LTORG
*
S        DS    0H
* DOUBLE BYTE OPCODE
* STIDP STCK STCKC STPT STPX STAP PC STCKE STCKF SRNM STFPC LFPC STFLE
* SRNMT LFAS
         USING *,R12
         BAL   R14,SRCHOPCD       SET UP OPCODE
         CLC   6(2,R7),=X'B218'   IS IT PC ?
         BE    SPC                EXIT IF IT IS
         MVC   SINST(2),6(R7)     SET THE INST DOUBLE OPCODE
         CLC   6(2,R7),=X'B2B9'   IS IT SRNMT ?
         BNE   SNTSRNMT           EXIT IF NOT
         SGR   R6,R6              SET DFP ROUNDING MODE=0
         B     SGO                EXIT
*
SNTSRNMT EQU   *
         LA    R6,SSTORE          DOESN'T MATTER WHAT WE DO TO THIS
         STFPC SSTORE             SET UP FOR LFPC/LFAS
*
SGO      EQU   *
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
SINST    STCK  0(R6)              TESTED INST
         BCT   R7,SINST           DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
         B     SDONE              EXIT
*
SPC      EQU   *
* PC
         SR    R6,R6              PREPARE FOR DIVIDE
         L     R7,SPINCNT         SET LOOP LIMIT
         D     R6,=F'50'          CHECK IF MULTIPLE OF 50
         LTR   R6,R6              ANY REMAINDER ?
         BNZ   SERROR             ERROR IF THERE IS
         MVC   SOUTER,SPINCNT     INITIAL OUTER SPIN LOOP
         XC    SACCUM,SACCUM      CLEAR TIME ACCUMULATOR
SOUTR1   EQU   *
         MVI   SPCPR+1,X'00'      ENSURE NOP
         L     R7,=F'50'          INNER SPIN COUNT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
SPCGO    PC    SPCBCT             TESTED INST
SPCPR    NOP   SPRDECR            USED FOR UNSTACKING
SPCBCT   BCT   R7,SPCGO           DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
*
         LG    R7,TIMEAFT         END TIME
         SG    R7,TIMEBEF                  - BEG TIME
         LG    R6,SACCUM          SUM OF TIME DIFFS
         AGR   R6,R7              + CURRENT DIFF
         STG   R6,SACCUM          STORE BACK
* NOW UNSTACK
* REGISTER RESTORE MEANS WE HAVE TO DO ALL LOOPS THE HARD WAY
         MVC   SSTORE(4),=F'50'   SET INNER SPIN COUNT
         MVI   SPCPR+1,X'F0'      ENSURE BRANCH
SPRGO    PR
SPRDECR  EQU   *
         L     R7,SSTORE          LOAD CURRENT LOOP VALUE
         S     R7,=F'1'           -1
         ST    R7,SSTORE          STORE CURRENT LOOP VALUE
         BNZ   SPRGO              UNSTACK LOOP
         L     R7,SOUTER          OUTER SPIN COUNT
         S     R7,=F'50'          -50
         ST    R7,SOUTER          STORE BACK
         BNZ   SOUTR1             OUTER LOOP
* NOW FIDDLE THE TIMES AND POINT AT THE RIGHT INST
         MVC   TIMEAFT,SACCUM     TOTAL TIME DIFFERENCES
         XC    TIMEBEF,TIMEBEF    CLEAR BEFORE TIME
         LA    R5,SPCGO           INST ADDRESS
         B     SDONE2             EXIT
*
SDONE    EQU   *
         LA    R5,SINST           INST ADDRESS
SDONE2   EQU   *
         LA    R4,4               INST LENGTH
         BAL   R14,OUTPUT         WRITE THE OUTPUT RECORD
         B     GET                EXIT TO GET ANOTHER TEST
*
SERROR   EQU   *
         MVC   RSLTMICS(10),=C'SPIN ERROR'
         BAL   R14,OUTMSG         WRITE AND CLEAR
         B     GET                EXIT TO GET ANOTHER TEST
*
SSTORE   DC    XL16'00'           STORE RESULT
SOUTER   DS    F                  OUTER LOOP CONTROL
SACCUM   DS    D                  SUM OF TIME DIFFERENCES
         LTORG
*
SI       DS    0H
* SINGLE BYTE OPCODE
* TM MVI TS NI CLI OI XI
         USING *,R12
         BAL   R14,SRCHOPCD       SET UP OPCODE
         MVC   SIINST(1),6(R7)    SET THE INST
         LA    R6,SIBYTE          DOESN'T MATTER WHAT WE DO TO THIS
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
SIINST   CLI   0(R6),X'99'        TESTED INST
         BCT   R7,SIINST          DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
*
         LA    R4,4               INST LENGTH
         LA    R5,SIINST          INST ADDRESS
         BAL   R14,OUTPUT         WRITE THE OUTPUT RECORD
         B     GET                EXIT TO GET ANOTHER TEST
SIBYTE   DC    X'00'              SI BYTE
         LTORG
*
RS       DS    0H
* SINGLE BYTE OPCODE
*
* L1 IS NO. OF REGISTERS
*    STM LM
* ALLOW 0B AND 00
*    SRL SLL SRA SLA SRDL SLDL SRDA SLDA
* SPECIALS
*    BXH BXLE MVCLE CLCLE
*    CS CDS
         USING *,R12
         BAL   R14,SRCHOPCD       SET UP OPCODE
         MVC   RSSAV1,RSSTM       SAVE ORIG INST
         MVC   RSSAV2,RSSTMX      SAVE ORIG INST
         MVC   RSSAV3,RSXTRA      SAVE ORIG INST
         MVC   RSSAV4,RSLM        SAVE ORIG INST
         MVC   RSSAV5,RSINSFT+2   SAVE MODIFIED BYTES
* 86-87
         CLI   6(R7),BXH          BXH ?
         BE    RSBX               EXIT IF IT IS
         CLI   6(R7),BXLE         BXLE ?
         BE    RSBX               EXIT IF IT IS
* 88-8F
         CLI   6(R7),SRL          SHIFT ?
         BL    RSABEND            ABEND IF NOT
         CLI   6(R7),SLDA         SHIFT ?
         BNH   RSSHIFT            EXIT IF IT IS
* 90 OR 98
         CLI   6(R7),LM           LM ?
         BE    RSLMSTM            EXIT IF IT IS
         CLI   6(R7),STM          STM ?
         BE    RSLMSTM            EXIT IF IT IS
* A8-A9
         CLI   6(R7),MVCLE        MVCLE ?
         BE    RSMVCLE            EXIT IF IT IS
         CLI   6(R7),CLCLE        CLCLE ?
         BE    RSMVCLE            EXIT IF IT IS
* BA-BB
         CLI   6(R7),CS           CS ?
         BE    RSCS               EXIT IF IT IS
         CLI   6(R7),CDS          CDS ?
         BE    RSCS               EXIT IF IT IS
* BD-BF
         CLI   6(R7),CLM          CLM, STCM, ICM ?
         BL    RSABEND            ABEND IF NOT
         CLI   6(R7),ICM          CLM, STCM, ICM ?
         BNH   RSMASK             EXIT IF IT IS
RSABEND  EQU   *
         ABEND X'222'             SHOULD NOT OCCUR--BUT HAS !
*
RSLMSTM  EQU   *
* LM OR STM
         MVC   RSLTLL1,INSTLL1    MOVE LENGTH 1 TO OUTPUT
* PROCESS LENGTH 1
         ZAP   RSDEC,=P'0'        CLEAR RSDEC
         PACK  RSDEC+14(2),INSTLL1-1(3) DECIMAL LENGTH 1
         CVBG  R10,RSDEC          CONVERT TO HEX
         LTR   R10,R10            IS IT ZERO ?
         BZ    RSERROR            EXIT IF IT IS
         C     R10,=F'16'         GT THAN 16 ?
         BH    RSERROR            EXIT IF IT IS
         BCTR  R10,0              -1 FOR END REGISTER
         EX    R10,RSOI1          PUT END REG INTO INST
         EX    R10,RSOI2          PUT END REG INTO INST--EXTRA
         EX    R10,RSOI3          PUT END REG INTO INST--STM
         EX    R10,RSOI4          PUT END REG INTO INST--LM
         CLI   6(R7),LM           IS IT LM ?
         BE    RSLMGO             EXIT IF IT IS
* ONLY STM GETS HERE
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
*              R1 R3 D(B)    900?BDDD
RSSTM    STM   R0,R0,RSOP1        TESTED INST
         BCT   R7,RSSTM           DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
         LA    R5,RSSTM           INST ADDRESS
         B     RSDONE             EXIT
*
RSLMGO   EQU   *
* ONLY LM GETS HERE
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
* TIME THE STM
*              R1 R3 D(B)    900?BDDD
RSSTMX   STM   R0,R0,RSOP1        SPECIAL INST
         BCT   R7,RSSTMX          DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
*
         LG    R7,TIMEAFT         END TIME
         SG    R7,TIMEBEF                  - BEG TIME
         SG    R7,TIMEBCT                  - BCT TIME
         STG   R7,TIMEXTRA        SAVE IT
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
RSXTRA   STM   R0,R0,RSOP1        EXTRA INST
*              R1 R3 D(B)    980?BDDD
RSLM     LM    R0,R0,RSOP1        TESTED INST
         BCT   R7,RSXTRA          DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
*
         LA    R5,RSLM            INST ADDRESS
         B     RSDONE             EXIT
*
RSOI1    OI    RSSTM+1,X'00'      EXECUTED INST
RSOI2    OI    RSSTMX+1,X'00'     EXECUTED INST
RSOI3    OI    RSXTRA+1,X'00'     EXECUTED INST
RSOI4    OI    RSLM+1,X'00'       EXECUTED INST
*
RSSHIFT  EQU   *
* PROCESS THE 8 SHIFT INSTS
         MVC   RSINSFT(1),6(R7)   SET THE INST--SHIFT
         MVC   RSLTLEN(2),INSTXB  MOVE THE XB TO OUTPUT
* USE OF INDEX ALWAYS INVALID
         CLC   INSTXB,=C'XB'      VALIDATE ?
         BE    RSXBERR            EXIT IF ERROR
         CLC   INSTXB,=C'X0'      VALIDATE ?
         BE    RSXBERR            EXIT IF ERROR
* XB INPUT VALID: 0B OR 00
         CLI   INSTXB+1,C'0'      IS BASE ZERO ?
         BNE   RSXB1              EXIT IF NOT
         NI    RSINSFT+2,X'0F'    KNOCK OUT THE BASE
RSXB1    EQU   *
         L     R10,=X'FFFFFFFF'   GIVE IT SOMETHING
         LR    R11,R10                              TO SHIFT
         LA    R6,1               WITH BASE SHIFT 2, ELSE 1
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
RSINSFT  SRL   R10,1(R6)          TESTED INST
         BCT   R7,RSINSFT         DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
*
         LA    R5,RSINSFT         INST ADDRESS
         B     RSDONE             EXIT
*
RSCS     EQU   *
* CS CDS
         MVC   RSCSGO(1),6(R7)    SET THE INST--CS TYPE
         LM    R4,R5,RSCSDTEQ     SET R3 REG/DOUBLE REG
         CLI   INSTTYPE,C'E'      R1=D2(B2) ?
         BNE   RSCSNTEQ           EXIT IF NOT
* R1=D2(B2)
         LA    R6,RSCSDTEQ        SET DATA FIELD
         B     RSCSSET            EXIT
RSCSNTEQ EQU   *
* R1<>D2(B2) (R3 NOT USED)
         LA    R6,RSCSDTNE        SET DATA FIELD
RSCSSET  EQU   *
* TIME A 2-REG LM
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
RSCSLM   LM    R10,R11,RSCSDTEQ   SPECIAL INST
         BCT   R7,RSCSLM          DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
*
         LG    R7,TIMEAFT         END TIME
         SG    R7,TIMEBEF                  - BEG TIME
         SG    R7,TIMEBCT                  - BCT TIME
         STG   R7,TIMEXTRA        SAVE IT
*
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
RSCSLM2  LM    R10,R11,RSCSDTEQ   SPECIAL INST
RSCSGO   CS    R10,R4,0(R6)       TESTED INST
         BCT   R7,RSCSLM2         DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
*
         LA    R5,RSCSGO          INST ADDRESS
         B     RSDONE             EXIT
RSCSDTEQ DC    F'1',F'1'
RSCSDTNE DC    F'2',F'2'
*
RSMASK   EQU   *
* CLM STCM ICM
         MVC   RSINMSK(1),6(R7)   SET THE INST--MASK TYPE
         L     R10,=X'FFFFFFFF'   GIVE IT SOMETHING TO DO
         MVC   RSICM,=X'0123'     SHOWS ICM WORKING
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
RSINMSK  CLM   R10,9,RSICM        TESTED INST
         BCT   R7,RSINMSK         DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
*
         LA    R5,RSINMSK         INST ADDRESS
         B     RSDONE             EXIT
RSICM    DS    H
*
RSMVCLE  EQU   *
* SPECIAL PROCESS FOR MVCLE CLCLE
         MVC   RSLONG(1),6(R7)    SET THE INST--LONG
         MVC   RSLTLEN,INSTLL     MOVE LENGTH TO OUTPUT
         ZAP   RSDEC,=P'0'        CLEAR RSDEC
         PACK  RSDEC+12(4),INSTLL-1(7) DECIMAL LENGTH
         CVBG  R1,RSDEC           CONVERT TO HEX
         LR    R5,R1              SET FOR MVCLE/CLCLE
* VALIDATE LEN
         LTR   R11,R1             SET FOR MVCLE/CLCLE
         BZ    RSLENERR           EXIT IF IT IS
         GETMAIN R,LV=1000000     'FROM' ADDRESS
         LR    R10,R1             SET FOR MVCLE/CLCLE
         GETMAIN R,LV=1000000     'TO' ADDRESS
         LR    R4,R1              SET FOR MVCLE/CLCLE
         STM   R4,R5,RSGET1       SAVE 'TO' REGS
         STM   R10,R11,RSGET2     SAVE 'FROM' REGS
* TIME A 2-REG LM
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
RSLMMVCL LM    R4,R5,RSGET1       SPECIAL INST
         BCT   R7,RSLMMVCL        DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
*
         LG    R7,TIMEAFT         END TIME
         SG    R7,TIMEBEF                  - BEG TIME
         SG    R7,TIMEBCT                  - BCT TIME
         AGR   R7,R7              DOUBLE IT (NEED 2 LM'S)
         STG   R7,TIMEXTRA        SAVE IT
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
RSXTRAMV LM    R4,R5,RSGET1       RESTORE 'TO' REGS
         LM    R10,R11,RSGET2     RESTORE 'FROM' REGS
* CAN FORM THE FOLLOWING INSTS:
*   MVCLE R4,R10,C' '
*   CLCLE R4,R10,C' '
RSLONG   MVCLE R4,R10,C' '        TESTED INST
         BCT   R7,RSXTRAMV        DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
*
         FREEMAIN LV=1000000,A=RSGET1
         FREEMAIN LV=1000000,A=RSGET2
         LA    R5,RSLONG          INST ADDRESS
         B     RSDONE             EXIT
*
RSLENERR EQU   *
         MVC   RSLTMICS+1(12),=C'LENGTH ERROR'
         BAL   R14,OUTMSG         WRITE AND CLEAR
         B     GET                EXIT TO GET ANOTHER TEST
*
RSGET1   DS    2F                 SAVE 'TO' REGS
RSGET2   DS    2F                 SAVE 'FROM' REGS
*
RSBX     EQU   *
* BXH OR BXLE
         MVC   RSINBX(1),6(R7)    SET THE INST
         CLI   RSINBX,BXH         BXH ?
         BNE   RSBXLE             EXIT IF NOT
* BXH
         L     R10,RSBXHGO        LOAD INDEX REGISTER--ALWAYS BRANCH
         CLI   INSTTYPE+1,C'0'    NOP REQUESTED ?
         BNE   RSBXGO             EXIT IF NOT
         L     R10,RSBXHNOP       LOAD INDEX REGISTER--NEVER BRANCH
         B     RSBXGO             EXIT
RSBXLE   EQU   *
* BXLE
         L     R10,RSBXLEGO       LOAD INDEX REGISTER--ALWAYS BRANCH
         CLI   INSTTYPE+1,C'0'    NOP REQUESTED ?
         BNE   RSBXGO             EXIT IF NOT
         L     R10,RSBXLENP       LOAD INDEX REGISTER--NEVER BRANCH
*
RSBXGO   EQU   *
         LA    R11,1              SET INCREMENT AND COMPARE
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
RSINBX   BXH   R10,R11,RSBXBCT    TESTED INST
RSBXBCT  BCT   R7,RSINBX          DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
*
         LA    R5,RSINBX          INST ADDRESS
         B     RSDONE             EXIT
RSBXHGO  DC    F'1'               BXH AND BRANCH
RSBXHNOP DC    X'80000000'        BXH AND NO BRANCH
RSBXLEGO DC    X'80000000'        BXLE AND BRANCH
RSBXLENP DC    F'1'               BXLE AND NO BRANCH
*
RSDONE   EQU   *
         LA    R4,4               INST LENGTH
         BAL   R14,OUTPUT         WRITE THE OUTPUT RECORD
         MVC   RSSTM(2),RSSAV1    RESTORE ORIG INST
         MVC   RSSTMX(2),RSSAV2   RESTORE ORIG INST
         MVC   RSXTRA(2),RSSAV3   RESTORE ORIG INST
         MVC   RSLM(2),RSSAV4     RESTORE ORIG INST
         MVC   RSINSFT+2(2),RSSAV5 RESTORE MODIFIED BYTES
         B     GET                EXIT TO GET ANOTHER TEST
*
RSXBERR  EQU   *
* XB FIELD INVALID FOR INSTRUCTION
         MVC   RSLTMICS(8),=C'XB ERROR' ERROR MSG
         B     RSERROR2           EXIT TO GET ANOTHER TEST
*
RSERROR  EQU   *
         MVC   RSLTMICS+1(12),=C'LENGTH ERROR'
RSERROR2 EQU   *
         BAL   R14,OUTMSG         WRITE AND CLEAR
         B     GET                EXIT TO GET ANOTHER TEST
*
RSSAV1   DS    H
RSSAV2   DS    H
RSSAV3   DS    H
RSSAV4   DS    H
RSSAV5   DS    H
RSDEC    DC    PL16'0'
RSOP1    DS    16CL4
         LTORG
*
RX       DS    0H
* SINGLE BYTE OPCODE
* LA STC IC EX BAL BCT B=BC LH CH SH AH MH BAS CVD CVB ST
* N CL O X L C A S M D AL SL MS
*
* FLOATING POINT
* STD MXD LD CD AD SD MD DD AW SW
* STE LE CE AE SE MDE=ME DE AU SU
         USING *,R12
         BAL   R14,SRCHOPCD       SET UP OPCODE
         MVC   RSLTLEN(2),INSTXB  MOVE THE XB TO OUTPUT
         MVC   RXINST(1),6(R7)    SET THE INST
* USE OF BASE ALWAYS VALID
         CLC   INSTXB,=C'XB'      VALIDATE ?
         BE    RXXBOK             EXIT IF IT IS
         CLC   INSTXB,=C'0B'      VALIDATE ?
         BE    RXXBOK             EXIT IF IT IS
         CLC   INSTXB,=C'X0'      VALIDATE ?
         BE    RXNOBASE           EXIT IF IT IS
         CLC   INSTXB,=C'00'      VALIDATE ?
         BNE   RXXBERR            ERROR IF NOT
RXNOBASE EQU   *
* X0 OR 00
* SOME INSTS REQUIRE FORMAT
         CLI   RXINST,CVB         IS IT CVB ?
         BE    RXXBERR            ERROR IF IT IS
* STORE INSTS MUST NOT STORE INTO LOW STORAGE
         CLI   INSTSTOR,C'S'      IS IT STORE TYPE ?
         BE    RXXBERR            ERROR IF IT IS
* DIVIDES MUST NOT ACCESS LOW STORAGE
         CLI   RXINST,D           IS IT D ?
         BE    RXXBERR            ERROR IF IT IS
         CLI   RXINST,DD          IS IT DD ?
         BE    RXXBERR            ERROR IF IT IS
         CLI   RXINST,DE          IS IT DE ?
         BE    RXXBERR            ERROR IF IT IS
* NEED BASE FOR BRANCHES
         CLI   RXINST,BC          IS IT BC ?
         BE    RXXBERR            ERROR IF IT IS
         CLI   RXINST,BAL         IS IT BAL ?
         BE    RXXBERR            ERROR IF IT IS
         CLI   RXINST,BCT         IS IT BCT ?
         BE    RXXBERR            ERROR IF IT IS
         CLI   RXINST,BAS         IS IT BAS ?
         BE    RXXBERR            ERROR IF IT IS
* SPECIAL
         CLI   RXINST,EX          IS IT EX ?
         BE    RXXBERR            ERROR IF IT IS
* NEVER HAVE FLOATING POINT VALUES IN LOW STORAGE
         CLI   INSTTYPE+1,C'P'    FLOATING POINT ?
         BE    RXXBERR            ERROR IF IT IS
         B     RXXBOK             EXIT
*
RXXBERR  EQU   *
* XB FIELD INVALID FOR INSTRUCTION
         MVC   RSLTMICS(8),=C'XB ERROR' ERROR MSG
         BAL   R14,OUTMSG         WRITE AND CLEAR
         B     GET                EXIT TO GET ANOTHER TEST
*
RXXBOK   EQU   *
* XB INPUT VALID
         MVC   RXSAVE,RXINST+1    SAVE MODIFIED BYTES
         MVC   RXSAVB,RXBR+1      SAVE MODIFIED BYTES--BRANCH
         MVC   RXSAVX,RXEX+1      SAVE MODIFIED BYTES--EXECUTE
         MVC   RXSAVC,RXBCTGO+1   SAVE MODIFIED BYTES--BCT
         MVC   RXSAVP,RXFPINS+1   SAVE MODIFIED BYTES--FP
         CLI   INSTXB,C'0'        IS INDEX ZERO ?
         BNE   RXXB1              EXIT IF NOT
         NI    RXINST+1,X'F0'     KNOCK OUT THE INDEX
         NI    RXBR+1,X'F0'       KNOCK OUT THE INDEX--BRANCH
         NI    RXEX+1,X'F0'       KNOCK OUT THE INDEX--EXECUTE
         NI    RXBCTGO+1,X'F0'    KNOCK OUT THE INDEX--BCT
         NI    RXFPINS+1,X'F0'    KNOCK OUT THE INDEX--FP
RXXB1    EQU   *
         CLI   INSTXB+1,C'0'      IS BASE ZERO ?
         BNE   RXXB2              EXIT IF NOT
         NI    RXINST+2,X'0F'     KNOCK OUT THE BASE
         NI    RXFPINS+2,X'0F'    KNOCK OUT THE BASE--FP
RXXB2    EQU   *
         CLI   RXINST,EX          IS IT EX ?
         BE    RXEXECUT           EXIT IF IT IS
         CLI   INSTTYPE,C'B'      BRANCH INST ?
         BE    RXBRANCH           EXIT IF IT IS
         LA    R10,99             SET RX R1-FIELD
         LR    R11,R10            SET RX R1+1-FIELD
* SET H, TEST H
         LA    R5,RXBASEH         SET RX BASE
         LA    R6,2               SET RX INDEX
         MVI   RXINST+3,X'02'     SET RX DISPLACEMENT
         CLI   INSTTYPE,C'H'      HALFWORD INST ?
         BE    RXSET              EXIT IF IT IS
* SET F, TEST F
         LA    R5,RXBASEF         SET RX BASE
         LA    R6,4               SET RX INDEX
         MVI   RXINST+3,X'04'     SET RX DISPLACEMENT
         CLI   INSTTYPE,C'F'      FULLWORD INST ?
         BE    RXSET              EXIT IF IT IS
* SET D, TEST D
         LA    R5,RXBASED         SET RX BASE
         LA    R6,8               SET RX INDEX
         MVI   RXINST+3,X'08'     SET RX DISPLACEMENT
         MVI   RXFPINS+3,X'08'    SET RX DISPLACEMENT--SPECIAL
         CLI   INSTTYPE,C'X'      LONG FP INST, BUT EXTENDED RESULT ?
         BE    RXLIKED            EXIT IF NOT
         CLI   INSTTYPE,C'D'      LONG FP INST ?
         BNE   RXTRYE             EXIT IF NOT
RXLIKED  EQU   *
* RX FP MUST USE F0, ALWAYS IS FOR SPECIALS
         NI    RXINST+1,X'0F'     MAKE R1=0 (F0)
         LZXR  F0                 CLEAR F0/F2
         B     RXSET              EXIT
*
RXTRYE   EQU   *
* SET E, TEST E
         LA    R5,RXBASEE         SET RX BASE
         LA    R6,4               SET RX INDEX
         MVI   RXINST+3,X'04'     SET RX DISPLACEMENT
         MVI   RXFPINS+3,X'04'    SET RX DISPLACEMENT--SPECIAL
         CLI   INSTTYPE,C'E'      SHORT FP INST ?
         BNE   RXTRYP8            EXIT IF NOT
* MDE=ME IS A SPECIAL CASE
         CLI   RXINST,MDE         IS IT MDE ?
         BE    RXMDE              EXIT IF IT IS
         CLI   RXINST,ME          IS IT ME ?
         BNE   RXNTMDE            EXIT IF NOT
RXMDE    EQU   *
         MVI   INSTTYPE,C'D'      CHANGE TO LONG RESULT
RXNTMDE  EQU   *
* RX FP MUST USE F0, ALWAYS IS FOR SPECIALS
         NI    RXINST+1,X'0F'     MAKE R1=0 (F0)
         LZDR  F0                 CLEAR F0
         B     RXSET              EXIT
RXTRYP8  EQU   *
* SET P8, TEST P8
         LA    R5,RXBASEP8        SET RX BASE
         LA    R6,8               SET RX INDEX
         MVI   RXINST+3,X'08'     SET RX DISPLACEMENT
         CLC   INSTTYPE(2),=C'P8' PL8 INST ?
         BE    RXSET              EXIT IF IT IS
         ABEND X'111'             ERROR IN TABLE
RXSET    EQU   *
* FP MULTIPLY/DIVIDE ARE SPECIAL
         CLI   RXINST,MXD         IS IT MXD ?
         BE    RXFP               EXIT IF IT IS
         CLI   RXINST,MD          IS IT MD ?
         BE    RXFP               EXIT IF IT IS
         CLI   RXINST,DD          IS IT DD ?
         BE    RXFP               EXIT IF IT IS
         CLI   RXINST,MDE         IS IT MDE ?
         BE    RXFP               EXIT IF IT IS
         CLI   RXINST,ME          IS IT ME ?
         BE    RXFP               EXIT IF IT IS
         CLI   RXINST,DE          IS IT DE ?
         BE    RXFP               EXIT IF IT IS
         CLI   INSTSTOR,C'S'      IS IT STORE TYPE ?
         BNE   RXNTSTOR           EXIT IF NOT
* STORE TYPE, USE SPECIAL AREA
         LA    R5,RXSTORE         COMMON AREA FOR ALL STORE TYPES
RXNTSTOR EQU   *
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
*              R1  D X2 B2   41A65002
RXINST   LA    R10,2(R6,R5)       TESTED INST
         BCT   R7,RXINST          DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
*
         B     RXDONE             EXIT
*
RXBRANCH EQU   *
* BRANCH TYPE
         MVC   RXBR(1),6(R7)      SET THE INST--BRANCH
         CLI   RXBR,BCT           IS IT BCT ?
         BE    RXBCT              EXIT IF IT IS
         CLI   INSTTYPE+1,C'0'    CONVERT TO NOP ?
         BNE   RXBRNTNP           EXIT IF NOT
* NOP REQUIRED
         MVC   RSLTMICS(10),=C'TYPE ERROR' PRESET ERROR MSG
         CLI   RXBR,BAL           IS IT BAL ?
         BE    RXTYPERR           ERROR IF IT IS
         CLI   RXBR,BAS           IS IT BAS ?
         BNE   RXNTBAL            EXIT IF NOT
RXTYPERR EQU   *
         BAL   R14,OUTMSG         WRITE AND CLEAR
         B     RXDONE3            EXIT
RXNTBAL  EQU   *
         NI    RXBR+1,X'0F'       KILL THE MASK
         MVC   RSLTOPCD(3),=C'NOP' MAKE IT NOP
RXBRNTNP EQU   *
         LA    R6,RXBCT2-RXBCT1   R6=BR TO RXBCT2 IF INDEXED
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
* CAN FORM THE FOLLOWING BRANCH TYPES:
*   B   LABEL
*   B   LABEL(R6)
*   NOP LABEL      ) THE
*   NOP LABEL(R6)  )     SAME
*   BAL R15,LABEL
*   BAL R15,LABEL(R6)
*   BAS R15,LABEL
*   BAS R15,LABEL(R6)
RXBR     BC    15,RXBCT1(R6)      TESTED INST
RXBCT1   BCT   R7,RXBR            DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
*
         B     RXBRDUN            EXIT
* INDEXED BRANCH
RXBCT2   BCT   R7,RXBR            DO IT (INDEXED)
         TIME  NS,TIMEAFT         AFTER TIME IN nS
*
RXBRDUN  EQU   *
         LA    R5,RXBR            INST ADDRESS
         B     RXDONE2            EXIT
*
RXEXECUT EQU   *
* PROCESS EXECUTE
* TIME THE TARGET
* TIME THE EXECUTED TARGET
* SUBTRACT GIVES OVERHEAD OF EXECUTE
* I PICK A 10-BYTE MVC AS TARGET FOR NO GOOD REASON
         CLI   INSTTYPE+1,C'0'    NO R1-FIELD ?
         BNE   RXEXR1             EXIT IF NOT
         NI    RXEX+1,X'0F'       KILL R1-FIELD
RXEXR1   EQU   *
         L     R7,SPINCNT         SET LOOP LIMIT
*
* TIME THE TARGET
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
RXEXTRA  MVC   RXSTORE(10),RXSTORE+10 10-BYTE MVC
         BCT   R7,RXEXTRA         DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
*
         LG    R7,TIMEAFT         END TIME
         SG    R7,TIMEBEF                  - BEG TIME
         SG    R7,TIMEBCT                  - BCT TIME
         STG   R7,TIMEXTRA        SAVE IT
*
         LA    R5,RXEXMVC         SET RX BASE
         LA    R6,6               SET RX INDEX
         LA    R10,9              SET EX OR FOR 10-BYTE MVC
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
*              R1  D X2 B2   44A65000
RXEX     EX    R10,0(R6,R5)       TESTED INST
         BCT   R7,RXEX            DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
*
         LA    R5,RXEX            INST ADDRESS
         B     RXDONE2            EXIT
*
RXBCT    EQU   *
* BCT
         SR    R11,R11            R11=0  INDEX TO NON-BRANCH DATA
* TIME AN L
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
RXXTRA   L     R10,RXBCTD1(R11)   SPECIAL INST
         BCT   R7,RXXTRA          DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
*
         LG    R7,TIMEAFT         END TIME
         SG    R7,TIMEBEF                  - BEG TIME
         SG    R7,TIMEBCT                  - BCT TIME
         STG   R7,TIMEXTRA        SAVE IT
*
         CLI   INSTTYPE+1,C'0'    NON-BRANCH TYPE ?
         BE    RXBCTNOB           EXIT IF IT IS
         LA    R11,4              R11=4  INDEX TO BRANCH DATA
RXBCTNOB EQU   *
         LA    R6,RXBCTBC2-RXBCTBC1 R6=BR TO RXBCTBC2 IF INDEXED
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
* CAN FORM THE FOLLOWING BRANCH TYPES:
*   BCT  10,LABEL       BRANCH
*   BCT  10,LABEL       NO BRANCH
*   BCT  10,LABEL(R6)   BRANCH
*   BCT  10,LABEL(R6)   NO BRANCH
RXBCTEX  L     R10,RXBCTD1(R11)   R10=1 FOR NO BRANCH, BIG FOR BRANCH
RXBCTGO  BCT   R10,RXBCTBC1(R6)   TESTED INST
RXBCTBC1 BCT   R7,RXBCTEX         DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
*
         B     RXBCTDUN           EXIT
* INDEXED BRANCH
RXBCTBC2 BCT   R7,RXBCTEX         DO IT (INDEXED)
         TIME  NS,TIMEAFT         AFTER TIME IN nS
*
RXBCTDUN EQU   *
         LA    R5,RXBCTGO         INST ADDRESS
         B     RXDONE2            EXIT
*
RXFP     EQU   *
* FP MULTIPLY AND DIVIDES
* MXD MD DD MDE=ME DE
         MVC   RXFPINS(1),6(R7)   SET THE INST--FP
* TIME AN LD
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
RXLD     LD    F0,=DH'1000'       SPECIAL INST
         BCT   R7,RXLD            DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
*
         LG    R7,TIMEAFT         END TIME
         SG    R7,TIMEBEF                  - BEG TIME
         SG    R7,TIMEBCT                  - BCT TIME
         STG   R7,TIMEXTRA        SAVE IT
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
RXFPSPC  LD    F0,=DH'1000'       SPECIAL INST
*              R1 D X2 B2   6C065000
RXFPINS  MD    F0,0(R6,R5)        TESTED INST
         BCT   R7,RXFPSPC         DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
*
         LA    R5,RXFPINS         INST ADDRESS
         B     RXDONE2            EXIT
*
RXDONE   EQU   *
         LA    R5,RXINST          INST ADDRESS
RXDONE2  EQU   *
         LA    R4,4               INST LENGTH
         BAL   R14,OUTPUT         WRITE THE OUTPUT RECORD
RXDONE3  EQU   *
         MVC   RXINST+1(2),RXSAVE RESTORE MODIFIED BYTES
         MVC   RXBR+1(2),RXSAVB   RESTORE MODIFIED BYTES--BRANCH
         MVC   RXEX+1(2),RXSAVX   RESTORE MODIFIED BYTES--EXECUTE
         MVC   RXBCTGO+1(2),RXSAVC RESTORE MODIFIED BYTES--BCT
         MVC   RXFPINS+1(2),RXSAVP RESTORE MODIFIED BYTES--FP
         B     GET                EXIT TO GET ANOTHER TEST
*
RXSAVE   DS    H
RXSAVB   DS    H
RXSAVC   DS    H
RXSAVX   DS    H
RXSAVP   DS    H
RXBASEH  DC    H'10'              BASE
         DC    H'20'              BASE+DISP
         DC    H'30'              BASE+INDEX+DISP
RXBASEF  DC    F'10'              BASE
         DC    F'20'              BASE+DISP
         DC    F'30'              BASE+INDEX+DISP
RXBASEE  DC    EH'10'             BASE
         DC    EH'20'             BASE+DISP
         DC    EH'30'             BASE+INDEX+DISP
RXBASED  DC    DH'10'             BASE
         DC    DH'20'             BASE+DISP
         DC    DH'30'             BASE+INDEX+DISP
RXBASEP8 DC    PL8'1000'          BASE
         DC    PL8'2000'          BASE+DISP
         DC    PL8'3000'          BASE+INDEX+DISP
RXBCTD1  DC    F'1'               BCT NON-BRANCH
         DC    X'7FFFFFFF'        BCT BRANCH
RXSTORE  DS    CL24               ALL STORE TYPES
RXEXMVC  MVC   RXSTORE(0),RXSTORE+10 EXECUTED 10-BYTE MVC BASE
         MVC   RXSTORE(0),RXSTORE+10 EXECUTED 10-BYTE MVC BASE+INDEX
         LTORG
*
RXE      DS    0H
* DOUBLE BYTE OPCODE, 2ND BYTE AT END
* LDE LXD LXE SQE SQD
* LDEB LXDB LXEB KEB CEB AEB SEB TCEB TCDB TCXB SQEB SQDB
* KDB CDB ADB SDB
*
* SPECIALS
* MEE MXDB MDEB DEB MEEB MDB DDB
*
* DFP FLOATING POINT
* TDCET TDCDT TDCXT TDGET TDGDT TDGXT
         USING *,R12
         BAL   R14,SRCHOPCD       SET UP OPCODE
         MVC   RSLTLEN(2),INSTXB  MOVE THE XB TO OUTPUT
         MVC   RXEINST(1),6(R7)   SET THE INST
         MVC   RXEINST+5(1),7(R7) SET THE INST 2ND OPCODE
         MVC   RXESAVE,RXEINST+1  SAVE MODIFIED BYTES
         MVC   RXESAVH,RXEFPINS+1 SAVE MODIFIED BYTES
         MVC   RXESAVB,RXEFPINB+1 SAVE MODIFIED BYTES
         MVC   RXESAVT,RXEFPINT+1 SAVE MODIFIED BYTES
         CLI   INSTTYPE+2,C'B'    BFP ?
         BE    RXEZEROB           EXIT IF IT IS
         LZXR  F0                 F0/F2=0 (HFP)
         B     RXEVALXB           EXIT
RXEZEROB EQU   *
         LXDB  F0,=DB'0'          F0/F2=0 (BFP)
*
RXEVALXB EQU   *
* USE OF BASE ALWAYS VALID
         CLC   INSTXB,=C'XB'      VALIDATE ?
         BE    RXEXBOK            EXIT IF IT IS
         CLC   INSTXB,=C'0B'      VALIDATE ?
         BE    RXEXBOK            EXIT IF IT IS
         CLC   INSTXB,=C'X0'      VALIDATE ?
         BE    RXENOBAS           EXIT IF IT IS
         CLC   INSTXB,=C'00'      VALIDATE ?
         BNE   RXEXBERR           ERROR IF NOT
RXENOBAS EQU *
* X0 OR 00
* NEVER HAVE FLOATING POINT VALUES IN LOW STORAGE
* DFP INSTS DON'T REFERENCE STORAGE
         CLI   INSTTYPE+2,C'T'    DFP FLOATING POINT ?
         BE    RXEXBOK            EXIT IF IT IS
         CLI   INSTTYPE+1,C'P'    FLOATING POINT ?
         BE    RXEXBERR           ERROR IF IT IS
         B     RXEXBOK            EXIT
*
RXEXBERR EQU   *
* XB FIELD INVALID FOR INSTRUCTION
         MVC   RSLTMICS(8),=C'XB ERROR' ERROR MSG
         BAL   R14,OUTMSG         WRITE AND CLEAR
         B     GET                EXIT TO GET ANOTHER TEST
*
RXEXBOK  EQU   *
* XB INPUT VALID
         CLI   INSTXB,C'0'        IS INDEX ZERO ?
         BNE   RXEXB1             EXIT IF NOT
         NI    RXEINST+1,X'F0'    KNOCK OUT THE INDEX
         NI    RXEFPINS+1,X'F0'   KNOCK OUT THE INDEX
         NI    RXEFPINB+1,X'F0'   KNOCK OUT THE INDEX
         NI    RXEFPINT+1,X'F0'   KNOCK OUT THE INDEX
RXEXB1   EQU   *
         CLI   INSTXB+1,C'0'      IS BASE ZERO ?
         BNE   RXEXB2             EXIT IF NOT
         NI    RXEINST+2,X'0F'    KNOCK OUT THE BASE
         NI    RXEFPINS+2,X'0F'   KNOCK OUT THE BASE
         NI    RXEFPINB+2,X'0F'   KNOCK OUT THE BASE
         NI    RXEFPINT+2,X'0F'   KNOCK OUT THE BASE
RXEXB2   EQU   *
         CLC   6(2,R7),=X'ED05'   IS IT LXDB ?
         BE    RXELONGB           EXIT IF IT IS
         CLC   6(2,R7),=X'ED07'   IS IT MXDB ?
         BE    RXELONGB           EXIT IF IT IS
         CLC   6(2,R7),=X'ED0C'   IS IT MDEB ?
         BE    RXESHRTB           EXIT IF IT IS
         CLI   INSTTYPE+2,C'B'    BFP ?
         BNE   RXEHFP             EXIT IF NOT
* BFP
         CLI   INSTTYPE,C'D'      LONG BFP ?
         BE    RXELONGB           EXIT IF IT IS
         B     RXESHRTB           EXIT
*
RXEHFP   EQU   *
         CLI   INSTTYPE+2,C'T'    DFP ?
         BE    RXEDFP             EXIT IF NOT
* HFP
         CLC   6(2,R7),=X'ED25'   IS IT LXD ?
         BE    RXELONGH           EXIT IF IT IS
         CLC   6(2,R7),=X'ED35'   IS IT SQD ?
         BE    RXELONGH           EXIT IF IT IS
* HFP SHORT OPERAND 2
         LA    R5,RXEBASEE        SET RXE BASE
         LA    R6,4               SET RXE INDEX
         MVI   RXEINST+3,X'04'    SET RXE DISPLACEMENT
         MVI   RXEFPINS+3,X'04'   SET RXE DISPLACEMENT
         B     RXESET             EXIT
*
RXESHRTB EQU   *
* BFP SHORT OPERAND 2
         LA    R5,RXEBASBE        SET RXE BASE
         LA    R6,4               SET RXE INDEX
         MVI   RXEINST+3,X'04'    SET RXE DISPLACEMENT
         MVI   RXEFPINB+3,X'04'   SET RXE DISPLACEMENT
         B     RXESET             EXIT
*
RXELONGH EQU   *
* HFP LONG OPERAND 2
         LA    R5,RXEBASED        SET RXE BASE
         LA    R6,8               SET RXE INDEX
         MVI   RXEINST+3,X'08'    SET RXE DISPLACEMENT
         B     RXESET             EXIT
*
RXELONGB EQU   *
* BFP LONG OPERAND 2
         LA    R5,RXEBASBD        SET RXE BASE
         LA    R6,8               SET RXE INDEX
         MVI   RXEINST+3,X'08'    SET RXE DISPLACEMENT
         MVI   RXEFPINB+3,X'08'   SET RXE DISPLACEMENT
*
RXESET   EQU   *
         CLC   6(2,R7),=X'ED07'   IS IT MXDB ?
         BE    RXESPECB           EXIT IF IT IS
         CLC   6(2,R7),=X'ED0C'   IS IT MDEB ?
         BE    RXESPECB           EXIT IF IT IS
         CLC   6(2,R7),=X'ED0D'   IS IT DEB ?
         BE    RXESPECB           EXIT IF IT IS
         CLC   6(2,R7),=X'ED17'   IS IT MEEB ?
         BE    RXESPECB           EXIT IF IT IS
         CLC   6(2,R7),=X'ED1C'   IS IT MDB ?
         BE    RXESPECB           EXIT IF IT IS
         CLC   6(2,R7),=X'ED1D'   IS IT DDB ?
         BE    RXESPECB           EXIT IF IT IS
         CLC   6(2,R7),=X'ED37'   IS IT MEE ?
         BE    RXESPECH           EXIT IF IT IS
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
*              R1 D X2 B2   ED0650080024
RXEINST  LDE   F0,8(R6,R5)        TESTED INST
         BCT   R7,RXEINST         DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
         B     RXEDONE1           EXIT
*
RXESPECH EQU   *
* MEE
         MVC   RXEFPINS(1),6(R7)  SET THE INST
         MVC   RXEFPINS+5(1),7(R7) SET THE INST 2ND OPCODE
* TIME AN LE
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
RXELE    LE    F0,=EH'1000'       SPECIAL INST
         BCT   R7,RXELE           DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
*
         LG    R7,TIMEAFT         END TIME
         SG    R7,TIMEBEF                  - BEG TIME
         SG    R7,TIMEBCT                  - BCT TIME
         STG   R7,TIMEXTRA        SAVE IT
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
RXEFPSPC LE    F0,=EH'1000'       SPECIAL INST
*              R1 D X2 B2   ED0650080037
RXEFPINS MEE   F0,8(R6,R5)        TESTED INST
         BCT   R7,RXEFPSPC        DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
*
         LA    R5,RXEFPINS        INST ADDRESS
         B     RXEDONE2           EXIT
*
RXESPECB EQU   *
* MXDB MDEB DEB MEEB
         MVC   RXEFPINB(1),6(R7)  SET THE INST
         MVC   RXEFPINB+5(1),7(R7) SET THE INST 2ND OPCODE
* TIME AN LXDB
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
RXELXDB  LXDB  F0,=DB'1000'       SPECIAL INST
         BCT   R7,RXELXDB         DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
*
         LG    R7,TIMEAFT         END TIME
         SG    R7,TIMEBEF                  - BEG TIME
         SG    R7,TIMEBCT                  - BCT TIME
         STG   R7,TIMEXTRA        SAVE IT
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
RXEFPSPB LXDB  F0,=DB'1000'       SPECIAL INST
*              R1 D X2 B2   ED0650080007
RXEFPINB MXDB  F0,8(R6,R5)        TESTED INST
         BCT   R7,RXEFPSPB        DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
*
         LA    R5,RXEFPINB        INST ADDRESS
         B     RXEDONE2           EXIT
*
RXEDFP   EQU   *
* DFP
         MVC   RXEFPINT(1),6(R7)  SET THE INST
         MVC   RXEFPINT+5(1),7(R7) SET THE INST 2ND OPCODE
         LA    R5,256             SET RXE BASE
         LA    R6,1024            SET RXE INDEX
* SET E, TEST E
         LE    F1,=ED'125.49'     F1=125.49
         CLI   INSTTYPE,C'E'      SHORT FP ?
         BE    RXESTDFP           EXIT IF IT IS
* SET D, TEST D
         LD    F1,=DD'5621.384'   F1=5621.384
         CLI   INSTTYPE,C'D'      LONG FP ?
         BE    RXESTDFP           EXIT IF IT IS
* SET X
         LD    F1,RXEVAL1         F1/
         LD    F3,RXEVAL1+8          F3=89765.456
*
RXESTDFP EQU   *
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
*              R1 D  X2 B2     ED1650400050
RXEFPINT TDCET F1,64(R6,R5)       TESTED INST
         BCT   R7,RXEFPINT        DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
         LA    R5,RXEFPINT        INST ADDRESS
         B     RXEDONE2           EXIT
*
RXEDONE1 EQU   *
         LA    R5,RXEINST         INST ADDRESS
RXEDONE2 EQU   *
         LA    R4,6               INST LENGTH
         BAL   R14,OUTPUT         WRITE THE OUTPUT RECORD
         MVC   RXEINST+1(2),RXESAVE RESTORE MODIFIED BYTES
         MVC   RXEFPINS+1(2),RXESAVH RESTORE MODIFIED BYTES
         MVC   RXEFPINB+1(2),RXESAVB RESTORE MODIFIED BYTES
         MVC   RXEFPINT+1(2),RXESAVT RESTORE MODIFIED BYTES
         B     GET                EXIT TO GET ANOTHER TEST
*
RXEBASEE DC    EH'36'             RXE HFP BASE
         DC    EH'37'             RXE HFP BASE+DISP
         DC    EH'38'             RXE HFP BASE+INDEX+DISP
RXEBASBE DC    EB'67'             RXE BFP BASE
         DC    EB'68'             RXE BFP BASE+DISP
         DC    EB'69'             RXE BFP BASE+INDEX+DISP
RXEBASED DC    DH'46'             RXE HFP BASE
         DC    DH'47'             RXE HFP BASE+DISP
         DC    DH'48'             RXE HFP BASE+INDEX+DISP
RXEBASBD DC    DB'50'             RXE BFP BASE
         DC    DB'51'             RXE BFP BASE+DISP
         DC    DB'52'             RXE BFP BASE+INDEX+DISP
RXEVAL1  DC    LD'89765.456'
RXESAVE  DS    H
RXESAVH  DS    H
RXESAVB  DS    H
RXESAVT  DS    H
         LTORG
*
SS1      DS    0H
* SINGLE BYTE OPCODE
* SS WITH SINGLE LENGTH
* TRTR MVN MVC MVZ NC CLC OC XC TR TRT MVCIN PKA UNPKA
* SPECIALS: ED EDMK
         USING *,R12
         BAL   R14,SRCHOPCD       SET UP OPCODE
         MVC   SS1INST(1),6(R7)   SET THE INST
         MVC   SS1SPEC(1),6(R7)   SET THE SPECIAL INST
         MVC   RSLTLEN,INSTLL     MOVE LENGTH TO OUTPUT
         MVC   SS1OP1,=CL256' '   RESET TO BLANKS
         MVC   SS1OP2,=CL256' '   RESET TO BLANKS
         ZAP   SS1DEC,=P'0'       CLEAR SS1DEC
         PACK  SS1DEC+12(4),INSTLL-1(7) DECIMAL LENGTH
         CVBG  R4,SS1DEC          CONVERT TO HEX
* VALIDATE LEN
         LTR   R4,R4              IS IT ZERO ?
         BZ    SS1ERROR           ERROR IF IT IS
         CLI   6(R7),PKA          IS IT PKA ?
         BE    SS1PKA             EXIT IF NOT
         CLI   6(R7),UNPKA        IS IT UNPKA ?
         BNE   SS1NTPKA           EXIT IF NOT
SS1PKA   EQU   *
* PKA UNPKA
         C     R4,=F'32'          GT THAN 32 FOR PKA/UKPKA ?
         BH    SS1ERROR           ERROR IF IT IS
         B     SS1LENOK           EXIT
SS1NTPKA EQU   *
         C     R4,=F'256'         GT THAN 256 ?
         BH    SS1ERROR           ERROR IF IT IS
SS1LENOK EQU   *
         BCTR  R4,0               -1 FOR INST LENGTH
         STC   R4,SS1INST+1       SAVE LEN IN INST
         STC   R4,SS1INST2+1      SAVE LEN IN INST
         CLI   6(R7),ED           IS IT ED ?
         BE    SS1SPECL           EXIT IF IT IS
         CLI   6(R7),EDMK         IS IT EDMK ?
         BNE   SS1NTSPC           EXIT IF NOT
SS1SPECL EQU   *
* ED EDMK
         STC   R4,SS1MVC+1        SAVE LEN IN INST
         STC   R4,SS1XTRA+1       SAVE LEN IN INST
         STC   R4,SS1SPEC+1       SAVE LEN IN INST
* TIME AN EXTRA MVC
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
SS1MVC   MVC   SS1OP1(0),SS1OP2   SPECIAL INST
         BCT   R7,SS1MVC          DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
*
         LG    R7,TIMEAFT         END TIME
         SG    R7,TIMEBEF                  - BEG TIME
         SG    R7,TIMEBCT                  - BCT TIME
         STG   R7,TIMEXTRA        SAVE IT
         MVC   SS1OP2(4),=X'12345678' PRIME 2ND OP
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
SS1XTRA  MVC   SS1OP1(0),SS1MASK  SPECIAL INST
SS1SPEC  MVC   SS1OP1(0),SS1OP2   TESTED INST
         BCT   R7,SS1XTRA         DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
*
         LA    R5,SS1SPEC         INST ADDRESS
         B     SS1DONE2           EXIT
*
SS1NTSPC EQU   *
         CLI   6(R7),TRTR         IS IT TRTR ?
         BE    SS1TRTR            EXIT IF IT IS
         CLI   6(R7),TRT          IS IT TRT ?
         BNE   SS1NTTRT           EXIT IF NOT
         XC    SS1OP2,SS1OP2      CLEAR OP2 FOR TRT
SS1NTTRT EQU   *
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
SS1INST  MVC   SS1OP1(0),SS1OP2   TESTED INST
         BCT   R7,SS1INST         DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
*
         LA    R5,SS1INST         INST ADDRESS
         B     SS1DONE2           EXIT
*
SS1TRTR  EQU   *
         XC    SS1OP2,SS1OP2      CLEAR OP2 FOR TRTR
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
SS1INST2 TRTR  SS1OP1+255(0),SS1OP2 TESTED INST
         BCT   R7,SS1INST2        DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
*
         LA    R5,SS1INST2        INST ADDRESS
*
SS1DONE2 EQU   *
         LA    R4,6               INST LENGTH
         BAL   R14,OUTPUT         WRITE THE OUTPUT RECORD
         B     GET                EXIT TO GET ANOTHER TEST
*
SS1ERROR EQU   *
         MVC   RSLTMICS+1(12),=C'LENGTH ERROR'
         BAL   R14,OUTMSG         WRITE AND CLEAR
         B     GET                EXIT TO GET ANOTHER TEST
*
SS1MASK  DC    X'4021',254X'20'
SS1DEC   DC    PL16'0'
SS1OP1   DC    CL256' '           SS1 1ST OP
SS1OP2   DC    CL256' '           SS1 2ND OP
         LTORG
*
SS2      DS    0H
* SINGLE BYTE OPCODE
* SS WITH TWO LENGTHS, DECIMALS HAVE TYPE=P
* LMD HAS SINGLE LENGTH CONVERTED INTO REGISTER RANGE
*
* ORDINARY: MVO PACK UNPK
* DECIMALS: ZAP CP AP SP MP
* SPECIALS: SRP DP LMD
         USING *,R12
         BAL   R14,SRCHOPCD       SET UP OPCODE
         CLI   6(R7),LMD          IS IT LMD ?
         BE    SS2LMD             EXIT IF IT IS
         MVC   SS2INST(1),6(R7)   SET THE ORDINARY INST
         MVC   RSLTLL1,INSTLL1    MOVE LENGTH 1 TO OUTPUT
         MVC   RSLTLL2,INSTLL2    MOVE LENGTH 2 TO OUTPUT
* PROCESS LENGTH 1
         ZAP   SS2DEC,=P'0'       CLEAR SS2DEC
         PACK  SS2DEC+14(2),INSTLL1-1(3) DECIMAL LENGTH 1
         CVBG  R7,SS2DEC          CONVERT TO HEX
         LTR   R7,R7              IS IT ZERO ?
         BZ    SS2ERROR           EXIT IF IT IS
         C     R7,=F'16'          GT THAN 16 ?
         BH    SS2ERROR           EXIT IF IT IS
         BCTR  R7,0               -1 FOR INST LENGTH
         STC   R7,SS2MVC+1        SAVE LEN IN SPECIAL MVC
         STC   R7,SS2XTRA1+1      SAVE LEN IN SPECIAL MVC
         STC   R7,SS2XTRA2+1      SAVE LEN IN SPECIAL MVC
         CLI   INSTTYPE,C'P'      PACKED FIELD ?
         BNE   SS2NOTP1           EXIT IF NOT
         XC    SS2OP1(16),SS2OP1  CLEAR MAXIMUM AREA
         LA    R5,SS2OP1(R7)      POINT TO LAST BYTE
         MVI   0(R5),X'2C'        MAKE IT A P'2'
SS2NOTP1 EQU   *
         CLI   SS2INST,SRP        IS IT SRP ?
         BE    SS2DOSRP           YES, NO LENGTH 2
* PROCESS LENGTH 2
         ZAP   SS2DEC,=P'0'       CLEAR SS2DEC
         PACK  SS2DEC+14(2),INSTLL2-1(3) DECIMAL LENGTH 2
         CVBG  R4,SS2DEC          CONVERT TO HEX
         LTR   R4,R4              IS IT ZERO ?
         BZ    SS2ERROR           EXIT IF IT IS
         C     R4,=F'16'          GT THAN 16 ?
         BH    SS2ERROR           EXIT IF IT IS
         BCTR  R4,0               -1 FOR INST LENGTH
         CLI   INSTTYPE,C'P'      PACKED FIELD ?
         BNE   SS2NOTP2           EXIT IF NOT
         XC    SS2OP2(16),SS2OP2  CLEAR MAXIMUM AREA
         LA    R5,SS2OP2(R4)      POINT TO LAST BYTE
         MVI   0(R5),X'2C'        MAKE IT A P'2'
SS2NOTP2 EQU   *
         CLI   SS2INST,DP         IS IT DP ?
         BNE   SS2NTDP            EXIT IF NOT
* EXTRA INTEGRITY CHECKS FOR DP
         C     R4,=F'8'           L2<8 ?
         BNL   SS2ERROR           ERROR IF NOT
         CR    R7,R4              L1>L2 ?
         BNH   SS2ERROR           ERROR IF NOT
         B     SS2NTDP            EXIT
SS2DOSRP EQU   *
* SPECIAL PROCESSING FOR SRP
         SLL   R7,4               POSITION FOR LEN 1
         LA    R7,5(R7)           ADD THE ROUND
         MVC   SS2OP1SV,SS2OP1    SAVE ORIG DEC
         STC   R7,SS2SRP+1        SAVE LEN/ROUND IN INST
         LA    R5,2               SET THE SHIFT
*
         BAL   R14,SS2TMMVC       TIME THE MVC
*
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
SS2XTRA1 MVC   SS2OP1(0),SS2OP1SV SPECIAL INST
SS2SRP   SRP   SS2OP1(0),2(R5),5  TESTED INST
         BCT   R7,SS2XTRA1        DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
*
         LA    R5,SS2SRP          INST ADDRESS
         B     SS2PROC            EXIT
*
SS2NTDP  EQU   *
         SLL   R7,4               POSITION FOR LEN 1
         OR    R7,R4              FORM DOUBLE LENGTH
         STC   R7,SS2INST+1       SAVE IN ORDINARY INST
         CLI   SS2INST,DP         IS IT DP ?
         BNE   SS2NTSPC           EXIT IF NOT
* SPECIAL PROCESSING FOR DP
         MVC   SS2OP1SV,SS2OP1    SAVE ORIG DEC
         STC   R7,SS2DP+1         SAVE LEN IN INST
*
         BAL   R14,SS2TMMVC       TIME THE MVC
*
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
SS2XTRA2 MVC   SS2OP1(0),SS2OP1SV SPECIAL INST
SS2DP    DP    SS2OP1(0),SS2OP2(0) TESTED INST
         BCT   R7,SS2XTRA2        DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
*
         LA    R5,SS2DP           INST ADDRESS
         B     SS2PROC            EXIT
*
SS2NTSPC EQU   *
* MVO PACK UNPK
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
SS2INST  UNPK  SS2OP1(0),SS2OP2(0) TESTED INST
         BCT   R7,SS2INST         DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
*
         LA    R5,SS2INST         INST ADDRESS
SS2PROC  EQU   *
         LA    R4,6               INST LENGTH
         LA    R6,SS2OP1          INST OPERAND 2
         BAL   R14,OUTPUT         WRITE THE OUTPUT RECORD
         B     GET                EXIT TO GET ANOTHER TEST
*
SS2LMD   EQU   *
* LMD
         MVC   RSLTLL1,INSTLL1    MOVE LENGTH 1 TO OUTPUT
         MVI   SS2STMH1+1,X'00'   RESET REGS
         MVI   SS2STM1+1,X'00'    RESET REGS
         MVI   SS2STMH2+1,X'00'   RESET REGS
         MVI   SS2STM2+1,X'00'    RESET REGS
         MVI   SS2LMDGO+1,X'00'   RESET REGS
* PROCESS LENGTH 1
         ZAP   SS2DEC,=P'0'       CLEAR SS2DEC
         PACK  SS2DEC+14(2),INSTLL1-1(3) DECIMAL LENGTH 1
         CVBG  R10,SS2DEC         CONVERT TO HEX
         LTR   R10,R10            IS IT ZERO ?
         BZ    SS2ERROR           EXIT IF IT IS
         C     R10,=F'16'         GT THAN 16 ?
         BH    SS2ERROR           EXIT IF IT IS
         BCTR  R10,0              -1 FOR END REGISTER
         EX    R10,SS2OI1         PUT END REG INTO INST--STMH1
         EX    R10,SS2OI2         PUT END REG INTO INST--STM1
         EX    R10,SS2OI3         PUT END REG INTO INST--STMH2
         EX    R10,SS2OI4         PUT END REG INTO INST--STM2
         EX    R10,SS2OI5         PUT END REG INTO INST--LMD
* TIME STMH STM
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
SS2STMH1 STMH  R0,R0,SS2OP1       SPECIAL INST
SS2STM1  STM   R0,R0,SS2OP2       SPECIAL INST
         BCT   R7,SS2STMH1        DO THEM
         TIME  NS,TIMEAFT         AFTER TIME IN nS
*
         LG    R7,TIMEAFT         END TIME
         SG    R7,TIMEBEF                  - BEG TIME
         SG    R7,TIMEBCT                  - BCT TIME
         STG   R7,TIMEXTRA        SAVE IT
*
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
SS2STMH2 STMH  R0,R0,SS2OP1       SPECIAL INST
SS2STM2  STM   R0,R0,SS2OP2       SPECIAL INST
SS2LMDGO LMD   R0,R0,SS2OP1,SS2OP2 TESTED INST
         BCT   R7,SS2STMH2        DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
*
         LA    R5,SS2LMDGO        INST ADDRESS
         B     SS2PROC            EXIT
*
SS2OI1   OI    SS2STMH1+1,X'00'   EXECUTED INST
SS2OI2   OI    SS2STM1+1,X'00'    EXECUTED INST
SS2OI3   OI    SS2STMH2+1,X'00'   EXECUTED INST
SS2OI4   OI    SS2STM2+1,X'00'    EXECUTED INST
SS2OI5   OI    SS2LMDGO+1,X'00'   EXECUTED INST
*
SS2ERROR EQU   *
         MVC   RSLTMICS+1(12),=C'LENGTH ERROR'
         BAL   R14,OUTMSG         WRITE AND CLEAR
         B     GET                EXIT TO GET ANOTHER TEST
*
SS2TMMVC EQU   *
* TIME THE MVC
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
SS2MVC   MVC   SS2OP1(0),SS2OP2   SPECIAL INST
         BCT   R7,SS2MVC          DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
*
         LG    R7,TIMEAFT         END TIME
         SG    R7,TIMEBEF                  - BEG TIME
         SG    R7,TIMEBCT                  - BCT TIME
         STG   R7,TIMEXTRA        SAVE IT
         BR    R14                RETURN
*
SS2DEC   DC    PL16'0'
SS2OP1SV DS    CL16
SS2OP1   DC    CL256' '           SS2 1ST OP
SS2OP2   DC    CL256' '           SS2 2ND OP
         LTORG
*
SSF      DS    0H
* 12-BIT OPCODE
* SINGLE LENGTH
* AS THERE IS ONLY ONE, TREAT AS 1-BYTE OPCODE
*
* MVCOS
         USING *,R12
         BAL   R14,SRCHOPCD       SET UP OPCODE
         MVC   SSFMVCOS(1),6(R7)  SET THE INST
         MVC   RSLTLEN,INSTLL     MOVE LENGTH TO OUTPUT
         ZAP   SSFDEC,=P'0'       CLEAR SSFDEC
         PACK  SSFDEC+12(4),INSTLL-1(7) DECIMAL LENGTH
         CVBG  R11,SSFDEC         CONVERT TO HEX
*
         GETMAIN R,LV=4K          'FROM' ADDRESS
         LR    R10,R1             SET FOR MVCOS
         GETMAIN R,LV=4K          'TO' ADDRESS
         LR    R4,R1              SET FOR MVCOS
* TIME AN EXTRA SGR, THE TIME MACRO AMENDS R0
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
SSFSGR   SGR   R0,R0              SPECIAL INST
         BCT   R7,SSFSGR          DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
*
         LG    R7,TIMEAFT         END TIME
         SG    R7,TIMEBEF                  - BEG TIME
         SG    R7,TIMEBCT                  - BCT TIME
         STG   R7,TIMEXTRA        SAVE IT
*
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
SSFINST  SGR   R0,R0              SPECIAL INST
SSFMVCOS MVCOS 0(R10),0(R4),R11   TESTED INST
         BCT   R7,SSFINST         DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
*
         FREEMAIN LV=4K,A=(R10)
         FREEMAIN LV=4K,A=(R4)
         LA    R5,SSFMVCOS        INST ADDRESS
         LA    R4,6               INST LENGTH
         BAL   R14,OUTPUT         WRITE THE OUTPUT RECORD
         B     GET                EXIT TO GET ANOTHER TEST
*
SSFDEC   DC    PL16'0'
         LTORG
*
RI       DS    0H
* SINGLE BYTE OPCODE AND NIBBLE OPCODE 2
*
* STANDARD
* IIHH IIHL IILH IILL
* NIHH NIHL NILH NILL
* OIHH OIHL OILH OILL
* LLIHH LLIHL LLILH LLILL
* TMLH TMH TMLL TML TMHH TMHL
* LHI LGHI AHI AGHI MHI MGHI CHI CGHI
*
* BRANCHES
* BRC=J=BRU BRAS=JAS BRCT=JCT BRCTG=JCTG
         USING *,R12
         BAL   R14,SRCHOPCD       SET UP OPCODE
         CLI   INSTTYPE,C'B'      BRANCH TYPE ?
         BE    RIBRANCH           EXIT IF IT IS
         MVC   RISAVE,RIINST      SAVE MODIFIED BYTES
         MVC   RIINST(1),6(R7)    SET THE INST
         OC    RIINST+1(1),7(R7)  OR IN THE 2ND OPCODE
         LG    R10,=X'789ABC012DEF3456' SO I KNOW
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
RIINST   IIHH  R10,X'0123'        TESTED INST
         BCT   R7,RIINST          DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
*
         B     RIDONE             EXIT
*
RIBRANCH EQU   *
* BRANCH TYPE
         MVC   RISAVB,RIBR        SAVE MODIFIED BYTES--BRANCH
         CLC   6(2,R7),=X'A705'   IS IT BRAS ?
         BE    RIBRNTNP           EXIT IF IT IS
         CLC   6(2,R7),=X'A706'   IS IT BRCT ?
         BE    RIBRCT             EXIT IF IT IS
         CLC   6(2,R7),=X'A707'   IS IT BRCTG ?
         BE    RIBRCT             EXIT IF IT IS
         CLI   INSTTYPE+1,C'0'    CONVERT TO JNOP ?
         BNE   RIBRNTNP           EXIT IF NOT
         NI    RIBR+1,X'0F'       KILL THE MASK
         MVC   RSLTOPCD(4),=C'JNOP' MAKE IT JNOP
RIBRNTNP EQU   *
         MVC   RIBR(1),6(R7)      SET THE INST
         OC    RIBR+1(1),7(R7)    OR IN THE 2ND OPCODE
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
* CAN FORM THE FOLLOWING BRANCH TYPES:
*   BRC   LABEL
*   JNOP  LABEL
*   BRAS  15,LABEL
RIBR     B     2                  X'47F00002' >> X'A7F40002'=J *+4
         BCT   R7,RIBR            DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
*
         LA    R5,RIBR            INST ADDRESS
         B     RIDONE2            EXIT
*
RIBRCT   EQU   *
* BRCT BRCTG
         MVC   RISAVC,RIBRCTGO    SAVE MODIFIED BYTES--BRCT/BRCTG
         MVC   RIBRCTGO(1),6(R7)  SET THE INST
         OC    RIBRCTGO+1(1),7(R7) OR IN THE 2ND OPCODE
         SGR   R11,R11            CLEAR INDEX REG
* TIME AN LG
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
RIXTRA   LG    R10,RIBCT(R11)     SPECIAL INST
         BCT   R7,RIXTRA          DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
*
         LG    R7,TIMEAFT         END TIME
         SG    R7,TIMEBEF                  - BEG TIME
         SG    R7,TIMEBCT                  - BCT TIME
         STG   R7,TIMEXTRA        SAVE IT
*
         CLI   INSTTYPE+1,C'0'    NON-BRANCH TYPE ?
         BNE   RIBCTNOB           EXIT IF NOT
         LA    R11,8              INDEX TO FL8'1'
RIBCTNOB EQU   *
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
* CAN FORM THE FOLLOWING BRANCH TYPES:
*   BRCT  10,LABEL
*   BRCTG 10,LABEL
RIBRCTEX LG    R10,RIBCT(R11)     R10=1 FOR NO BRANCH, BIG FOR BRANCH
RIBRCTGO BCT   R10,2              X'46A00002' >> X'A7A60002'=BRCT *+4
         BCT   R7,RIBRCTEX        DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
*
         LA    R5,RIBRCTGO        INST ADDRESS
         B     RIDONE2            EXIT
*
RIDONE   EQU   *
         LA    R5,RIINST          INST ADDRESS
RIDONE2  EQU   *
         LA    R4,4               INST LENGTH
         BAL   R14,OUTPUT         WRITE THE OUTPUT RECORD
         MVC   RIINST(2),RISAVE   RESTORE MODIFIED BYTES
         MVC   RIBR(2),RISAVB     RESTORE MODIFIED BYTES
         MVC   RIBRCTGO(2),RISAVC RESTORE MODIFIED BYTES
         B     GET                EXIT TO GET ANOTHER TEST
*
RISAVE   DS    H
RISAVB   DS    H
RISAVC   DS    H
RIBCT    DC    X'000000007FFFFFFF'
         DC    FL8'1'
         LTORG
*
RIE      DS    0H
* DOUBLE BYTE OPCODE, 2ND BYTE AT END
* BRXHG BRXLG
         USING *,R12
         BAL   R14,SRCHOPCD       SET UP OPCODE
         MVC   RIEINBX(1),6(R7)   SET THE INST
         MVC   RIEINBX+5(1),7(R7) SET THE INST 2ND OPCODE
         CLC   6(2,R7),=X'EC44'   IS IT BRXHG ?
         BNE   RIEBXLE            EXIT IF NOT
* BRXHG
         LG    R10,RIEBXHGO       LOAD INDEX REGISTER--ALWAYS BRANCH
         CLI   INSTTYPE+1,C'0'    NOP REQUESTED ?
         BNE   RIEBXGO            EXIT IF NOT
         LG    R10,RIEBXHNP       LOAD INDEX REGISTER--NEVER BRANCH
         B     RIEBXGO            EXIT
RIEBXLE  EQU   *
* BRXLG
         LG    R10,RIEBXLEG       LOAD INDEX REGISTER--ALWAYS BRANCH
         CLI   INSTTYPE+1,C'0'    NOP REQUESTED ?
         BNE   RIEBXGO            EXIT IF NOT
         LG    R10,RIEBXLEN       LOAD INDEX REGISTER--NEVER BRANCH
*
RIEBXGO  EQU   *
         LG    R11,RIEBXHGO       SET INCREMENT AND COMPARE
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
RIEINBX  BRXHG R10,R11,RIEBXBCT   TESTED INST
RIEBXBCT BCT   R7,RIEINBX         DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
*
         LA    R4,6               INST LENGTH
         LA    R5,RIEINBX         INST ADDRESS
         BAL   R14,OUTPUT         WRITE THE OUTPUT RECORD
         B     GET                EXIT TO GET ANOTHER TEST
RIEBXHGO DC    FL8'1'             BRXHG AND BRANCH
RIEBXHNP DC    X'8000000000000000' BRXHG AND NO BRANCH
RIEBXLEG DC    X'8000000000000000' BRXLG AND BRANCH
RIEBXLEN DC    FL8'1'             BRXLG AND NO BRANCH
         LTORG
*
RIL      DS    0H
* SINGLE BYTE OPCODE AND NIBBLE OPCODE 2
*
* STANDARD
* LARL LGFI XIHF XILF IIHF IILF NIHF NILF OIHF OILF LLIHF LLILF
* SLGFI SLFI AGFI AFI ALGFI ALFI CGFI CFI CLGFI CLFI
*
* BRANCHES
* BRCL=JLU=BRUL BRASL=JASL
         USING *,R12
         BAL   R14,SRCHOPCD       SET UP OPCODE
         MVC   RILSAVE,RILINST    SAVE MODIFIED BYTES
         MVC   RILSAVB,RILBR      SAVE MODIFIED BYTES--BRANCH
         MVC   RILSAVC,RILC2GO    SAVE MODIFIED BYTES--C2 SET
         CLI   INSTTYPE,C'B'      BRANCH TYPE ?
         BE    RILBRNCH           EXIT IF IT IS
         CLI   6(R7),X'C0'        IS IT NON-BRANCH C0 SET ?
         BE    RILC2              EXIT IF IT IS
         CLC   6(2,R7),=X'C204'   IS IT THE NEW C2 SET ?
         BL    RILOLD1            EXIT IF NOT
         CLC   6(2,R7),=X'C20F'   IS IT THE NEW C2 SET ?
         BH    RILOLD1            EXIT IF NOT
         B     RILC2              EXIT
RILOLD1  EQU   *
* LARL
         MVC   RILINST(1),6(R7)   SET THE INST
         OC    RILINST+1(1),7(R7) OR IN THE 2ND OPCODE
         LG    R10,=X'789ABC012DEF3456' SO I KNOW
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
RILINST  LARL  R10,RILBCT         TESTED INST
RILBCT   BCT   R7,RILINST         DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
*
         B     RILDONE            EXIT
*
RILBRNCH EQU   *
* BRANCH TYPE
* BRCL=JLU=BRUL BRASL=JASL
         CLC   6(2,R7),=X'C005'   IS IT BRASL ?
         BE    RILBRNTP           EXIT IF IT IS
* BRCL=JLU
         CLI   INSTTYPE+1,C'0'    CONVERT TO JLNOP ?
         BNE   RILBRNTP           EXIT IF NOT
         NI    RILBR+1,X'0F'      KILL THE MASK
* BRCL NOP=JLNOP
         MVC   RSLTOPCD(5),=C'JLNOP' MAKE IT JLNOP
RILBRNTP EQU   *
         MVC   RILBR(1),6(R7)     SET THE INST
         OC    RILBR+1(1),7(R7)   OR IN THE 2ND OPCODE
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
* CAN FORM THE FOLLOWING BRANCH TYPES:
*   BRCL  15,LABEL
*   JLU   LABEL
*   BRUL  LABEL
*   JLNOP LABEL
*   BRASL 15,LABEL
*   JASL  LABEL
RILBR    LARL  R15,RILBCT1        X'C0F000000003'
RILBCT1  BCT   R7,RILBR           DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
*
         LA    R5,RILBR           INST ADDRESS
         B     RILDONE2           EXIT
*
RILC2    EQU   *
* C2 SET AND NON-BRANCH C0 SET
         MVC   RILC2GO(1),6(R7)   SET THE INST
         OC    RILC2GO+1(1),7(R7) OR IN THE 2ND OPCODE
         LG    R10,=FL8'1'        SO I KNOW
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
RILC2GO  LARL  R10,5              TESTED INST
         BCT   R7,RILC2GO         DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
*
         LA    R5,RILC2GO         INST ADDRESS
         B     RILDONE2           EXIT
*
RILDONE  EQU   *
         LA    R5,RILINST         INST ADDRESS
RILDONE2 EQU   *
         LA    R4,6               INST LENGTH
         BAL   R14,OUTPUT         WRITE THE OUTPUT RECORD
         MVC   RILINST(2),RILSAVE RESTORE MODIFIED BYTES
         MVC   RILBR(2),RILSAVB   RESTORE MODIFIED BYTES--BRANCH
         MVC   RILC2GO(2),RILSAVC RESTORE MODIFIED BYTES--C2 SET
         B     GET                EXIT TO GET ANOTHER TEST
*
RILSAVE  DS    H
RILSAVB  DS    H
RILSAVC  DS    H
         LTORG
*
RRE      DS    0H
* DOUBLE BYTE OPCODE
* IPM BAKR MSR LPGR LNGR LTGR LCGR LGR AGR SGR ALGR SLGR MSGR
* DSGR LRVGR LPGFR LNGFR LTGFR LCGFR LGFR LLGFR LLGTR AGFR SGFR
* ALGFR SLGFR MSGFR DSGFR LRVR CGR CLGR CGFR CLGFR
* NGR OGR XGR MLGR DLGR ALCGR SLBGR MLR DLR ALCR SLBR
* BCTGR LGBR LGHR LBR LHR FLOGR LLGCR LLGHR LLCR LLHR
*
* SPECIALS
* THDER THDR
* CUSE MVST CLST SRST TRE
*
* FLOATING POINT--HFP
* DXR SQDR SQER LDER LXDR LXER SQXR MEER LPXR LNXR LTXR LCXR LXR
* LEXR FIXR CXR LZER LZDR LZXR FIER FIDR SFPC SFASR EFPC
* CEFR CDFR CXFR CEGR CDGR CXGR
*
* FLOATING POINT--BFP
* LPEBR LNEBR LTEBR LCEBR LDEBR LXDBR LXEBR MXDBR KEBR CEBR AEBR
* SEBR MDEBR DEBR LPDBR LNDBR LTDER LCDER SQEBR SQDBR SQXBR
* MEEBR KDBR CDBR ADBR SDBR MDBR DDBR LPXBR LNXBR LTXBR LCXBR
* LEDBR LDXBR LEXBR KXBR CXBR AXBR SXBR MXBR DXBR
* CEFBR CDFBR CXFBR CEGBR CDGBR CXGBR
*
* FLOATING POINT--DFP
* LPDFR LNDFR LCDFR LDGR LTDTR LTXTR KDTR CDTR CXTR KXTR
* CDGTR CEDTR CXGTR CEXTR
*
* FLOATING POINT--DFP SPECIALS
* LGDR CUDTR EEDTR ESDTR EEXTR ESXTR CUXTR
* CDSTR CXSTR
* CDUTR CXUTR
* CGDTR CGXTR
         USING *,R12
         BAL   R14,SRCHOPCD       SET UP OPCODE
         MVC   RRESAVE,RREBCTR+2  SAVE MODIFIED BYTES
         MVC   RRESVBK1,RREBAKR1+2 SAVE MODIFIED BYTES
         MVC   RRESVBK2,RREBAKR2+2 SAVE MODIFIED BYTES
* BAKR IS SPECIAL
         CLC   6(2,R7),=X'B240'   IS IT BAKR ?
         BE    RREBAKR            EXIT IF IT IS
* SPECIALS CUSE MVST CLST SRST
         CLC   6(2,R7),=X'B255'   IS IT MVST ?
         BE    RRECUSE            EXIT IF IT IS
         CLC   6(2,R7),=X'B257'   IS IT CUSE ?
         BE    RRECUSE            EXIT IF IT IS
         CLC   6(2,R7),=X'B25D'   IS IT CLST ?
         BE    RRECUSE            EXIT IF IT IS
         CLC   6(2,R7),=X'B25E'   IS IT SRST ?
         BE    RRECUSE            EXIT IF IT IS
         CLC   6(2,R7),=X'B2A5'   IS IT TRE ?
         BE    RRECUSE            EXIT IF IT IS
* CONVERT-TO-FIXED AND CONVERT-TO-HFP ARE SPECIAL
         CLC   6(2,R7),=X'B358'   IS IT CONVERT TO HFP ?
         BE    RRECTH             EXIT IF IT IS
         CLC   6(2,R7),=X'B359'   IS IT CONVERT TO HFP ?
         BE    RRECTH             EXIT IF IT IS
         CLI   INSTTYPE,C'B'      BRANCH TYPE ?
         BE    RREBRNCH           EXIT IF IT IS
         CLI   INSTTYPE+1,C'P'    FLOATING POINT ?
         BE    RREFP              EXIT IF IT IS
         MVC   RREINST(2),6(R7)   SET THE INST DOUBLE OPCODE
         LG    R10,=X'0000000000001234' SO I KNOW
         LG    R11,=X'0000000000000005' SO I KNOW
         CLC   6(2,R7),=X'B384'   IS IT SFPC ?
         BE    RREEFPC            EXIT IF IT IS
         CLC   6(2,R7),=X'B385'   IS IT SFASR ?
         BNE   RRETSFPC           EXIT IF NOT
RREEFPC  EQU   *
         EFPC  R10                SET UP FOR SFPC
RRETSFPC EQU   *
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
RREINST  NGR   R10,R11            TESTED INST
         BCT   R7,RREINST         DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
         B     RREDONE            EXIT
*
RREBRNCH EQU   *
* BCTGR
         LG    R11,=X'7FFFFFFFFFFFFFFF' SO I KNOW
         LA    R10,RREBCTRC       R2-FIELD
         CLI   INSTTYPE+1,C'0'    ZERO R2-FIELD ?
         BNE   RRENOTB0           EXIT IF NOT
* ZERO R2-FIELD
         NI    RREBCTR+3,X'F0'    MAKE IT BCTGR R11,0
RRENOTB0 EQU   *
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
* CAN FORM THE FOLLOWING BRANCH TYPES:
*   BCTGR  R11,R10
*   BCTGR  R11,0
RREBCTR  BCTGR R11,R10            TESTED INST
RREBCTRC BCT   R7,RREBCTR         DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
*
         LA    R5,RREBCTR         INST ADDRESS
         B     RREDONE2           EXIT
*
RREBAKR  EQU   *
* CAN FORM THE FOLLOWING BRANCH TYPES:
* 00 BAKR 0,0        STACK, DROP THRU    , RETURN TO NSI -- STACK
* R0 BAKR R11,0      STACK, DROP THRU    , RETURN TO R11 -- CALLED
* 0R BAKR 0,R10      STACK, BRANCH TO R10, RETURN TO NSI -- CALLER
* RR BAKR R11,R10    STACK, BRANCH TO R10, RETURN TO R11 -- CALLED
         SR    R6,R6              PREPARE FOR DIVIDE
         L     R7,SPINCNT         SET LOOP LIMIT
         D     R6,=F'50'          CHECK IF MULTIPLE OF 50
         LTR   R6,R6              ANY REMAINDER ?
         BNZ   RRERROR            ERROR IF THERE IS
         CLI   INSTTYPE+2,C'0'    ZERO R2-FIELD ?
         BNE   RRENOTZ2           EXIT IF NOT
* ZERO R2-FIELD
         NI    RREBAKR1+3,X'F0'   MAKE IT BAKR R11,0
         NI    RREBAKR2+3,X'F0'   MAKE IT BAKR 0,0
RRENOTZ2 EQU   *
         CLI   INSTTYPE+1,C'R'    RETURN TO R11 ?
         BNE   RREBAKG2           EXIT IF NOT
* R0 BAKR R11,0      STACK, DROP THRU    , RETURN TO R11 -- CALLED
* RR BAKR R11,R10    STACK, BRANCH TO R10, RETURN TO R11 -- CALLED
         MVC   RREOUTER,SPINCNT   INITIAL OUTER SPIN LOOP
         XC    RREACCUM,RREACCUM  CLEAR TIME ACCUMULATOR
RREOUTR1 EQU   *
         L     R7,=F'50'          INNER SPIN COUNT
         LA    R10,RREBKLP1       BRANCH ADDRESS IF THERE IS ONE
         LA    R11,RREDECR        RETURN ADDRESS FOR UNSTACK
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
RREBAKR1 BAKR  R11,R10            TESTED INST
RREBKLP1 BCT   R7,RREBAKR1        DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
*
         LG    R7,TIMEAFT         END TIME
         SG    R7,TIMEBEF                  - BEG TIME
         LG    R6,RREACCUM        SUM OF TIME DIFFS
         AGR   R6,R7              + CURRENT DIFF
         STG   R6,RREACCUM        STORE BACK
         B     RREUNSTK           EXIT
*
RREBAKG2 EQU   *
* 00 BAKR 0,0        STACK, DROP THRU    , RETURN TO NSI -- STACK
* 0R BAKR 0,R10      STACK, BRANCH TO R10, RETURN TO NSI -- CALLER
*
* BAKR 0,0 TIMES AN EXTRA NOP, WE MUST HANDLE THIS
         CLC   INSTTYPE+1(2),=C'00' BAKR 0,0 ?
         BNE   RRENT00            EXIT IF NOT
* TIME A NOP
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
RRENOP   NOP   RRENOP             SPECIAL INST
         BCT   R7,RRENOP          DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
*
         LG    R7,TIMEAFT         END TIME
         SG    R7,TIMEBEF                  - BEG TIME
         SG    R7,TIMEBCT                  - BCT TIME
         STG   R7,TIMEXTRA        SAVE IT
*
RRENT00  EQU   *
         MVC   RREOUTER,SPINCNT   INITIAL OUTER SPIN LOOP
         XC    RREACCUM,RREACCUM  CLEAR TIME ACCUMULATOR
RREOUTR2 EQU   *
         MVI   RREBAKPR+1,X'00'   ENSURE NOP
         L     R7,=F'50'          INNER SPIN COUNT
         LA    R10,RREBKLP2       BRANCH ADDRESS IF THERE IS ONE
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
RREBAKR2 BAKR  0,R10              TESTED INST
RREBAKPR NOP   RREDECR            USED FOR UNSTACKING
RREBKLP2 BCT   R7,RREBAKR2        DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
*
         LG    R7,TIMEAFT         END TIME
         SG    R7,TIMEBEF                  - BEG TIME
         LG    R6,RREACCUM        SUM OF TIME DIFFS
         AGR   R6,R7              + CURRENT DIFF
         STG   R6,RREACCUM        STORE BACK
*
RREUNSTK EQU   *
* NOW UNSTACK
* REGISTER RESTORE MEANS WE HAVE TO DO ALL LOOPS THE HARD WAY
* AND POINT AT THE INSTRUCTION
         MVC   RRESTORE,=F'50'    SET INNER SPIN COUNT
         MVI   RREBAKPR+1,X'F0'   ENSURE BRANCH
RREPR    PR
RREDECR  EQU   *
         L     R7,RRESTORE        LOAD CURRENT LOOP VALUE
         S     R7,=F'1'           -1
         ST    R7,RRESTORE        STORE CURRENT LOOP VALUE
         BNZ   RREPR              UNSTACK LOOP
         L     R7,RREOUTER        OUTER SPIN COUNT
         S     R7,=F'50'          -50
         ST    R7,RREOUTER        STORE BACK
         BZ    RREFIDTM           EXIT IF LOOP DONE
* OUTER LOOP INCOMPLETE, DECIDE WHERE TO GO
         CLI   INSTTYPE+1,C'R'    RETURN TO R11 ?
         BE    RREOUTR1           EXIT IF IT IS
         B     RREOUTR2           EXIT
*
RREFIDTM EQU   *
* NOW FIDDLE THE TIMES AND POINT AT THE RIGHT INST
         MVC   TIMEAFT,RREACCUM   TOTAL TIME DIFFERENCES
         XC    TIMEBEF,TIMEBEF    CLEAR BEFORE TIME
         LA    R5,RREBAKR1        INST ADDRESS
         CLI   INSTTYPE+1,C'R'    RETURN TO R11 ?
         BE    RREDONE2           EXIT IF IT IS
         LA    R5,RREBAKR2        INST ADDRESS
         B     RREDONE2           EXIT
*
RRECUSE  EQU   *
* SPECIAL PROCESS FOR CUSE MVST CLST SRST TRE
         MVC   RRECUSEG(2),6(R7)  SET THE INST DOUBLE OPCODE
         MVC   RSLTLEN,INSTLL     MOVE LENGTH TO OUTPUT
         MVC   RSLTSL,INSTSL      MOVE STRING LENGTH TO OUTPUT
         ZAP   RREDEC,=P'0'       CLEAR RREDEC
         PACK  RREDEC+12(4),INSTLL-1(7) DECIMAL LENGTH
         CVBG  R1,RREDEC          CONVERT TO HEX
         LR    R5,R1              SET FOR CUSE/TRE
* VALIDATE LEN
         LTR   R11,R1             SET FOR CUSE
         BZ    RRELENER           EXIT IF IT IS
         GETMAIN R,LV=1000000     OP1 ADDRESS
         LR    R10,R1             SET FOR CUSE/MVST/CLST/TRE
         GETMAIN R,LV=1000000     OP2 ADDRESS
         LR    R4,R1              SET FOR CUSE/MVST/CLST/SRST/TRE
         ST    R4,RRER4           SAVE IT
         SGR   R6,R6              CLEAR R6
         LA    R6,X'FF'           STOPPER FOR MVST/CLST/TRE
*                                 FIND FOR SRST
         CLC   6(2,R7),=X'B255'   IS IT MVST ?
         BE    RRENOSL            EXIT IF IT IS
         CLC   6(2,R7),=X'B25D'   IS IT CLST ?
         BE    RRENOSL            EXIT IF IT IS
         CLC   6(2,R7),=X'B25E'   IS IT SRST ?
         BE    RRENOSL            EXIT IF IT IS
         CLC   6(2,R7),=X'B2A5'   IS IT TRE ?
         BE    RRENOSL            EXIT IF IT IS
* PROCESS STRING LENGTH FOR CUSE
         PACK  RREDEC+12(4),INSTSL(3) STRING LENGTH
         CVBG  R6,RREDEC          CONVERT TO HEX
         CG    R6,=FL8'255'       0-255 ?
         BH    RRELENER           ERROR IF NOT
RRENOSL  EQU   *
         STM   R4,R5,RREOP1       SAVE OP1 REGS
         STM   R10,R11,RREOP2     SAVE OP2 REGS
* INITIALISE GETMAINED STORAGE SO CUSE NEVER COMPARES EQUAL
         SR    R11,R11            CLEAR SECOND LENGTH
         MVCLE R4,R10,X'00'       SET OP1 TO X'00'
         LM    R4,R5,RREOP1       RESTORE OP1 REGS
         LM    R10,R11,RREOP2     RESTORE OP2 REGS
         SR    R5,R5              CLEAR SECOND LENGTH
         CLC   6(2,R7),=X'B255'   IS IT MVST ?
         BE    RRECLSTQ           EXIT IF IT IS
         CLC   6(2,R7),=X'B25D'   IS IT CLST ?
         BE    RRECLSTQ           EXIT IF IT IS
         CLC   6(2,R7),=X'B25E'   IS IT SRST ?
         BE    RRESRSTQ           EXIT IF IT IS
         MVCLE R10,R4,X'01'       SET OP2 TO X'01' FOR CUSE/TRT
         CLC   6(2,R7),=X'B2A5'   IS IT TRE ?
         BE    RRETRE             EXIT IF IT IS
         B     RREGOCUS           EXIT
*
RRECLSTQ EQU   *
* MVST CLST
* OPERANDS MUST COMPARE EQUAL FOR CLST
* SET THE STOPPER BYTES IN OP1/OP2
         MVCLE R10,R4,X'00'       SET OP2 TO X'00'
RRETRE   EQU   *
         LM    R4,R5,RREOP1       RESTORE OP1 REGS
         LM    R10,R11,RREOP2     RESTORE OP2 REGS
         LGR   R7,R4              OP1 ADDRESS
         AGR   R7,R5              + LENGTH
         MVI   0(R7),X'FF'        SET STOPPER AT OP1 LAST BYTE+1
         LGR   R7,R10             OP2 ADDRESS
         AGR   R7,R11             + LENGTH
         MVI   0(R7),X'FF'        SET STOPPER AT OP2 LAST BYTE+1
         B     RREGOCUS           EXIT
*
RRESRSTQ EQU   *
* R4 IS ADDRESS OF END+1 OF R10 AREA
         MVCLE R10,R4,X'00'       SET OP2 TO X'00'
         LM    R4,R5,RREOP1       RESTORE OP1 REGS
         LM    R10,R11,RREOP2     RESTORE OP2 REGS
         LA    R4,0(R10,R11)      LAST BYTE+1
         ST    R4,RREOP1          SAVE IT
*
RREGOCUS EQU   *
* TIME 2, 2-REG LM'S PLUS AN LR
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
RRELMLR  LM    R4,R5,RREOP1       SPECIAL INST
         LM    R10,R11,RREOP2     SPECIAL INST
         LR    R0,R6              SPECIAL INST
         BCT   R7,RRELMLR         DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
*
         LG    R7,TIMEAFT         END TIME
         SG    R7,TIMEBEF                  - BEG TIME
         SG    R7,TIMEBCT                  - BCT TIME
         STG   R7,TIMEXTRA        SAVE IT
         L     R7,SPINCNT         SET LOOP LIMIT
*
* R0 IS DESTROYED BY TIME MACRO SO MUST REFRESH IT
* SO IS R1, BUT LENGTHS ARE EQUAL SO WE DON'T NEED THE PADDING BYTE
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
RRECUSEX LM    R4,R5,RREOP1       RESTORE OP1 REGS
         LM    R10,R11,RREOP2     RESTORE OP2 REGS
         LR    R0,R6              REFRESH STRING LENGTH/SEARCH BYTE
RRECUSEG CUSE  R4,R10             TESTED INST
         BCT   R7,RRECUSEX        DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
*
         MVC   RREOP1,RRER4       RESTORE R4
         FREEMAIN LV=1000000,A=RREOP1
         FREEMAIN LV=1000000,A=RREOP2
         LA    R5,RRECUSEG        INST ADDRESS
         B     RREDONE2           EXIT
*
RRELENER EQU   *
         MVC   RSLTMICS+1(12),=C'LENGTH ERROR'
         BAL   R14,OUTMSG         WRITE AND CLEAR
         B     GET                EXIT TO GET ANOTHER TEST
*
RREFP    EQU   *
* F1 CANNOT BE USED HERE, CONVERTS WOULD USE GR1 WHICH IS DESTROYED
* BY THE TIME MACRO.
*
         CLI   INSTTYPE+2,C'H'    HFP ?
         BNE   RREBFP             EXIT IF NOT
* FLOATING POINT--HFP
* DXR SQDR SQER LDER LXDR LXER SQXR MEER LPXR LNXR LTXR LCXR LXR
* LEXR FIXR CXR LZER LZDR LZXR FIER FIDR
* CEFR CDFR CXFR CEGR CDGR CXGR
         MVC   RREFPINS(2),6(R7)  SET THE INST DOUBLE OPCODE
         LZXR  F0                 F0/F2=0
* LOADED VALUES ARE EXACT IN ALL SIZES
         LXD   F5,=DH'3'          SET F5/F7
         LG    R5,=FL8'99'        SET R5 FOR CONVERTS
* TIME AN LD
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
RRELD    LD    F0,=DH'10'         SPECIAL INST
         BCT   R7,RRELD           DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
*
         LG    R7,TIMEAFT         END TIME
         SG    R7,TIMEBEF                  - BEG TIME
         SG    R7,TIMEBCT                  - BCT TIME
         STG   R7,TIMEXTRA        SAVE IT
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
RREFPSPC LD    F0,=DH'10'         SPECIAL INST
RREFPINS DXR   F0,F5              TESTED INST, CAN BE F0,R5
         BCT   R7,RREFPSPC        DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
         LXR   F1,F5              TRANSFER TO F1 FOR DISPLAY
         LA    R5,RREFPINS        INST ADDRESS
         B     RREDONE2           EXIT
*
RREBFP   EQU   *
         CLI   INSTTYPE+2,C'B'    BFP ?
         BNE   RRETFP             EXIT IF NOT
* FLOATING POINT--BFP
* LPEBR LNEBR LTEBR LCEBR LDEBR LXDBR LXEBR MXDBR KEBR CEBR AEBR
* SEBR MDEBR DEBR LPDBR LNDBR LTDER LCDER SQEBR SQDBR SQXBR
* MEEBR KDBR CDBR ADBR SDBR MDBR DDBR LPXBR LNXBR LTXBR LCXBR
* LEDBR LDXBR LEXBR KXBR CXBR AXBR SXBR MXBR DXBR
* CEFBR CDFBR CXFBR CEGBR CDGBR CXGBR
         MVC   RREFPINB(2),6(R7)  SET THE INST DOUBLE OPCODE
         LXDB  F0,=DB'0'          F0/F2=0
* LOADED VALUES ARE EXACT IN ALL SIZES
         LXDB  F5,=DB'101'        SET F5/F7
         LG    R5,=FL8'99'        SET R5 FOR CONVERTS
* TIME AN LXDB
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
RRELXDB  LXDB  F0,=DB'10'         SPECIAL INST
         BCT   R7,RRELXDB         DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
*
         LG    R7,TIMEAFT         END TIME
         SG    R7,TIMEBEF                  - BEG TIME
         SG    R7,TIMEBCT                  - BCT TIME
         STG   R7,TIMEXTRA        SAVE IT
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
RREFPSPB LXDB  F0,=DB'10'         SPECIAL INST
RREFPINB LPEBR F0,F5              TESTED INST, CAN BE F0,R5
         BCT   R7,RREFPSPB        DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
         LTXBR F1,F5              TRANSFER TO F1 FOR DISPLAY
         LA    R5,RREFPINB        INST ADDRESS
         B     RREDONE2           EXIT
*
RRETFP   EQU   *
* FLOATING POINT--DFP
         CLC   6(2,R7),=X'B3CD'   IS IT LGDR ?
         BE    RRETFPSP           EXIT IF IT IS
         CLC   6(2,R7),=X'B3E5'   IS IT EEDTR ?
         BE    RRETFPSP           EXIT IF IT IS
         CLC   6(2,R7),=X'B3ED'   IS IT EEXTR ?
         BE    RRETFPSP           EXIT IF IT IS
         CLC   6(2,R7),=X'B3E7'   IS IT ESDTR ?
         BE    RRETFPSP           EXIT IF IT IS
         CLC   6(2,R7),=X'B3EF'   IS IT ESXTR ?
         BE    RRETFPSP           EXIT IF IT IS
         CLC   6(2,R7),=X'B3E2'   IS IT CUDTR ?
         BE    RRETFPSP           EXIT IF IT IS
         CLC   6(2,R7),=X'B3EA'   IS IT CUXTR ?
         BE    RRETFPSP           EXIT IF IT IS
         CLC   6(2,R7),=X'B3F3'   IS IT CDSTR ?
         BE    RRETFPS2           EXIT IF IT IS
         CLC   6(2,R7),=X'B3FB'   IS IT CXSTR ?
         BE    RRETFPS2           EXIT IF IT IS
         CLC   6(2,R7),=X'B3F2'   IS IT CDUTR ?
         BE    RRETFPS3           EXIT IF IT IS
         CLC   6(2,R7),=X'B3FA'   IS IT CXUTR ?
         BE    RRETFPS3           EXIT IF IT IS
         CLC   6(2,R7),=X'B3E1'   IS IT CGDTR ?
         BE    RRETFPS4           EXIT IF IT IS
         CLC   6(2,R7),=X'B3E9'   IS IT CGXTR ?
         BE    RRETFPS4           EXIT IF IT IS
* LPDFR LNDFR LCDFR LDGR LTDTR LTXTR KDTR CDTR CXTR KXTR
* CDGTR CEDTR CXGTR CEXTR
         MVC   RREFPINT(2),6(R7)  SET THE INST DOUBLE OPCODE
         LD    F0,=DD'99.99'      F0=99.99
         LD    F1,=DD'100.1'      F1=100.1
         CLI   INSTTYPE,C'D'      IT IS LONG ?
         BE    RREINIT            EXIT IF IT IS
* EXTENDED
         LD    F0,RREVAL1         F0/
         LD    F2,RREVAL1+8          F2=100.98
         LD    F1,RREVAL2         F1/
         LD    F3,RREVAL2+8          F3=101.99
RREINIT  EQU   *
* WHEN R2-FIELD IS A GENERAL REG, GR0=8
         SGR   R0,R0              TIME SETS LOW R0=8, ENSURE HIGH=0
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
RREFPINT CDTR  F1,F0              TESTED INST
         BCT   R7,RREFPINT        DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
         LA    R5,RREFPINT        INST ADDRESS
         B     RREDONE2           EXIT
*
RRETFPSP EQU   *
* FLOATING POINT--DFP SPECIALS
* LGDR CUDTR EEDTR ESDTR EEXTR ESXTR CUXTR
         MVC   RREFPSP2(2),6(R7)  SET THE INST DOUBLE OPCODE
         LD    F1,=DD'100.2'      F1=100.2
         CLI   INSTTYPE,C'D'      IT IS LONG ?
         BE    RREINIT1           EXIT IF IT IS
* EXTENDED
         LD    F1,RREVAL3         F1/
         LD    F3,RREVAL3+8          F3=6666.99
RREINIT1 EQU   *
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
RREFPSP2 EEDTR R10,F1             TESTED INST
         BCT   R7,RREFPSP2        DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
         LA    R5,RREFPSP2        INST ADDRESS
         B     RREDONE2           EXIT
*
RRETFPS2 EQU   *
* FLOATING POINT--DFP SPECIALS
* CDSTR CXSTR
         MVC   RREFPSP3(2),6(R7)  SET THE INST DOUBLE OPCODE
         LG    R10,=PL8'-987'     R10=-987
         CLI   INSTTYPE,C'D'      IT IS LONG ?
         BE    RREINIT2           EXIT IF IT IS
* EXTENDED
         LMG   R10,R11,=PL16'-789' R10/R11=-789
RREINIT2 EQU   *
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
RREFPSP3 CDSTR F1,R10             TESTED INST
         BCT   R7,RREFPSP3        DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
         LA    R5,RREFPSP3        INST ADDRESS
         B     RREDONE2           EXIT
*
RRETFPS3 EQU   *
* FLOATING POINT--DFP SPECIALS
* CDUTR CXUTR
         MVC   RREFPSP4(2),6(R7)  SET THE INST DOUBLE OPCODE
         LG    R10,=XL8'0234'     R10=234
         CLI   INSTTYPE,C'D'      IT IS LONG ?
         BE    RREINIT3           EXIT IF IT IS
* EXTENDED
         SGR   R10,R10            R10/
         LG    R11,=XL8'0456'         R11=456
RREINIT3 EQU   *
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
RREFPSP4 CDUTR F1,R10             TESTED INST
         BCT   R7,RREFPSP4        DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
         LA    R5,RREFPSP4        INST ADDRESS
         B     RREDONE2           EXIT
*
RRETFPS4 EQU   *
* FLOATING POINT--DFP SPECIALS
* CGDTR CGXTR
         MVC   RREFPSP5(2),6(R7)  SET THE INST DOUBLE OPCODE
         LD    F1,=DD'999.99'     F1=999.99
         CLI   INSTTYPE,C'D'      IT IS LONG ?
         BE    RREINIT4           EXIT IF IT IS
* EXTENDED
         LD    F1,RREVAL2         F1/
         LD    F3,RREVAL2+8          F3=101.99
RREINIT4 EQU   *
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
RREFPSP5 CGDTR R10,10,F1          TESTED INST
         BCT   R7,RREFPSP5        DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
         LA    R5,RREFPSP5        INST ADDRESS
         B     RREDONE2           EXIT
*
RRECTH   EQU   *
* SPECIALS
* THDER THDR
         MVC   RREFPCTH(2),6(R7)  SET THE INST DOUBLE OPCODE
         LZXR  F0                 F0/F2=0
         LD    F1,=DH'99'         SET F1
         TBDR  F1,0,F1            CONVERT TO BFP
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
RREFPCTH THDER F0,F1              TESTED INST
         BCT   R7,RREFPCTH        DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
         LA    R5,RREFPCTH        INST ADDRESS
         B     RREDONE2           EXIT
*
RREDONE  EQU   *
         LA    R5,RREINST         INST ADDRESS
RREDONE2 EQU   *
         LA    R4,4               INST LENGTH
         BAL   R14,OUTPUT         WRITE THE OUTPUT RECORD
         MVC   RREBCTR+2(2),RRESAVE RESTORE MODIFIED BYTES
         MVC   RREBAKR1+2(2),RRESVBK1 RESTORE MODIFIED BYTES
         MVC   RREBAKR2+2(2),RRESVBK2 RESTORE MODIFIED BYTES
         B     GET                EXIT TO GET ANOTHER TEST
*
RRERROR  EQU   *
         MVC   RSLTMICS(10),=C'SPIN ERROR'
         BAL   R14,OUTMSG         WRITE AND CLEAR
         B     GET                EXIT TO GET ANOTHER TEST
*
RRESAVE  DS    H
RRESVBK1 DS    H
RRESVBK2 DS    H
RRESTORE DS    F
RREOUTER DS    F                  OUTER LOOP CONTROL
RREACCUM DS    D                  SUM OF TIME DIFFERENCES
RREDEC   DC    PL16'0'
RREOP1   DS    2F                 SAVE OP1 REGS
RREOP2   DS    2F                 SAVE OP2 REGS
RRER4    DS    F                  SAVE R4
RREVAL1  DC    LD'100.98'
RREVAL2  DC    LD'101.99'
RREVAL3  DC    LD'6666.99'
         LTORG
*
RRF      DS    0H
* DOUBLE BYTE OPCODE
* TRTT TRTO TROT TROO
*
* HFP FLOATING POINT
* MAER MSER MAYLR MYLR MAYR MYR MAYHR MYHR MADR MSDR
*
* BFP FLOATING POINT
* MAEBR MSEBR MADBR MSDBR FIXBR TBEDR TBDR FIEBR FIDBR
*
* DFP FLOATING POINT
* CPSDR CSDTR CSXTR
*
* HFP SPECIALS
* CFER CFDR CFXR CGER CGDR CGXR
*
* BFP SPECIALS
* DIEBR DIDBR
* CFEBR CFDBR CFXBR CGEBR CGDBR CGXBR
*
* DFP SPECIALS
* IEDTR IEXTR
* FIDTR FIXTR LEDTR LDXTR
* LDETR LXDTR
* QADTR QAXTR
* RRDTR RRXTR
         USING *,R12
         BAL   R14,SRCHOPCD       SET UP OPCODE
         MVC   RRFINST(2),6(R7)   SET THE INST DOUBLE OPCODE
         LA    R10,RRFOPCD        ADDRESS SPECIAL OPCODE TABLE
RRFTABLP EQU   *
         CLC   0(2,R10),6(R7)     MATCH OPCODE ?
         BE    RRFGOSPC           EXIT IF IT DOES
         BH    RRFNTCTF           EXIT IF NOT IN TABLE
         LA    R10,6(R10)         BUMP POINTER
         B     RRFTABLP           LOOP
RRFGOSPC EQU   *
         L     R14,2(R10)         R14=PROCESSING ADDRESS
         BR    R14                GO THERE
*
RRFNTCTF EQU   *
         CLI   INSTTYPE+2,C'H'    HFP ?
         BNE   RRFBFP             EXIT IF NOT
RRFHFP   EQU   *
* FLOATING POINT--HFP
         LXD   F0,=DH'1'          F0/F2=1 (HFP)
         LD    F1,=DH'2'          F1   =2 (HFP)
         LD    F5,=DH'3'          F5   =3 (HFP)
         B     RRFGO              EXIT
*
RRFBFP   EQU   *
* FLOATING POINT--BFP
         CLI   INSTTYPE+2,C'B'    BFP ?
         BNE   RRFTFP             EXIT IF NOT
         LXDB  F0,=DB'4'          F0/F2=4 (BFP)
         LXDB  F1,=DB'5'          F1/F3=5 (BFP)
         LXDB  F5,=DB'6'          F5/F7=6 (BFP)
         B     RRFGO              EXIT
*
RRFTFP   EQU   *
* FLOATING POINT--DFP
* CPSDR CSDTR CSXTR
         MVC   RRFTFPGO(2),6(R7)  SET THE INST DOUBLE OPCODE
         LD    F0,=DD'100.1'      F0=100.1
         LD    F1,=DD'-100.1'     F1=-100.1
         CLI   INSTTYPE,C'D'      IT IS LONG ?
         BE    RRFINIT            EXIT IF IT IS
* EXTENDED
         LD    F1,RRFVAL1         F1/
         LD    F3,RRFVAL1+8          F3=99.99
RRFINIT  EQU   *
*
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
*              R1  R2 M4
*              R1  R3 R2
RRFTFPGO CSDTR R10,F1,F0          TESTED INST
         BCT   R7,RRFTFPGO        DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
         LA    R5,RRFTFPGO        INST ADDRESS
         B     RRFDONE2           EXIT
*
RRFTFP2  EQU   *
* FLOATING POINT--DFP
* IEDTR IEXTR
         MVC   RRFTFPG2(2),6(R7)  SET THE INST DOUBLE OPCODE
         LD    F0,=DD'135.7'      F0=135.7
         LG    R10,=XL8'190'      R10=X'190' BIAS BY E3=1.357E5=135700
         CLI   INSTTYPE,C'D'      IT IS LONG ?
         BE    RRFINIT2           EXIT IF IT IS
* EXTENDED
         LD    F0,RRFVAL2         F0/
         LD    F2,RRFVAL2+8          F2=89.912
         LG    R10,=XL8'1830'     R10=X'1830' BIAS BY E19=8.9912E20
RRFINIT2 EQU   *
*
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
*              R1 R3 R2
RRFTFPG2 IEDTR F1,F0,R10          TESTED INST
         BCT   R7,RRFTFPG2        DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
         LA    R5,RRFTFPG2        INST ADDRESS
         B     RRFDONE2           EXIT
*
RRFTFP3  EQU   *
* FLOATING POINT--DFP
* FIDTR FIXTR LEDTR LDXTR
         MVC   RRFTFPG3(2),6(R7)  SET THE INST DOUBLE OPCODE
         LD    F0,=DD'35.999'     F0=35.999
         CLI   7(R7),X'D5'        IS IT LEDTR ?
         BE    RRFINIT3           EXIT IF IT IS
         CLI   7(R7),X'DD'        IS IT LDXTR ?
         BE    RRFEXT3            EXIT IF IT IS
         CLI   INSTTYPE,C'D'      IT IS LONG ?
         BE    RRFINIT3           EXIT IF IT IS
RRFEXT3  EQU   *
* EXTENDED
         LD    F0,RRFVAL3         F0/
         LD    F2,RRFVAL3+8          F2=-59.999
RRFINIT3 EQU   *
*
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
*              R1 M3 R2 M4
RRFTFPG3 FIDTR F1,10,F0,0         TESTED INST
         BCT   R7,RRFTFPG3        DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
         LA    R5,RRFTFPG3        INST ADDRESS
         B     RRFDONE2           EXIT
*
RRFTFP4  EQU   *
* FLOATING POINT--DFP
* LDETR LXDTR
         MVC   RRFTFPG4(2),6(R7)  SET THE INST DOUBLE OPCODE
         LE    F0,=ED'58.987'     F0=58.987
         CLI   INSTTYPE,C'D'      IT IS SHORT TO LONG ?
         BE    RRFINIT4           EXIT IF IT IS
* LONG TO EXTENDED
         LD    F0,=DD'37.789'     F0=37.789
RRFINIT4 EQU   *
*
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
*              R1 R2 M4
RRFTFPG4 LDETR F1,F0,0            TESTED INST
         BCT   R7,RRFTFPG4        DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
         LA    R5,RRFTFPG4        INST ADDRESS
         B     RRFDONE2           EXIT
*
RRFTFP5  EQU   *
* FLOATING POINT--DFP
* QADTR QAXTR
         MVC   RRFTFPG5(2),6(R7)  SET THE INST DOUBLE OPCODE
         LD    F0,=DD'765.879'    F0=765.879
         LD    F5,=DD'1E-2'       F5=0.01
         CLI   INSTTYPE,C'D'      IT IS LONG ?
         BE    RRFINIT5           EXIT IF IT IS
* EXTENDED
         LD    F0,RRFVAL4         F0/
         LD    F2,RRFVAL4+8          F2=0.01239
         LD    F5,RRFVAL5         F5/
         LD    F7,RRFVAL5+8          F7=0.0001
RRFINIT5 EQU   *
*
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
*              R1 R3 R2 M4
RRFTFPG5 QADTR F1,F0,F5,10        TESTED INST
         BCT   R7,RRFTFPG5        DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
         LA    R5,RRFTFPG5        INST ADDRESS
         B     RRFDONE2           EXIT
*
RRFTFP6  EQU   *
* FLOATING POINT--DFP
* RRDTR RRXTR
         MVC   RRFTFPG6(2),6(R7)  SET THE INST DOUBLE OPCODE
         LD    F0,=DD'357.889'    F0=357.889
         LA    R10,5              R10=5 SIGNIFICANT DIGITS
         CLI   INSTTYPE,C'D'      IT IS LONG ?
         BE    RRFINIT6           EXIT IF IT IS
* EXTENDED
         LD    F0,RRFVAL6         F0/
         LD    F2,RRFVAL6+8          F2=234.15999
         LA    R10,5              R10=5 SIGNIFICANT DIGITS
RRFINIT6 EQU   *
*
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
*              R1 R3 R2  M4
RRFTFPG6 RRDTR F1,F0,R10,10       TESTED INST
         BCT   R7,RRFTFPG6        DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
         LA    R5,RRFTFPG6        INST ADDRESS
         B     RRFDONE2           EXIT
*
RRFGO    EQU   *
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
*              R1 R3 R2           HFP
*              R1 M3 R2           BFP CONVERTS/LOAD FP INTEGER
RRFINST  MAER  F0,F1,F5           TESTED INST
         BCT   R7,RRFINST         DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
         B     RRFDONE            EXIT
*
RRFCTFH  EQU   *
* SPECIALS
* CFER CFDR CFXR CGER CGDR CGXR
         MVC   RRFFPCFH(2),6(R7)  SET THE INST DOUBLE OPCODE
         LXD   F0,=DH'99'         SET F0/F2
         SGR   R10,R10            R10=0
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
RRFFPCFH CFER  R10,0,F0           TESTED INST
         BCT   R7,RRFFPCFH        DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
         LA    R5,RRFFPCFH        INST ADDRESS
         B     RRFDONE2           EXIT
*
RRFCTFB  EQU   *
* SPECIALS
* CFEBR CFDBR CFXBR CGEBR CGDBR CGXBR
         MVC   RRFFPCFB(2),6(R7)  SET THE INST DOUBLE OPCODE
         LXDB  F0,=DB'987'        SET F0/F2
         SGR   R10,R10            R10=0
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
RRFFPCFB CFEBR R10,0,F0           TESTED INST
         BCT   R7,RRFFPCFB        DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
         LA    R5,RRFFPCFB        INST ADDRESS
         B     RRFDONE2           EXIT
*
RRFDIE   EQU   *
* SPECIALS
* DIEBR
* ALL LOADS MUST BE THE SAME, THE 1ST OF ITS TYPE
         MVC   RRFBFPDE(2),6(R7)  SET THE INST DOUBLE OPCODE
         LE    F2,=EB'5'          SET F2
* TIME AN LE
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
RRFXTRAE LE    F1,=EB'99'         SET F1
         BCT   R7,RRFXTRAE        DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
*
         LG    R7,TIMEAFT         END TIME
         SG    R7,TIMEBEF                  - BEG TIME
         SG    R7,TIMEBCT                  - BCT TIME
         STG   R7,TIMEXTRA        SAVE IT
*
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
RRFLE    LE    F1,=EB'99'         SET F1
*              R1 R3 R2 M4        B3530012
RRFBFPDE DIEBR F1,F0,F2,0         TESTED INST
         BCT   R7,RRFLE           DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
         LA    R5,RRFBFPDE        INST ADDRESS
         B     RRFDONE2           EXIT
*
RRFDID   EQU   *
* SPECIALS
* DIDBR
* ALL LOADS MUST BE THE SAME, THE 1ST OF ITS TYPE
         MVC   RRFBFPDD(2),6(R7)  SET THE INST DOUBLE OPCODE
         LD    F2,=DB'4'          SET F2
* TIME AN LD
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
RRFXTRAD LD    F1,=DB'99'         SET F1
         BCT   R7,RRFXTRAD        DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
*
         LG    R7,TIMEAFT         END TIME
         SG    R7,TIMEBEF                  - BEG TIME
         SG    R7,TIMEBCT                  - BCT TIME
         STG   R7,TIMEXTRA        SAVE IT
*
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
RRFLD    LD    F1,=DB'99'         SET F1
*              R1 R3 R2 M4        B3530012
RRFBFPDD DIDBR F1,F0,F2,0         TESTED INST
         BCT   R7,RRFLD           DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
         LA    R5,RRFBFPDD        INST ADDRESS
         B     RRFDONE2           EXIT
*
RRFTR    EQU   *
* TRTT TRTO TROT TROO
         MVC   RRFTREX(2),6(R7)   SET THE INST DOUBLE OPCODE
         MVC   RSLTLEN,INSTLL     MOVE LENGTH TO OUTPUT
         ZAP   RRFDEC,=P'0'       CLEAR RRFDEC
         PACK  RRFDEC+12(4),INSTLL-1(7) DECIMAL LENGTH
         CVBG  R11,RRFDEC         CONVERT TO HEX
* VALIDATE LEN
* MUST BE EVEN FOR TRTT AND TRTO
         CLC   6(2,R7),=X'B992'   IS IT TROT ?
         BE    RRFNT2             EXIT IF IT IS
         CLC   6(2,R7),=X'B993'   IS IT TROO ?
         BE    RRFNT2             EXIT IF IT IS
* TRTT TRTO
         TM    INSTLL+5,X'01'     IS IT ODD ?
         BO    RRFLENER           ERROR IF IT IS
RRFNT2   EQU   *
         GETMAIN R,LV=(R11),LOC=24 OP2
         ST    R1,RRFSAVE2        STORE IT
         LR    R0,R11             GETMAIN LENGTH
         AR    R0,R0              MAXIMUM SIZE OF OP1
         GETMAIN R,LV=(0),LOC=24  OP1
         LR    R10,R1             SAVE ADDRESS
         STM   R10,R11,RRFSAVE    SAVE OP1 ADDRESS, OP2 LENGTH
         GETMAIN R,LV=128K,LOC=24 MAXIMUM TRANSLATE TABLE
         LR    R4,R1              SAVE ADDRESS
* TIME AN LM, L AND LR
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
RRFXTRA  LM    R10,R11,RRFSAVE    SPECIAL INST
         L     R5,RRFSAVE2        SPECIAL INST
         LR    R1,R4              SPECIAL INST
         BCT   R7,RRFXTRA         DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
*
         LG    R7,TIMEAFT         END TIME
         SG    R7,TIMEBEF                  - BEG TIME
         SG    R7,TIMEBCT                  - BCT TIME
         STG   R7,TIMEXTRA        SAVE IT
*
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
RRFTRGO  LM    R10,R11,RRFSAVE    RESTORE OP1 ADDRESS, OP2 LENGTH
         L     R5,RRFSAVE2        RESTORE OP2 ADDRESS
         LR    R1,R4              RESTORE TRANSLATE TABLE ADDRESS
*              R1  R2 M3     B99?10A5
RRFTREX  TRTT  R10,R5,1           TESTED INST
         BCT   R7,RRFTRGO         DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
*
         L     R11,RRFSAVE+4      LENGTH TO FREEMAIN
         FREEMAIN LV=(R11),A=RRFSAVE2 FREE OP2
         AR    R11,R11            LENGTH TO FREEMAIN
         FREEMAIN LV=(R11),A=RRFSAVE FREE OP1
         FREEMAIN LV=128K,A=(R4) FREE TRANSLATE TABLE
         LA    R5,RRFTREX         INST ADDRESS
         B     RRFDONE2           EXIT
*
RRFLENER EQU   *
         MVC   RSLTMICS+1(12),=C'LENGTH ERROR'
         BAL   R14,OUTMSG         WRITE AND CLEAR
         B     GET                EXIT TO GET ANOTHER TEST
*
RRFDONE  EQU   *
         LA    R5,RRFINST         INST ADDRESS
RRFDONE2 EQU   *
         LA    R4,4               INST LENGTH
         BAL   R14,OUTPUT         WRITE THE OUTPUT RECORD
         B     GET                EXIT TO GET ANOTHER TEST
*
* OPCODE TABLE FOR SPECIAL PROCESSING
* MUST BE IN OPCODE SEQUENCE
RRFOPCD  EQU   *
         DC    X'B350',AL4(RRFHFP) TBEDR
         DC    X'B351',AL4(RRFHFP)  TBDR
         DC    X'B353',AL4(RRFDIE)  DIEBR
         DC    X'B35B',AL4(RRFDID)  DIDBR
         DC    X'B398',AL4(RRFCTFB) CFEBR
         DC    X'B399',AL4(RRFCTFB) CFDBR
         DC    X'B39A',AL4(RRFCTFB) CFXBR
         DC    X'B3A8',AL4(RRFCTFB) CGEBR
         DC    X'B3A9',AL4(RRFCTFB) CGDBR
         DC    X'B3AA',AL4(RRFCTFB) CGXBR
         DC    X'B3B8',AL4(RRFCTFH) CFER
         DC    X'B3B9',AL4(RRFCTFH) CFDR
         DC    X'B3BA',AL4(RRFCTFH) CFXR
         DC    X'B3C8',AL4(RRFCTFH) CGER
         DC    X'B3C9',AL4(RRFCTFH) CGDR
         DC    X'B3CA',AL4(RRFCTFH) CGXR
         DC    X'B3D4',AL4(RRFTFP4) LDETR
         DC    X'B3D5',AL4(RRFTFP3) LEDTR
         DC    X'B3D7',AL4(RRFTFP3) FIDTR
         DC    X'B3DC',AL4(RRFTFP4) LXDTR
         DC    X'B3DD',AL4(RRFTFP3) LDXTR
         DC    X'B3DF',AL4(RRFTFP3) FIXTR
         DC    X'B3F5',AL4(RRFTFP5) QADTR
         DC    X'B3F6',AL4(RRFTFP2) IEDTR
         DC    X'B3F7',AL4(RRFTFP6) RRDTR
         DC    X'B3FD',AL4(RRFTFP5) QAXTR
         DC    X'B3FE',AL4(RRFTFP2) IEXTR
         DC    X'B3FF',AL4(RRFTFP6) RRXTR
         DC    X'B990',AL4(RRFTR)   TRTT
         DC    X'B991',AL4(RRFTR)   TRTO
         DC    X'B992',AL4(RRFTR)   TROT
         DC    X'B993',AL4(RRFTR)   TROO
         DC    X'FFFF'  STOPPER
*
RRFDEC   DC    PL16'0'
RRFVAL1  DC    LD'99.99'
RRFVAL2  DC    LD'89.912'
RRFVAL3  DC    LD'-59.999'
RRFVAL4  DC    LD'0.01239'
RRFVAL5  DC    LD'1E-4'
RRFVAL6  DC    LD'234.159999'
RRFSAVE  DS    CL8
RRFSAVE2 DS    CL4
*
         LTORG
*
RRR      DS    0H
* DOUBLE BYTE OPCODE
*
* FLOATING POINT--DFP
* MDTR DDTR ADTR SDTR MXTR DXTR AXTR SXTR
         USING *,R12
         BAL   R14,SRCHOPCD       SET UP OPCODE
         MVC   RRRFPINT(2),6(R7)  SET THE INST DOUBLE OPCODE
* ASSUME LONG
         LD    F0,=DD'99.98'      F0=99.98
         LD    F5,=DD'99.99'      F5=99.99
         CLI   INSTTYPE,C'D'      IT IS LONG ?
         BE    RRRINIT            EXIT IF IT IS
* EXTENDED
         LD    F0,RRRVAL1         F0/
         LD    F2,RRRVAL1+8          F2=99.98
         LD    F5,RRRVAL2         F5/
         LD    F7,RRRVAL2+8          F7=99.99
RRRINIT  EQU   *
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
RRRFPINT ADTR  F1,F0,F5           TESTED INST
         BCT   R7,RRRFPINT        DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
         LA    R5,RRRFPINT        INST ADDRESS
         B     RRRDONE            EXIT
*
RRRDONE  EQU   *
         LA    R5,RRRFPINT        INST ADDRESS
         LA    R4,4               INST LENGTH
         BAL   R14,OUTPUT         WRITE THE OUTPUT RECORD
         B     GET                EXIT TO GET ANOTHER TEST
*
         PRINT DATA
RRRVAL1  DC    LD'99.98'
RRRVAL2  DC    LD'99.99'
         PRINT NODATA
*
         LTORG
*
RXF      DS    0H
* DOUBLE BYTE OPCODE, 2ND BYTE AT END
* HFP FLOATING POINT
* MAE MSE MAYL MYL MAY MY MAYH MYH MAD MSD
*
* BFP FLOATING POINT
* MAEB MSEB MADB MSDB
*
* DFP FLOATING POINT
* SLDT SRDT SLXT SRXT
         USING *,R12
         BAL   R14,SRCHOPCD       SET UP OPCODE
         MVC   RSLTLEN(2),INSTXB  MOVE THE XB TO OUTPUT
         MVC   RXFINST(1),6(R7)   SET THE INST
         MVC   RXFINST+5(1),7(R7) SET THE INST 2ND OPCODE
* USE OF BASE ALWAYS VALID
         CLC   INSTXB,=C'XB'      VALIDATE ?
         BE    RXFXBOK            EXIT IF IT IS
         CLC   INSTXB,=C'0B'      VALIDATE ?
         BE    RXFXBOK            EXIT IF IT IS
         CLC   INSTXB,=C'X0'      VALIDATE ?
         BE    RXFNOBAS           EXIT IF IT IS
         CLC   INSTXB,=C'00'      VALIDATE ?
         BNE   RXFXBERR           ERROR IF NOT
RXFNOBAS EQU *
* X0 OR 00
* NEVER HAVE FLOATING POINT VALUES IN LOW STORAGE
* DFP INSTS DON'T REFERENCE STORAGE
         CLI   INSTTYPE+2,C'T'    DFP FLOATING POINT ?
         BE    RXFXBOK            EXIT IF IT IS
         CLI   INSTTYPE+1,C'P'    FLOATING POINT ?
         BE    RXFXBERR           ERROR IF IT IS
         B     RXFXBOK            EXIT
*
RXFXBERR EQU   *
* XB FIELD INVALID FOR INSTRUCTION
         MVC   RSLTMICS(8),=C'XB ERROR' ERROR MSG
         BAL   R14,OUTMSG         WRITE AND CLEAR
         B     GET                EXIT TO GET ANOTHER TEST
*
RXFXBOK  EQU   *
* XB INPUT VALID
         MVC   RXFSAVE,RXFINST+1  SAVE MODIFIED BYTES
         CLI   INSTXB,C'0'        IS INDEX ZERO ?
         BNE   RXFXB1             EXIT IF NOT
         NI    RXFINST+1,X'F0'    KNOCK OUT THE INDEX
RXFXB1   EQU   *
         CLI   INSTXB+1,C'0'      IS BASE ZERO ?
         BNE   RXFXB2             EXIT IF NOT
         NI    RXFINST+2,X'0F'    KNOCK OUT THE BASE
RXFXB2   EQU   *
* SPECIAL TEST, ALWAYS LONG OPERANDS, EXTENDED RESULTS, ED38-D
         CLC   6(2,R7),=X'ED38'   IS IT SPECIAL ?
         BL    RXFNTSPC           EXIT IF NOT
         CLC   6(2,R7),=X'ED3D'   IS IT SPECIAL ?
         BH    RXFNTSPC           EXIT IF NOT
         LXD   F0,=DH'1'          SET HFP F0/F2
         LD    F1,=DH'2'          SET HFP F1
         B     RXFD               EXIT
*
RXFNTSPC EQU   *
         CLI   INSTTYPE+2,C'H'    HFP ?
         BNE   RXFBFP             EXIT IF NOT
* HFP
         LXD   F0,=DH'1'          SET HFP F0/F2
         LD    F1,=DH'2'          SET HFP F1
* SET E, TEST E
         LA    R5,RXFBASHE        SET RXF BASE
         LA    R6,4               SET RXF INDEX
         MVI   RXFINST+3,X'04'    SET RXF DISPLACEMENT
         CLI   INSTTYPE,C'E'      SHORT FP ?
         BE    RXFSET             EXIT IF IT IS
RXFD     EQU   *
* SET D
         LA    R5,RXFBASHD        SET RXF BASE
         LA    R6,8               SET RXF INDEX
         MVI   RXFINST+3,X'08'    SET RXF DISPLACEMENT
         B     RXFSET             EXIT
*
RXFBFP   EQU   *
         CLI   INSTTYPE+2,C'B'    BFP ?
         BNE   RXFDFP             EXIT IF NOT
* BFP
* SET E, TEST E
         LE    F0,=EB'1'          SET BFP F0
         LE    F1,=EB'2'          SET BFP F1
         LA    R5,RXFBASBE        SET RXF BASE
         LA    R6,4               SET RXF INDEX
         MVI   RXFINST+3,X'04'    SET RXF DISPLACEMENT
         CLI   INSTTYPE,C'E'      SHORT FP ?
         BE    RXFSET             EXIT IF IT IS
* SET D
         LD    F0,=DB'1'          SET BFP F0
         LD    F1,=DB'2'          SET BFP F1
         LA    R5,RXFBASBD        SET RXF BASE
         LA    R6,8               SET RXF INDEX
         MVI   RXFINST+3,X'08'    SET RXF DISPLACEMENT
         B     RXFSET             EXIT
*
RXFDFP   EQU   *
* DFP
         LA    R5,1               SET RXF BASE
         LA    R6,2               SET RXF INDEX
         MVI   RXFINST+3,X'01'    SET RXF DISPLACEMENT
* SET D, TEST D
         LD    F1,=DD'98765.432'  F1=98765.432
         CLI   INSTTYPE,C'D'      LONG FP ?
         BE    RXFSET             EXIT IF IT IS
* SET X
         LD    F1,RXFVAL1         F1/
         LD    F3,RXFVAL1+8          F3=76543.21
*
RXFSET   EQU   *
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
*              R1 R3 D X2 B2   ED165001002E
RXFINST  MAE   F0,F1,8(R6,R5)     TESTED INST
         BCT   R7,RXFINST         DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
         B     RXFDONE            EXIT
*
RXFDONE  EQU   *
         LA    R5,RXFINST         INST ADDRESS
         LA    R4,6               INST LENGTH
         BAL   R14,OUTPUT         WRITE THE OUTPUT RECORD
         MVC   RXFINST+1(2),RXFSAVE RESTORE MODIFIED BYTES
         B     GET                EXIT TO GET ANOTHER TEST
*
RXFSAVE  DS    H
RXFBASHE DC    EH'10'             RXF HFP BASE
         DC    EH'20'             RXF HFP BASE+DISP
         DC    EH'30'             RXF HFP BASE+INDEX+DISP
RXFBASHD DC    DH'10'             RXF HFP BASE
         DC    DH'20'             RXF HFP BASE+DISP
         DC    DH'30'             RXF HFP BASE+INDEX+DISP
RXFBASBE DC    EB'11'             RXF BFP BASE
         DC    EB'21'             RXF BFP BASE+DISP
         DC    EB'31'             RXF BFP BASE+INDEX+DISP
RXFBASBD DC    DB'11'             RXF BFP BASE
         DC    DB'21'             RXF BFP BASE+DISP
         DC    DB'31'             RXF BFP BASE+INDEX+DISP
RXFVAL1  DC    LD'76543.21'
         LTORG
*
RXY      DS    0H
* DOUBLE BYTE OPCODE, 2ND BYTE AT END
* LG CVBY AG SG ALG SLG MSG DSG CVBG LRVG LGF LGH LLGF LLGT
* AGF SGF ALGF SLGF MSGF DSGF LRV LRVH CG CLG STG CVDY CVDG
* STRVG CGF CLGF STRV STRVH STY MSY NY CLY OY XY LY CY AY SY
* ALY SLY STHY LAY STCY ICY LB LGB LHY CHY AHY SHY NG OG XG
* MLG DLG ALCG SLBG STPQ LPQ LLGC LLGH ML DL ALC SLB
* BCTG LTG LT LLC LLH
*
* FLOATING POINT
* LEY LDY STEY STDY
         USING *,R12
         BAL   R14,SRCHOPCD       SET UP OPCODE
         MVC   RSLTLEN(2),INSTXB  MOVE THE XB TO OUTPUT
         MVC   RXYINST(1),6(R7)   SET THE INST
         MVC   RXYINST+5(1),7(R7) SET THE INST 2ND OPCODE
* USE OF BASE ALWAYS VALID
         CLC   INSTXB,=C'XB'      VALIDATE ?
         BE    RXYXBOK            EXIT IF IT IS
         CLC   INSTXB,=C'0B'      VALIDATE ?
         BE    RXYXBOK            EXIT IF IT IS
         CLC   INSTXB,=C'X0'      VALIDATE ?
         BE    RXYNOBAS           EXIT IF IT IS
         CLC   INSTXB,=C'00'      VALIDATE ?
         BNE   RXYXBERR           ERROR IF NOT
RXYNOBAS EQU *
* X0 OR 00
* SOME INSTS REQUIRE FORMAT
         CLC   6(2,R7),=X'E306'   IS IT CVBY ?
         BE    RXYXBERR           ERROR IF IT IS
         CLC   6(2,R7),=X'E30E'   IS IT CVBG ?
         BE    RXYXBERR           ERROR IF IT IS
* STORE INSTS MUST NOT STORE INTO LOW STORAGE
         CLI   INSTSTOR,C'S'      IS IT STORE TYPE ?
         BE    RXYXBERR           ERROR IF IT IS
* DIVIDES MUST NOT ACCESS LOW STORAGE
         CLC   6(2,R7),=X'E30D'   IS IT DSG ?
         BE    RXYXBERR           ERROR IF IT IS
         CLC   6(2,R7),=X'E31D'   IS IT DSGF ?
         BE    RXYXBERR           ERROR IF IT IS
         CLC   6(2,R7),=X'E387'   IS IT DLG ?
         BE    RXYXBERR           ERROR IF IT IS
         CLC   6(2,R7),=X'E397'   IS IT DL ?
         BE    RXYXBERR           ERROR IF IT IS
* NEED BASE FOR BRANCHES
         CLC   6(2,R7),=X'E346'   IS IT BCTG ?
         BE    RXYXBERR           ERROR IF IT IS
* NEVER HAVE FLOATING POINT VALUES IN LOW STORAGE
         CLI   INSTTYPE+1,C'P'    FLOATING POINT ?
         BE    RXYXBERR           ERROR IF IT IS
         B     RXYXBOK            EXIT
*
RXYXBERR EQU   *
* XB FIELD INVALID FOR INSTRUCTION
         MVC   RSLTMICS(8),=C'XB ERROR' ERROR MSG
         BAL   R14,OUTMSG         WRITE AND CLEAR
         B     GET                EXIT TO GET ANOTHER TEST
*
RXYXBOK  EQU   *
* XB INPUT VALID
         MVC   RXYSAVE,RXYINST+1  SAVE MODIFIED BYTES
         MVC   RXYSAVB,RXYBCTGO+1 SAVE MODIFIED BYTES--BRANCH
         MVC   RXYSAVP,RXYINSFP+1 SAVE MODIFIED BYTES--FP
         CLI   INSTXB,C'0'        IS INDEX ZERO ?
         BNE   RXYXB1             EXIT IF NOT
         NI    RXYINST+1,X'F0'    KNOCK OUT THE INDEX
         NI    RXYBCTGO+1,X'F0'   KNOCK OUT THE INDEX--BRANCH
         NI    RXYINSFP+1,X'F0'   KNOCK OUT THE INDEX--FP
RXYXB1   EQU   *
         CLI   INSTXB+1,C'0'      IS BASE ZERO ?
         BNE   RXYXB2             EXIT IF NOT
         NI    RXYINST+2,X'0F'    KNOCK OUT THE BASE
RXYXB2   EQU   *
         CLI   INSTTYPE+1,C'P'    FLOATING POINT ?
         BE    RXYFP              EXIT IF IT IS
         LG    R10,=FL8'99'       SET RXY R1-FIELD
         LGR   R11,R10            SET RXY R1+1-FIELD
         CLI   INSTTYPE,C'B'      BRANCH INST ?
         BE    RXYBRNCH           EXIT IF IT IS
* SET S, TEST S (SINGLE BYTE)
         LA    R5,RXYBASES        SET RXY BASE
         LA    R6,1               SET RXY INDEX
         MVI   RXYINST+3,X'01'    SET RXY DISPLACEMENT
         CLI   INSTTYPE,C'S'      SINGLE BYTE INST ?
         BE    RXYSET             EXIT IF IT IS
* SET H, TEST H
         LA    R5,RXYBASEH        SET RXY BASE
         LA    R6,2               SET RXY INDEX
         MVI   RXYINST+3,X'02'    SET RXY DISPLACEMENT
         CLI   INSTTYPE,C'H'      HALFWORD INST ?
         BE    RXYSET             EXIT IF IT IS
* SET F, TEST F
         LA    R5,RXYBASEF        SET RXY BASE
         LA    R6,4               SET RXY INDEX
         MVI   RXYINST+3,X'04'    SET RXY DISPLACEMENT
         CLI   INSTTYPE,C'F'      FULLWORD INST ?
         BE    RXYSET             EXIT IF IT IS
* SET D, TEST D
         LA    R5,RXYBASED        SET RXY BASE
         LA    R6,8               SET RXY INDEX
         MVI   RXYINST+3,X'08'    SET RXY DISPLACEMENT
         CLI   INSTTYPE,C'D'      DOUBLEWORD INST ?
         BE    RXYSET             EXIT IF IT IS
* SET Q, TEST Q
         LA    R5,RXYBASEQ        SET RXY BASE
         LA    R6,16              SET RXY INDEX
         MVI   RXYINST+3,X'10'    SET RXY DISPLACEMENT
         CLI   INSTTYPE,C'Q'      QUADWORD INST ?
         BE    RXYSET             EXIT IF IT IS
* SET P8, TEST P8
         LA    R5,RXYBASP8        SET RX BASE
         LA    R6,8               SET RX INDEX
         MVI   RXYINST+3,X'08'    SET RX DISPLACEMENT
         CLC   INSTTYPE(2),=C'P8' PL8 INST ?
         BE    RXYSET             EXIT IF IT IS
* SET P16, TEST P16
         LA    R5,RXYBSP16        SET RX BASE
         LA    R6,16              SET RX INDEX
         MVI   RXYINST+3,X'10'    SET RX DISPLACEMENT
         CLC   INSTTYPE(3),=C'P16' PL16 INST ?
         BE    RXYSET             EXIT IF IT IS
         ABEND X'111'             ERROR IN TABLE
*
RXYSET   EQU   *
         CLI   INSTSTOR,C'S'      IS IT STORE TYPE ?
         BNE   RXYNTSTR           EXIT IF NOT
* STORE TYPE, USE SPECIAL AREA
         LA    R5,RXYSTORE        COMMON AREA FOR ALL STORE TYPES
RXYNTSTR EQU   *
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
*              R1  D X2 B2   E8A65008005A
RXYINST  AY    R10,8(R6,R5)       TESTED INST
         BCT   R7,RXYINST         DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
*
         B     RXYDONE            EXIT
*
RXYBRNCH EQU   *
* BRANCH TYPE
         CLC   6(2,R7),=X'E346'   IS IT BCTG ?
         BE    RXYBCTG            EXIT IF IT IS
         ABEND X'123'
*
RXYBCTG  EQU   *
* BCTG
         SR    R11,R11            R11=0  INDEX TO NON-BRANCH DATA
* TIME AN LG
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
RXYXTRA  LG    R10,RXYBCTD1(R11)  SPECIAL INST
         BCT   R7,RXYXTRA         DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
*
         LG    R7,TIMEAFT         END TIME
         SG    R7,TIMEBEF                  - BEG TIME
         SG    R7,TIMEBCT                  - BCT TIME
         STG   R7,TIMEXTRA        SAVE IT
*
         CLI   INSTTYPE+1,C'0'    NON-BRANCH TYPE ?
         BE    RXBCTGNB           EXIT IF IT IS
         LA    R11,8              R11=8  INDEX TO BRANCH DATA
RXBCTGNB EQU   *
         LA    R6,RXYBCTB2-RXYBCTB1 R6=BR TO RXYBCTB2 IF INDEXED
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
* CAN FORM THE FOLLOWING BRANCH TYPES:
*   BCTG  10,LABEL       BRANCH
*   BCTG  10,LABEL       NO BRANCH
*   BCTG  10,LABEL(R6)   BRANCH
*   BCTG  10,LABEL(R6)   NO BRANCH
RXYBCTEX LG    R10,RXYBCTD1(R11)  R10=1 FOR NO BRANCH, BIG FOR BRANCH
RXYBCTGO BCTG  R10,RXYBCTB1(R6)   TESTED INST
RXYBCTB1 BCT   R7,RXYBCTEX        DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
*
         B     RXYBCTDN           EXIT
* INDEXED BRANCH
RXYBCTB2 BCTG  R7,RXYBCTEX        DO IT (INDEXED)
         TIME  NS,TIMEAFT         AFTER TIME IN nS
*
RXYBCTDN EQU   *
         LA    R5,RXYBCTGO        INST ADDRESS
         B     RXYDONE2           EXIT
*
RXYFP    EQU   *
* FLOATING POINT
* LEY LDY STEY STDY
*
         MVC   RXYINSFP(1),6(R7)  SET THE INST
         MVC   RXYINSFP+5(1),7(R7) SET THE INST 2ND OPCODE
* SET EH, TEST EH
         LA    R5,RXYBASPE        SET RXY BASE
         LA    R6,4               SET RXY INDEX
         MVI   RXYINSFP+3,X'04'   SET RXY DISPLACEMENT
         CLI   INSTTYPE,C'E'      SHORT FP ?
         BE    RXYFPSET           EXIT IF IT IS
* SET DH, TEST DH
         LA    R5,RXYBASPD        SET RXY BASE
         LA    R6,8               SET RXY INDEX
         MVI   RXYINSFP+3,X'08'   SET RXY DISPLACEMENT
         CLI   INSTTYPE,C'D'      LONG FP ?
         BE    RXYFPSET           EXIT IF IT IS
         ABEND 864                INVALID PARMS
*
RXYFPSET EQU   *
         CLI   INSTSTOR,C'S'      IS IT STORE TYPE ?
         BNE   RXYFPGO            EXIT IF NOT
* STORE TYPE, USE SPECIAL AREA
         LD    F0,=DH'98'         SO I KNOW
         LA    R5,RXYSTORE        COMMON AREA FOR ALL STORE TYPES
*
RXYFPGO  EQU   *
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
*              R1 D X2 B2   EDA650080064
RXYINSFP LEY   F0,8(R6,R5)        TESTED INST
         BCT   R7,RXYINSFP        DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
*
         LA    R5,RXYINSFP        INST ADDRESS
         B     RXYDONE2           EXIT
*

RXYDONE  EQU   *
         LA    R5,RXYINST         INST ADDRESS
RXYDONE2 EQU   *
         LA    R4,6               INST LENGTH
         BAL   R14,OUTPUT         WRITE THE OUTPUT RECORD
         MVC   RXYINST+1(2),RXYSAVE RESTORE MODIFIED BYTES
         MVC   RXYBCTGO+1(2),RXYSAVB RESTORE MODIFIED BYTES--BRANCH
         MVC   RXYINSFP+1(2),RXYSAVP RESTORE MODIFIED BYTES--FP
         B     GET                EXIT TO GET ANOTHER TEST
*
RXYSAVE  DS    H
RXYSAVB  DS    H
RXYSAVP  DS    H
RXYBASES DC    X'10'              RXY BASE
         DC    X'80'              RXY BASE+DISP
         DC    X'99'              RXY BASE+INDEX+DISP
RXYBASEH DC    H'10'              RXY BASE
         DC    H'20'              RXY BASE+DISP
         DC    H'30'              RXY BASE+INDEX+DISP
RXYBASPE DC    EH'10'             RXY BASE
         DC    EH'20'             RXY BASE+DISP
         DC    EH'30'             RXY BASE+INDEX+DISP
RXYBASEF DC    F'10'              RXY BASE
         DC    F'20'              RXY BASE+DISP
         DC    F'30'              RXY BASE+INDEX+DISP
RXYBASED DC    FL8'10'            RXY BASE
         DC    FL8'20'            RXY BASE+DISP
         DC    FL8'30'            RXY BASE+INDEX+DISP
RXYBASPD DC    DH'10'             RXY BASE
         DC    DH'20'             RXY BASE+DISP
         DC    DH'30'             RXY BASE+INDEX+DISP
RXYBASEQ DC    2FL8'10'           RXY BASE
         DC    2FL8'20'           RXY BASE+DISP
         DC    2FL8'30'           RXY BASE+INDEX+DISP
RXYBASP8 DC    PL8'1000'          RXY BASE
         DC    PL8'2000'          RXY BASE+DISP
         DC    PL8'3000'          RXY BASE+INDEX+DISP
RXYBSP16 DC    PL16'1000'         RXY BASE
         DC    PL16'2000'         RXY BASE+DISP
         DC    PL16'3000'         RXY BASE+INDEX+DISP
RXYSTORE DS    CL48               RXY ALL STORE TYPES
RXYBCTD1 DC    FL8'1'             BCTG NON-BRANCH
         DC    X'7FFFFFFFFFFFFFFF' BCTG BRANCH
         LTORG
*
RSY      DS    0H
* DOUBLE BYTE OPCODE, 2ND BYTE AT END
* LMG STMG STMH STMY LMH LMY
* CLMH CLMY STCMH STCMY ICMH ICMY
* BXHG BXLEG
* SRAG SLAG SRLG SLLG RLLG RLL
* CSY
         USING *,R12
         BAL   R14,SRCHOPCD       SET UP OPCODE
* LMG STMG STMH STMY LMH LMY
         CLC   6(2,R7),=X'EB04'   IS IT LMG ?
         BE    RSYLMSTM           EXIT IF IT IS
         CLC   6(2,R7),=X'EB24'   IS IT STMG ?
         BE    RSYLMSTM           EXIT IF IT IS
         CLC   6(2,R7),=X'EB26'   IS IT STMH ?
         BE    RSYLMSTM           EXIT IF IT IS
         CLC   6(2,R7),=X'EB90'   IS IT STMY ?
         BE    RSYLMSTM           EXIT IF IT IS
         CLC   6(2,R7),=X'EB96'   IS IT LMH ?
         BE    RSYLMSTM           EXIT IF IT IS
         CLC   6(2,R7),=X'EB98'   IS IT LMY ?
         BE    RSYLMSTM           EXIT IF IT IS
* CSY CSG CDSY CDSG
         CLC   6(2,R7),=X'EB14'   IS IT CSY ?
         BE    RSYCS              EXIT IF IT IS
         CLC   6(2,R7),=X'EB30'   IS IT CSG ?
         BE    RSYCS              EXIT IF IT IS
         CLC   6(2,R7),=X'EB31'   IS IT CDSY ?
         BE    RSYCS              EXIT IF IT IS
         CLC   6(2,R7),=X'EB3E'   IS IT CDSG ?
         BE    RSYCS              EXIT IF IT IS
* CLMH CLMY STCMH STCMY ICMH ICMY
         CLC   6(2,R7),=X'EB20'   IS IT CLMH ?
         BE    RSYMASK            EXIT IF IT IS
         CLC   6(2,R7),=X'EB21'   IS IT CLMY ?
         BE    RSYMASK            EXIT IF IT IS
         CLC   6(2,R7),=X'EB2C'   IS IT STCMH ?
         BE    RSYMASK            EXIT IF IT IS
         CLC   6(2,R7),=X'EB2D'   IS IT STCMY ?
         BE    RSYMASK            EXIT IF IT IS
         CLC   6(2,R7),=X'EB80'   IS IT ICMH ?
         BE    RSYMASK            EXIT IF IT IS
         CLC   6(2,R7),=X'EB81'   IS IT ICMY ?
         BE    RSYMASK            EXIT IF IT IS
* BXHG BXLEG
         CLC   6(2,R7),=X'EB44'   IS IT BXHG ?
         BE    RSYBX              EXIT IF IT IS
         CLC   6(2,R7),=X'EB45'   IS IT BXLEG ?
         BE    RSYBX              EXIT IF IT IS
* SRAG SLAG SRLG SLLG RLLG RLL
         CLC   6(2,R7),=X'EB0A'   IS IT SHIFT ?
         BL    RSYABEND           ABEND IF NOT
         CLC   6(2,R7),=X'EB0D'   IS IT SHIFT ?
         BNH   RSYSHIFT           EXIT IF IT IS
         CLC   6(2,R7),=X'EB1C'   IS IT SHIFT (RLLG) ?
         BE    RSYSHIFT           EXIT IF IT IS
         CLC   6(2,R7),=X'EB1D'   IS IT SHIFT (RLL) ?
         BE    RSYSHIFT           EXIT IF IT IS
*
RSYABEND EQU   *
         ABEND X'222'
*
RSYLMSTM EQU   *
* LMG STMG STMH STMY LMH LMY
         MVC   RSYSAV1,RSYSTM     SAVE ORIG INST
         MVC   RSYSAV2,RSYSTMX    SAVE ORIG INST
         MVC   RSYSAV3,RSYXTRA    SAVE ORIG INST
         MVC   RSYSAV4,RSYLM      SAVE ORIG INST
         MVC   RSLTLL1,INSTLL1    MOVE LENGTH 1 TO OUTPUT
* PROCESS LENGTH 1
         ZAP   RSYDEC,=P'0'       CLEAR RSYDEC
         PACK  RSYDEC+14(2),INSTLL1-1(3) DECIMAL LENGTH 1
         CVBG  R10,RSYDEC         CONVERT TO HEX
         LTR   R10,R10            IS IT ZERO ?
         BZ    RSYERROR           EXIT IF IT IS
         C     R10,=F'16'         GT THAN 16 ?
         BH    RSYERROR           EXIT IF IT IS
         BCTR  R10,0              -1 FOR END REGISTER
         EX    R10,RSYOI1         PUT END REG INTO INST
         EX    R10,RSYOI2         PUT END REG INTO INST--EXTRA
         EX    R10,RSYOI3         PUT END REG INTO INST--STMY
         EX    R10,RSYOI4         PUT END REG INTO INST--LMY
         CLC   6(2,R7),=X'EB04'   IS IT LMG ?
         BE    RSYLMGO            EXIT IF IT IS
         CLC   6(2,R7),=X'EB96'   IS IT LMH ?
         BE    RSYLMGO            EXIT IF IT IS
         CLC   6(2,R7),=X'EB98'   IS IT LMY ?
         BE    RSYLMGO            EXIT IF IT IS
* ONLY STMG,STMH,STMY GETS HERE
         MVC   RSYSTM(1),6(R7)    SET THE INST
         MVC   RSYSTM+5(1),7(R7)  SET THE INST 2ND OPCODE
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
*              R1 R3 D(B)    EB0?BDDDDD??
RSYSTM   STMY  R0,R0,RSYOP1       TESTED INST
         BCT   R7,RSYSTM          DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
         LA    R5,RSYSTM          INST ADDRESS
         B     RSYDONE            EXIT
*
RSYLMGO  EQU   *
* ONLY LMG,LMH,LMY GETS HERE
* MAKE SURE THE RIGHT FLAVOUR OF STM MATCHES THE LM
         MVC   RSYLM(1),6(R7)     SET THE INST
         MVC   RSYLM+5(1),7(R7)   SET THE INST 2ND OPCODE
         MVC   RSYSTMX(1),6(R7)   SET THE INST
         MVC   RSYXTRA(1),6(R7)   SET THE INST
         MVI   RSYSTMX+5,X'24'    ASSUME STMG
         MVI   RSYXTRA+5,X'24'    ASSUME STMG
         CLI   7(R7),X'04'        LMG ?
         BE    RSYLMOK            EXIT IF IT IS
         MVI   RSYSTMX+5,X'26'    ASSUME STMH
         MVI   RSYXTRA+5,X'26'    ASSUME STMH
         CLI   7(R7),X'96'        LMH ?
         BE    RSYLMOK            EXIT IF IT IS
         MVI   RSYSTMX+5,X'90'    ASSUME STMY
         MVI   RSYXTRA+5,X'90'    ASSUME STMY
         CLI   7(R7),X'98'        LMY ?
         BE    RSYLMOK            EXIT IF IT IS
         ABEND X'111'
RSYLMOK  EQU   *
         L     R7,SPINCNT         SET LOOP LIMIT
*
* TIME THE STMG,STMH,STMY
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
*              R1 R3 D(B)    EB0?BDDDDD??
RSYSTMX  STMY  R0,R0,RSYOP1       SPECIAL INST
         BCT   R7,RSYSTMX         DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
*
         LG    R7,TIMEAFT         END TIME
         SG    R7,TIMEBEF                  - BEG TIME
         SG    R7,TIMEBCT                  - BCT TIME
         STG   R7,TIMEXTRA        SAVE IT
*
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
RSYXTRA  STMY  R0,R0,RSYOP1       EXTRA INST
*              R1 R3 D(B)    EB0?BDDDDD??
RSYLM    LMY   R0,R0,RSYOP1       TESTED INST
         BCT   R7,RSYXTRA         DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
*
         LA    R5,RSYLM           INST ADDRESS
         B     RSYDONE            EXIT
*
RSYERROR EQU   *
         MVC   RSLTMICS+1(12),=C'LENGTH ERROR'
         BAL   R14,OUTMSG         WRITE AND CLEAR
         B     GET                EXIT TO GET ANOTHER TEST
*
RSYOI1   OI    RSYSTM+1,X'00'     EXECUTED INST
RSYOI2   OI    RSYSTMX+1,X'00'    EXECUTED INST
RSYOI3   OI    RSYXTRA+1,X'00'    EXECUTED INST
RSYOI4   OI    RSYLM+1,X'00'      EXECUTED INST
RSYOP1   DS    16CL8
*
RSYCS    EQU   *
* CSY CSG CDSY CDSG
         MVC   RSYCSGO(1),6(R7)   SET THE INST--CS TYPE
         MVC   RSYCSGO+5(1),7(R7) SET THE INST 2ND OPCODE
         LMG   R4,R5,RSYCSDTQ     SET R3 REG/DOUBLE REG
         CLI   INSTTYPE,C'E'      R1=D2(B2) ?
         BNE   RSYCSNTQ           EXIT IF NOT
* R1=D2(B2)
         LA    R6,RSYCSDTQ+4      SET DATA FIELD (FULLWORD)
         CLC   6(2,R7),=X'EB14'   IS IT CSY ?
         BE    RSYCSSET           EXIT IF IT IS
         LA    R6,RSYCSYEQ        SET DATA FIELD (DOUBLEWORD-CDSY)
         CLC   6(2,R7),=X'EB31'   IS IT CDSY ?
         BE    RSYCSSET           EXIT IF IT IS
         LA    R6,RSYCSDTQ        SET DATA FIELD (DOUBLEWORD/QUADWORD)
         B     RSYCSSET           EXIT
RSYCSNTQ EQU   *
* R1<>D2(B2) (R3 NOT USED)
         LA    R6,RSYCSDNE+4      SET DATA FIELD (FULLWORD)
         CLC   6(2,R7),=X'EB14'   IS IT CSY ?
         BE    RSYCSSET           EXIT IF IT IS
         LA    R6,RSYCSYNE        SET DATA FIELD (DOUBLEWORD-CDSY)
         CLC   6(2,R7),=X'EB31'   IS IT CDSY ?
         BE    RSYCSSET           EXIT IF IT IS
         LA    R6,RSYCSDNE        SET DATA FIELD (DOUBLEWORD/QUADWORD)
RSYCSSET EQU   *
* TIME A 2-REG LMG
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
RSYCSLMG LMG   R10,R11,RSYCSDTQ   SPECIAL INST
         BCT   R7,RSYCSLMG        DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
*
         LG    R7,TIMEAFT         END TIME
         SG    R7,TIMEBEF                  - BEG TIME
         SG    R7,TIMEBCT                  - BCT TIME
         STG   R7,TIMEXTRA        SAVE IT
*
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
RSYCSLM2 LMG   R10,R11,RSYCSDTQ   SPECIAL INST
RSYCSGO  CSY   R10,R4,0(R6)       TESTED INST
         BCT   R7,RSYCSLM2        DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
*
         LA    R5,RSYCSGO         INST ADDRESS
         B     RSYDONE            EXIT
RSYCSDTQ DC    FL8'1',FL8'1'
RSYCSDNE DC    FL8'2',FL8'2'
RSYCSYEQ DC    F'1',F'1'
RSYCSYNE DC    F'2',F'2'
*
RSYMASK  EQU   *
* CLMH CLMY STCMH STCMY ICMH ICMY
         MVC   RSYINMSK(1),6(R7)  SET THE INST--MASK TYPE
         MVC   RSYINMSK+5(1),7(R7) SET THE INST 2ND OPCODE
         LG    R10,=8X'FF'        GIVE IT SOMETHING TO DO
         MVC   RSYICM,=X'0123'    SHOWS ICM WORKING
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
RSYINMSK CLMH  R10,9,RSYICM       TESTED INST
         BCT   R7,RSYINMSK        DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
*
         LA    R5,RSYINMSK        INST ADDRESS
         B     RSYDONE            EXIT
RSYICM   DS    H
*
RSYBX    EQU   *
* BXHG OR BXLEG
         MVC   RSYINBX(1),6(R7)   SET THE INST
         MVC   RSYINBX+5(1),7(R7) SET THE INST 2ND OPCODE
         CLI   RSYINBX+5,X'44'    BXHG ?
         BNE   RSYBXLE            EXIT IF NOT
* BXHG
         LG    R10,RSYBXHGO       LOAD INDEX REGISTER--ALWAYS BRANCH
         CLI   INSTTYPE+1,C'0'    NOP REQUESTED ?
         BNE   RSYBXGO            EXIT IF NOT
         LG    R10,RSYBXHNP       LOAD INDEX REGISTER--NEVER BRANCH
         B     RSYBXGO            EXIT
RSYBXLE  EQU   *
* BXLEG
         LG    R10,RSYBXLGO       LOAD INDEX REGISTER--ALWAYS BRANCH
         CLI   INSTTYPE+1,C'0'    NOP REQUESTED ?
         BNE   RSYBXGO            EXIT IF NOT
         LG    R10,RSYBXLNP       LOAD INDEX REGISTER--NEVER BRANCH
*
RSYBXGO  EQU   *
         LAY   R11,1              SET INCREMENT AND COMPARE
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
RSYINBX  BXHG  R10,R11,RSYBXBCT   TESTED INST
RSYBXBCT BCT   R7,RSYINBX         DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
*
         LA    R5,RSYINBX         INST ADDRESS
         B     RSYDONE            EXIT
RSYBXHGO DC    FL8'1'             BXHG AND BRANCH
RSYBXHNP DC    X'80',7X'00'       BXHG AND NO BRANCH
RSYBXLGO DC    X'80',7X'00'       BXLEG AND BRANCH
RSYBXLNP DC    FL8'1'             BXLEG AND NO BRANCH
*
RSYSHIFT EQU   *
* SRAG SLAG SRLG SLLG RLLG RLL
         MVC   RSYSAV5,RSYINSFT+1 SAVE MODIFIED BYTES
         MVC   RSYINSFT(1),6(R7)  SET THE INST--SHIFT
         MVC   RSYINSFT+5(1),7(R7) SET THE INST 2ND OPCODE
         MVC   RSLTLEN(2),INSTXB  MOVE THE XB TO OUTPUT
         CLI   INSTXB+1,C'0'      IS BASE ZERO ?
         BNE   RSYXB1             EXIT IF NOT
         NI    RSYINSFT+2,X'0F'   KNOCK OUT THE BASE
RSYXB1   EQU   *
         LG    R11,=X'8000000001234567' GIVE IT SOMETHING TO SHIFT
         LA    R6,1               WITH BASE SHIFT 2, ELSE 1
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
RSYINSFT SRAG  R10,R11,1(R6)      TESTED INST
         BCT   R7,RSYINSFT        DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
*
         LA    R5,RSYINSFT        INST ADDRESS
         B     RSYDONE            EXIT
*
RSYDONE  EQU   *
         LA    R4,6               INST LENGTH
         BAL   R14,OUTPUT         WRITE THE OUTPUT RECORD
         MVC   RSYSTM(2),RSYSAV1  RESTORE ORIG INST
         MVC   RSYSTMX(2),RSYSAV2 RESTORE ORIG INST
         MVC   RSYXTRA(2),RSYSAV3 RESTORE ORIG INST
         MVC   RSYLM(2),RSYSAV4   RESTORE ORIG INST
         MVC   RSYINSFT+1(2),RSYSAV5 RESTORE MODIFIED BYTES
         B     GET                EXIT TO GET ANOTHER TEST
*
RSYSAV1  DS    H
RSYSAV2  DS    H
RSYSAV3  DS    H
RSYSAV4  DS    H
RSYSAV5  DS    H
RSYDEC   DC    PL16'0'
         LTORG
*
SIY      DS    0H
* DOUBLE BYTE OPCODE, 2ND BYTE AT END
* TMY MVIY NIY CLIY OIY XIY
         USING *,R12
         BAL   R14,SRCHOPCD       SET UP OPCODE
         MVC   SIYINST(1),6(R7)   SET THE INST
         MVC   SIYINST+5(1),7(R7) SET THE INST 2ND OPCODE
         LA    R6,SIYBYTE         DOESN'T MATTER WHAT WE DO TO THIS
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
SIYINST  TMY   0(R6),X'99'        TESTED INST
         BCT   R7,SIYINST         DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
*
         LA    R4,6               INST LENGTH
         LA    R5,SIYINST         INST ADDRESS
         BAL   R14,OUTPUT         WRITE THE OUTPUT RECORD
         B     GET                EXIT TO GET ANOTHER TEST
SIYBYTE  DC    X'00'              SIY BYTE
         LTORG
*
RSI      DS    0H
* SINGLE BYTE OPCODE
* BRXH=JXH BRXLE=JXLE
         USING *,R12
         BAL   R14,SRCHOPCD       SET UP OPCODE
         MVC   RSIINBX(1),6(R7)   SET THE INST
         CLI   RSIINBX,BRXH       BRXH ?
         BNE   RSIBXLE            EXIT IF NOT
* BRXH
         L     R10,RSIBXHGO       LOAD INDEX REGISTER--ALWAYS BRANCH
         CLI   INSTTYPE+1,C'0'    NOP REQUESTED ?
         BNE   RSIBXGO            EXIT IF NOT
         L     R10,RSIBXHNP       LOAD INDEX REGISTER--NEVER BRANCH
         B     RSIBXGO            EXIT
RSIBXLE  EQU   *
* BRXLE
         L     R10,RSIBXLEG       LOAD INDEX REGISTER--ALWAYS BRANCH
         CLI   INSTTYPE+1,C'0'    NOP REQUESTED ?
         BNE   RSIBXGO            EXIT IF NOT
         L     R10,RSIBXLEN       LOAD INDEX REGISTER--NEVER BRANCH
*
RSIBXGO  EQU   *
         LA    R11,1              SET INCREMENT AND COMPARE
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
RSIINBX  BRXH  R10,R11,RSIBXBCT   TESTED INST
RSIBXBCT BCT   R7,RSIINBX         DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
*
         LA    R4,4               INST LENGTH
         LA    R5,RSIINBX         INST ADDRESS
         BAL   R14,OUTPUT         WRITE THE OUTPUT RECORD
         B     GET                EXIT TO GET ANOTHER TEST
RSIBXHGO DC    F'1'               BRXH AND BRANCH
RSIBXHNP DC    X'80000000'        BRXH AND NO BRANCH
RSIBXLEG DC    X'80000000'        BRXLE AND BRANCH
RSIBXLEN DC    F'1'               BRXLE AND NO BRANCH
         LTORG
*
RSL      DS    0H
* DOUBLE BYTE OPCODE, 2ND BYTE AT END
* TP
         USING *,R12
         BAL   R14,SRCHOPCD       SET UP OPCODE
         MVC   RSLTLEN,INSTLL     MOVE LENGTH TO OUTPUT
         ZAP   RSLDEC,=P'0'       CLEAR RSLDEC
         PACK  RSLDEC+12(4),INSTLL-1(7) DECIMAL LENGTH
         CVBG  R4,RSLDEC          CONVERT TO HEX
* VALIDATE LEN
         LTR   R4,R4              IS IT ZERO ?
         BZ    RSLERROR           ERROR IF IT IS
         C     R4,=F'16'          GT THAN 16 ?
         BH    RSLERROR           ERROR IF IT IS
         BCTR  R4,0               -1 FOR INST LENGTH
         SLL   R4,4               POSITION LENGTH
         STC   R4,RSLINST+1       INSERT LENGTH
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
RSLINST  TP    RSLDEC(0)          TESTED INST
         BCT   R7,RSLINST         DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
*
         LA    R4,6               INST LENGTH
         LA    R5,RSLINST         INST ADDRESS
         BAL   R14,OUTPUT         WRITE THE OUTPUT RECORD
         B     GET                EXIT TO GET ANOTHER TEST
*
RSLERROR EQU   *
         MVC   RSLTMICS+1(12),=C'LENGTH ERROR'
         BAL   R14,OUTMSG         WRITE AND CLEAR
         B     GET                EXIT TO GET ANOTHER TEST
*
RSLDEC   DC    PL16'0'
         LTORG
*
SVC      DS    0H
* FUNCTION TIMING
         USING *,R12,R9
         LR    R9,R12             SET
         AHI   R9,4096                BASE 2
         MVC   RSLTOPCD(8),INSTOPCD MOVE OPCODE TO OUTPUT
*
         CLC   INSTOPCD(5),=C'XLATE' IS IT XLATE ?
         BE    SVCXL              EXIT IF IT IS
         CLC   INSTOPCD(4),=C'TIME' IS IT TIME ?
         BE    SVCTM              EXIT IF IT IS
         CLC   INSTOPCD(3),=C'CTD' IS IT CTD ?
         BE    SVCCTD             EXIT IF IT IS
         CLC   INSTOPCD(3),=C'CFD' IS IT CFD ?
         BE    SVCCFD             EXIT IF IT IS
         CLC   INSTOPCD(7),=C'GETMAIN' IS IT GETMAIN ?
         BE    SVCGETMN           EXIT IF IT IS
         CLC   INSTOPCD(8),=C'FREEMAIN' IS IT FREEMAIN ?
         BE    SVCFREMN           EXIT IF IT IS
         CLC   INSTOPCD(6),=C'GETENV' IS IT GETENV ?
         BE    SVCGETEV           EXIT IF IT IS
         B     SVCBADOP           ERROR IF NOT
*
SVCXL    EQU   *
* PROCESS XLATE
         MVC   RSLTTYPE,INSTTYPE  MOVE TYPE TO OUTPUT
         MVC   RSLTLEN,INSTLL     MOVE LENGTH TO OUTPUT
         ZAP   SVCDEC,=P'0'       CLEAR SVCDEC
         PACK  SVCDEC+12(4),INSTLL-1(7) DECIMAL LENGTH
         CVBG  R11,SVCDEC         CONVERT TO HEX
* VALIDATE LEN
         LTR   R11,R11            IS IT ZERO ?
         BZ    SVCERROR           ERROR IF IT IS
         GETMAIN R,LV=(R11),LOC=24 XLATE AREA
         ST    R1,SVCSAVE         SAVE FOR FREEMAIN
         LR    R10,R1             SAVE FOR XLATE
         CLI   INSTTYPE,C'A'      ASCII CONVERT ?
         BE    SVCXLATA           EXIT IF IT IS
         CLI   INSTTYPE,C'E'      EBCDIC CONVERT ?
         BE    SVCXLATE           EXIT IF IT IS
         B     SVCERRTP           ERROR IF NEITHER
*
SVCXLATA EQU   *
* XLATE TO=A
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
SVCXLAT1 XLATE (R10),(R11),TO=A   TESTED FUNCTION
         BCT   R7,SVCXLAT1        DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
         LA    R5,SVCXLAT1+8      SVC ADDRESS
         B     SVCDONEX           EXIT
*
SVCXLATE EQU   *
* XLATE TO=E
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
SVCXLAT2 XLATE (R10),(R11),TO=E   TESTED FUNCTION
         BCT   R7,SVCXLAT2        DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
         LA    R5,SVCXLAT2+4      SVC ADDRESS
*
SVCDONEX EQU   *
         FREEMAIN LV=(R11),A=SVCSAVE FREE XLATE AREA
         B     SVCDONE            EXIT
*
SVCTM    EQU   *
* PROCESS TIME
         MVC   RSLTTYPE(11),INSTTYPE MOVE EXTENDED TYPE TO OUTPUT
         LA    R4,SVCTMTAB
SVCLOOP1 EQU   *
         CLC   0(11,R4),INSTTYPE  MATCH TYPE ?
         BNE   SVCBUMP1           EXIT IF NOT MATCHED
         L     R4,11(R4)          LOAD PROCESSING ROUTINE
         BR    R4                 GO TO IT
SVCBUMP1 EQU   *
         LA    R4,15(R4)          BUMP POINTER
         CLI   0(R4),X'FF'        END OF TABLE ?
         BE    SVCERRTP           ERROR IF IT IS
         B     SVCLOOP1           LOOP THROUGH TABLE
*
SVCTMDEC EQU   *
* TIME DEC,LINKAGE=SVC
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
SVCTIM01 TIME  DEC                TESTED FUNCTION
         BCT   R7,SVCTIM01        DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
         LA    R5,SVCTIM01+4      SVC ADDRESS
         B     SVCDONE            EXIT
*
SVCTMBIN EQU   *
* TIME BIN,LINKAGE=SVC
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
SVCTIM02 TIME  BIN                TESTED FUNCTION
         BCT   R7,SVCTIM02        DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
         LA    R5,SVCTIM02+4      SVC ADDRESS
         B     SVCDONE            EXIT
*
SVCTMTU  EQU   *
* TIME TU,LINKAGE=SVC
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
SVCTIM03 TIME  TU                 TESTED FUNCTION
         BCT   R7,SVCTIM03        DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
         LA    R5,SVCTIM03+4      SVC ADDRESS
         B     SVCDONE            EXIT
*
SVCTMMIC EQU   *
* TIME MIC,LABEL,LINKAGE=SVC
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
SVCTIM04 TIME  MIC,SVCTIME        TESTED FUNCTION
         BCT   R7,SVCTIM04        DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
         LA    R5,SVCTIM04+8      SVC ADDRESS
         B     SVCDONE2           EXIT
*
SVCTMNS  EQU   *
* TIME NS,LABEL,LINKAGE=SVC
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
SVCTIM05 TIME  NS,SVCTIME         TESTED FUNCTION
         BCT   R7,SVCTIM05        DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
         LA    R5,SVCTIM05+8      SVC ADDRESS
         B     SVCDONE2           EXIT
*
SVCTMSTK EQU   *
* TIME STCK,LABEL,LINKAGE=SVC
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
SVCTIM06 TIME  STCK,SVCTIME       TESTED FUNCTION
         BCT   R7,SVCTIM06        DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
         LA    R5,SVCTIM06+8      SVC ADDRESS
         B     SVCDONE2           EXIT
*
SVCTMST  EQU   *
* TIME TS,LABEL,LINKAGE=SVC
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
SVCTIM27 TIME  TS,SVCTIME         TESTED FUNCTION
         BCT   R7,SVCTIM27        DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
         LA    R5,SVCTIM27+8      SVC ADDRESS
         B     SVCDONE2           EXIT
*
SVCTMCK  EQU   *
* TIME CLOCK,LABEL,CLOCKTYPE=STCK
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
SVCTIM07 TIME  CLOCK,SVCTIME,CLOCKTYPE=STCK TESTED FUNCTION
         BCT   R7,SVCTIM07        DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
         LA    R5,SVCTIM07+8      SVC ADDRESS
         B     SVCDONE2           EXIT
*
SVCTMCKE EQU   *
* TIME CLOCK,LABEL,CLOCKTYPE=STCKE
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
SVCTIM08 TIME  CLOCK,SVCTIME,CLOCKTYPE=STCKE TESTED FUNCTION
         BCT   R7,SVCTIM08        DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
         LA    R5,SVCTIM08+8      SVC ADDRESS
         B     SVCDONE2           EXIT
*
SVCTMCJV EQU   *
* TIME CLOCK,LABEL,CLOCKTYPE=JAVA
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
SVCTIM09 TIME  CLOCK,SVCTIME,CLOCKTYPE=JAVA TESTED FUNCTION
         BCT   R7,SVCTIM09        DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
         LA    R5,SVCTIM09+8      SVC ADDRESS
         B     SVCDONE2           EXIT
*
SVCTMDYD EQU   *
* TIME DEC,LABEL,DATETYPE=YYYYDDD,LINKAGE=SYSTEM
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
SVCTIM10 TIME  DEC,SVCTIME,DATETYPE=YYYYDDD,LINKAGE=SYSTEM
         BCT   R7,SVCTIM10        DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
         LA    R5,SVCTIM10+12     SVC ADDRESS
         B     SVCDONE2           EXIT
*
SVCTMDMD EQU   *
* TIME DEC,LABEL,DATETYPE=MMDDYYYY,LINKAGE=SYSTEM
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
SVCTIM11 TIME  DEC,SVCTIME,DATETYPE=MMDDYYYY,LINKAGE=SYSTEM
         BCT   R7,SVCTIM11        DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
         LA    R5,SVCTIM11+12     SVC ADDRESS
         B     SVCDONE2           EXIT
*
SVCTMDDM EQU   *
* TIME DEC,LABEL,DATETYPE=DDMMYYYY,LINKAGE=SYSTEM
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
SVCTIM12 TIME  DEC,SVCTIME,DATETYPE=DDMMYYYY,LINKAGE=SYSTEM
         BCT   R7,SVCTIM12        DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
         LA    R5,SVCTIM12+12     SVC ADDRESS
         B     SVCDONE2           EXIT
*
SVCTMDYM EQU   *
* TIME DEC,LABEL,DATETYPE=YYYYMMDD,LINKAGE=SYSTEM
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
SVCTIM13 TIME  DEC,SVCTIME,DATETYPE=YYYYMMDD,LINKAGE=SYSTEM
         BCT   R7,SVCTIM13        DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
         LA    R5,SVCTIM13+12     SVC ADDRESS
         B     SVCDONE2           EXIT
*
SVCTMBYD EQU   *
* TIME BIN,LABEL,DATETYPE=YYYYDDD,LINKAGE=SYSTEM
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
SVCTIM14 TIME  BIN,SVCTIME,DATETYPE=YYYYDDD,LINKAGE=SYSTEM
         BCT   R7,SVCTIM14        DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
         LA    R5,SVCTIM14+12     SVC ADDRESS
         B     SVCDONE2           EXIT
*
SVCTMBMD EQU   *
* TIME BIN,LABEL,DATETYPE=MMDDYYYY,LINKAGE=SYSTEM
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
SVCTIM15 TIME  BIN,SVCTIME,DATETYPE=MMDDYYYY,LINKAGE=SYSTEM
         BCT   R7,SVCTIM15        DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
         LA    R5,SVCTIM15+12     SVC ADDRESS
         B     SVCDONE2           EXIT
*
SVCTMBDM EQU   *
* TIME BIN,LABEL,DATETYPE=DDMMYYYY,LINKAGE=SYSTEM
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
SVCTIM16 TIME  BIN,SVCTIME,DATETYPE=DDMMYYYY,LINKAGE=SYSTEM
         BCT   R7,SVCTIM16        DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
         LA    R5,SVCTIM16+12     SVC ADDRESS
         B     SVCDONE2           EXIT
*
SVCTMBYM EQU   *
* TIME BIN,LABEL,DATETYPE=YYYYMMDD,LINKAGE=SYSTEM
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
SVCTIM17 TIME  BIN,SVCTIME,DATETYPE=YYYYMMDD,LINKAGE=SYSTEM
         BCT   R7,SVCTIM17        DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
         LA    R5,SVCTIM17+12     SVC ADDRESS
         B     SVCDONE2           EXIT
*
SVCTMMYD EQU   *
* TIME MIC,LABEL,DATETYPE=YYYYDDD,LINKAGE=SYSTEM
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
SVCTIM18 TIME  MIC,SVCTIME,DATETYPE=YYYYDDD,LINKAGE=SYSTEM
         BCT   R7,SVCTIM18        DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
         LA    R5,SVCTIM18+12     SVC ADDRESS
         B     SVCDONE2           EXIT
*
SVCTMMMD EQU   *
* TIME MIC,LABEL,DATETYPE=MMDDYYYY,LINKAGE=SYSTEM
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
SVCTIM19 TIME  MIC,SVCTIME,DATETYPE=MMDDYYYY,LINKAGE=SYSTEM
         BCT   R7,SVCTIM19        DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
         LA    R5,SVCTIM19+12     SVC ADDRESS
         B     SVCDONE2           EXIT
*
SVCTMMDM EQU   *
* TIME MIC,LABEL,DATETYPE=DDMMYYYY,LINKAGE=SYSTEM
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
SVCTIM20 TIME  MIC,SVCTIME,DATETYPE=DDMMYYYY,LINKAGE=SYSTEM
         BCT   R7,SVCTIM20        DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
         LA    R5,SVCTIM20+12     SVC ADDRESS
         B     SVCDONE2           EXIT
*
SVCTMMYM EQU   *
* TIME MIC,LABEL,DATETYPE=YYYYMMDD,LINKAGE=SYSTEM
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
SVCTIM21 TIME  MIC,SVCTIME,DATETYPE=YYYYMMDD,LINKAGE=SYSTEM
         BCT   R7,SVCTIM21        DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
         LA    R5,SVCTIM21+12     SVC ADDRESS
         B     SVCDONE2           EXIT
*
SVCTMSYD EQU   *
* TIME STCK,LABEL,DATETYPE=YYYYDDD,LINKAGE=SYSTEM
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
SVCTIM22 TIME  STCK,SVCTIME,DATETYPE=YYYYDDD,LINKAGE=SYSTEM
         BCT   R7,SVCTIM22        DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
         LA    R5,SVCTIM22+12     SVC ADDRESS
         B     SVCDONE2           EXIT
*
SVCTMSMD EQU   *
* TIME STCK,LABEL,DATETYPE=MMDDYYYY,LINKAGE=SYSTEM
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
SVCTIM23 TIME  STCK,SVCTIME,DATETYPE=MMDDYYYY,LINKAGE=SYSTEM
         BCT   R7,SVCTIM23        DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
         LA    R5,SVCTIM23+12     SVC ADDRESS
         B     SVCDONE2           EXIT
*
SVCTMSDM EQU   *
* TIME STCK,LABEL,DATETYPE=DDMMYYYY,LINKAGE=SYSTEM
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
SVCTIM24 TIME  STCK,SVCTIME,DATETYPE=DDMMYYYY,LINKAGE=SYSTEM
         BCT   R7,SVCTIM24        DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
         LA    R5,SVCTIM24+12     SVC ADDRESS
         B     SVCDONE2           EXIT
*
SVCTMSYM EQU   *
* TIME STCK,LABEL,DATETYPE=YYYYMMDD,LINKAGE=SYSTEM
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
SVCTIM25 TIME  STCK,SVCTIME,DATETYPE=YYYYMMDD,LINKAGE=SYSTEM
         BCT   R7,SVCTIM25        DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
         LA    R5,SVCTIM25+12     SVC ADDRESS
         B     SVCDONE2           EXIT
*
SVCTMSKE EQU   *
* TIME STCKE,LABEL,LINKAGE=SYSTEM
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
SVCTIM26 TIME  STCKE,SVCTIME,LINKAGE=SYSTEM
         BCT   R7,SVCTIM26        DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
         LA    R5,SVCTIM26+8      SVC ADDRESS
         B     SVCDONE2           EXIT
*
SVCCTD   EQU   *
* PROCESS CTD
         MVC   RSLTTYPE(11),INSTTYPE MOVE EXTENDED TYPE TO OUTPUT
         LA    R4,SVCCTTAB
SVCLOOP2 EQU   *
         CLC   0(11,R4),INSTTYPE  MATCH TYPE ?
         BNE   SVCBUMP2           EXIT IF NOT MATCHED
         L     R4,11(R4)          LOAD PROCESSING ROUTINE
         BR    R4                 GO TO IT
SVCBUMP2 EQU   *
         LA    R4,15(R4)          BUMP POINTER
         CLI   0(R4),X'FF'        END OF TABLE ?
         BE    SVCERRTP           ERROR IF IT IS
         B     SVCLOOP2           LOOP THROUGH TABLE
*
SVCCTINT EQU   *
* CTD CTD_INT128,IN==FL16'1234567890123456',OUT=LABEL,LINKAGE=SVC
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
SVCCTD01 EQU   *
         CTD   CTD_INT128,IN==FL16'1234567890123456',OUT=SVCCONV
         BCT   R7,SVCCTD01        DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
         LA    R5,SVCCTD01+12     SVC ADDRESS
         B     SVCDONE3           EXIT
*
SVCCTEH  EQU   *
* CTD CTD_EH,IN==EH'1.4142135',OUT=LABEL,LINKAGE=SVC
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
SVCCTD02 EQU   *
         CTD   CTD_EH,IN==EH'1.4142135',OUT=SVCCONV
         BCT   R7,SVCCTD02        DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
         LA    R5,SVCCTD02+12     SVC ADDRESS
         B     SVCDONE3           EXIT
*
SVCCTEB  EQU   *
* CTD CTD_EB,IN==EB'1.4142135',OUT=LABEL,LINKAGE=SVC
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
SVCCTD03 EQU   *
         CTD   CTD_EB,IN==EB'1.4142135',OUT=SVCCONV
         BCT   R7,SVCCTD03        DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
         LA    R5,SVCCTD03+12     SVC ADDRESS
         B     SVCDONE3           EXIT
*
SVCCTED  EQU   *
* CTD CTD_ED,IN==ED'1.732051',OUT=LABEL,LINKAGE=SVC
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
SVCCTD08 EQU   *
         CTD   CTD_ED,IN==ED'1.732051',OUT=SVCCONV
         BCT   R7,SVCCTD08        DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
         LA    R5,SVCCTD08+12     SVC ADDRESS
         B     SVCDONE3           EXIT
*
SVCCTDH  EQU   *
* CTD CTD_DH,IN==DH'1.4142135',OUT=LABEL,LINKAGE=SVC
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
SVCCTD04 EQU   *
         CTD   CTD_DH,IN==DH'1.4142135',OUT=SVCCONV
         BCT   R7,SVCCTD04        DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
         LA    R5,SVCCTD04+12     SVC ADDRESS
         B     SVCDONE3           EXIT
*
SVCCTDB  EQU   *
* CTD CTD_DB,IN==DB'1.4142135',OUT=LABEL,LINKAGE=SVC
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
SVCCTD05 EQU   *
         CTD   CTD_DB,IN==DB'1.4142135',OUT=SVCCONV
         BCT   R7,SVCCTD05        DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
         LA    R5,SVCCTD05+12     SVC ADDRESS
         B     SVCDONE3           EXIT
*
SVCCTDD  EQU   *
* CTD CTD_DD,IN==DD'1.732050808',OUT=LABEL,LINKAGE=SVC
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
SVCCTD09 EQU   *
         CTD   CTD_DD,IN==DD'1.732050808',OUT=SVCCONV
         BCT   R7,SVCCTD09        DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
         LA    R5,SVCCTD09+12     SVC ADDRESS
         B     SVCDONE3           EXIT
*
SVCCTLH  EQU   *
* CTD CTD_LH,IN==LH'1.4142135',OUT=LABEL,LINKAGE=SVC
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
SVCCTD06 EQU   *
         CTD   CTD_LH,IN==LH'1.4142135',OUT=SVCCONV
         BCT   R7,SVCCTD06        DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
         LA    R5,SVCCTD06+12     SVC ADDRESS
         B     SVCDONE3           EXIT
*
SVCCTLB  EQU   *
* CTD CTD_LB,IN==LB'1.4142135',OUT=LABEL,LINKAGE=SVC
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
SVCCTD07 EQU   *
         CTD   CTD_LB,IN==LB'1.4142135',OUT=SVCCONV
         BCT   R7,SVCCTD07        DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
         LA    R5,SVCCTD07+12     SVC ADDRESS
         B     SVCDONE3           EXIT
*
SVCCTLD  EQU   *
* CTD CTD_LD,IN==LD'1.732050808',OUT=LABEL,LINKAGE=SVC
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
SVCCTD10 EQU   *
         CTD   CTD_LD,IN==LD'1.732050808',OUT=SVCCONV
         BCT   R7,SVCCTD10        DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
         LA    R5,SVCCTD10+12     SVC ADDRESS
         B     SVCDONE3           EXIT
*
SVCCFD   EQU   *
* PROCESS CFD
         MVC   RSLTTYPE(11),INSTTYPE MOVE EXTENDED TYPE TO OUTPUT
         LA    R4,SVCCFTAB
SVCLOOP3 EQU   *
         CLC   0(11,R4),INSTTYPE  MATCH TYPE ?
         BNE   SVCBUMP3           EXIT IF NOT MATCHED
         L     R4,11(R4)          LOAD PROCESSING ROUTINE
         BR    R4                 GO TO IT
SVCBUMP3 EQU   *
         LA    R4,15(R4)          BUMP POINTER
         CLI   0(R4),X'FF'        END OF TABLE ?
         BE    SVCERRTP           ERROR IF IT IS
         B     SVCLOOP3           LOOP THROUGH TABLE
*
SVCCFINT EQU   *
* CFD CFD_INT128,IN=INPUT,OUT=OUTPUT,LINKAGE=SVC
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
SVCCFD01 EQU   *
         CFD   CFD_INT128,IN=SVCCONV3,OUT=SVCCONV
         BCT   R7,SVCCFD01        DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
         LA    R5,SVCCFD01+4      SVC ADDRESS
         B     SVCDONE3           EXIT
*
SVCCFEH  EQU   *
* CFD CFD_EH,IN=INPUT,OUT=OUTPUT,LINKAGE=SVC
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
SVCCFD02 EQU   *
         CFD   CFD_EH,IN=SVCCONV2,OUT=SVCCONV
         BCT   R7,SVCCFD02        DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
         LA    R5,SVCCFD02+4      SVC ADDRESS
         B     SVCDONE3           EXIT
*
SVCCFEB  EQU   *
* CFD CFD_EB,IN=INPUT,OUT=OUTPUT,LINKAGE=SVC
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
SVCCFD03 EQU   *
         CFD   CFD_EB,IN=SVCCONV2,OUT=SVCCONV
         BCT   R7,SVCCFD03        DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
         LA    R5,SVCCFD03+4      SVC ADDRESS
         B     SVCDONE3           EXIT
*
SVCCFED  EQU   *
* CFD CFD_ED,IN=INPUT,OUT=OUTPUT,LINKAGE=SVC
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
SVCCFD08 EQU   *
         CFD   CFD_ED,IN=SVCCONV2,OUT=SVCCONV
         BCT   R7,SVCCFD08        DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
         LA    R5,SVCCFD08+4      SVC ADDRESS
         B     SVCDONE3           EXIT
*
SVCCFDH  EQU   *
* CFD CFD_DH,IN=INPUT,OUT=OUTPUT,LINKAGE=SVC
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
SVCCFD04 EQU   *
         CFD   CFD_DH,IN=SVCCONV2,OUT=SVCCONV
         BCT   R7,SVCCFD04        DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
         LA    R5,SVCCFD04+4      SVC ADDRESS
         B     SVCDONE3           EXIT
*
SVCCFDB  EQU   *
* CFD CFD_DB,IN=INPUT,OUT=OUTPUT,LINKAGE=SVC
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
SVCCFD05 EQU   *
         CFD   CFD_DB,IN=SVCCONV2,OUT=SVCCONV
         BCT   R7,SVCCFD05        DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
         LA    R5,SVCCFD05+4      SVC ADDRESS
         B     SVCDONE3           EXIT
*
SVCCFDD  EQU   *
* CFD CFD_DD,IN=INPUT,OUT=OUTPUT,LINKAGE=SVC
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
SVCCFD09 EQU   *
         CFD   CFD_DD,IN=SVCCONV2,OUT=SVCCONV
         BCT   R7,SVCCFD09        DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
         LA    R5,SVCCFD09+4      SVC ADDRESS
         B     SVCDONE3           EXIT
*
SVCCFLH  EQU   *
* CFD CFD_LH,IN=INPUT,OUT=OUTPUT,LINKAGE=SVC
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
SVCCFD06 EQU   *
         CFD   CFD_LH,IN=SVCCONV2,OUT=SVCCONV
         BCT   R7,SVCCFD06        DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
         LA    R5,SVCCFD06+4      SVC ADDRESS
         B     SVCDONE3           EXIT
*
SVCCFLB  EQU   *
* CFD CFD_LB,IN=INPUT,OUT=OUTPUT,LINKAGE=SVC
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
SVCCFD07 EQU   *
         CFD   CFD_LB,IN=SVCCONV2,OUT=SVCCONV
         BCT   R7,SVCCFD07        DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
         LA    R5,SVCCFD07+4      SVC ADDRESS
         B     SVCDONE3           EXIT
*
SVCCFLD  EQU   *
* CFD CFD_LD,IN=INPUT,OUT=OUTPUT,LINKAGE=SVC
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
SVCCFD10 EQU   *
         CFD   CFD_LD,IN=SVCCONV2,OUT=SVCCONV
         BCT   R7,SVCCFD10        DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
         LA    R5,SVCCFD10+4      SVC ADDRESS
         B     SVCDONE3           EXIT
*
SVCGETMN EQU   *
         MVC   RSLTTYPE+1(2),INSTTYPE MOVE TYPE TO OUTPUT
* GETMAIN ENOUGH STORAGE TO HOLD SPIN NUMBER OF 4-BYTE ADDRESSES
         L     R0,SPINCNT         CURRENT SPIN COUNT
         SLL   R0,2               * 4
         ST    R0,SVCGETLN        SAVE LENGTH
         GETMAIN R,LV=(0)         FREEMAIN ADDRESSES
         ST    R1,SVCGETAD        SAVE ADDRESS LIST
         LR    R4,R1              INDEX TO ADDRESS LIST
* TIME AN LA AND ST
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
SVCGETLA EQU   *
         LA    R5,1(R5)           SPECIAL INST
         ST    R5,SVCSAVE         SPECIAL INST
         BCT   R7,SVCGETLA        DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
*
         LG    R7,TIMEAFT         END TIME
         SG    R7,TIMEBEF                  - BEG TIME
         SG    R7,TIMEBCT                  - BCT TIME
         STG   R7,TIMEXTRA        SAVE IT
*
         CLC   INSTTYPE(2),=C'R ' GETMAIN R ?
         BE    SVCGETR            EXIT IF IT IS
         CLC   INSTTYPE(2),=C'RC' GETMAIN RC ?
         BE    SVCGETRC           EXIT IF IT IS
         CLC   INSTTYPE(2),=C'RU' GETMAIN RU ?
         BE    SVCGETRU           EXIT IF IT IS
         B     SVCERRTP           ERROR IF NOT
*
SVCGETR  EQU   *
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
SVCGETM1 EQU   *
         GETMAIN R,LV=10          GET 10 BYTES
         ST    R1,0(R4)           SAVE ADDRESS FOR FREEMAIN
         LA    R4,4(R4)           BUMP POINTER
         BCT   R7,SVCGETM1        DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
*
* NOW FREEMAIN ALL THE GETMAINS, UNTIMED
         L     R4,SVCGETAD        FREEMAIN POINTERS
         L     R7,SPINCNT         SET LOOP LIMIT
SVCGETF1 EQU   *
         FREEMAIN LV=10,A=0(R4)   FREE ALL THE GETS
         LA    R4,4(R4)           BUMP POINTER
         BCT   R7,SVCGETF1        DO IT
         FREEMAIN LA=SVCGETLN,A=SVCGETAD
         LA    R5,SVCGETM1+8      SVC ADDRESS
         B     SVCDONE            EXIT
*
SVCGETRC EQU   *
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
SVCGETM2 EQU   *
         GETMAIN RC,LV=10         GET 10 BYTES
         ST    R1,0(R4)           SAVE ADDRESS FOR FREEMAIN
         LA    R4,4(R4)           BUMP POINTER
         BCT   R7,SVCGETM2        DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
*
* NOW FREEMAIN ALL THE GETMAINS, UNTIMED
         L     R4,SVCGETAD        FREEMAIN POINTERS
         L     R7,SPINCNT         SET LOOP LIMIT
SVCGETF2 EQU   *
         FREEMAIN LV=10,A=0(R4)   FREE ALL THE GETS
         LA    R4,4(R4)           BUMP POINTER
         BCT   R7,SVCGETF2        DO IT
         FREEMAIN LA=SVCGETLN,A=SVCGETAD
         LA    R5,SVCGETM2+8      SVC ADDRESS
         B     SVCDONE            EXIT
*
SVCGETRU EQU   *
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
SVCGETM3 EQU   *
         GETMAIN RU,LV=10         GET 10 BYTES
         ST    R1,0(R4)           SAVE ADDRESS FOR FREEMAIN
         LA    R4,4(R4)           BUMP POINTER
         BCT   R7,SVCGETM3        DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
*
* NOW FREEMAIN ALL THE GETMAINS, UNTIMED
         L     R4,SVCGETAD        FREEMAIN POINTERS
         L     R7,SPINCNT         SET LOOP LIMIT
SVCGETF3 EQU   *
         FREEMAIN LV=10,A=0(R4)   FREE ALL THE GETS
         LA    R4,4(R4)           BUMP POINTER
         BCT   R7,SVCGETF3        DO IT
         FREEMAIN LA=SVCGETLN,A=SVCGETAD
         LA    R5,SVCGETM3+8      SVC ADDRESS
         B     SVCDONE            EXIT
*
SVCFREMN EQU   *
* GETMAIN ENOUGH STORAGE TO HOLD SPIN NUMBER OF 4-BYTE ADDRESSES
         L     R0,SPINCNT         CURRENT SPIN COUNT
         SLL   R0,2               * 4
         ST    R0,SVCGETLN        SAVE LENGTH
         GETMAIN R,LV=(0)         FREEMAIN ADDRESSES
         ST    R1,SVCGETAD        SAVE ADDRESS LIST
         LR    R4,R1              INDEX TO ADDRESS LIST
* TIME AN LA
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
SVCFRELA EQU   *
         LA    R5,1(R5)           SPECIAL INST
         BCT   R7,SVCFRELA        DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
*
         LG    R7,TIMEAFT         END TIME
         SG    R7,TIMEBEF                  - BEG TIME
         SG    R7,TIMEBCT                  - BCT TIME
         STG   R7,TIMEXTRA        SAVE IT
*
         L     R7,SPINCNT         SET LOOP LIMIT
* GETMAINS, UNTIMED
SVCFREG  EQU   *
         GETMAIN R,LV=10          GET 10 BYTES
         ST    R1,0(R4)           SAVE ADDRESS FOR FREEMAIN
         LA    R4,4(R4)           BUMP POINTER
         BCT   R7,SVCFREG         DO IT
* NOW FREEMAIN ALL THE GETMAINS, TIMED
         L     R4,SVCGETAD        FREEMAIN POINTERS
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
SVCFREE  EQU   *
         FREEMAIN LV=10,A=0(R4)   FREE ALL THE GETS
         LA    R4,4(R4)           BUMP POINTER
         BCT   R7,SVCFREE         DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
*
         FREEMAIN LA=SVCGETLN,A=SVCGETAD
         LA    R5,SVCFREE+8       SVC ADDRESS
         B     SVCDONE            EXIT
*
SVCGETEV EQU   *
* GETMAIN ENOUGH STORAGE TO HOLD SPIN NUMBER OF 4-BYTE ADDRESSES
         L     R0,SPINCNT         CURRENT SPIN COUNT
         SLL   R0,2               * 4
         ST    R0,SVCGEVLN        SAVE LENGTH
         GETMAIN R,LV=(0)         FREEMAIN ADDRESSES
         ST    R1,SVCGEVAD        SAVE ADDRESS LIST
         LR    R4,R1              INDEX TO ADDRESS LIST
* TIME AN LA AND ST
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
SVCGEVLA EQU   *
         LA    R5,1(R5)           SPECIAL INST
         ST    R5,SVCSAVE         SPECIAL INST
         BCT   R7,SVCGEVLA        DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
*
         LG    R7,TIMEAFT         END TIME
         SG    R7,TIMEBEF                  - BEG TIME
         SG    R7,TIMEBCT                  - BCT TIME
         STG   R7,TIMEXTRA        SAVE IT
*
         L     R7,SPINCNT         SET LOOP LIMIT
*
         TIME  NS,TIMEBEF         BEFORE TIME IN nS
SVCGEVM1 EQU   *
         GETENV SVCGEV            GET ENVIRONMENT VARIABLE
         ST    R2,0(R4)           SAVE ADDRESS FOR FREEMAIN
         LA    R4,4(R4)           BUMP POINTER
         BCT   R7,SVCGEVM1        DO IT
         TIME  NS,TIMEAFT         AFTER TIME IN nS
*
* NOW FREEMAIN ALL THE GETMAINS, UNTIMED
         L     R4,SVCGEVAD        FREEMAIN POINTERS
         L     R7,SPINCNT         SET LOOP LIMIT
SVCGEVF1 EQU   *
         FREEMAIN LV=16,A=0(R4)   FREE ALL THE GETS
         LA    R4,4(R4)           BUMP POINTER
         BCT   R7,SVCGEVF1        DO IT
         FREEMAIN LA=SVCGEVLN,A=SVCGEVAD
         LA    R5,SVCGEVM1+8      SVC ADDRESS
         B     SVCDONE            EXIT
*
SVCDONE3 EQU   *
         LMG   R10,R11,SVCCONV    FOR DISPLAY
         B     SVCDONE            EXIT
SVCDONE2 EQU   *
         LMG   R10,R11,SVCTIME    FOR DISPLAY
SVCDONE  EQU   *
         LA    R4,2               INST LENGTH
         BAL   R14,OUTPUT         WRITE THE OUTPUT RECORD
         B     GET                EXIT TO GET ANOTHER TEST
*
SVCERRTP EQU   *
         MVC   RSLTMICS+1(10),=C'TYPE ERROR'
         BAL   R14,OUTMSG         WRITE AND CLEAR
         B     GET                EXIT TO GET ANOTHER TEST
*
SVCBADOP EQU   *
         MVC   RSLTMICS(14),=C'FUNCTION ERROR'
         BAL   R14,OUTMSG         WRITE AND CLEAR
         B     GET                EXIT TO GET ANOTHER TEST
*
SVCERROR EQU   *
         MVC   RSLTMICS+1(12),=C'LENGTH ERROR'
         BAL   R14,OUTMSG         WRITE AND CLEAR
         B     GET                EXIT TO GET ANOTHER TEST
*
SVCGETAD DS    A
SVCGETLN DS    F
SVCGEVAD DS    A
SVCGEVLN DS    F
SVCDEC   DC    PL16'0'
SVCGEV   DC    C'GETENV',X'00'
SVCTIME  DS    CL29
SVCSAVE  DS    CL4
SVCCONV  DS    CL45
SVCCONV2 DC    CL45'12.345E5'
SVCCONV3 DC    CL45'1234567890123456'
* TIME MACRO TABLE
SVCTMTAB EQU   *
* 1) LINKAGE=SVC
         DC    CL11'DEC/SVC',AL4(SVCTMDEC)     DEC
         DC    CL11'BIN/SVC',AL4(SVCTMBIN)     BIN
         DC    CL11'TU/SVC',AL4(SVCTMTU)       TU
         DC    CL11'MIC/SVC',AL4(SVCTMMIC)     MIC
         DC    CL11'NS/SVC',AL4(SVCTMNS)       NS
         DC    CL11'STCK/SVC',AL4(SVCTMSTK)    STCK
         DC    CL11'TS/SVC',AL4(SVCTMST)       TS
         DC    CL11'CLOCK/STCK',AL4(SVCTMCK)   CLOCK,LABEL,STCK
         DC    CL11'CLOCK/STCKE',AL4(SVCTMCKE) CLOCK,LABEL,STCKE
         DC    CL11'CLOCK/JAVA',AL4(SVCTMCJV)  CLOCK,LABEL,JAVA
* 2) LINKAGE=SYSTEM
         DC    CL11'DEC/SYS/YD',AL4(SVCTMDYD)  DEC,LABEL,YYYYDDD
         DC    CL11'DEC/SYS/MDY',AL4(SVCTMDMD) DEC,LABEL,MMDDYYYY
         DC    CL11'DEC/SYS/DMY',AL4(SVCTMDDM) DEC,LABEL,DDMMYYYY
         DC    CL11'DEC/SYS/YMD',AL4(SVCTMDYM) DEC,LABEL,YYYYMMDD
         DC    CL11'BIN/SYS/YD',AL4(SVCTMBYD)  BIN,LABEL,YYYYDDD
         DC    CL11'BIN/SYS/MDY',AL4(SVCTMBMD) BIN,LABEL,MMDDYYYY
         DC    CL11'BIN/SYS/DMY',AL4(SVCTMBDM) BIN,LABEL,DDMMYYYY
         DC    CL11'BIN/SYS/YMD',AL4(SVCTMBYM) BIN,LABEL,YYYYMMDD
         DC    CL11'MIC/SYS/YD',AL4(SVCTMMYD)  MIC,LABEL,YYYYDDD
         DC    CL11'MIC/SYS/MDY',AL4(SVCTMMMD) MIC,LABEL,MMDDYYYY
         DC    CL11'MIC/SYS/DMY',AL4(SVCTMMDM) MIC,LABEL,DDMMYYYY
         DC    CL11'MIC/SYS/YMD',AL4(SVCTMMYM) MIC,LABEL,YYYYMMDD
         DC    CL11'STK/SYS/YD',AL4(SVCTMSYD)  STCK,LABEL,YYYYDDD
         DC    CL11'STK/SYS/MDY',AL4(SVCTMSMD) STCK,LABEL,MMDDYYYY
         DC    CL11'STK/SYS/DMY',AL4(SVCTMSDM) STCK,LABEL,DDMMYYYY
         DC    CL11'STK/SYS/YMD',AL4(SVCTMSYM) STCK,LABEL,YYYYMMDD
         DC    CL11'STCKE/SYS',AL4(SVCTMSKE)   STCKE,LABEL
         DC    X'FF'                           STOPPER
*
* CTD MACRO TABLE
SVCCTTAB EQU   *
         DC    CL11'INT',AL4(SVCCTINT) INT128
         DC    CL11'EH',AL4(SVCCTEH)   SHORT HFP
         DC    CL11'EB',AL4(SVCCTEB)   SHORT BFP
         DC    CL11'ED',AL4(SVCCTED)   SHORT DFP
         DC    CL11'DH',AL4(SVCCTDH)   LONG HFP
         DC    CL11'DB',AL4(SVCCTDB)   LONG BFP
         DC    CL11'DD',AL4(SVCCTDD)   LONG DFP
         DC    CL11'LH',AL4(SVCCTLH)   EXTENDED HFP
         DC    CL11'LB',AL4(SVCCTLB)   EXTENDED BFP
         DC    CL11'LD',AL4(SVCCTLD)   EXTENDED DFP
         DC    X'FF'                   STOPPER
*
* CFD MACRO TABLE
SVCCFTAB EQU   *
         DC    CL11'INT',AL4(SVCCFINT) INT128
         DC    CL11'EH',AL4(SVCCFEH)   SHORT HFP
         DC    CL11'EB',AL4(SVCCFEB)   SHORT BFP
         DC    CL11'ED',AL4(SVCCFED)   SHORT DFP
         DC    CL11'DH',AL4(SVCCFDH)   LONG HFP
         DC    CL11'DB',AL4(SVCCFDB)   LONG BFP
         DC    CL11'DD',AL4(SVCCFDD)   LONG DFP
         DC    CL11'LH',AL4(SVCCFLH)   EXTENDED HFP
         DC    CL11'LB',AL4(SVCCFLB)   EXTENDED BFP
         DC    CL11'LD',AL4(SVCCFLD)   EXTENDED DFP
         DC    X'FF'                   STOPPER
*
         LTORG
*
******************************
* END OF PROCESSING ROUTINES *
******************************
* ### ESTAE ROUTINE
*INSTABND DS    0H
         BALR  R12,0
         USING *,R12
         MVC   RSLTMICS+1(7),=C'ABENDED'
         BAL   R14,OUTMSG         WRITE AND CLEAR
         J     GET                EXIT TO GET ANOTHER TEST
**********
* DSECTS *
**********
INSTDSCT DSECT
INSTFMAT DS    CL4                FORMAT
* FOR FORMATS SUPPORTED, SEE TYPETABL
         DS    CL2
INSTOPCD DS    CL6                OPCODE
         DS    CL2
INSTTYPE DS    CL3
*                                 B    BRANCH
*                                 B0   BRANCH AS NOP
*                                      FOR BALR/BCTR/BASR R2=0
*                                      FOR BXH/BXLE/BCT BRANCH OR NOT
*                                 B00/BR0/B0R/BRR FOR BAKR
*                                 S    SINGLE BYTE
*                                 H    HALFWORD
*                                 F    FULLWORD
*                                 D    DOUBLEWORD  (64 BIT BINARY)
*                                 Q    QUADWORD   (128 BIT BINARY)
*                                 P    VARIABLE LENGTH DECIMAL
*                                 P8   8-BYTE DECIMAL
*                                 P16  16-BYTE DECIMAL
*                                 R0   NO 'OR' REG FOR EXECUTE
*                                 E    CS-OPCODES, EQUAL
*                                 N    CS-OPCODES, UNEQUAL
*
*                                 .P   FLOATING POINT
*                                 E    SHORT
*                                 D    LONG
*                                 X    EXTENDED
*                                 ..H  HFP
*                                 ..B  BFP
*                                 ..T  DFP
*                           EG.   DPH  LONG HFP
         DS    CL2
INSTLL   DS    CL6                LENGTH
         DS    C
INSTSL   DS    CL3                STRING LENGTH
         ORG   INSTLL
INSTLL1  DS    CL2                LENGTH 1
         DS    CL2
INSTLL2  DS    CL2                LENGTH 2
         ORG   INSTLL
INSTXB   DS    CL2                XB FIELDS
*                                 00  NO INDEX, NO BASE
*                                 0B     INDEX, NO BASE
*                                 X0  NO INDEX,    BASE
*                                 XB     INDEX,    BASE
INSTSTOR DS    C                  S   STORE OR FORMAT TYPE
*                                 F   FP DISPLAY WITH DEC POS
         DS    CL7
*
TYPEDSCT DSECT
TYPEFMAT DS    CL4                TABLE FORMAT
TYPEPROC DS    AL4                TABLE PROCESSING ROUTINE
*
RSLTDSCT DSECT
RSLTTITL DS    0CL8               HEADER
RSLTOPCD DS    CL6                OPCODE
         DS    C
RSLTTYPE DS    CL3                TYPE
         DS    C
RSLTLEN  DS    CL6                LENGTH
         DS    C
RSLTSL   DS    CL3                STRING LENGTH
         ORG   RSLTLEN
RSLTLL1  DS    CL2                LENGTH 1
RSLTSTOR DS    C                  STORE TYPE
         DS    C
RSLTLL2  DS    CL2                LENGTH 2
         DS    CL4
RSLTMICS DS    CL13               00000.000000 uS
         DS    C
RSLTOPER DS    CL12               RESULT INST
         DS    C
RSLTR10  DS    CL8                RESULT R10 (32)
         DS    C
RSLTR11  DS    CL8                RESULT R11 (32)
         DS    CL18
         ORG   RSLTR10
RSLTR10G DS    CL16               RESULT R10 (64)
         DS    C
RSLTR11G DS    CL16               RESULT R11 (64)
         DS    CL2
         ORG   RSLTR10
RSLTPAK1 DS    CL8                RESULT PACKED PART 1
         DS    C
RSLTPAK2 DS    CL8                RESULT PACKED PART 2
         DS    C
RSLTPAK3 DS    CL8                RESULT PACKED PART 3
         DS    C
RSLTPAK4 DS    CL8                RESULT PACKED PART 4
*
         DCBD
* OPCODE EQUATES
SPM      EQU   X'04'
BALR     EQU   X'05'
BCTR     EQU   X'06'
BCR      EQU   X'07'
BSM      EQU   X'0B'
BASSM    EQU   X'0C'
BASR     EQU   X'0D'
MVCL     EQU   X'0E'
CLCL     EQU   X'0F'
MXR      EQU   X'26'
MXDR     EQU   X'27'
MDR      EQU   X'2C'
DDR      EQU   X'2D'
MDER     EQU   X'3C'
MER      EQU   X'3C'
DER      EQU   X'3D'
EX       EQU   X'44'
BAL      EQU   X'45'
BCT      EQU   X'46'
BC       EQU   X'47'
BAS      EQU   X'4D'
CVB      EQU   X'4F'
D        EQU   X'5D'
MXD      EQU   X'67'
MD       EQU   X'6C'
DD       EQU   X'6D'
MDE      EQU   X'7C'
ME       EQU   X'7C'
DE       EQU   X'7D'
BRXH     EQU   X'84'
BXH      EQU   X'86'
BXLE     EQU   X'87'
SRL      EQU   X'88'
SLDA     EQU   X'8F'
STM      EQU   X'90'
LM       EQU   X'98'
MVCLE    EQU   X'A8'
CLCLE    EQU   X'A9'
CS       EQU   X'BA'
CDS      EQU   X'BB'
CLM      EQU   X'BD'
ICM      EQU   X'BF'
TRTR     EQU   X'D0'
TRT      EQU   X'DD'
ED       EQU   X'DE'
EDMK     EQU   X'DF'
PKA      EQU   X'E9'
UNPKA    EQU   X'EA'
LMD      EQU   X'EF'
SRP      EQU   X'F0'
DP       EQU   X'FD'
* GENERAL REGISTER EQUATES
R0       EQU   0
R1       EQU   1
R2       EQU   2                  FORMAT TABLE
R3       EQU   3                  PARMS BASE
R4       EQU   4                  ) INSTRUCTION
R5       EQU   5                  )             USE
R6       EQU   6                  )
R7       EQU   7                  WORK AND SPIN COUNT
R8       EQU   8                  RESULT BASE
R9       EQU   9                  USED IN LINREG/BASE 2 FOR ROUTINES
R10      EQU   10                 ) EVEN/ODD
R11      EQU   11                 )          REG USE
R12      EQU   12                 BASE 2
R13      EQU   13                 BASE 1
R14      EQU   14                 LINK REG
R15      EQU   15                 RETURN CODE
*
         EQUREGS REGS=FPR
         END
