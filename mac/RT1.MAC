         MACRO
.*********************************************************************
.* z390 - Mainframe assembler emulator and run-time engine
.* Copyright (C) 2021 z390 Assembler LLC
.*
.* This file is part of z390.
.*
.* z390 is free software; you can redistribute it and/or modify
.* it under the terms of the GNU General Public License as published by
.* the Free Software Foundation; either version 2 of the License, or
.* (at your option) any later version.
.* z390 is distributed in the hope that it will be useful,
.* but WITHOUT ANY WARRANTY; without even the implied warranty of
.* MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
.* GNU General Public License for more details.
.*
.* You should have received a copy of the GNU General Public License 
.* along with this program; if not, see https://www.gnu.org/licenses.
.*********************************************************************
.* MAINTENANCE 
.* 09/30/05 INITIAL CODING BY DON HIGGINS
.* 10/04/05 ADD SUPPORT FOR ASCII MODE ED MASK WITH X'A0' VS X'20'
.* 11/02/05 REMOVE ASCII MODE ED MASK WITH X'A0' NO LONGER REQUIRED
.* 11/20/05 CHANGE LA 1,X'101', SVC 13 TO ABEND 101                  
.* 09/22/06 ADD CCLE AND CCGE OPTIONS FOR USE IN TESTTIM2 TESTS        
.* 03/05/08 ADD RT1 OK - INC COUNT BY 1                                
.* 04/19/08 RPI 833 add SETC quotes for HLASM compatibility
.* 08/17/24 #540 Add parameter to select either WTO or file output
.*               Add option to request a user abend nnnn
.*********************************************************************
&NAME    RT1   &CMD,&CLC1,&CLC2,&LL,&TYPE=WTO,&ABEND=101                 #540
.*
.* REGRESSION TEST V1 COMMAND MACROS
.* 
.* 1.  RT1 MAIN  - START PGM AND DISPLAY STARTUP WTO
.*                 &TYPE=FILE selects output to file                     #540
.* 2,  RT1 START - START OF RT1 TEST MACROS FOLLOWING DATA
.* 3.  RT1 CCE   - TEST FOR PSW CC EQUAL (OPTIONAL CLC PARMS)
.* 4.  RT1 CCL   - TEST FOR PSW CC LOW   (OPTIONAL CLC PARMS)
.* 5.  RT1 CCH   - TEST FOR PSW CC HIGH  (OPTIONAL CLC PARMS)
.* 6.  RT1 CCGE  - TEST FOR PSW CC HIGH  (OPTIONAL CLC PARMS) RPI 461
.* 7.  RT1 CCLE  - TEST FOR PSW CC HIGH  (OPTIONAL CLC PARMS) RPI 461
.* 8.  RT1 CC?   - TEST FOR PSW CC = CC0-CC3     
.* 9.  RT1 OK    - INC TEST COUNT BY 1                                    
.*10.  RT1 ABORT - ABORT WITH TOTAL TESTS TO FAILURE AND USER 101 DUMP   #540
.*11.  RT1 END   - DISPLAY TOTAL TESTS AND ENDED OK WTO                  #540
.*12   RT1 ABEND - Abend with specified abend code                       #540
.*                 &ABEND=nnnn specifies desired user abend code         #540
.*
         GBLA  &RT1_TOT
         GBLC  &RT1NAME
         GBLC  &RT1TYPE                WTO / FILE                        #540
         AIF   ('&CMD' EQ 'MAIN').RT1MAIN
         AIF   ('&CMD' EQ 'START').RT1START
         AIF   ('&CMD' EQ 'END').RT1END
&NAME    DS    0H
&RT1_TOT SETA  &RT1_TOT+1
         AIF   ('&CMD' EQ 'OK').RT1OK
         AIF   ('&CMD' EQ 'ABEND').RT1ABEND                              #540
         AIF   ('&CMD' EQ 'ABORT').RT1ABORT
         AIF   ('&CLC1' EQ '').NOCLC
         AIF   ('&LL'   EQ '').NOCLCLL
         LA    1,&CLC1
         LA    2,&CLC2
         CLC   0(&LL,1),0(2)
         AGO   .NOCLC
.NOCLCLL ANOP
         CLC   &CLC1,&CLC2
.NOCLC   ANOP
         AIF   ('&CMD' EQ 'CCE').RT1CCE
         AIF   ('&CMD' EQ 'CCL').RT1CCL
         AIF   ('&CMD' EQ 'CCH').RT1CCH
         AIF   ('&CMD' EQ 'CCLE').RT1CCLE
         AIF   ('&CMD' EQ 'CCGE').RT1CCGE
         AIF   ('&CMD' EQ 'CC0').RT1CC0
         AIF   ('&CMD' EQ 'CC1').RT1CC1
         AIF   ('&CMD' EQ 'CC2').RT1CC2
         AIF   ('&CMD' EQ 'CC3').RT1CC3
         MNOTE 8,'RT1 INVALID COMMAND - &CMD'
         MEXIT
.*                                                                       #540
.RT1MAIN ANOP
         AIF   ('&TYPE' EQ 'WTO').TYPE_OK                                #540
         AIF   ('&TYPE' EQ 'FILE').TYPE_OK                               #540
         MNOTE 8,'RT1 MAIN,TYPE=&TYPE - Invalid value for Type: should * #540
               be WTO or FILE'                                           #540
         MEXIT ,                                                         #540
.TYPE_OK ANOP  ,                                                         #540
&RT1TYPE SETC  '&TYPE'                 Save for subsequent invocations   #540
.*                                                                       #540
.* Define inner macro to route output to either WTO or file              #540
         MACRO ,                                                         #540
         RT1SAY &MSG                                                     #540
         GBLC  &RT1TYPE                WTO / FILE                        #540
.*                                                                       #540
         AIF   ('&RT1TYPE' EQ 'FILE').DO_FILE                            #540
         AIF   ('&RT1TYPE' EQ 'WTO').DO_WTO                              #540
         MNOTE 8,'RT1 - internal logic error - TYPE=&RT1TYPE is not sup* #540
               ported'                                                   #540
         MEXIT ,                                                         #540
.DO_FILE ANOP  ,                                                         #540
         PUT   RT1OUT,&MSG             Write message to file             #540
         MEXIT ,                                                         #540
.DO_WTO  ANOP  ,                                                         #540
         WTO   MF=(E,&MSG)             Write message to log              #540
         MEND  ,                                                         #540
.*                                                                       #540
&RT1NAME SETC  '&NAME'
&RT1NAME SUBENTRY
         AIF   ('&RT1TYPE' NE 'FILE').NO_OPEN                            #540
         OPEN  (RT1OUT,(OUTPUT))       Open output file                  #540
.NO_OPEN ANOP  ,                                                         #540
         RT1SAY RT1_WTO0               Write start message               #540
         BRCL    15,RT1_START
.*                                                                       #540
RT1_ABORT DS 0H
         MVI   RT1_ABORTED,C'T'
         RT1SAY RT1_WTO9               Write abort message               #540
.*                                                                       #540
RT1_END  DS    0H
         MVC   RT1_DTOT,=X'402020202020'
         ED    RT1_DTOT,RT1_TOT
         RT1SAY RT1_WTO1               Write test count                  #540
         CLI   RT1_ABORTED,C'T'
         BE    RT1_DUMP
         RT1SAY RT1_WTO8               Write ended okay message          #540
         AIF   ('&RT1TYPE' NE 'FILE').NO_CLOS                            #540
         CLOSE (RT1OUT)                Close output file                 #540
.NO_CLOS ANOP  ,                                                         #540
         SUBEXIT
RT1_DUMP DS    0H
         AIF   ('&RT1TYPE' NE 'FILE').NO_CLSE                            #540
         CLOSE (RT1OUT)                Close output file                 #540
.NO_CLSE ANOP  ,                                                         #540
         ABEND 101     
RT1_LINK EQU   11
RT1_DATA EQU   *
RT1_TOT  DC    PL3'0' TOTAL RT1 TESTS COMPLETED
RT1_ONE  DC    P'1'
RT1_WTO0 DC    AL2(RT1_WTO0_END-*,0),C'&RT1NAME RT1 STARTED'             #540
RT1_WTO0_END EQU *                                                       #540
RT1_WTO1 DC    AL2(RT1_WTO_END-*,0),C'&RT1NAME RT1 TESTS COMPLETED = '
RT1_DTOT DC    CL6' ZZZZZ'
RT1_WTO_END EQU *         
RT1_WTO8 DC    AL2(RT1_WTO8_END-*,0),C'&RT1NAME RT1 ENDED OK'            #540
RT1_WTO8_END EQU *                                                       #540
RT1_WTO9 DC    AL2(RT1_WTO9_END-*,0),C'&RT1NAME RT1 TESTING ABORTED'     #540
RT1_WTO9_END EQU *                                                       #540
RT1_ABORTED DC C'F'
         AIF   ('&RT1TYPE' NE 'FILE').NO_DCB                             #540
RT1OUT   DCB   DSORG=PS,MACRF=PM,DDNAME=RT1OUT,LRECL=160,RECFM=VT        #540
.NO_DCB  ANOP  ,                                                         #540
         LTORG
RT1_TEST_CC0  DS    0H
         BNE   RT1_ABORT
RT1_OK   DS    0H
         AP    RT1_TOT,RT1_ONE
         BR    RT1_LINK
RT1_TEST_CC1  DS    0H
         BNL   RT1_ABORT
         AP    RT1_TOT,RT1_ONE
         BR    RT1_LINK
RT1_TEST_CC2  DS    0H
         BNH   RT1_ABORT
         AP    RT1_TOT,RT1_ONE
         BR    RT1_LINK
RT1_TEST_CC3  DS    0H
         BNO   RT1_ABORT
         AP    RT1_TOT,RT1_ONE
         BR    RT1_LINK
RT1_TEST_CCGE  DS    0H
         BL    RT1_ABORT
         AP    RT1_TOT,RT1_ONE
         BR    RT1_LINK
RT1_TEST_CCLE  DS    0H
         BH    RT1_ABORT
         AP    RT1_TOT,RT1_ONE
         BR    RT1_LINK
         MEXIT
.RT1START ANOP
RT1_START DS  0H
         MEXIT
.*                                                                       #540
.RT1CCE  ANOP
.RT1CC0  ANOP
RT1_TEST&RT1_TOT BAS   RT1_LINK,RT1_TEST_CC0
         USING *,RT1_LINK
         B     RT1_CCE&RT1_TOT
         LTORG
RT1_CCE&RT1_TOT DS 0H
         MEXIT
.*                                                                       #540
.RT1CCL  ANOP
.RT1CC1  ANOP
RT1_TEST&RT1_TOT BAS   RT1_LINK,RT1_TEST_CC1
         USING *,RT1_LINK
         B     RT1_CCL&RT1_TOT
         LTORG
RT1_CCL&RT1_TOT DS 0H
         MEXIT
.*                                                                       #540
.RT1CCH  ANOP
.RT1CC2  ANOP
RT1_TEST&RT1_TOT BAS   RT1_LINK,RT1_TEST_CC2
         USING *,RT1_LINK
         B     RT1_CCH&RT1_TOT
         LTORG
RT1_CCH&RT1_TOT DS 0H
         MEXIT
.*                                                                       #540
.RT1CC3  ANOP
RT1_TEST&RT1_TOT BAS   RT1_LINK,RT1_TEST_CC3
         USING *,RT1_LINK
         B     RT1_CC3&RT1_TOT
         LTORG
RT1_CC3&RT1_TOT DS 0H
         MEXIT
.*                                                                       #540
.RT1CCLE ANOP
RT1_TEST&RT1_TOT BAS   RT1_LINK,RT1_TEST_CCLE
         USING *,RT1_LINK
         B     RT1_CCLE&RT1_TOT
         LTORG
RT1_CCLE&RT1_TOT DS 0H
         MEXIT
.*                                                                       #540
.RT1CCGE ANOP
RT1_TEST&RT1_TOT BAS   RT1_LINK,RT1_TEST_CCGE
         USING *,RT1_LINK
         B     RT1_CCGE&RT1_TOT
         LTORG
RT1_CCGE&RT1_TOT DS 0H
         MEXIT
.*                                                                       #540
.RT1OK   ANOP
         BAS   RT1_LINK,RT1_OK
         USING *,RT1_LINK
         B     RT1_OK&RT1_TOT
         LTORG
RT1_OK&RT1_TOT DS 0H
         MEXIT
.*                                                                       #540
.RT1ABEND ANOP  ,                                                        #540
         AIF   ('&RT1TYPE' NE 'FILE').NO_CLS                             #540
         CLOSE (RT1OUT)                Close output file                 #540
.NO_CLS  ANOP  ,                                                         #540
         LA    6,&ABEND                Pick up abend code                #540
         ABEND (6)                                                       #540
         MEXIT  ,                                                        #540
.*                                                                       #540
.RT1ABORT ANOP
         B      RT1_ABORT
         MEXIT
.*                                                                       #540
.RT1END  ANOP
         B      RT1_END
         MEXIT
         MEND
