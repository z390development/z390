         MACRO
.********************************************************************
.*             AUTHOR.   DON HIGGINS.
.*             DATE.     09/23/08.
.*             REMARKS.  ADD SPM MACROS FOR CASENTRY, CASE, ENDCASE
.********************************************************************
.* 09/23/08 RPI 909 upgrade to use ZSTRMAC SPE's and extend compat.
.********************************************************************
         CASENTRY &REG
         COPY  ZSTRGBL
.*       AIF   (&ZSTR_LVL EQ &ZSTR_MAX_LVL)
           AIF   (NOT(&ZSTR_LVL EQ &ZSTR_MAX_LVL)).AIF_1_1
               MNOTE 8,'CASENTRY - LEVEL OVERFLOW'
               MEXIT
.*       AEND
.AIF_1_1   ANOP
&ZSTR_LVL SETA &ZSTR_LVL+1
&ZSTR_LVL_TYPE(&ZSTR_LVL) SETC 'CASENTRY'
&ZSTR_CASE_CNT SETA &ZSTR_CASE_CNT+1
&ZSTR_LVL_TCNT(&ZSTR_LVL) SETA &ZSTR_CASE_CNT
&ZSTR_LVL_BCNT(&ZSTR_LVL) SETA 0 RESET BLK CTR
&ZSTR_LVL_ELSE(&ZSTR_LVL) SETB 0 RESET OTHRWISE BLK
&ZSTR_LVL_TEND(&ZSTR_LVL) SETB 1 REQUEST END LABEL
&ZSTR_CASE_REG(&ZSTR_LVL) SETC '&REG' SAVE FOR ENDCASE CODE
.*       AIF ('&REG' EQ '')
           AIF (NOT('&REG' EQ '')).AIF_2_1
             MNOTE 8,'CASENTRY - MISSING REGISTER WITH INDEX 0-N'
.*       AEND
.AIF_2_1   ANOP
#@CAS_&ZSTR_LVL_TCNT(&ZSTR_LVL)_L LOCTR
#@CAS_&ZSTR_LVL_TCNT(&ZSTR_LVL)_T DS 0A START CASENTRY BRANCH TABLE
&SYSECT  CSECT
         B     #@CAS_&ZSTR_LVL_TCNT(&ZSTR_LVL)_G BRANCH TO CASE CODE
         MEND
