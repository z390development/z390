         MACRO
.**********************************************************************
.* z390 - Mainframe assembler emulator and run-time engine
.* Copyright (C) 2021 z390 Assembler LLC
.*
.* This file is part of z390.
.*
.* z390 is free software; you can redistribute it and/or modify
.* it under the terms of the GNU General Public License as published by
.* the Free Software Foundation; either version 2 of the License, or
.* (at your option) any later version.
.* z390 is distributed in the hope that it will be useful,
.* but WITHOUT ANY WARRANTY; without even the implied warranty of
.* MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
.* GNU General Public License for more details.
.*
.* You should have received a copy of the GNU General Public License 
.* along with this program; if not, see https://www.gnu.org/licenses.
.**********************************************************************
.* 06/22/07 INITIAL CODING BY DON HIGGINS
.* 07/28/07 RPI 644 INITIAL CODING OF RPL OPTIONS                     *
.* 08/04/07 RPI 644 UPDATE RPLFEEDB TO 4 BYTE CODES                   *
.* 08/19/07 RPI 680 CORRECT LOOP GE TO GT, ADD LCLB &LRD              *
.* 08/29/07 RPI 688 CORRECT SETTING OF MACR1_XRBA OPTION BIT, ADD XRBA*
.* 09/14/07 RPI 672 ISSUE MNOTE ERROR FOR UNDEFINED OPTIONS           *
.* 09/16/07 RPI 682 SHARE ZGENOPTD TO GEN OPTCD BITS                  *
.* 09/24/07 RPI 702 REPLACE RPLXRBA WITH RPLLXRTA AND RPLCXRBA        *
.*          ADD RPLOPENC UNIQUE ACB OPEN COUNT FOR REPOSITION REQ'D   *
.* 11/26/07 RPI 723 MOVE GETOK, GETNF FLAGS TO RPLFLAG, ADD KSIT,KSIR *
.* 04/19/08 RPI 833 add SETC quotes for HLASM compatibility           *
.* 12/31/18 Updated for zVSAM V2 by Melvyn Maltz                      *
.*          Added MSGAREA, MSGLEN, TRANSID                            *
.*          Added MNOTEs for unsupported TRANSID                      *
.*          Rearranged RPL fields to conform with D&L                 *
.*          Added MODE=RPL to ZGENOPTD                                *
.* 12/20/20 zVSAM RENAMED TO RPL2 FOR HYBRID \MAC                     *
.**********************************************************************
&N       RPL2  &AM=VSAM,          VSAM                                 X
               &ACB=0,            VSAM ACB ADDRESS                     X
               &AREA=0,           RECORD AREA (DATA/MVE, ADDR/LOC)     X
               &AREALEN=0,        LENGTH OF AREA                       X
               &ARG=0,            KEY, XRBA/RBA, REC#(RRDS)            X
               &ECB=0,            EXTERNAL ECB                         X
               &KEYLEN=0,         KEY LENGTH FOR GENERIC KEY SEARCH    X
               &MSGAREA=0,        MESSAGE AREA                         X
               &MSGLEN=0,         MESSAGE LENGTH                       X
               &NXTRPL=0,         NEXT RPL IN CHAINED RPL LIST         X
               &OPTCD=,           OPTIONS                              X
               &RECLEN=0,         RECORD LENGTH                        X
               &TRANSID=          TRANSID (NOT SUPPORTED)
.*
         GBLC  &ZGENOPT1,&ZGENOPT2,&ZGENECB
.*
         AIF   ('&N' NE '')
&N       DS    0D
         AELSE
         DS    0D
         AEND
.*
.* ELIMINATE POSITIONALS
&NPOS    SETA  N'&SYSLIST
.POSLOOP ANOP
         AIF   (&NPOS EQ 0).POSEND
         MNOTE 12,'RPL UNSUPPORTED OPERAND - &SYSLIST(&NPOS)'
&NPOS    SETA  &NPOS-1
         AIF   (&NPOS GT 0).POSLOOP
         MEXIT
.*
.POSEND  ANOP
         AIF   ('&AM' NE 'VSAM')
         MNOTE 12,'ONLY AM=VSAM IS SUPPORTED'
         AEND
         AIF   ('&TRANSID' NE '')
         MNOTE 12,'TRANSID IS NOT SUPPORTED'
         AEND
&ZGENECB SETC  '&ECB'
         ZGENOPTD OPTCD=&OPTCD,MODE=RPL GENERATE RPL OPTCD
.*
.* GENERATE zVSAM V2 RPL
.*
         DC    C'zRPL'            RPLEYE    EYECATCHER
         DC    A(&ACB)            RPLDACB   ACB
         DC    A(&AREA)           RPLAREA   AREA
         DC    A(&AREALEN)        RPLAREAL  AREA LENGTH
         DC    A(&ARG)            RPLARG    ARG (KS KEY, RR NUM, RBA)
         DC    A(&ECB)            RPLECB    ECB (POSTED IF PRESENT)
         DC    A(&MSGAREA)        RPLMSGAR  MESSAGE AREA
         DC    A(&NXTRPL)         RPLNXTRP  POINTER TO RPL CHAIN
         DC    A(&RECLEN)         RPLRECLN  RECORD LENGTH
         DC    AL2(&MSGLEN)       RPLMSGLN  MESSAGE LENGTH
         DC    AL1(&KEYLEN)       RPLKEYLN  KEY LENGTH
         DC    B'&ZGENOPT1'       RPLOPTCD  OPTION BYTE 1
         DC    B'&ZGENOPT2'       RPLOPTCD  OPTION BYTE 2
         DC    XL4'00'            RPLFEEDB  FEEDBACK CODES
         DC    XL8'00'            RPLCXRBA  CURRENT XRBA
         DC    X'00'              RPLAIXID  AIX POINTER TYPE
         DC    XL2'00'            ALIGN
         DS    0D
.*
         RPLD
.*
         MEXIT
         MEND
