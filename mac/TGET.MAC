         MACRO
.*********************************************************************
.* z390 - Mainframe assembler emulator and run-time engine
.* Copyright (C) 2021 z390 Assembler LLC
.*
.* This file is part of z390.
.*
.* z390 is free software; you can redistribute it and/or modify
.* it under the terms of the GNU General Public License as published by
.* the Free Software Foundation; either version 2 of the License, or
.* (at your option) any later version.
.* z390 is distributed in the hope that it will be useful,
.* but WITHOUT ANY WARRANTY; without even the implied warranty of
.* MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
.* GNU General Public License for more details.
.*
.* You should have received a copy of the GNU General Public License 
.* along with this program; if not, see https://www.gnu.org/licenses.
.*********************************************************************
.* 11/30/05 INITIAL CODING BY DON HIGGINS
.* 12/11/05 RPI47 - add TGET/TPUT support for GUI window
.* 01/09/06 RPI163 SKIP LR FOR SAME REGISTER
.* 02/21/06 RPI211 MAKE TGET/TPUT MAINFRAME COMPATIBLE AT SVC LEVEL
.* 02/25/06 RPI211 MAKE TGET/TPUT MAINFRAME COMPATIBLE (DEMOGUI4 TEST)
.* 06/14/07 RPI 641 DEFAULT MF=I
.* 08/21/07 RPI 670 CORRECT REG OPTIMIZATION TO HANDLE ANY REG SYMBOL
.* 04/19/08 RPI 833 add SETC quotes for HLASM compatibility
.*********************************************************************
.*
.* TGET BUFFER,LBUFFER,OPTIONS
.*
.*     BUFFER  - RX or reg pointer to TN3290 data stream buffer
.*     LBUFFER - absolute value or reg length of buffer
.*     OPTIONS - the follwing options may be coded:                  
.*                 EDIT   - read text and remove controls (default)
.*                 NOEDIT - read text with controls
.*                 WAIT   - wait for input (default)
.*                 NOWAIT - return with R15=4 if no response avail.
.*                 ASIS   - read TN3270 data stream unedited  
.*
.* TGET (R0),(R1),OPTIONS
.*
.*     If TGET (0),(1) and no options are coded then 
.*     simple SVC 93 is generated so user can set options in regs
.*
.* Register input:                                           
.*
.* R0LH = ASID (NOT USED)
.* R0LL = BUFFER LENGTH
.* R1   = LOW 24 BIT BUFFER ADDRESS (RMODE24 REQUIRED)
.* R1   = FLAGS IN HIGH BYTE     
.*           X'80' - 1=TGET 0=TPUT
.*           X'40' - USERID IN R15 (NOT USED)
.*           X'20' - LOWP PROCESSING REQUIRED (NOT USED)
.*           X'10' - 1=NOWAIT 0=WAIT
.*           X'08' - 1=HOLD 0=NOHOLD
.*           X'04' - 1=BREAKIN 0=NOBREAKIN
.*           X'03' - 00=EDIT, 01=ASIS, 10=CONTROL, 11=FULLSCR 
.* Register output:
.* R1    = bytes acutally read
.*********************************************************************
&N       TGET  &BUFF,                                                  X
               &LBUFF,                                                 X
               &MF=I      NOT SUPPORTED  
         AIF   ('&N' EQ '').SKIPDS
&N       DS    0H
.SKIPDS  ANOP
         AIF   ('&MF' NE 'I').ERR1
         AIF   ('&BUFF'(1,1) EQ '(').RECREG
         LA    1,&BUFF
         AGO   .CHKLEN
.RECREG  ANOP
         ZOPTLR 1,&BUFF(1)
.CHKLEN  ANOP
         AIF   ('&LBUFF'(1,1) EQ '(').LENREG
         LA    0,&LBUFF
         AGO   .SETOPT
.LENREG  ANOP
         AIF   (&LBUFF(1) NE 1).LENREGOK
         MNOTE 12,'TGET INVALID LENGTH REGISTER - &LBUFF(1)'
.LENREGOK ANOP
         ZOPTLR 0,&LBUFF(1)
.SETOPT  ANOP
&OP      SETC  '1'  X'80' 1=TGET, 0=TPUT
&USERID  SETC  '0'  X'40' 0=NO USERID IN R15    (NOT USED)
&LOWP    SETC  '0'  X'20' 0=LOWP                (NOT USED)
&WAIT    SETC  '0'  X'10' 0=WAIT, 1=NOWAIT
&HOLD    SETC  '0'  X'08' 0=NOHOLD, 1=HOLD      (NOT USED)
&BREAKIN SETC  '0'  X'04' 0=BREAKIN, 1=BREANKIN (NOT USED)
&TYPE    SETC  '00' X'03' 00=EDIT, 01=ASIS, 10=CONTROL, 11=FULLSCR
.CHKOPT  ANOP
&NP      SETA  N'&SYSLIST
         AIF   (&NP GE 3 AND '&SYSLIST(3)' EQ 'R').SVC USE REG PARMS                     
&I       SETA  2
.OPTLOOP ANOP
&I       SETA  &I+1
         AIF   (&I GT &NP).OPTEND
         AIF   ('&SYSLIST(&I)' EQ 'EDIT').EDIT
         AIF   ('&SYSLIST(&I)' EQ 'ASIS').ASIS
         AIF   ('&SYSLIST(&I)' EQ 'WAIT').WAIT
         AIF   ('&SYSLIST(&I)' EQ 'NOWAIT').NOWAIT
         MNOTE 12,'TGET INVALID OPTION - &SYSLIST(&I)'
         AGO   .OPTLOOP
.EDIT    ANOP
&TYPE    SETC  '00' X'03' 00=EDIT
         AGO   .OPTLOOP
.ASIS    ANOP
&TYPE    SETC  '01' X'03'  01=ASIS
         AGO   .OPTLOOP
.WAIT    ANOP
&WAIT    SETC  '0' X'10'  0=WAIT 
         AGO   .OPTLOOP
.NOWAIT  ANOP
&WAIT    SETC  '1' X'10'  1=NOWAIT
         AGO   .OPTLOOP
.OPTEND  ANOP
         ICM   1,8,=B'&OP&USERID&LOWP&WAIT&HOLD&BREAKIN&TYPE'
.SVC     ANOP
         SVC   93 R0=(ASID,LENGTH) R1=(FLAGS,ADDR)
         MEXIT
.ERR1    MNOTE 12,'TGET MF= NOT SUPPORTED'
         MEND

