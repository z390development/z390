@echo off
REM create KSF1ZIP alternate index
rem %0 in z390\vsam1\test\bat; switch to z390
pushd %~dps0..\..\..

SET VSAMPATH=vsam1\test
SET ZVSAM=ZVSAM(1)
SET TRACE=%
IF /I "%1" == "traceall" (
    SET TRACE=TRACEALL
    shift /1
)

SET RC=0

REM BUILD AIX KSF1ZIP
SET INFILE=%VSAMPATH%\TESTCAT.KSF1NAME
SET OUTFILE=%VSAMPATH%\TESTCAT.KSF1ZIP
CALL bat\EXEC linklib\REPRO %1 %2 %3 %4 %5 %ZVSAM% %TRACE%
SET RC=%ERRORLEVEL%
IF %RC% NEQ 0 (
    SET ERRMSG=ERROR IN BUILDAIX KSF1ZIP
	GOTO errmsg
)
REM READ KSDS ZIPLIST PATH AND COMPARE RECORDS
SET QFILE=%VSAMPATH%\KSF1ZIP.TF1
SET VFILE=%VSAMPATH%\TESTCAT.ZIPLIST
CALL bat\ASMLG %VSAMPATH%\KSF1COMP %1 %2 %3 %4 %5 %ZVSAM% %TRACE%
SET RC=%ERRORLEVEL%
IF %RC% NEQ 0 (
    SET ERRMSG=ERROR IN READ ZIPLIST PATH RECS BY KEY
	GOTO errmsg
)

:return
popd
exit /b %RC%
:errmsg
echo ERROR: %ERRMSG%
SET RC=8
GOTO return

