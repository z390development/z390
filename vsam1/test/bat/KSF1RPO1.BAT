@echo off
REM LOAD KSDS KSF1NAME CLUSTER, UNLOAD, AND VERIFY
rem %0 in z390\vsam1\test\bat; switch to z390
pushd %~dps0..\..\..

SET PGMNAME=KSF1RPO1
SET VSAMPATH=vsam1\test
SET ZVSAM=ZVSAM(1)
SET TRACE=%
IF /I "%1" == "traceall" (
    SET TRACE=TRACEALL
    shift /1
)

SET RC=0

REM LOAD KSDS KSF1NAME FROM KSF1NAME.TF1
SET INFILE=%VSAMPATH%\KSF1NAME.TF1[RECFM=FT]
SET OUTFILE=%VSAMPATH%\TESTCAT.KSF1NAME
CALL bat\EXEC linklib\REPRO %1 %2 %3 %4 %5 %ZVSAM% %TRACE%
SET RC=%ERRORLEVEL%
IF %RC% NEQ 0 (
    SET ERRMSG=ERROR IN LOAD KSF1NAME
	GOTO errmsg
)
REM UNLOAD KSDS KSF1NAME.VES TO KSF1NAME.TF2
SET INFILE=%VSAMPATH%\TESTCAT.KSF1NAME
SET OUTFILE=%VSAMPATH%\KSF1NAME.TF2[RECFM=FT]
IF EXIST %VSAMPATH%\KSF1NAME.TF2 ERASE %VSAMPATH%\KSF1NAME.TF2
CALL bat\EXEC linklib\REPRO %1 %2 %3 %4 %5 %ZVSAM% %TRACE%
SET RC=%ERRORLEVEL%
IF %RC% NEQ 0 (
    SET ERRMSG=ERROR IN UNLOAD KSF1NAME
	GOTO errmsg
)
REM COMPARE INPUT AND OUTPUT QSAM FILES
fc %VSAMPATH%\KSF1NAME.TF1 %VSAMPATH%\KSF1NAME.TF2 > %VSAMPATH%\%PGMNAME%.DIF
SET RC=%ERRORLEVEL% 
IF %RC% NEQ 0 (
    SET ERRMSG=ERROR IN COMPARING KSF1NAME
	GOTO errmsg
)
:return
popd
exit /b %RC%
:errmsg
echo ERROR: %ERRMSG%
SET RC=8
GOTO return
