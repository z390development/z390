@echo off
REM LOAD/UNLOAD ESF4 USING DEF EF FORMAT
rem %0 in z390\vsam1\test\bat; switch to z390
pushd %~dps0..\..\..

SET PGMNAME=ESF4RPO1
SET VSAMPATH=vsam1\test
SET ZVSAM=ZVSAM(1)
SET TRACE=%
IF /I "%1" == "traceall" (
    SET TRACE=TRACEALL
    shift /1
)

SET RC=0

REM RUN REPRO TO LOAD ESF4.VES FROM ESF4.TF1
SET INFILE=%VSAMPATH%\ESF4.TF1           
SET OUTFILE=%VSAMPATH%\TESTCAT.ESF4
CALL bat\EXEC linklib\REPRO %1 %2 %3 %4 %5 %ZVSAM% %TRACE%
SET RC=%ERRORLEVEL%
IF %RC% NEQ 0 (
    SET ERRMSG=ERROR IN REPRO LOAD OF ESF4
	GOTO errmsg
)
REM SAVE REPRO LOAD STATS
COPY linklib\REPRO.LOG %VSAMPATH%\%PGMNAME%.RLG  
IF "%TRACE%" == "TRACEALL" COPY linklib\REPRO.TRE %VSAMPATH%\%PGMNAME%.RLE
REM RUN REPRO TO UNLOAD FROM ESF4.VES TO ESF4.TF2
SET INFILE=%VSAMPATH%\TESTCAT.ESF4
SET OUTFILE=%VSAMPATH%\ESF4.TF2
CALL bat\EXEC linklib\REPRO %1 %2 %3 %4 %5 %ZVSAM% %TRACE%
SET RC=%ERRORLEVEL%
IF %RC% NEQ 0 (
    SET ERRMSG=ERROR IN REPRO UNLOAD OF ESF4
	GOTO errmsg
)
REM SAVE REPRO UNLOAD STATS
COPY linklib\REPRO.LOG %VSAMPATH%\%PGMNAME%.RUG  
IF "%TRACE%" == "TRACEALL" COPY linklib\REPRO.TRE %VSAMPATH%\%PGMNAME%.RUE
REM COMPARE INPUT AND OUTPUT QSAM FILES
fc %VSAMPATH%\ESF4.TF1 %VSAMPATH%\ESF4.TF2 > %VSAMPATH%\%PGMNAME%.DIF
SET RC=%ERRORLEVEL%
IF %RC% NEQ 0 (
    SET ERRMSG=REPRO LOAD/UNLOAD QSAM FILES DO NOT MATCH
	GOTO errmsg
)

:return
popd
exit /b %RC%
:errmsg
echo ERROR: %ERRMSG%
SET RC=8
GOTO return