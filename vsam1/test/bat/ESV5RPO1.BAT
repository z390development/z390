@echo off
REM LOAD/UNLOAD ESV5 USING DEF EV LLLL,REC FORMAT
rem %0 in z390\vsam1\test\bat; switch to z390
pushd %~dps0..\..\..

SET PGMNAME=ESV5RPO1
SET VSAMPATH=vsam1\test
SET ZVSAM=ZVSAM(1)
SET TRACE=%
IF /I "%1" == "traceall" (
    SET TRACE=TRACEALL
    shift /1
)

SET RC=0

REM RUN REPRO TO LOAD ESV5.VES FROM ESV5.TF1
SET INFILE=%VSAMPATH%\ESV5.TF1                    
SET OUTFILE=%VSAMPATH%\TESTCAT.ESV5
CALL bat\EXEC linklib\REPRO %1 %2 %3 %4 %5 %ZVSAM% %TRACE%
SET RC=%ERRORLEVEL%
IF %RC% NEQ 0 (
    SET ERRMSG=ERROR IN REPRO LOAD OF ESV5
	GOTO errmsg
)
REM SAVE REPRO LOAD STATS
COPY linklib\REPRO.LOG %VSAMPATH%\%PGMNAME%.RLG  
IF "%TRACE%" == "TRACEALL" COPY linklib\REPRO.TRE %VSAMPATH%\%PGMNAME%.RLE
REM RUN REPRO TO UNLOAD FROM ESV5.VES TO ESV5.TF2
SET INFILE=%VSAMPATH%\TESTCAT.ESV5
SET OUTFILE=%VSAMPATH%\ESV5.TF2
CALL bat\EXEC linklib\REPRO %1 %2 %3 %4 %5 %ZVSAM% %TRACE%
SET RC=%ERRORLEVEL%
IF %RC% NEQ 0 (
    SET ERRMSG=ERROR IN REPRO UNLOAD OF ESV5
	GOTO errmsg
)
REM SAVE REPRO UNLOAD STATS
COPY linklib\REPRO.LOG %VSAMPATH%\%PGMNAME%.RUG  
IF "%TRACE%" == "TRACEALL" COPY linklib\REPRO.TRE %VSAMPATH%\%PGMNAME%.RUE
REM COMPARE INPUT AND OUTPUT QSAM FILES
fc %VSAMPATH%\ESV5.TF1 %VSAMPATH%\ESV5.TF2  > %VSAMPATH%\%PGMNAME%.DIF
SET RC=%ERRORLEVEL%
IF %RC% NEQ 0 (
    SET ERRMSG=REPRO LOAD/UNLOAD QSAM FILES DO NOT MATCH
	GOTO errmsg
)

:return
popd
exit /b %RC%
:errmsg
echo ERROR: %ERRMSG%
SET RC=8
GOTO return