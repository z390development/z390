@echo off
REM LOAD/UNLOAD ESF1 USING RECFM=F
rem %0 in z390\vsam1\test\bat; switch to z390
pushd %~dps0..\..\..

SET PGMNAME=ESF1RPQ1
SET VSAMPATH=vsam1\test
SET ZVSAM=ZVSAM(1)
SET TRACE=%
IF /I "%1" == "traceall" (
    SET TRACE=TRACEALL
    shift /1
)

SET RC=0

REM RUN REPRO TO LOAD ESF1.VES FROM ESF1.TQ1
SET INFILE=%VSAMPATH%\ESF1.TQ1[RECFM=F]
SET OUTFILE=%VSAMPATH%\TESTCAT.ESF1
CALL bat\EXEC linklib\REPRO %1 %2 %3 %4 %5 %ZVSAM% %TRACE%
SET RC=%ERRORLEVEL%
IF %RC% NEQ 0 (
    SET ERRMSG=ERROR IN REPRO LOAD OF ESF1
	GOTO errmsg
)
REM SAVE REPRO LOAD STATS
COPY linklib\REPRO.LOG %VSAMPATH%\%PGMNAME%.RLG  
IF "%TRACE%" == "TRACEALL" COPY linklib\REPRO.TRE %VSAMPATH%\%PGMNAME%.RLE
REM RUN REPRO TO UNLOAD FROM ESF1.VES TO ESF1.TQ2
SET INFILE=%VSAMPATH%\TESTCAT.ESF1
SET OUTFILE=%VSAMPATH%\ESF1.TQ2[RECFM=F]
CALL bat\EXEC linklib\REPRO %1 %2 %3 %4 %5 %ZVSAM% %TRACE%
SET RC=%ERRORLEVEL%
IF %RC% NEQ 0 (
    SET ERRMSG=ERROR IN REPRO UNLOAD OF ESF1
	GOTO errmsg
)
REM SAVE REPRO UNLOAD STATS
COPY linklib\REPRO.LOG %VSAMPATH%\%PGMNAME%.RUG  
IF "%TRACE%" == "TRACEALL" COPY linklib\REPRO.TRE %VSAMPATH%\%PGMNAME%.RUE
REM COMPARE INPUT AND OUTPUT QSAM FILES
fc %VSAMPATH%\ESF1.TQ1 %VSAMPATH%\ESF1.TQ2 > %VSAMPATH%\ESF1RPQ1.DIF
SET RC=%ERRORLEVEL%
IF %RC% NEQ 0 (
    SET ERRMSG=REPRO LOAD/UNLOAD QSAM FILES DO NOT MATCH
	GOTO errmsg
)

:return
popd
exit /b %RC%
:errmsg
echo ERROR: %ERRMSG%
SET RC=8
GOTO return


