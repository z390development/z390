@echo off
REM LOAD KSDS KSF1NAME CLUSTER, UNLOAD, AND VERIFY
rem %0 in z390\vsam1\demo\bat; switch to z390
pushd %~dps0..\..\..

SET PGMNAME=KSF1RPO1
SET VSAMPATH=vsam1\demo
SET ZVSAM=ZVSAM(1)
SET TRACE=%
IF /I "%1" == "traceall" (
    SET TRACE=TRACEALL
    shift /1
)

SET RC=0

SET INFILE=%VSAMPATH%\KSF1NAME.TF1[RECFM=FT]
SET OUTFILE=%VSAMPATH%\DEMOCAT.KSF1NAME
CALL bat\EXEC linklib\REPRO %1 %2 %3 %4 %5 %ZVSAM% %TRACE%
SET RC=%ERRORLEVEL%
IF %RC% NEQ 0 (
    SET ERRMSG=ERROR IN LOAD KSF1NAME
	GOTO errmsg
)
SET INFILE=%VSAMPATH%\DEMOCAT.KSF1NAME
SET OUTFILE=%VSAMPATH%\KSF1NAME.TF2[RECFM=FT]
IF EXIST %VSAMPATH%\KSF1NAME.TF2 ERASE %VSAMPATH%\KSF1NAME.TF2
CALL bat\EXEC linklib\REPRO %1 %2 %3 %4 %5 %ZVSAM% %TRACE%
SET RC=%ERRORLEVEL%
IF %RC% NEQ 0 (
    SET ERRMSG=ERROR IN UNLOAD KSF1NAME
	GOTO errmsg
)
fc %VSAMPATH%\KSF1NAME.TF1 %VSAMPATH%\KSF1NAME.TF2 > %VSAMPATH%\%PGMNAME%.DIF
SET RC=%ERRORLEVEL% 
IF %RC% NEQ 0 (
    SET ERRMSG=ERROR IN COMPARING KSF1NAME
	GOTO errmsg
)
:return
popd
exit /b %RC%
:errmsg
echo ERROR: %ERRMSG%
SET RC=8
GOTO return
