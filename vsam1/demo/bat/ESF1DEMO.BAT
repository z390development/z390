@echo off
REM BUILD VSAM ESDS CLUSTER VCDT ESF1.390 AND RUN ESF1SP, ESF1SG, fc
rem %0 in z390\vsam1\demo\bat; switch to z390
pushd %~dps0..\..\..

SET PGMNAME=ESF1DEMO
SET VSAMPATH=vsam1\demo
SET ZVSAM=ZVSAM(1)
SET TRACE=%
IF /I "%1" == "traceall" (
    SET TRACE=TRACEALL
    shift /1
)

SET RC=0

REM RUN ESF1SP SEQ PUT TO BUILD ESF1.VES
SET QFILE=%VSAMPATH%\ESF1.TF1
SET VFILE=%VSAMPATH%\DEMOCAT.ESF1
IF EXIST %VSAMPATH%\ESF1.VES ERASE %VSAMPATH%\ESF1.VES
CALL bat\ASMLG %VSAMPATH%\ESF1SP %1 %2 %3 %4 %5 %ZVSAM% %TRACE%
SET RC=%ERRORLEVEL%
IF %RC% NEQ 0 (
    SET ERRMSG=ERROR IN ESF1SP
	GOTO errmsg
)
REM RUN ESF1SG SEQ GET FROM ESF1.VES TO ESF1.TF2
IF EXIST %VSAMPATH%\ESF1.TF2 ERASE %VSAMPATH%\ESF1.TF2
SET VFILE=%VSAMPATH%\DEMOCAT.ESF1
SET QFILE=%VSAMPATH%\ESF1.TF2
CALL bat\ASMLG %VSAMPATH%\ESF1SG %1 %2 %3 %4 %5 %ZVSAM% %TRACE%
SET RC=%ERRORLEVEL%
IF %RC% NEQ 0 (
    SET ERRMSG=ERROR IN ESF1SG
	GOTO errmsg
)
REM COMPARE INPUT AND OUTPUT QSAM FILES
fc %VSAMPATH%\ESF1.TF1 %VSAMPATH%\ESF1.TF2 > %VSAMPATH%\%PGMNAME%.DIF 
SET RC=%ERRORLEVEL%
IF %RC% NEQ 0 (
    SET ERRMSG=ESF1.TF1 NE ESF1.TF2
	GOTO errmsg
)

:return
popd
exit /b %RC%
:errmsg
echo ERROR: %ERRMSG%
SET RC=8
GOTO return
