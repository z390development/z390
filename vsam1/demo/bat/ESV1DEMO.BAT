@echo off
REM BUILD VSAM ESDS CLUSTER VCDT ESV1.390 AND RUN ESV1SP, ESV1SG, fc
rem %0 in z390\vsam1\demo\bat; switch to z390
pushd %~dps0..\..\..

SET PGMNAME=ESV1DEMO
SET VSAMPATH=vsam1\demo
SET ZVSAM=ZVSAM(1)
SET TRACE=%
IF /I "%1" == "traceall" (
    SET TRACE=TRACEALL
    shift /1
)

SET RC=0

REM RUN ESV1SP SEQ PUT TO BUILD ESV1.VES
SET QFILE=%VSAMPATH%\ESV1.TF1
SET VFILE=%VSAMPATH%\DEMOCAT.ESV1
IF EXIST %VSAMPATH%\ESV1.VES ERASE %VSAMPATH%\ESV1.VES
rem IF EXIST %VSAMPATH%\ESV1.VX0 ERASE %VSAMPATH%\ESV1.VX0
CALL bat\ASMLG %VSAMPATH%\ESV1SP %1 %2 %3 %4 %5  %ZVSAM% %TRACE%
SET RC=%ERRORLEVEL%
IF %RC% NEQ 0 (
    SET ERRMSG=ERROR IN ESV1SP
	GOTO errmsg
)
REM RUN ESV1SG SEQ GET FROM ESV1.VES TO ESV1.TF2
IF EXIST %VSAMPATH%\ESV1.TF2 ERASE %VSAMPATH%\ESV1.TF2
SET VFILE=%VSAMPATH%\DEMOCAT.ESV1
SET QFILE=%VSAMPATH%\ESV1.TF2
CALL bat\ASMLG %VSAMPATH%\ESV1SG %1 %2 %3 %4 %5 %ZVSAM% %TRACE%
SET RC=%ERRORLEVEL%
IF %RC% NEQ 0 (
    SET ERRMSG=ERROR IN ESV1SG
	GOTO errmsg
)
REM COMPARE INPUT AND OUTPUT QSAM FILES
fc %VSAMPATH%\ESV1.TF1 %VSAMPATH%\ESV1.TF2 > %VSAMPATH%\%PGMNAME%.DIF 
SET RC=%ERRORLEVEL%
IF %RC% NEQ 0 (
    SET ERRMSG=ESV1.TF1 NE ESV1.TF2
	GOTO errmsg
)

:return
popd
exit /b %RC%
:errmsg
echo ERROR: %ERRMSG%
SET RC=8
GOTO return
