         TITLE 'DEMOAST1 - DEMO USE OF ASSIST TO PRINT PRIMES'
*********************************************************************
* z390 - Mainframe assembler emulator and run-time engine
* Copyright (C) 2021 z390 Assembler LLC
*
* This file is part of z390.
*
* z390 is free software; you can redistribute it and/or modify
* it under the terms of the GNU General Public License as published by
* the Free Software Foundation; either version 2 of the License, or
* (at your option) any later version.
* z390 is distributed in the hope that it will be useful,
* but WITHOUT ANY WARRANTY; without even the implied warranty of
* MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
* GNU General Public License for more details.
*
* You should have received a copy of the GNU General Public License 
* along with this program; if not, see https://www.gnu.org/licenses.
********************************************************************
* Author - Don Higgins                                              *
* Date   - 02/29/08                                                 *
*********************************************************************
* 03/02/08 RPI 812 ADD OPTION ASSIST DEMO USING XDECO AND XPRNT PRIMES 
*********************************************************************
DEMOAST1 CSECT
         USING *,R15
         XPRNT =C'           2',12 DISPLAY FIRST TWO PRIMES
         XPRNT =C'           3',12
         LA    R3,3          R3 = NUMBER TO TEST FOR PRIME
         LA    R5,PRIMES     R5 -> LAST PRIME IN TABLE OF PRIMES
LOOP1    DS    0H
         AHI   R3,2          R3 = NEXT ODD NUMBER
         CHI   R3,MAXPRIME
         BH    EXIT
         LA    R4,PRIMES     R4 -> CURRENT PRIME DIVISOR IN TABLE
LOOP2    DS    0H
         SR    R0,R0
         LR    R1,R3
         D     R0,0(R4)      DIVIDE NUMBER BY TRIAL DIVISOR
         LTR   R0,R0         IS THERE A REMAINDER
         BZ    LOOP1         NO, THEN SKIP NON PRIME
         C     R1,0(R4)      IS QUOTIENT < DIVISOR
         BL    ADDPRIME      YES GO PRINT AND ADD PRIME 
         AHI   R4,4
         B     LOOP2
ADDPRIME DS    0H
         AHI   R5,4          SLOT FOR NEXT PRIME
         ST    R3,0(R5)      SAVE NEXT PRIME
         XDECO R3,DISPLAY    CONVERT IT FOR PRINTING
         XPRNT DISPLAY,12    PRINT IT
         B     LOOP1         LOOK FOR NEXT PRIME
EXIT     DS    0H
         SR    R15,R15
         BR    R14
*
* DATA AREAS
*
R0       EQU   0       WORK/REMAINDER
R1       EQU   1       WORK/QUOTIENT
R2       EQU   2       WORK
R3       EQU   3       TRIAL PRIME NUMBER
R4       EQU   4       CURRENT PRIME DIVISOR ADDR IN TABLE
R5       EQU   5       ADDRESS OF LAST PRIME IN TABLE
R14      EQU   14      RETURN ADDRESS 
R15      EQU   15      ENTRY  ADDRESS
MAXPRIME EQU   100
PRIMES   DC    F'3',(MAXPRIME/2)F'0' TABLE OF PRIMES
DISPLAY  DS    CL12
         END         
