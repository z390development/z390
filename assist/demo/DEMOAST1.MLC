         TITLE 'DEMOAST1 - DEMO USE OF ASSIST TO PRINT PRIMES'
*********************************************************************
* Copyright 2005 Automated Software Tools Corporation               *
* This source code is part of z390 assembler/emulator package       *
* The z390 package is distributed under GNU general public license  *
* Author - Don Higgins                                              *
* Date   - 02/29/08                                                 *
*********************************************************************
* 03/02/08 RPI 812 ADD OPTION ASSIST DEMO USING XDECO AND XPRNT PRIMES 
*********************************************************************
DEMOAST1 CSECT
         USING *,R15
         XPRNT =C'           1',12 DISPLAY 1, 2, 3 AS PRIMES
         XPRNT =C'           2',12
         XPRNT =C'           3',12
         LA    R3,3          R3 = NUMBER TO TEST FOR PRIME
LOOP1    DS    0H
         AHI   R3,2          R3 = NEXT ODD NUMBER
         CHI   R3,MAXPRIME
         BH    EXIT
         LA    R4,PRIMES     R4 = CURRENT PRIME DIVISOR IN TABLE
LOOP2    DS    0H
         SR    R0,R0
         LR    R1,R3
         D     R0,0(R4)      DIVIDE NUMBER BY TRIAL DIVISOR
         LTR   R0,R0         IS THERE A REMAINDER
         BZ    LOOP1         NO, THEN SKIP NON PRIME
         C     R1,0(R4)      IS QUOTIENT < DIVIOR
         BL    ADDPRIME      YES GO PRINT AND ADD PRIME 
         AHI   R4,4
         B     LOOP2
ADDPRIME DS    0H
         AHI   R4,4
         ST    R3,0(R4)
         XDECO R3,DISPLAY
         XPRNT DISPLAY,12
         B     LOOP1
EXIT     DS    0H
         SR    R15,R15
         BR    R14
*
* DATA AREAS
*
R0       EQU   0       WORK/REMAINDER
R1       EQU   1       WORK/QUOTIENT
R2       EQU   2       WORK
R3       EQU   3       TRIAL PRIME NUMBER
R4       EQU   4       CURRENT PRIME DIVISOR ADDR IN TABLE
R14      EQU   14      RETURN ADDRESS 
R15      EQU   15      ENTRY  ADDRESS
MAXPRIME EQU   100
PRIMES   DC    F'3',(MAXPRIME/10)F'0' ONLY TEST UP TO  SQRT MAXPRIME
DISPLAY  DS    CL12
         END         
