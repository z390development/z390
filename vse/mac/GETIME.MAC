         MACRO
.*********************************************************************
.* z390 - Mainframe assembler emulator and run-time engine
.* Copyright (C) 2021 z390 Assembler LLC
.*
.* This file is part of z390.
.*
.* z390 is free software; you can redistribute it and/or modify
.* it under the terms of the GNU General Public License as published by
.* the Free Software Foundation; either version 2 of the License, or
.* (at your option) any later version.
.* z390 is distributed in the hope that it will be useful,
.* but WITHOUT ANY WARRANTY; without even the implied warranty of
.* MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
.* GNU General Public License for more details.
.*
.* You should have received a copy of the GNU General Public License 
.* along with this program; if not, see https://www.gnu.org/licenses.
.* Author - Don Higgins                                              *
.* Date   - 03/11/07                                                 *
.*********************************************************************
.* 03/11/07 RPI 558 ADD Z390 VSE MACRO SUPPORT                  
.* 10/01/07 RPI 709 CHANGE CLOCK=NO AND FIX MIC STORE/LOAD OFFSETS
.* 04/19/08 RPI 833 add SETC quotes for HLASM compatibility
.*********************************************************************
&N       GETIME &TYPE,     STANDARD(PD)/BINARY/TU/MIC                  X  
               &ZONE,       LOCAL/GMT                      (IGNORED)   X
               &CLOCK=NO,   R0-R1 CONTAINTS STCK TOD INPUT             X
               &MFG=        RX 64 BYTE WORK AREA FOR RENT  (IGNORED)
         AIF   ('&N' EQ '').SKIPDS
&N       DS    0H
.SKIPDS  ANOP
         AIF   ('&CLOCK' EQ 'YES').ERR1
         AIF   ('&TYPE' EQ '').STANDARD
         AIF   ('&TYPE' EQ 'STANDARD').STANDARD
         AIF   ('&TYPE' EQ 'BINARY').BINARY
         AIF   ('&TYPE' EQ 'TU').TU
         AIF   ('&TYPE' EQ 'MIC').MIC
         MNOTE 12,'GETIME INVALID TYPE - &TYPE'
         MEXIT
.STANDARD ANOP
         TIME  DEC    R0=0CYYDDDC R1=HHMMSSTH  TOD
         LR    1,0
         IC    1,=X'C0'
         SRL   1,4    R1=0HHMMSSC
         AGO   .EXIT
.BINARY  ANOP
         TIME  BIN   R0=0.01 SEC TOD
         LR    1,0
         SR    0,0
         D     0,=F'100' R1=SECONDS TOD
         AGO   .EXIT
.TU      ANOP
         TIME  TU
         LR    1,0
         AGO   .EXIT
.MIC     ANOP
         BAL   14,*+12
         DC    XL8'00'
         TIME  MIC,0(14) STORE TOD MIC
         LM    0,1,0(14) R0-R1 = TOD MIC
         AGO   .EXIT
.ERR1    MNOTE 12,'GETIME DOES NOT CURRENTLY SUPPORT CLOCK=YES'
         MEXIT
.EXIT    ANOP
         MEND
