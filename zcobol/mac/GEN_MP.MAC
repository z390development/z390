         MACRO
.*********************************************************************
.* z390 - Mainframe assembler emulator and run-time engine
.* Copyright (C) 2021 z390 Assembler LLC
.*
.* This file is part of z390.
.*
.* z390 is free software; you can redistribute it and/or modify
.* it under the terms of the GNU General Public License as published by
.* the Free Software Foundation; either version 2 of the License, or
.* (at your option) any later version.
.* z390 is distributed in the hope that it will be useful,
.* but WITHOUT ANY WARRANTY; without even the implied warranty of
.* MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
.* GNU General Public License for more details.
.*
.* You should have received a copy of the GNU General Public License 
.* along with this program; if not, see https://www.gnu.org/licenses.
.*********************************************************************
.* Author - Abe Kornelis                                             *
.* Date   - 01/24/26                                                 *
.*********************************************************************
.* 01/24/26 Issue 730 MP with invalid length operands causes S0C6
.*********************************************************************
.* Notes:
.*  1.  ROUNDED not supported yet.
.*********************************************************************
         GEN_MP &TGTNAM,&TGTLEN,&SRCNAM,&SRCLEN,&ONSIZE
.*
.* &TGTNAM = name of target field
.* &TGTLEN = length of target field
.*           target field is operand1 and result field.
.* &SRCNAM = name of source field
.* &SRCLEN = length of source field
.*           source field is immutable operand2
.* &ONSIZE = '1' if ON SIZE clause is in effect
.* &MPY_LAB= GBLA variable used to compose branch target address
.*           for handling overflow, should one occur
.*
         COPY  ZC_WS
.*
.* Check how to calculate the product of our two Packed fields
.* Restriction of MP instruction:
.* - Target length max 16 bytes (31 digits)
.* - Source length max  8 bytes (15 digits)
.* - Source length must be less than target length
.* - At run-time target must contain (source-length) leading zero bytes
.*   this last requirement cannot now be checked.
         AIF   (&TGTLEN LE 16 AND &SRCLEN LT &TGTLEN).GENMP
         AIF   (&SRCLEN LE 16 AND &TGTLEN LT &SRCLEN).SWAPEM
         AIF   (&TGTLEN LE  8 AND &SRCLEN EQ &TGTLEN).LENGTHN
         MNOTE 12,'MATRIX logic not implemented yet'
         AGO   .MEND

.*
.* The requirements for a normal MP instruction (see above) are met.
.* A single MP suffices - no whistles or bells needed
.* Overflow irrelevant - we get a S0C6 abend when overflow might occur
.GENMP   ANOP  ,
         MP    &TGTNAM.(&TGTLEN),&SRCNAM.(&SRCLEN)
         AGO   .MEND

.*
.* The requirements for a normal MP instruction (see above) are met.
.* Yet when we swap the operands, the restrictions are met.
.* But since we cannot overwrite the source field we use a temp. field
.* This temp field is 16 bytes long, so can be used also when
.* lengths are equal.
.* A single MP suffices - but we need to either swap/unswap the fields
.* or we need to use a longer result field.
.SWAPEM  ANOP  ,
.LENGTHN ANOP  ,
         ZAP   ZCVT_RES0,&SRCNAM.(&SRCLEN)
         MP    ZCVT_RES0,&TGTNAM.(&TGTLEN)
         ZAP   &TGTNAM.(&TGTLEN),ZCVT_RES0
         AIF   ('&ONSIZE' NE '1').MEND
         JO    PG_MPY_&MPY_LAB._ON_SIZE    Goto ON SIZE clause
         AGO   .MEND

.MEND    ANOP  ,
         MEND
