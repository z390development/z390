         MACRO
.*********************************************************************
.* Copyright 2008 Automated Software Tools Corporation               *
.* This source code is part of z390 zCOBOL/assembler/emulator package*
.* The z390 package is distributed under GNU general public license  *
.* Author - Don Higgins                                              *
.* Date   - 04/07/08                                                 *
.*********************************************************************
.* 04/08/08 initial coding to build global WS definition 
.* 04/30/08 add 88's, change SYM_FILE_IX to SYM_REF, add SYM_LVL
.* 05/01/08 add PIC_TYPE support for DEFHPXZ based on PIC,VALUE,
.*          USAGE, and OCCURS clauses (REDEFINES pending)                                  
.* 05/18/08 add REDEFINE support                                
.* 06/10/08 add USAGE IS POINTER support                        
.* 06/10/08 add INDEXED BY support                        
.* 10/06/08 ZSTRMAC, GROUP OCCURS, USAGE, DUP SYM IN DIFF LEVELS
.* 12/03/08 add level 91 filename 
.* 12/21/08 inc SYM_FILLER_VAL_CNT for label used by CICS GEN_INIT_WS 
.* 01/08/09 set SYM_SIT used by ZCGETFLD.CPY subscript parser
.* 02/23/09 RPI 1001 set SYM_LOC for 88 to first V88 value index
.* 02/25/09 RPI 1002 change index data items from type A to I     
.* 02/26/09 RPI 1012 add support for VALUE ALL clause, 66 RENAMES,
.* 02/27/09 RPI 1012 support SIGN LEADING/TRAILING SEPARATE CHARACTER
.* 02/27/09 RPI 1012 support single & in text literal (double it)    
.* 03/06/09 RPI 1004 add HFP/BFP/DFP/FLOAT-SHORT/LONG/EXTENDED        
.* 03/11/09 RPI 1004 add COMP PIC S9(39) type Q 16 byte 128 bit int
.* 03/20/09 RPI 1004 rename explicit 1-9 FLOAT-HEX/BINARY/DECIMAL-N
.* 04/29/09 RPI 1032 issue error if PIC clause has quote or no 9ZX      
.* 06/20/09 RPI 1042 add computational                                  
.* 07/09/09 RPI 1065 move FILE_IX from SYM_REF to SYM_FD to supp groups 
.* 07/19/09 RPI 1065 add SYNC clause                                        
.* 07/23/09 RPI 1065 add support for P assumed decimal point in PIC         
.* 07/25/09 RPI 1065 gen 2 quotes in value for WS_GEN to gen 1 quote        
.* 08/02/09 RPI 1065 check for duplicates and allow 01/nn level dups        
.* 08/08/09 RPI 1065 make BY in INDEXED BY optional                         
.* 08/08/09 RPI 1065 support DISPLAY clause (its the default)               
.* 08/08/09 RPI 1065 use SCALE_NUM_LIT to scale PIC_VAL for FGHPQZ               
.* 08/12/09 RPI 1065 set SYM_FXT to head of duplicate symbol list                
.* 08/17/09 RPI 1065 support 88 level VALUES ARE n1 THRU n2                      
.* 08/18/09 RPI 1065 add clauses JUSTIFIED, LEADING, TRAILING,                   
.*          ASCENDING, DESCENDING, BLANK WHEN ZERO
.* 10/12/09 RPI 1090 CORRECT COMP-4/COMP-5 TO COMP_4/COMP_5 FOR COMP
.* 07/25/10 RPI 1126 SUPPORT NE+N VALUE
.* 11/26/10 RPI 1138 CORRECT SYM_PIC_DEC FOR DISPLAY PICTURE Z.99 ETC.
.* 04/05/11 RPI 1139 FIX SETA STRING FOR HLASM COMP
.* 02/16/12 RPI 1181 FIX TO HANDLE PIC X(8). WITH PERIOD FOLLOWING.
.*********************************************************************
.* 
.* WS level,name,clauses in any order (PIC,VALUE,USAGE supported)
.* Notes:
.*   1.  If name omitted FILLER is assumed.
.*   2.  For 88 level, must follow WS item, only VALUE or VALUES
.*   3.  Level 77 treated like 01
.*   4.  S9(4) COMP = H, S9(9) COMP = F, S9(18) COMP = G (FL8)
.*   5.  REDEFINE sets &WS_LVL_RDEF flag and &WS_LVL_RDEF_END
.*   6.  USAGE   on group propagated to lower level items
.*   6.  OCCURS on group duplicates calculated length at end of group
.*   7.  VALUE QUOTE(S) assumes APOST(') vs QUOTE(") for now  
.*   8.  PIC_TYPE 123=HFP,456=BFP,789=DFP
.*   9.  PIC_TYPE Q COMP PIC S9(3) QUADWORD 16 byte 128 bit integer
.*********************************************************************
         WS    &LEVEL,&NAME
         COPY  ZC_WS            
         :&NEW_LVL_NO SETA  &LEVEL MAY CHANGE FROM 77 TO 01 RPI 1139
         :&NEW_SYM    SETC  '&NAME'    MAY CHANGE FROM ?  TO FILLER
         :&ERR_ID     SETC  '&NEW_LVL_NO &NEW_SYM'
         :&REF_IX     SETA  0
         :&PIC_SEP    SETB  0 SEPARATE SIGN
         :&PIC_LEAD   SETB  0 LEADING
         :&PIC_TRAIL  SETB  0 TRAILING
         AIF   (&NEW_LVL_NO EQ 88)     CONDITION VALUES
               ACALL WS_88             
               MEXIT
         AELSEIF (&NEW_LVL_NO EQ 66)   RENAMES
               ACALL WS_66
               MEXIT
         AEND
         AIF   (&NEW_LVL_NO EQ 91)
               :&NEW_LVL_NO  SETA 1 
               :&CUR_SET_LVL SETA 0
               ACALL WS_END_PRIOR
               :&NEW_LVL_NO  SETA 91
               ACALL WS_91_FILE  ADD FILE DCB 
               MEXIT
         AEND
         AIF   (&NEW_LVL_NO EQ 77)
               :&NEW_LVL_NO SETA 1  TREAT 77 LIKE 01
         AEND
         AIF   (&NEW_LVL_NO EQ 1)
               :&CUR_SET_LVL SETA 0  RESET CURRENT SUBSCRIPT LVL
         AEND
         ACALL WS_END_PRIOR
.*
.*       ADD NEW LEVEL # AT SAME OR HIGHER LEVEL
.*
         AIF   (&WS_LVL EQ 0)
               ACALL WS_ADD_LVL
         AELSEIF (&NEW_LVL_NO GT &WS_LVL_NO(&WS_LVL))
               ACALL WS_ADD_LVL
         AEND
         AIF   (&WS_LVL EQ 1)
               :&WS_LVL_RDEF(1) SETB 0   RESET REDEFINE AT LVL 1
               :&WS_LVL_USE(1)  SETC ''  RESET GROUP USAGE AT LVL 1
         AEND
         ACALL WS_STORE_NN
         AIF (&FILE_IX GT 0)
             AIF (&FILE_RECORD(&FILE_IX) EQ '')
                 :&FILE_RECORD(&FILE_IX) SETC '&SYM_NAME(&SYM_TOT)'
             AEND
         AEND
         AIF   (&WS_LVL EQ 1 AND '&PIC' EQ '')
               :&WS_LVL_USE(1) SETC '&USAGE' SET NEW LVL 1 USAGE DEF
         AEND
         :&WS_LVL_SYM_IX(&WS_LVL) SETA &SYM_TOT   WS_LVL SYM INDEX
         MEXIT
.*
.* END PRIOR
.*
         AENTRY WS_END_PRIOR
         :&WS_LOC_SAVE SETA &WS_LOC
         AIF  (&WS_LVL GT 0)
.*
.*            END HIGHER LEVEL # GROUPS/REDEF ITEMS
.*
              ACALL WS_END_NN  
.*
.*            END REDEFINE AT SAME LEVEL # IF ANY
.*
              AIF   (&WS_LVL_RDEF(&WS_LVL)                             X  
                    AND &WS_LOC GE &WS_LVL_RDEF_END(&WS_LVL))
                    :&WS_LVL_RDEF(&WS_LVL) SETB 0                 
              AEND
         AEND
         AEND
.*
.* END ALL HIGHER LEVEL WS GROUP AND REDEFINE ITEMS
.*
         AENTRY WS_END_NN
         AWHILE (&WS_LVL GT 1 AND &NEW_LVL_NO LT &WS_LVL_NO(&WS_LVL))
              ACALL WS_END_GROUP                   
              AIF (&WS_LVL_RDEF(&WS_LVL))
                  ACALL WS_END_REDEFINE
              AEND
              :&WS_LVL SETA &WS_LVL-1
         AEND
         AIF   (&WS_LVL_SYM_IX(&WS_LVL) GT 0)
               ACALL WS_END_GROUP
         AEND
         AIF   (&WS_LVL_RDEF(&WS_LVL)                                  X
                AND &WS_LOC GE &WS_LVL_RDEF_END(&WS_LVL))
              :&WS_LVL_RDEF(&WS_LVL) SETB 0  END REDEFINE
         AEND
         AEND
.*
.* WS END GROUP - SET SYM_LEN AND WS_LOC IF GROUP OCCURS > 1
.*
         AENTRY WS_END_GROUP
         :&IX SETA  &WS_LVL_SYM_IX(&WS_LVL)
       AIF  (&IX GT 0)
         AIF (&SYM_LEN(&IX) EQ 0)                     IS THIS GROUP LVL
             :&LEN     SETA  &WS_LOC-&SYM_LOC(&IX)    CALC GROUP LENGTH
             AIF (&LEN GT 0)
                 :&SYM_LEN(&IX) SETA &LEN                      
                 :&WS_PAD SETA &SYM_LOC(&IX)+&SYM_DUP(&IX)*&LEN-&WS_LOC
                 AIF (&WS_PAD GT 0)
                     :&WS_LOC SETA &WS_LOC+&WS_PAD
                 AEND
                 AIF (&IX GT 1)
                     AIF (&SYM_LEN(&IX-1) EQ 0) IS PRIOR WS ITEM 0 LEN
                         AIF (&SYM_LVL(&IX-1) EQ &SYM_LVL(&IX))
                             :&SYM_LEN(&IX-1) SETA &LEN      FIX 0 LEN
                         AEND
                     AEND
                 AEND 
             AEND  
         AEND
       AEND
         AEND
.*
.* STORE NEW 01-49 LEVEL WS ITEM AT CURRENT WS_LVL LEVEL
.*
         AENTRY WS_STORE_NN
         :&SYM_TOT SETA  &SYM_TOT+1
         :&SYM_NAME(&SYM_TOT) SETC '&NEW_SYM' ASSUME NAME VS CLAUSE   
         AIF  (&WS_LVL GT 1)
              :&QIX SETA &WS_LVL_SYM_IX(&WS_LVL-1)
         AELSE
              :&QIX SETA 0  
         AEND
         AIF  (&LK_MODE)        
              AIF  (&NEW_LVL_NO EQ 1)
                   :&LK_TOT  SETA &LK_TOT+1
                   :&LAST_LK_DSECT_IX SETA &SYM_TOT
                   :&WS_LOC SETA 0
              AEND
              :&SYM_LK(&SYM_TOT) SETA &LAST_LK_DSECT_IX
         AEND
         :&SYM_LVL(&SYM_TOT)  SETA &NEW_LVL_NO     SYMBOL LEVEL
         :&SYM_REF(&SYM_TOT)  SETA &REF_IX SYM 88,RDEF, OR LK VAR IX
         :&SYM_FD(&SYM_TOT)   SETA &FILE_IX SYM FD INDEX 
         :&SYM_LOC(&SYM_TOT)  SETA &WS_LOC  SYM FIELD WS OFFSET
         :&SYM_LEN(&SYM_TOT)  SETA 0        SYM FIELD LENGTH DEFAULT
         :&SYM_DUP(&SYM_TOT)  SETA 1        SYM FIELD OCCURANCES     
         :&SYM_PIC_TYPE(&SYM_TOT) SETC 'X'  SYM PIC TYPE DEFAULT DISP
         AIF   (&NEW_LVL_NO GT 49)
               MNOTE 8,'WS &ERR_ID INVALID LVL'
               MEXIT
         AEND
         :&SYM_IX_LST SETA &SYM_TOT     SAVE WS ITEM BEING ADDED
         ACALL WS_SCAN_CLAUSES          DEFINE ALL CLAUSES IN ANY ORDER
         ACALL WS_PROC_CLAUSES          PROCESS ALL CLAUSES FOR WS ITEM
         AIF  (&ZC_CICS                                                X
               AND '&NEW_SYM' EQ 'FILLER'                              X
               AND '&SYM_VAL(&SYM_TOT)' NE '') GEN FILLER NAME FOR VAL
               :&SYM_FILLER_VAL_CNT SETA &SYM_FILLER_VAL_CNT+1
               :&NEW_SYM SETC 'ZC_FILLER_V&SYM_FILLER_VAL_CNT'
               :&SYM_NAME(&SYM_TOT) SETC '&NEW_SYM'
               :&QIX SETA &SYM_QIX(&SYM_TOT)
         AEND
         :&SYM_QIX(&SYM_TOT) SETA &QIX SET QUALFIER IX OR 0     
         AIF  ('&NEW_SYM' NE 'FILLER')
              ZC_SYM_INDEX &NEW_SYM,&QIX   ADD QUALIFIED NAME
         AEND
         AIF   (&SYM_DUP(&SYM_TOT) GT 1)
               :&SYM_SIT(&SYM_TOT) SETA 1
         AEND
         :&CUR_QIX SETA &SYM_QIX(&SYM_TOT)
         AWHILE (&CUR_QIX GT 0)
               AIF (&SYM_DUP(&CUR_QIX) GT 1)
                 :&SYM_SIT(&SYM_TOT) SETA &SYM_SIT(&SYM_TOT)+1
               AEND
               :&CUR_QIX SETA &SYM_QIX(&CUR_QIX)
         AEND
         AIF   (&SYM_TOT GT 1)
               AIF (&SYM_LVL(&SYM_TOT-1) LT &SYM_LVL(&SYM_TOT)         X
                    AND '&SYM_PIC(&SYM_TOT-1)' NE '')
                   MNOTE 8,'WS ITEM LEVEL > PREV ELEMENTARY ITEM LEVEL'
               AEND
         AEND
         AEND
.*
.* END REDEFINE ADDING FILLER IF NEEDED AND RESET WS_LOC TO END
.*
         AENTRY WS_END_REDEFINE
         AIF   (&WS_LVL_RDEF_END(&WS_LVL) LT &WS_LOC)
               :&DIFF SETA &WS_LOC-&WS_LVL_RDEF_END(&WS_LVL)
               MNOTE 8,'WS &ERR_ID REDEFINE EXCEEDS LENGTH BY &DIFF'
         AEND
         AIF   (&WS_LVL_RDEF_END(&WS_LVL) GT &WS_LOC)
               :&WS_PAD SETA &WS_LVL_RDEF_END(&WS_LVL)-&WS_LOC
               ACALL GEN_WS_FILLER
         AEND
         :&WS_LOC SETA &WS_LVL_RDEF_END(&WS_LVL)
         :&WS_LVL_RDEF(&WS_LVL) SETB 0
         AEND
.*
.* GEN WS FILLER TO PAD REDEFINE GAP = &WS_PAD
.*
         AENTRY GEN_WS_FILLER
         :&SYM_TOT SETA  &SYM_TOT+1
         :&SYM_NAME(&SYM_TOT) SETC 'FILLER' PAD REDEFINE                   
         :&SYM_LVL(&SYM_TOT)  SETA &NEW_LVL_NO     SYMBOL LVL
         :&SYM_REF(&SYM_TOT)  SETA &REF_IX  SYM FILE,88,REDF,LK-V
         :&SYM_FD(&SYM_TOT)   SETA &FILE_IX SYM FD INDEX
         :&SYM_LOC(&SYM_TOT)  SETA &WS_LOC  SYM FIELD WS OFFSET
         :&SYM_LEN(&SYM_TOT)  SETA &WS_PAD 
         :&SYM_DUP(&SYM_TOT)  SETA 1        SYM FIELD OCCURANCES     
         :&SYM_PIC_TYPE(&SYM_TOT) SETC 'X'  SYM PIC TYPE DEF DSP
         AEND
.*
.* STORE SINGLE OR MULTIPLE 88 CONDITION VALUES FOR PRIOR WS ITEM
.*
         AENTRY WS_88
         :&SYM_TOT SETA  &SYM_TOT+1
         :&SYM_NAME(&SYM_TOT) SETC '&NEW_SYM'
         :&SYM_QIX(&SYM_TOT)  SETA &SYM_IX_LST  SET LEVEL RPI 1065
         ZC_SYM_INDEX &NEW_SYM
         :&SYM_LVL(&SYM_TOT) SETA &NEW_LVL_NO SYMBOL LVL
         AIF   (&SYM_IX_LST EQ 0)
               MNOTE 8,'WS &ERR_ID  MISSING PREVIOUS ITEM'
               MEXIT
         AEND
         AIF   ('&SYM_NAME(&SYM_IX_LST)' EQ 'FILLER') MAKE UNIQUE NAME
               :&SYM_FILLER_VAL_CNT SETA &SYM_FILLER_VAL_CNT+1 RPI 1065
               :&SYM_NAME(&SYM_IX_LST) SETC 'ZC_FILLER_V&SYM_FILLER_VALX
               _CNT'
         AEND
         :&SYM_REF(&SYM_TOT)  SETA &SYM_IX_LST  POINT TO LAST WS ITEM
         :&SYM_LOC(&SYM_TOT) SETA &V88_TOT+1 PTR TO FIRST 88 VALUE
         AIF   ('&SYSLIST(3)' EQ 'VALUE')                              
               :&I       SETA  4
               AIF   ('&SYSLIST(&I)' EQ 'IS')          
                     :&I SETA &I+1
               AELSEIF ('&SYSLIST(&I)' EQ 'ALL')   RPI 1012         
                     :&I SETA &I+1
                     :&SYM_VAL_ALL(&SYM_TOT) SETB 1 TURN ON ALL FLAG
               AEND
               :&V88_TOT SETA &V88_TOT+1
               :&V88_VAL(&V88_TOT) SETC '&SYSLIST(&I)'
               :&I SETA &I+1
               AIF  ('&V88_VAL(&V88_TOT)' EQ 'X')
                    :&V88_VAL(&V88_TOT) SETC 'X&SYSLIST(&I)'
                    :&I SETA &I+1
               AEND
               AIF  ('&V88_VAL(&V88_TOT)' EQ 'X')
                    :&V88_VAL(&V88_TOT) SETC 'X&SYSLIST(&I)'
                    :&I SETA &I+1
               AELSEIF  ('&V88_VAL(&V88_TOT)' EQ 'QUOTE'               X
                         OR '&V88_VAL(&V88_TOT)' EQ 'QUOTES')
                    :&V88_VAL(&V88_TOT) SETC ''''''''''  RPI 1065 'QQ'
               AEND
         AELSEIF ('&SYSLIST(3)' EQ 'VALUES')  
               :&NP      SETA  N'&SYSLIST
               :&I       SETA  4
               AIF   ('&SYSLIST(4)' EQ 'ARE')             
                     :&I       SETA  5
               AEND
               AWHILE (&I LE &NP)
                 AIF  ('&SYSLIST(&I+1)' EQ 'THRU'                      X
                     OR '&SYSLIST(&I+1)' EQ 'THROUGH')
                    :&V1 SETA &SYSLIST(&I)
                    :&V2 SETA &SYSLIST(&I+2)
                    AIF (&V1 GT &V2)
                        MNOTE 8,'WS 88 VALUE RANGE ERROR'
                        MEXIT
                    AEND
                    AWHILE (&V1 LE &V2)
                       :&V88_TOT SETA &V88_TOT+1
                       :&V88_VAL(&V88_TOT) SETC '&V1'
                       :&V88_NEXT(&V88_TOT) SETA &V88_TOT+1
                       :&V1 SETA &V1+1
                    AEND
                    :&I SETA &I+3
                 AELSE
                    :&V88_TOT SETA &V88_TOT+1
                    :&V88_VAL(&V88_TOT) SETC '&SYSLIST(&I)'
                    AIF  ('&V88_VAL(&V88_TOT)' EQ 'X')
                        :&V88_VAL(&V88_TOT) SETC 'X&SYSLIST(&I)'
                        :&I SETA &I+1
                    AELSEIF  ('&V88_VAL(&V88_TOT)' EQ 'QUOTE'          X
                              OR '&V88_VAL(&V88_TOT)' EQ 'QUOTES')
                        :&V88_VAL(&V88_TOT) SETC ''''''''''  1065 'QQ' 
                    AEND
                    :&V88_NEXT(&V88_TOT) SETA &V88_TOT+1
                    :&I SETA   &I+1
                 AEND
               AEND
               :&V88_NEXT(&V88_TOT) SETA 0
         AELSE
               MNOTE 8,'WS &ERR_ID INVALID LVL 88 VALUE &PIC_VAL'
         AEND
         AEND
.*
.* 66 name1 RENAMES name2 [THRU/THROUGH name3]  RPI 1012
.*
         AENTRY WS_66
         :&SYM_TOT SETA  &SYM_TOT+1
         :&SYM_NAME(&SYM_TOT) SETC '&NEW_SYM'
         :&SYM_QIX(&SYM_TOT) SETA &WS_LVL_SYM_IX(1)
         :&SYM_LVL(&SYM_TOT) SETA 2 REDEFINE 66 AS 02
         ZC_SYM_INDEX &NEW_SYM
         AIF   ('&SYSLIST(3)' NE 'RENAMES')
               MNOTE 8,'WS 66 MISSING RENAMES'
               MEXIT
         AEND                    
         :&PARM_IX SETA 4
         ACALL GET_PARM_FIELD
         AIF   (&FIELD_IX EQ 0)
               MNOTE 'WS RENAMED FIELD NOT FOUND - &SYSLIST(4)'
               MEXIT 
         AEND
         :&NAME2_IX SETA &FIELD_IX
         :&SYM_LOC(&SYM_TOT) SETA &SYM_LOC(&NAME2_IX)
         AIF   (N'&SYSLIST LT &PARM_IX)  REDEFINE SINGLE ITEM
               :&SYM_LEN(&SYM_TOT) SETA &SYM_LEN(&NAME2_IX)
               :&SYM_PIC_TYPE(&SYM_TOT) SETC 'X'
               AEXIT AENTRY
         AEND
         AIF   ('&SYSLIST(&PARM_IX)' NE 'THRU'                         X                              
               AND '&SYSLIST(&PARM_IX)' NE 'THROUGH')          
               MNOTE 8,'WS RENAME UNRECOGNIZED CLAUSE - &SYSLIST(&PARM_X
               IX)'
               MEXIT
         AEND
         :&PARM_IX SETA &PARM_IX+1
         ACALL GET_PARM_FIELD
         AIF   (&FIELD_IX EQ 0)
               MNOTE 'WS RENAMED FIELD NOT FOUND - &SYSLIST(&PARM_IX)'
               MEXIT 
         AEND
         :&NAME3_IX SETA &FIELD_IX
         :&SYM_LEN(&SYM_TOT) SETA &SYM_LOC(&NAME3_IX)+&SYM_LEN(&NAME3_IX
               X)-&SYM_LOC(&NAME2_IX)
         :&SYM_PIC_TYPE(&SYM_TOT) SETC 'X'
         AEND
.*
.* ADD FILE DCB
.* 
         AENTRY WS_91_FILE
         :&SYM_TOT SETA  &SYM_TOT+1
         :&SYM_NAME(&SYM_TOT) SETC '&NEW_SYM'
         ZC_SYM_INDEX &NEW_SYM
         :&SYM_LVL(&SYM_TOT) SETA &NEW_LVL_NO SYMBOL LVL
         :&SYM_FD(&SYM_TOT)  SETA &FILE_IX  POINT TO FILE FOUND IN FD
         :&WS_LOC SETA &WS_LOC+(&WS_LOC+7)/8*8-&WS_LOC 
         :&SYM_LOC(&SYM_TOT)  SETA &WS_LOC
         :&SYM_LEN(&SYM_TOT)  SETA &ZC_DCB_LEN
         :&SYM_DUP(&SYM_TOT)  SETA 1
         :&SYM_PIC_TYPE(&SYM_TOT) SETC 'X'
         :&WS_LOC SETA &WS_LOC+&ZC_DCB_LEN
         AEND
.*
.* SCAN FOR CLAUSES AND SET NAME TO FILLER IF NO NAME FIELD
.*
.*  INDEXED BY- SET  INDEX TO INDEX NAME FOR OCCURS FIELD                                
.*  OCCURS    - SET  DUP 
.*  PIC       - SETS PIC
.*  REDEFINES - SAVE CUR WS_LOC, RESET WS_LOC, TURN ON RDEF FLAG 
.*  SIGN      - SETS PIC_LEAD IF SIGN IS LEADING SEPARATE CHARACTER
.*  USAGE     - SETS USAGE
.*  VALUE     - SETS PIC_VAL
.*
         AENTRY WS_SCAN_CLAUSES
         AIF   (&WS_LVL GT 1)
               :&USAGE SETC '&WS_LVL_USE(&WS_LVL-1)'
         AELSE
               :&USAGE   SETC ''
         AEND
         :&INDEX   SETC ''
         :&PIC     SETC  ''
         :&PIC_TYPE SETC 'X'  DEFAULT DISPLAY X (INCLUDES A AND Z)
         :&PIC_LEN  SETA  0   EXPLICIT LENGTH IF ANY
         :&PIC_SIGN SETB  0   DEFAULT UNSIGNED
         :&PIC_LEAD SETB  0   DEFAULT SIGN NOT LEADING
         :&PIC_VAL  SETC  ''  INITIAL VALUE IF ANY
         :&PIC_VAL_ALL SETB 0 REPEAT VALUE           RPI 1012
         :&DUP     SETA  1    DEFAULT OCCURANCES
         :&NP      SETA  N'&SYSLIST
         :&I       SETA  2
         AWHILE (&I LE &NP)
               AIF ('&SYSLIST(&I)' EQ 'PIC'                            X
                    OR '&SYSLIST(&I)' EQ 'PICTURE')         
                    AIF  (&I EQ 2)    
                         :&NEW_SYM SETC 'FILLER'            
                         :&SYM_NAME(&SYM_TOT) SETC 'FILLER'
                    AEND
                    :&I       SETA  &I+1
                    AIF   ('&SYSLIST(&I)' EQ 'IS')
                          :&I       SETA  &I+1
                    AEND
                    ACALL SET_PIC_AND_LEN
                    :&I  SETA  &I+1
               AELSEIF   ('&SYSLIST(&I)' EQ 'VALUE')          
                    AIF  (&I EQ 2)                
                         :&NEW_SYM SETC 'FILLER'            
                         :&SYM_NAME(&SYM_TOT) SETC 'FILLER'
                    AEND
                    :&I       SETA  &I+1
                    AIF   ('&SYSLIST(&I)' EQ 'IS')
                         :&I       SETA  &I+1
                    AELSEIF ('&SYSLIST(&I)' EQ 'ALL')   RPI 1012         
                     :&I SETA &I+1
                     :&PIC_VAL_ALL SETB 1
                    AEND
                    AIF  (&WS_LVL NE 0)                                 
                         AIF (&WS_LVL_RDEF(&WS_LVL) AND &ZC_WARN)
                             MNOTE 4,'WS &ERR_ID REDEFINE VALUE IGNOREDX
               '
                         AELSE
                             :&PIC_VAL SETC '&SYSLIST(&I)'
                         AEND
                    AELSE
                         :&PIC_VAL SETC '&SYSLIST(&I)'
                    AEND
                    :&I       SETA  &I+1
                    AIF  ('&PIC_VAL' EQ 'ZERO'                         X
                          OR '&PIC_VAL' EQ 'ZEROS'                     X
                          OR '&PIC_VAL' EQ 'ZEROES')
                         :&PIC_VAL SETC '0' 
                    AELSEIF  ('&PIC_VAL' EQ 'X')
                         :&PIC_VAL SETC 'X&SYSLIST(&I)'
                         :&I SETA &I+1
                    AELSEIF ('&PIC_VAL' EQ '+' OR '&PIC_VAL' EQ '-')
                         :&PIC_VAL SETC '&PIC_VAL&SYSLIST(&I)'
                         :&I SETA &I+1
                    AELSEIF ('&PIC_VAL' EQ '''&&''')
                         :&PIC_VAL SETC '''&&&&''' REQ'D FOR DC
                    AELSEIF  ('&PIC_VAL' EQ 'QUOTE'                    X
                              OR '&PIC_VAL' EQ 'QUOTES')
                         :&PIC_VAL SETC ''''''''''  RPI 1065 'QQ'
                    AEND
                    AIF  ('&SYSLIST(&I)' EQ '+')  RPI 1126
                         :&PIC_VAL SETC '&PIC_VAL+&SYSLIST(&I+1)'
                         :&I SETA &I+2
                    AEND
               AELSEIF   ('&SYSLIST(&I)' EQ 'OCCURS')    
                    :&CUR_SET_LVL SETA &CUR_SET_LVL+1  INC SUB LVL CTR
                    :&DUP SETA  &SYSLIST(&I+1)
                    :&I   SETA  &I+2
                    AIF   ('&SYSLIST(&I)' EQ 'TIMES')
                          :&I       SETA  &I+1
                    AELSEIF ('&SYSLIST(&I)' EQ 'TO') n1 TO n2 TIMES
                          :&DUP_MIN SETA &DUP
                          :&DUP SETA &SYSLIST(&I+1)   (USE N2)
                          :&I   SETA &I+2
                          AIF   ('&SYSLIST(&I)' EQ 'TIMES')
                                :&I       SETA  &I+1
                          AEND
                          AIF   ('&SYSLIST(&I)' EQ 'DEPENDING'         X
                                 AND '&SYSLIST(&I+1)' EQ 'ON')
                                :&SYM_DEPEND(&I) SETC '&SYSLIST(&I+2)'
                                :&I SETA &I+3
                          AELSE
                                MNOTE 8,'WS &ERR_ID MISSING DEPENDING OX
               N'
                          AEND
                    AEND     
                    :&SYM_DUP(&SYM_TOT) SETA &DUP  
               AELSEIF   ('&SYSLIST(&I)' EQ 'INDEXED')                 
                    :&I       SETA  &I+1
                    AIF  ('&SYSLIST(&I)' EQ 'BY')  RPI 1062
                         :&I SETA &I+1
                    AEND
                    :&INDEX   SETC '&SYSLIST(&I)'
                    :&I       SETA &I+1
               AELSEIF   ('&SYSLIST(&I)' EQ 'REDEFINES')              
                    ZC_SYM_FIND &SYSLIST(&I+1)
                    AIF   (&SYM_IX EQ 0)
                          MNOTE 8,'WS &ERR_ID REDEFINE ITEM NOT FOUND -X
                &SYSLIST(&I+1)'
                          MEXIT
                    AELSE
                      AIF (&SYM_FXT(&SYM_IX) GT 0)  RPI 1065
                          :&SYM_IX SETA &SYM_FXT(&SYM_IX)
                          AWHILE (&SYM_IX GT 0) 
                              AIF (&WS_LVL_SYM_IX(&WS_LVL-1) EQ &SYM_QIX
               X(&SYM_IX))            
                                  AEXIT AWHILE
                              AELSE
                                  :&SYM_IX SETA &SYM_NXT(&SYM_IX)
                                  AIF (&SYM_IX EQ 0)
         MNOTE 8,'WS QUAL REDEFINE NOT FOUND FOR - &SYM_NAME(&SYM_TOT)'
                          MEXIT
                                  AEND
                              AEND
                          AEND
                      AEND
                    AEND
                    :&SYM_REF(&SYM_TOT) SETA &SYM_IX  SET REDEFINE IX 
                    AIF (&SYM_LEN(&SYM_IX) EQ 0)
                        :&SYM_LEN(&SYM_IX) SETA &WS_LOC-&SYM_LOC(&SYM_IX
               X)
                    AEND
                    :&WS_LVL_RDEF_END(&WS_LVL) SETA &SYM_LOC(&SYM_IX)+&X
               SYM_LEN(&SYM_IX)*&SYM_DUP(&SYM_IX)
                    :&WS_LOC  SETA  &SYM_LOC(&SYM_IX) RESET LOC TO RED
                    :&WS_LVL_RDEF_IX(&WS_LVL) SETA &SYM_IX WS SYM IX
                    :&SYM_LOC(&SYM_TOT) SETA &WS_LOC
                    :&WS_LVL_RDEF(&WS_LVL) SETB 1     TURN ON REDEFINE
                    :&I       SETA  &I+2
               AELSEIF   ('&SYSLIST(&I)' EQ 'SYNC'                     X
                       OR '&SYSLIST(&I)' EQ 'SYNCHRONIZED') RPI 1062
                    :&SYM_OPT(&SYM_TOT) SETC '&SYM_OPT(&SYM_TOT).S'
                    :&I       SETA  &I+1
                    AIF   ('&SYSLIST(&I)' EQ 'RIGHT'                   X 
                        OR '&SYSLIST(&I)' EQ 'LEFT') RPI 1062
                          :&I       SETA  &I+1
                    AEND
               AELSEIF   ('&SYSLIST(&I)' EQ 'USAGE')            
                    :&I       SETA  &I+1
                    AIF   ('&SYSLIST(&I)' EQ 'IS') 
                          :&I       SETA  &I+1
                    AEND
                    :&USAGE   SETC  '&SYSLIST(&I)'
                    :&I       SETA  &I+1
               AELSEIF   ('&SYSLIST(&I)' EQ 'SIGN')
                    :&I       SETA  &I+1
                    AIF   ('&SYSLIST(&I)' EQ 'IS') 
                          :&I       SETA  &I+1
                    AEND
                    AIF   ('&SYSLIST(&I)' EQ 'LEADING') 
                          :&PIC_LEAD SETB 1      LEADING SIGN 
                          :&I       SETA  &I+1
                    AELSEIF ('&SYSLIST(&I)' EQ 'TRAILING')
                          :&PIC_TRAIL SETB 1      TRAILING SIGN 
                          :&I       SETA  &I+1
                    AELSE
                          MNOTE 8,'WS SIGN UNKNOWN OPTION &SYLIST(&I)'
                          MEXIT
                    AEND
                    AIF   ('&SYSLIST(&I)' EQ 'SEPARATE')
                          :&PIC_SEP SETB 1
                          :&I  SETA  &I+1
                          AIF  ('&SYSLIST(&I)' EQ 'CHARACTER')
                               :&I SETA  &I+1
                          AEND
                    AEND
               AELSEIF  ('&SYSLIST(&I)' EQ 'LEADING') 
                    :&PIC_LEAD SETB 1      LEADING SIGN RPI 1012
                    :&I       SETA  &I+1
                    AIF   ('&SYSLIST(&I)' EQ 'SEPARATE')
                          :&PIC_SEP SETB 1
                          :&I  SETA  &I+1
                          AIF  ('&SYSLIST(&I)' EQ 'CHARACTER')
                               :&I SETA  &I+1
                          AEND
                    AEND
               AELSEIF ('&SYSLIST(&I)' EQ 'TRAILING')
                    :&PIC_TRAIL SETB 1      TRAILING SIGN RPI 1012
                    :&I       SETA  &I+1
                    AIF   ('&SYSLIST(&I)' EQ 'SEPARATE')
                          :&PIC_SEP SETB 1
                          :&I  SETA  &I+1
                          AIF  ('&SYSLIST(&I)' EQ 'CHARACTER')
                               :&I SETA  &I+1
                          AEND
                    AEND
               AELSEIF ('&SYSLIST(&I)'  EQ 'COMP'                      X
                      OR '&SYSLIST(&I)' EQ 'COMPUTATIONAL'             X
                      OR '&SYSLIST(&I)' EQ 'COMP_1'                    X
                      OR '&SYSLIST(&I)' EQ 'COMP_2'                    X
                      OR '&SYSLIST(&I)' EQ 'COMP_3'                    X
                      OR '&SYSLIST(&I)' EQ 'COMP_4'                    X
                      OR '&SYSLIST(&I)' EQ 'COMP_5'                    X
                      OR '&SYSLIST(&I)' EQ 'BINARY'                    X
                      OR '&SYSLIST(&I)' EQ 'FLOAT_BINARY_7'            X
                      OR '&SYSLIST(&I)' EQ 'FLOAT_BINARY_16'           X
                      OR '&SYSLIST(&I)' EQ 'FLOAT_BINARY_34'           X
                      OR '&SYSLIST(&I)' EQ 'FLOAT_DECIMAL_7'           X
                      OR '&SYSLIST(&I)' EQ 'FLOAT_DECIMAL_16'          X
                      OR '&SYSLIST(&I)' EQ 'FLOAT_DECIMAL_34'          X
                      OR '&SYSLIST(&I)' EQ 'FLOAT_HEX_7'               X
                      OR '&SYSLIST(&I)' EQ 'FLOAT_HEX_15'              X
                      OR '&SYSLIST(&I)' EQ 'FLOAT_HEX_30'              X
                      OR '&SYSLIST(&I)' EQ 'FLOAT_SHORT'               X
                      OR '&SYSLIST(&I)' EQ 'FLOAT_LONG'                X
                      OR '&SYSLIST(&I)' EQ 'FLOAT_EXTENDED'            X
                      OR '&SYSLIST(&I)' EQ 'INDEX'                     X
                      OR '&SYSLIST(&I)' EQ 'POINTER') 1002 1004 1042
                    :&USAGE   SETC  '&SYSLIST(&I)'
                    :&I       SETA  &I+1
               AELSEIF ('&SYSLIST(&I)' EQ 'DISPLAY')
                    :&I SETA &I+1  RPI 1065 IGNORE (ITS THE DEFAULT)
               AELSEIF ('&SYSLIST(&I)' EQ 'LINE')
                    AIF ('&SYSLIST(&I+1)' EQ 'NUMBER'                  X
                         AND '&SYSLIST(&I+2)' EQ 'IS'                  X
                         AND '&SYSLIST(&I+3)' EQ 'PLUS')
                        :&I SETA &I+5
                    AELSE
                        :&I SETA &I+2  
                    AEND
                    MNOTE 8,'WS REPORT WRITER LINE CLAUSE IGNORED'
               AELSEIF ('&SYSLIST(&I)' EQ 'COLUMN')
                    AIF  (&I EQ 2)                
                        :&NEW_SYM SETC 'FILLER'            
                        :&SYM_NAME(&SYM_TOT) SETC 'FILLER'
                    AEND
                    AIF ('&SYSLIST(&I+1)' EQ 'NUMBER'                  X
                        AND '&SYSLIST(&I+2)' EQ 'IS')
                        :&I SETA &I+4
                    AELSE
                        :&I SETA &I+2  RPI 1065 IGNORE RW LINE NUM
                    AEND
                    MNOTE 8,'WS REPORT WRITER COLUMN CLAUSE IGNORED'
               AELSEIF ('&SYSLIST(&I)' EQ 'SOURCE')
                    AIF ('&SYSLIST(&I+1)' EQ 'IS')
                        :&I SETA &I+3
                    AELSE
                        :&I SETA &I+2  RPI 1065 IGNORE RW LINE NUM
                    AEND
                    MNOTE 8,'WS REPORT WRITER SOURCE CLAUSE IGNORED'
               AELSEIF ('&SYSLIST(&I)' EQ 'TYPE'                       X
                        AND '&SYSLIST(&I+1)' EQ 'IS')
                    :&I SETA &I+2  RPI 1065 IGNORE RW TYPE
                    MNOTE 8,'WS REPORT WRITER TYPE CLAUSE IGNORED'
                    AIF ('&SYSLIST(&I+2)' EQ 'PAGE')
                        :&I SETA &I+4
                    AELSE
                        :&I SETA &I+3
                    AEND
               AELSEIF ('&SYSLIST(&I)' EQ 'JUSTIFIED'                  X
                        OR '&SYSLIST(&I)' EQ 'JUST') RPI 1065
                        :&SYM_OPT(&SYM_TOT) SETC '&SYM_OPT(&SYM_TOT).R'
                    AIF ('&SYSLIST(&I+1)' EQ 'RIGHT')
                        :&I SETA &I+2
                    AELSE
                        :&I SETA &I+2   
                    AEND 
               AELSEIF ('&SYSLIST(&I)' EQ 'LEADING')
                    :&SYM_OPT(&SYM_TOT) SETC '&SYM_OPT(&SYM_TOT).L'
                    :&I SETA &I+1    
               AELSEIF ('&SYSLIST(&I)' EQ 'BLANK')
                    :&SYM_OPT(&SYM_TOT) SETC '&SYM_OPT(&SYM_TOT).B'
                    :&I SETA &I+1
                    AIF ('&SYSLIST(&I)' EQ 'WHEN')
                        :&I SETA &I+1
                    AEND
                    AIF ('&SYSLIST(&I)' EQ 'ZERO')
                        :&I SETA &I+1
                    AEND
               AELSEIF ('&SYSLIST(&I)' EQ 'GLOBAL')
                    :&SYM_OPT(&SYM_TOT) SETC '&SYM_OPT(&SYM_TOT).G'
                    :&I SETA &I+1
               AELSEIF ('&SYSLIST(&I)' EQ 'EXTERNAL')
                    :&SYM_OPT(&SYM_TOT) SETC '&SYM_OPT(&SYM_TOT).E'
                    :&I SETA &I+1
               AELSEIF (&I GT 2)
                    MNOTE 8,'WS &ERR_ID UNRECOGNIZED CLAUSE &SYSLIST(&IX
               )'
                    MEXIT
               AELSE
                    :&I SETA &I+1
               AEND
         AEND
         AEND
.*
.* SET PIC AND DEFAULT PIC_LEN
.*
.*  1.  IF LEADING S SET PIC_SIGN AND REMOVE S
.*  2.  DO NOT ALLOW QUOTE IN PIC CLAUSE
.*  3.  Set SYM_PIC_DEC for V or '.' RPI 1138
.*                               
         AENTRY  SET_PIC_AND_LEN
         :&PIC     SETC '&SYSLIST(&I)'
         AIF  ('&PIC'(1,1) EQ '''')
              :&PIC SETC '&PIC'(2,K'&PIC-2) 
         AEND
         AIF  ('&PIC'(1,1) EQ 'S')
              :&PIC_SIGN SETB 1
              :&PIC     SETC  '&PIC'(2,*)
         AEND
         :&J       SETA 1
         :&DEC_MODE SETB 0
         AWHILE (&J LE K'&PIC)
                AIF ('&PIC'(&J,1) EQ 'V')
                    :&DEC_MODE SETB 1
                AELSEIF ('&PIC'(&J,2) EQ 'X('                          X
                         OR '&PIC'(&J,2) EQ '9('                       X
                         OR '&PIC'(&J,2) EQ 'A('                       X
                         OR '&PIC'(&J,2) EQ 'P('                       X
                         OR '&PIC'(&J,2) EQ 'Z(')  RPI 1065 ADD P,A
                    :&PIC_DUP SETA D2A('&PIC'(&J+2,1)) RPI 1139
                    :&J SETA &J+3
                    AWHILE (&J LT K'&PIC                               X
                            AND '&PIC'(&J,1) NE ')')
                        :&DIGIT   SETA D2A('&PIC'(&J,1)) RPI 1139
                        :&PIC_DUP SETA &PIC_DUP*10+&DIGIT
                        :&J SETA &J+1
                    AEND
                    :&PIC_LEN SETA &PIC_LEN+&PIC_DUP
                    AIF (&DEC_MODE)
                        :&SYM_PIC_DEC(&SYM_TOT) SETA &SYM_PIC_DEC(&SYM_X
               TOT)+&PIC_DUP
                    AEND
                AELSEIF ('&PIC'(&J,1) EQ '.' AND K'&PIC LE &J)   RPI 1181
                    AEXIT AWHILE
                AELSEIF ('&PIC'(&J,1) EQ '''')  RPI 1032
                    MNOTE 8,'WS UNKNOWN PIC CLAUSE &PIC'
                    AEXIT AWHILE
                AELSE
                    :&PIC_LEN SETA &PIC_LEN+1
                    AIF ('&PIC'(&J,1) EQ '.') RPI 1138
                        :&DEC_MODE SETB 1
                    AELSEIF (&DEC_MODE)
                        :&SYM_PIC_DEC(&SYM_TOT) SETA &SYM_PIC_DEC(&SYM_X
               TOT)+1 
                    AEND
                AEND
                :&J SETA &J+1
         AEND
         AEND
.*
.* PROCESS WS ITEM WITH ALL CLAUSE VALUES DEFINED
.*
         AENTRY WS_PROC_CLAUSES
         AIF   (&NP LT 2)
               :&NEW_SYM SETC 'FILLER'            
               :&SYM_NAME(&SYM_TOT) SETC 'FILLER'
         AELSEIF ('&USAGE' EQ 'COMP'                                   X
                  OR '&USAGE' EQ 'COMPUTATIONAL'                       X
                  OR '&USAGE' EQ 'BINARY'                              X
                  OR '&USAGE' EQ 'COMP_4'                              X
                  OR '&USAGE' EQ 'COMP_5')  RPI 1042 RPI 1090            
               AIF  ('&PIC' NE '')  
                    AIF  (&PIC_LEN LE 0) 
                         :&PIC_LEN SETA K'&PIC
                    AEND
                    AIF  (&PIC_LEN LE 4)
                         :&PIC_LEN SETA 2
                         :&PIC_TYPE SETC 'H'
                    AELSEIF (&PIC_LEN LE 9)
                         :&PIC_LEN  SETA 4
                         :&PIC_TYPE SETC 'F'
                    AELSEIF (&PIC_LEN LE 18)
                         :&PIC_LEN  SETA 8
                         :&PIC_TYPE SETC 'G'
                    AELSEIF (&ZC_EXTEND AND &PIC_LEN LE 39) RPI 1004
                         :&PIC_LEN  SETA 16
                         :&PIC_TYPE SETC 'Q'
                    AELSE
                         MNOTE 8,'WS INVALID COMP PIC LEN - &PIC_LEN'
                    AEND
               AEND
               :&NUM     SETC '&PIC_VAL'
               :&DEC_SYM SETA &SYM_PIC_DEC(&SYM_TOT)
               ACALL SCALE_NUM_LIT
               :&PIC_VAL SETC '&NUM'
         AELSEIF ('&USAGE' EQ 'COMP_1')                 
               :&PIC_LEN SETA 4
               :&PIC_TYPE SETC '1'       RPI 1004 WAS E NOW 1=EH
         AELSEIF ('&USAGE' EQ 'COMP_2')                
               :&PIC_LEN SETA 8
               :&PIC_TYPE SETC '2'       RPI 1004 WAS D NOW 2=DH
         AELSEIF ('&USAGE' EQ 'COMP_3')             
               AIF   ('&PIC' NE '')                       
                     AIF  (&PIC_LEN EQ 0)                 
                          :&PIC_LEN SETA K'&PIC
                     AEND
               AEND
               :&PIC_TYPE SETC 'P'
               :&NUM     SETC '&PIC_VAL'
               :&DEC_SYM SETA &SYM_PIC_DEC(&SYM_TOT)
               ACALL SCALE_NUM_LIT
               :&PIC_VAL SETC '&NUM'
         AELSEIF ('&USAGE' EQ 'FLOAT_HEX_7')     RPI 1004      
              :&PIC_LEN SETA 4
              :&PIC_TYPE SETC '1'
         AELSEIF ('&USAGE' EQ 'FLOAT_HEX_15')      RPI 1004      
              :&PIC_LEN SETA 8
              :&PIC_TYPE SETC '2'
         AELSEIF ('&USAGE' EQ 'FLOAT_HEX_30' AND &ZC_EXTEND) RPI 1004      
              :&PIC_LEN SETA 16
              :&PIC_TYPE SETC '3'
         AELSEIF ('&USAGE' EQ 'FLOAT_BINARY_7' AND &ZC_EXTEND) RPI 1004      
              :&PIC_LEN SETA 4
              :&PIC_TYPE SETC '4'
         AELSEIF ('&USAGE' EQ 'FLOAT_BINARY_16' AND &ZC_EXTEND) RPI1004      
              :&PIC_LEN SETA 8
              :&PIC_TYPE SETC '5'
         AELSEIF ('&USAGE' EQ 'FLOAT_BINARY_34' AND &ZC_EXTEND) RPI1004      
              :&PIC_LEN SETA 16
              :&PIC_TYPE SETC '6'
         AELSEIF ('&USAGE' EQ 'FLOAT_DECIMAL_7' AND &ZC_EXTEND) RPI1004      
              :&PIC_LEN SETA 4
              :&PIC_TYPE SETC '7'
         AELSEIF ('&USAGE' EQ 'FLOAT_DECIMAL_16' AND &ZC_EXTEND) RP1004      
              :&PIC_LEN SETA 8
              :&PIC_TYPE SETC '8'
         AELSEIF ('&USAGE' EQ 'FLOAT_DECIMAL_34' AND &ZC_EXTEND) RP1004      
              :&PIC_LEN SETA 16
              :&PIC_TYPE SETC '9'
         AELSEIF ('&USAGE' EQ 'FLOAT_SHORT' AND &ZC_EXTEND) RPI 1004      
              :&PIC_LEN SETA 4
              :&PIC_TYPE SETC '&ZC_FLOAT_SHORT_TYPE'
         AELSEIF ('&USAGE' EQ 'FLOAT_LONG' AND &ZC_EXTEND)  RPI 1004      
              :&PIC_LEN SETA 8
              :&PIC_TYPE SETC '&ZC_FLOAT_LONG_TYPE'
         AELSEIF ('&USAGE' EQ 'FLOAT_EXTENDED' AND &ZC_EXTEND) RPI 1004      
              :&PIC_LEN SETA 16
              :&PIC_TYPE SETC '&ZC_FLOAT_EXTENDED_TYPE'
         AELSEIF ('&USAGE' EQ 'INDEX')           RPI 1002         
              :&PIC_LEN SETA 4
              :&PIC_TYPE SETC 'I'
         AELSEIF ('&USAGE' EQ 'POINTER')                   
              :&PIC_LEN SETA 4
              :&PIC_TYPE SETC 'A'
         AELSEIF ('&USAGE' EQ 'PROCEDURE_POINTER')   RPI 1010                
              :&PIC_LEN SETA 4
              :&PIC_TYPE SETC 'A'
         AELSEIF (K'&PIC GT 0)
               AIF   (('&PIC'(1,1) FIND '9PV') GT 0) RPI 1062 ADD PV             
                     :&PIC_TYPE SETC 'Z'
                     :&NUM     SETC '&PIC_VAL'
                     :&DEC_SYM SETA &SYM_PIC_DEC(&SYM_TOT)
                     ACALL SCALE_NUM_LIT
                     :&PIC_VAL SETC '&NUM'
               AEND
               AIF   (&PIC_LEN EQ 0)               
                     :&PIC_LEN SETA K'&PIC
               AEND
         AEND
         :&SYM_PIC_TYPE(&SYM_TOT) SETC '&PIC_TYPE'
         :&SYM_PIC_SIGN(&SYM_TOT) SETB &PIC_SIGN
         :&SYM_PIC(&SYM_TOT) SETC '&PIC'     SYM FIELD PIC (999,X(3))
         AIF (K'&PIC_VAL GT 3)
             AIF ('&PIC_VAL'(1,2) EQ 'X"')   CONVERT X"??" TO X'??'
                 :&PIC_VAL SETC 'X'''.'&PIC_VAL'(3,K'&PIC_VAL-3).''''
             AEND
         AEND
         AIF  ('&PIC_TYPE' EQ 'X'                                      X
               AND '&PIC_VAL' NE '')
               :&AMP SETA ('&PIC_VAL' FIND '&&')
               AWHILE (&AMP GT 0)
                  :&PIC_VAL SETC '&PIC_VAL'(1,&AMP).'&&'.'&PIC_VAL'(&AMX
               P+1,*)
                  :&NEXT_AMP SETA ('&PIC_VAL'(&AMP+2,*) FIND '&&')
                  AIF (&NEXT_AMP GT 0)
                      :&AMP SETA &AMP+&NEXT_AMP+1
                  AELSE
                      :&AMP SETA 0
                  AEND
               AEND
         AELSEIF (K'&PIC_VAL GT 2)  REMOVE '..' FROM NUMERIC VALUES               
               AIF ('&PIC_VAL'(1,1) EQ '''')   RPI 1065
                   :&PIC_VAL SETC '&PIC_VAL'(2,K'&PIC_VAL-2)
               AEND
         AEND
         :&SYM_VAL(&SYM_TOT) SETC '&PIC_VAL' SYM FIELD VALUE (0,SPACE)
         :&SYM_VAL_ALL(&SYM_TOT) SETB &PIC_VAL_ALL  RPI 1012
         AIF   ('&PIC_TYPE' EQ 'P')
               :&PIC_LEN SETA &PIC_LEN/2+1
         AEND
         :&SYM_LEN(&SYM_TOT) SETA  &PIC_LEN  SYM FILED LENGTH
         :&SYM_DUP(&SYM_TOT) SETA  &DUP      SYM FIELD OCCURANCES
         AIF   (&PIC_LEN EQ 0)
               ZC_PIC_LEN
               :&PIC_LEN SETA  &SYM_LEN(&SYM_TOT)
         AEND
         :&WS_LOC  SETA  &WS_LOC+&SYM_DUP(&SYM_TOT)*&SYM_LEN(&SYM_TOT)
         AIF   (&DUP GT 1 AND '&INDEX' NE '')                     
               :&SET_TOT SETA  &SET_TOT+1
               :&SET_SYM_IX(&SET_TOT) SETA &SYM_TOT
               :&SET_SYM_LVL(&SET_TOT) SETA &CUR_SET_LVL
               :&SET_INDEX(&SET_TOT) SETC '&INDEX'
         AEND
         AEND
.*
.* ADD NEW LVL
.*
         AENTRY WS_ADD_LVL
         :&WS_LVL     SETA  &WS_LVL+1
         :&WS_LVL_NO(&WS_LVL) SETA &NEW_LVL_NO
         :&WS_LVL_SYM_IX(&WS_LVL) SETA 0
         AEND
         COPY  ZCGETFLD
         COPY  ZCNUMLIT
         MEND
