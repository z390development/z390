         MACRO
.*********************************************************************
.* z390 - Mainframe assembler emulator and run-time engine
.* Copyright (C) 2021 z390 Assembler LLC
.*
.* This file is part of z390.
.*
.* z390 is free software; you can redistribute it and/or modify
.* it under the terms of the GNU General Public License as published by
.* the Free Software Foundation; either version 2 of the License, or
.* (at your option) any later version.
.* z390 is distributed in the hope that it will be useful,
.* but WITHOUT ANY WARRANTY; without even the implied warranty of
.* MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
.* GNU General Public License for more details.
.*
.* You should have received a copy of the GNU General Public License 
.* along with this program; if not, see https://www.gnu.org/licenses.
.* Author - Don Higgins                                              *
.* Date   - 04/07/08                                                 *
.*********************************************************************
.* 04/11/08 initial coding for z390 ACCEPT
.* 10/06/08 ZSTRMAC
.* 07/25/09 RPI 1065 SUPPORT DEFAULT DDNAME=PGMID OR SYSIN        
.*          AND FROM CONSOLE OR FROM DDNAME
.*********************************************************************
         GEN_ACCEPT 
         COPY   ZC_WS
         GBLB   &ZC_ACCEPT
         :&ZC_ACCEPT SETB 1 REQUEST ACCEPT OPTION FLAGS IN GEN_PROC_END
         :&PARM_IX SETA 1
         :&FIELD_SS1 SETB 0
         ACALL GET_PARM_FIELD
         AIF   (&FIELD_IX EQ 0)
               MNOTE 8,'ACCEPT TARGET FIELD NOT FOUND - &FIELD_NAME'
               MEXIT
         AEND
         LA    ZC_R2,&FIELD_NAME
         :&LTARGET  SETA &SYM_LEN(&FIELD_IX)
         :&FROM     SETC '&SYSLIST(&PARM_IX)'
       AIF    ('&FROM' EQ '') DEFAULT DDNAME=SYSIN
         LARL  ZC_R1,=A(ZCVT_ACCEPT_DEFAULT,&LTARGET)
         L     ZC_R15,ZCVT_ACCEPT
         BASR  ZC_R14,ZC_R15
         MEXIT
       AELSEIF ('&FROM' NE 'FROM')
         MNOTE 8,'ACCEPT UNKNOWN OPERAND - &FROM'
         MEXIT
       AEND
       :&SOURCE SETC '&SYSLIST(&PARM_IX+1)'
       AIF     ('&SOURCE' EQ 'CONSOLE')            
.* FROM CONSOLE
         :&LNAME SETA K'&SYM_NAME(&FIELD_IX)+1/2*2
         LARL  ZC_R1,=A(ZCVT_ACCEPT_CONSOLE,&LTARGET,&LNAME)
         LARL  ZC_R3,=CL&LNAME'&SYM_NAME(&FIELD_IX)'
       AELSEIF ('&SOURCE' EQ 'DATE')               
.* FROM DATE - MMDDYYYY TO PIC 9(8)
         LARL  ZC_R1,=A(ZCVT_ACCEPT_DATE,&LTARGET)
       AELSEIF ('&SOURCE' EQ 'TIME')          
.* FROM TIME - HHMMSSTH TO PIC 9(8)
         LARL  ZC_R1,=A(ZCVT_ACCEPT_TIME,&LTARGET)
       AELSEIF ('&SOURCE' EQ 'DAY_OF_WEEK')  
.* FROM DAY-OF-WEEK -  DOW (1-7) TO PIC 9
         LARL  ZC_R1,=A(ZCVT_ACCEPT_DOW,&LTARGET)
       AELSE
.* FROM DDNAME
         AIF (K'&SOURCE EQ 0 OR K'&SOURCE GT 8)
             MNOTE 8,'ACCEPT FROM DDNAME INVALID LENGTH - &SOURCE'
             MEXIT
         AEND
         LARL  ZC_R1,=A(ZCVT_ACCEPT_DDNAME,&LTARGET)
         LARL  ZC_R3,=CL8'&SOURCE'
       AEND    
         L     ZC_R15,ZCVT_ACCEPT
         BASR  ZC_R14,ZC_R15
         MEXIT
         COPY  ZCGETFLD
         MEND
