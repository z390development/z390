         MACRO
.*********************************************************************
.* Copyright 2008 Automated Software Tools Corporation               *
.* This source code is part of z390 zCOBOL/assembler/emulator package*
.* The z390 package is distributed under GNU general public license  *
.* Author - Don Higgins                                              *
.* Date   - 03/12/09                                                 *
.*********************************************************************
.* 03/12/09 initial coding for use in GEN_ADD/SUB/MPY/DIV for 1-9,Q   
.* 03/26/09 RPI 1004 use CVTQTLB,CVTLBTQ FOR Q And CVTLDTP for P   
.*********************************************************************
.* CONVERT NAME2 FROM TYPE2 TO TYPE1 VALUE IN F0-F2 
.* FOR FP OR ZC_R0-ZC_R1 Q
.*********************************************************************
         ZC_CONV &TYPE1,&TYPE2,&NAME2
         COPY  ZC_WS
         AIF   (K'&TYPE1 LT 1 OR K'&TYPE1 GT 2                         X
               OR K'&TYPE2 LT 1 OR K'&TYPE2 GT 2)
               MNOTE 8,'ZC_CONV INVALID TYPE LENGTH'
               MEXIT
         AEND
         AIF   ('&TYPE1' EQ '&TYPE2')
               AIF  ('&TYPE1'(1,1) EQ 'E')
                    AIF (T'&NAME2 EQ 'O')
         LER   ZC_F0,ZC_F4
                    AELSE
         LE    ZC_F0,&NAME2
                    AEND
               AELSEIF ('&TYPE1'(1,1) EQ 'D')
                    AIF (T'&NAME2 EQ 'O')
         LDR   ZC_F0,ZC_F4
                    AELSE
         LD    ZC_F0,&NAME2
                    AEND
               AELSEIF ('&TYPE1'(1,1) EQ 'L')
                    AIF (T'&NAME2 EQ 'O')
         LDR   ZC_F0,ZC_F4
         LDR   ZC_F2,ZC_F6
                    AELSE
         LD    ZC_F0,&NAME2
         LD    ZC_F2,8+&NAME2
                    AEND
               AELSE
                    MNOTE 8,'ZC_CONV INVALID TYPE - &TYPE1'
                    MEXIT
               AEND
               MEXIT
         AEND
       :&PFPO_FROM SETC 'PFPO_TYPE_&TYPE2'  
       AIF ('&TYPE2' EQ 'Q') CVT Q TO LB      
           AIF ('&NAME2' NE '')  ALREADY LOADED IF NO NAME2
         LG    ZC_R0,&NAME2
         LG    ZC_R1,8+&NAME2
           AEND
         L     ZC_R15,ZCVT_CVTQTLB   CVT Q IN ZC_R0-ZC_R1              X
                                     TO LB IN ZC_F4-ZC_F6
         BASR  ZC_R14,ZC_R15
         :&PFPO_FROM SETC 'PFPO_TYPE_LB'  
       AELSEIF ('&TYPE2' EQ 'P')   CVT P TO LD
         ZAP   ZCVT_PWORK2,&NAME2
         LMG   ZC_R0,ZC_R1,ZCVT_PWORK2
         CXSTR ZC_F4,ZC_R0
         :&PFPO_FROM SETC 'PFPO_TYPE_LD'  
       AELSE
           AIF ('&NAME2' NE '')  IS FP SOURCE LOADED
               AIF ('&TYPE2'(1,1) EQ 'E')
         LE    ZC_F4,&NAME2  
               AELSEIF ('&TYPE2'(1,1) EQ 'D')
         LD    ZC_F4,&NAME2  
               AELSEIF ('&TYPE2'(1,1) EQ 'L')
         LD    ZC_F4,&NAME2  
         LD    ZC_F6,8+&NAME2  
               AELSE
                   MNOTE 8,'ZC_CONV UNSUPPORTED TYPE2 - &TYPE2'
                   MEXIT
               AEND
          AEND
       AEND
       :&PFPO_TO SETC 'PFPO_TYPE_&TYPE1'
       AIF ('&TYPE1' EQ 'Q') CVT FP ?? TO Q
         :&PFPO_TO SETC 'PFPO_TYPE_LB'
         LRL   ZC_R0,=AL1(PFPO_RADIX,&PFPO_TO,&PFPO_FROM,PFPO_RND)
         PFPO
         L     ZC_R15,ZCVT_CVTLBTQ CVT LB IN F0-F2 TO Q IN ZC_R0-ZC_R1
         BASR  ZC_R14,ZC_R15
       AELSEIF ('&TYPE1' EQ 'P') CVT FP ?? TO PL31 IN ZC_R0-ZC_R1
         :&PFPO_TO SETC 'PFPO_TYPE_LD'
         LRL   ZC_R0,=AL1(PFPO_RADIX,&PFPO_TO,&PFPO_FROM,PFPO_RND)
         PFPO   
         L     ZC_R15,ZCVT_CVTLDTP IN F0-F2 TO PL31 IN ZC_R0-ZC_R1
         BASR  ZC_R14,ZC_R15
       AELSE
         LRL   ZC_R0,=AL1(PFPO_RADIX,&PFPO_TO,&PFPO_FROM,PFPO_RND)
         PFPO
       AEND
       MEND
