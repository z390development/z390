000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID.
000300     SQ220A.
000400****************************************************************
000500*                                                              *
000600*    VALIDATION FOR:-                                          *
000700*    "ON-SITE VALIDATION, NATIONAL INSTITUTE OF STD & TECH.     ".
000800*                                                              *
000900*    CREATION DATE     /     VALIDATION DATE                   *
001000*    "COBOL 85 VERSION 4.2, Apr  1993 SSVG                      ".
001100*                                                              *
001200*        THIS ROUTINE CHECKS THE
001300*
001400*           RECORD IS VARYING IN SIZE FROM 120 TO 151 CHARACTERS
001500*             DEPENDING ON DATA-NAME-1
001600*    AND THE
001700*             NEXT RECORD  CLAUSE.
001800*
001900*        THIS ROUTINE BUILDS A SEQUENTIAL MASS STORAGE FILE
002000*    WHICH CONTAINS BOTH 120 CHARACTER AND 151 CHARACTER
002100*    RECORDS.  THE MASS STORAGE FILE CONSISTS OF 1 SHORT,
002200*    1 LONG, 10 SHORT, 100 LONG, AND 338 SHORT RECORDS FOR
002300*    A TOTAL OF 450 RECORDS IN THE FILE.  THE MASS STORAGE
002400*    FILE IS READ AND FIELDS IN THE RECORDS ARE CHECKED
002500*    AGAINST THE EXPECTED VALUES.
002600*
002700*        AN INFORMATION SECTION AT THE END OF THE ROUTINE
002800*    CHECKS THE FIELD WHICH CONTAINS THE XRECORD-NUMBER.
002900*    THIS FIELD IS PART OF A LONG RECORD ONLY.  IF THE
003000*    XRECORD-NUMBER IS THERE FOR A SHORT RECORD, IT MEANS
003100*    THE MAXIMUM SIZE RECORD IS ALWAYS WRITTEN.
003200 ENVIRONMENT DIVISION.
003300 CONFIGURATION SECTION.
003400 SOURCE-COMPUTER.
003500     XXXXX082.
003600 OBJECT-COMPUTER.
003700     XXXXX083.
003800 INPUT-OUTPUT SECTION.
003900 FILE-CONTROL.
004000P    SELECT RAW-DATA   ASSIGN TO
004100P    XXXXX062
004200P           ORGANIZATION IS INDEXED
004300P           ACCESS MODE IS RANDOM
004400P           RECORD KEY IS RAW-DATA-KEY.
004500     SELECT PRINT-FILE ASSIGN TO
004600     XXXXX055.
004700     SELECT SQ-VS7 ASSIGN TO
004800     XXXXX014
004900     ORGANIZATION SEQUENTIAL
005000     ACCESS SEQUENTIAL.
005100 DATA DIVISION.
005200 FILE SECTION.
005300P
005400PFD  RAW-DATA.
005500P
005600P01  RAW-DATA-SATZ.
005700P    05  RAW-DATA-KEY        PIC X(6).
005800P    05  C-DATE              PIC 9(6).
005900P    05  C-TIME              PIC 9(8).
006000P    05  C-NO-OF-TESTS       PIC 99.
006100P    05  C-OK                PIC 999.
006200P    05  C-ALL               PIC 999.
006300P    05  C-FAIL              PIC 999.
006400P    05  C-DELETED           PIC 999.
006500P    05  C-INSPECT           PIC 999.
006600P    05  C-NOTE              PIC X(13).
006700P    05  C-INDENT            PIC X.
006800P    05  C-ABORT             PIC X(8).
006900 FD  PRINT-FILE
007000C    LABEL RECORDS
007100C    XXXXX084
007200C    DATA RECORD IS PRINT-REC DUMMY-RECORD
007300               .
007400 01  PRINT-REC PICTURE X(120).
007500 01  DUMMY-RECORD PICTURE X(120).
007600 FD  SQ-VS7
007700C    LABEL RECORDS ARE STANDARD
007800     RECORD IS VARYING IN SIZE FROM 120 TO 151 CHARACTERS
007900       DEPENDING ON RECORD-LENGTH.
008000 01  SQ-VS7R1-M-G-120.
008100     02  SQ-VS7R1-FIRST PICTURE X(120).
008200 01  SQ-VS7R2-M-G-151.
008300     02  SQ-VS7R2-FIRST PICTURE X(120).
008400     02  LONG-OR-SHORT  PICTURE X(5).
008500     02  SQ-VS7-RECNO  PICTURE X(5).
008600 02  SQ-VS7-FILLER  PICTURE X(21).
008700 WORKING-STORAGE SECTION.
008800 01  RECORD-LENGTH       PICTURE 999  VALUE ZERO.
008900 01  SAVE-COUNT-OF-RECS  PICTURE X(5) VALUE SPACE.
009000 01  COUNT-OF-RECS  PICTURE S9(5) COMPUTATIONAL.
009100 01  RECORDS-IN-ERROR  PICTURE S9(5) COMPUTATIONAL.
009200 01  ERROR-FLAG PICTURE 9.
009300 01  EOF-FLAG  PICTURE 9.
009400 01  DUMP-AREA.
009500     02  TYPE-OF-REC PICTURE X(5).
009600     02  RECNO  PICTURE 9(5).
009700     02  REC-FILLER PICTURE X(21).
009800 01  FILE-RECORD-INFORMATION-REC.
009900     03 FILE-RECORD-INFO-SKELETON.
010000        05 FILLER                 PICTURE X(48)       VALUE
010100             "FILE=      ,RECORD=      /0,RECNO=000000,UPDT=00".
010200        05 FILLER                 PICTURE X(46)       VALUE
010300             ",ODO=0000,PGM=     ,LRECL=000000,BLKSIZ  =0000".
010400        05 FILLER                 PICTURE X(26)       VALUE
010500             ",LFIL=000000,ORG=  ,LBLR= ".
010600        05 FILLER                 PICTURE X(37)       VALUE
010700             ",RECKEY=                             ".
010800        05 FILLER                 PICTURE X(38)       VALUE
010900             ",ALTKEY1=                             ".
011000        05 FILLER                 PICTURE X(38)       VALUE
011100             ",ALTKEY2=                             ".
011200        05 FILLER                 PICTURE X(7)        VALUE SPACE.
011300     03 FILE-RECORD-INFO          OCCURS  10  TIMES.
011400        05 FILE-RECORD-INFO-P1-120.
011500           07 FILLER              PIC X(5).
011600           07 XFILE-NAME           PIC X(6).
011700           07 FILLER              PIC X(8).
011800           07 XRECORD-NAME         PIC X(6).
011900           07 FILLER              PIC X(1).
012000           07 REELUNIT-NUMBER     PIC 9(1).
012100           07 FILLER              PIC X(7).
012200           07 XRECORD-NUMBER       PIC 9(6).
012300           07 FILLER              PIC X(6).
012400           07 UPDATE-NUMBER       PIC 9(2).
012500           07 FILLER              PIC X(5).
012600           07 ODO-NUMBER          PIC 9(4).
012700           07 FILLER              PIC X(5).
012800           07 XPROGRAM-NAME        PIC X(5).
012900           07 FILLER              PIC X(7).
013000           07 XRECORD-LENGTH       PIC 9(6).
013100           07 FILLER              PIC X(7).
013200           07 CHARS-OR-RECORDS    PIC X(2).
013300           07 FILLER              PIC X(1).
013400           07 XBLOCK-SIZE          PIC 9(4).
013500           07 FILLER              PIC X(6).
013600           07 RECORDS-IN-FILE     PIC 9(6).
013700           07 FILLER              PIC X(5).
013800           07 XFILE-ORGANIZATION   PIC X(2).
013900           07 FILLER              PIC X(6).
014000           07 XLABEL-TYPE          PIC X(1).
014100        05 FILE-RECORD-INFO-P121-240.
014200           07 FILLER              PIC X(8).
014300           07 XRECORD-KEY          PIC X(29).
014400           07 FILLER              PIC X(9).
014500           07 ALTERNATE-KEY1      PIC X(29).
014600           07 FILLER              PIC X(9).
014700           07 ALTERNATE-KEY2      PIC X(29).
014800           07 FILLER              PIC X(7).
014900 01  TEST-RESULTS.
015000     02 FILLER                    PICTURE X VALUE SPACE.
015100     02 FEATURE                   PICTURE X(20) VALUE SPACE.
015200     02 FILLER                    PICTURE X VALUE SPACE.
015300     02 P-OR-F                    PICTURE X(5) VALUE SPACE.
015400     02 FILLER                    PICTURE X  VALUE SPACE.
015500     02  PAR-NAME.
015600       03 FILLER PICTURE X(12) VALUE SPACE.
015700       03  PARDOT-X PICTURE X  VALUE SPACE.
015800       03 DOTVALUE PICTURE 99  VALUE ZERO.
015900       03 FILLER PIC X(5) VALUE SPACE.
016000     02 FILLER PIC X(10) VALUE SPACE.
016100     02 RE-MARK PIC X(61).
016200 01  TEST-COMPUTED.
016300     02 FILLER PIC X(30) VALUE SPACE.
016400     02 FILLER PIC X(17) VALUE "       COMPUTED=".
016500     02 COMPUTED-X.
016600     03 COMPUTED-A                PICTURE X(20) VALUE SPACE.
016700     03 COMPUTED-N REDEFINES COMPUTED-A PICTURE -9(9).9(9).
016800     03 COMPUTED-0V18 REDEFINES COMPUTED-A  PICTURE -.9(18).
016900     03 COMPUTED-4V14 REDEFINES COMPUTED-A  PICTURE -9(4).9(14).
017000     03 COMPUTED-14V4 REDEFINES COMPUTED-A  PICTURE -9(14).9(4).
017100     03       CM-18V0 REDEFINES COMPUTED-A.
017200         04 COMPUTED-18V0                   PICTURE -9(18).
017300         04 FILLER                          PICTURE X.
017400     03 FILLER PIC X(50) VALUE SPACE.
017500 01  TEST-CORRECT.
017600     02 FILLER PIC X(30) VALUE SPACE.
017700     02 FILLER PIC X(17) VALUE "       CORRECT =".
017800     02 CORRECT-X.
017900     03 CORRECT-A                 PICTURE X(20) VALUE SPACE.
018000     03 CORRECT-N REDEFINES CORRECT-A PICTURE -9(9).9(9).
018100     03 CORRECT-0V18 REDEFINES CORRECT-A    PICTURE -.9(18).
018200     03 CORRECT-4V14 REDEFINES CORRECT-A    PICTURE -9(4).9(14).
018300     03 CORRECT-14V4 REDEFINES CORRECT-A    PICTURE -9(14).9(4).
018400     03      CR-18V0 REDEFINES CORRECT-A.
018500         04 CORRECT-18V0                    PICTURE -9(18).
018600         04 FILLER                          PICTURE X.
018700     03 FILLER PIC X(50) VALUE SPACE.
018800 01  CCVS-C-1.
018900     02 FILLER PICTURE IS X(99) VALUE IS " FEATURE              PA
019000-    "SS  PARAGRAPH-NAME
019100-    "        REMARKS".
019200     02 FILLER PICTURE IS X(20) VALUE IS SPACE.
019300 01  CCVS-C-2.
019400     02 FILLER PICTURE IS X VALUE IS SPACE.
019500     02 FILLER PICTURE IS X(6) VALUE IS "TESTED".
019600     02 FILLER PICTURE IS X(15) VALUE IS SPACE.
019700     02 FILLER PICTURE IS X(4) VALUE IS "FAIL".
019800     02 FILLER PICTURE IS X(94) VALUE IS SPACE.
019900 01  REC-SKL-SUB PICTURE 9(2) VALUE ZERO.
020000 01  REC-CT PICTURE 99 VALUE ZERO.
020100 01  DELETE-CNT                   PICTURE 999  VALUE ZERO.
020200 01  ERROR-COUNTER PICTURE IS 999 VALUE IS ZERO.
020300 01  INSPECT-COUNTER PIC 999 VALUE ZERO.
020400 01  PASS-COUNTER PIC 999 VALUE ZERO.
020500 01  TOTAL-ERROR PIC 999 VALUE ZERO.
020600 01  ERROR-HOLD PIC 999 VALUE ZERO.
020700 01  DUMMY-HOLD PIC X(120) VALUE SPACE.
020800 01  RECORD-COUNT PIC 9(5) VALUE ZERO.
020900 01  CCVS-H-1.
021000     02  FILLER   PICTURE X(27)  VALUE SPACE.
021100     02 FILLER PICTURE X(67) VALUE
021200     " FEDERAL SOFTWARE TESTING CENTER COBOL COMPILER VALIDATION
021300-    " SYSTEM".
021400     02  FILLER     PICTURE X(26)  VALUE SPACE.
021500 01  CCVS-H-2.
021600     02 FILLER PICTURE X(52) VALUE IS
021700     "CCVS85 FSTC COPY, NOT FOR DISTRIBUTION.".
021800     02 FILLER PICTURE IS X(19) VALUE IS "TEST RESULTS SET-  ".
021900     02 TEST-ID PICTURE IS X(9).
022000     02 FILLER PICTURE IS X(40) VALUE IS SPACE.
022100 01  CCVS-H-3.
022200     02  FILLER PICTURE X(34) VALUE
022300     " FOR OFFICIAL USE ONLY    ".
022400     02  FILLER PICTURE X(58) VALUE
022500     "COBOL 85 VERSION 4.2, Apr  1993 SSVG                      ".
022600     02  FILLER PICTURE X(28) VALUE
022700     "  COPYRIGHT   1985 ".
022800 01  CCVS-E-1.
022900     02 FILLER PICTURE IS X(52) VALUE IS SPACE.
023000     02 FILLER PICTURE IS X(14) VALUE IS "END OF TEST-  ".
023100     02 ID-AGAIN PICTURE IS X(9).
023200     02 FILLER PICTURE X(45) VALUE IS
023300     " NTIS DISTRIBUTION COBOL 85".
023400 01  CCVS-E-2.
023500     02  FILLER                   PICTURE X(31)  VALUE
023600     SPACE.
023700     02  FILLER                   PICTURE X(21)  VALUE SPACE.
023800     02 CCVS-E-2-2.
023900         03 ERROR-TOTAL PICTURE IS XXX VALUE IS SPACE.
024000         03 FILLER PICTURE IS X VALUE IS SPACE.
024100         03 ENDER-DESC PIC X(46) VALUE "ERRORS ENCOUNTERED".
024200 01  CCVS-E-3.
024300     02  FILLER PICTURE X(22) VALUE
024400     " FOR OFFICIAL USE ONLY".
024500     02  FILLER PICTURE X(12) VALUE SPACE.
024600     02  FILLER PICTURE X(58) VALUE
024700     "ON-SITE VALIDATION, NATIONAL INSTITUTE OF STD & TECH.     ".
024800     02  FILLER PICTURE X(13) VALUE SPACE.
024900     02 FILLER PIC X(15) VALUE " COPYRIGHT 1985".
025000 01  CCVS-E-4.
025100     02 CCVS-E-4-1 PIC XXX VALUE SPACE.
025200     02 FILLER PIC XXXX VALUE " OF ".
025300     02 CCVS-E-4-2 PIC XXX VALUE SPACE.
025400     02 FILLER PIC X(40) VALUE
025500      "  TESTS WERE EXECUTED SUCCESSFULLY".
025600 01  XXINFO.
025700     02 FILLER PIC X(30) VALUE "        *** INFORMATION  ***".
025800     02 INFO-TEXT.
025900     04 FILLER PIC X(20) VALUE SPACE.
026000     04 XXCOMPUTED PIC X(20).
026100     04 FILLER PIC X(5) VALUE SPACE.
026200     04 XXCORRECT PIC X(20).
026300 01  HYPHEN-LINE.
026400     02 FILLER PICTURE IS X VALUE IS SPACE.
026500     02 FILLER PICTURE IS X(65) VALUE IS "************************
026600-    "*****************************************".
026700     02 FILLER PICTURE IS X(54) VALUE IS "************************
026800-    "******************************".
026900 01  CCVS-PGM-ID PIC X(6) VALUE
027000     "SQ220A".
027100 PROCEDURE DIVISION.
027200 CCVS1 SECTION.
027300 OPEN-FILES.
027400P    OPEN I-O RAW-DATA.
027500P    MOVE CCVS-PGM-ID TO RAW-DATA-KEY.
027600P    READ RAW-DATA INVALID KEY GO TO END-E-1.
027700P    MOVE "ABORTED " TO C-ABORT.
027800P    ADD 1 TO C-NO-OF-TESTS.
027900P    ACCEPT C-DATE  FROM DATE.
028000P    ACCEPT C-TIME  FROM TIME.
028100P    REWRITE RAW-DATA-SATZ INVALID KEY GO TO END-E-1.
028200PEND-E-1.
028300P    CLOSE RAW-DATA.
028400     OPEN     OUTPUT PRINT-FILE.
028500     MOVE CCVS-PGM-ID TO TEST-ID. MOVE CCVS-PGM-ID TO ID-AGAIN.
028600     MOVE    SPACE TO TEST-RESULTS.
028700     PERFORM  HEAD-ROUTINE THRU COLUMN-NAMES-ROUTINE.
028800     MOVE ZERO TO REC-SKL-SUB.
028900     PERFORM CCVS-INIT-FILE 9 TIMES.
029000 CCVS-INIT-FILE.
029100     ADD 1 TO REC-SKL-SUB.
029200     MOVE FILE-RECORD-INFO-SKELETON TO
029300                  FILE-RECORD-INFO (REC-SKL-SUB).
029400 CCVS-INIT-EXIT.
029500     GO TO CCVS1-EXIT.
029600 CLOSE-FILES.
029700     PERFORM END-ROUTINE THRU END-ROUTINE-13. CLOSE PRINT-FILE.
029800P    OPEN I-O RAW-DATA.
029900P    MOVE CCVS-PGM-ID TO RAW-DATA-KEY.
030000P    READ RAW-DATA INVALID KEY GO TO END-E-2.
030100P    MOVE "OK.     " TO C-ABORT.
030200P    MOVE PASS-COUNTER TO C-OK.
030300P    MOVE ERROR-HOLD   TO C-ALL.
030400P    MOVE ERROR-COUNTER TO C-FAIL.
030500P    MOVE DELETE-CNT TO C-DELETED.
030600P    MOVE INSPECT-COUNTER TO C-INSPECT.
030700P    REWRITE RAW-DATA-SATZ INVALID KEY GO TO END-E-2.
030800PEND-E-2.
030900P    CLOSE RAW-DATA.
031000 TERMINATE-CCVS.
031100S    EXIT PROGRAM.
031200STERMINATE-CALL.
031300     STOP     RUN.
031400 INSPT. MOVE "INSPT" TO P-OR-F. ADD 1 TO INSPECT-COUNTER.
031500 PASS.  MOVE "PASS " TO P-OR-F.  ADD 1 TO PASS-COUNTER.
031600 FAIL.  MOVE "FAIL*" TO P-OR-F.  ADD 1 TO ERROR-COUNTER.
031700 DE-LETE.  MOVE "*****" TO P-OR-F.  ADD 1 TO DELETE-CNT.
031800     MOVE "****TEST DELETED****" TO RE-MARK.
031900 PRINT-DETAIL.
032000     IF REC-CT NOT EQUAL TO ZERO
032100             MOVE "." TO PARDOT-X
032200             MOVE REC-CT TO DOTVALUE.
032300     MOVE     TEST-RESULTS TO PRINT-REC. PERFORM WRITE-LINE.
032400     IF P-OR-F EQUAL TO "FAIL*"  PERFORM WRITE-LINE
032500        PERFORM FAIL-ROUTINE THRU FAIL-ROUTINE-EX
032600          ELSE PERFORM BAIL-OUT THRU BAIL-OUT-EX.
032700     MOVE SPACE TO P-OR-F. MOVE SPACE TO COMPUTED-X.
032800     MOVE SPACE TO CORRECT-X.
032900     IF     REC-CT EQUAL TO ZERO  MOVE SPACE TO PAR-NAME.
033000     MOVE     SPACE TO RE-MARK.
033100 HEAD-ROUTINE.
033200     MOVE CCVS-H-1 TO DUMMY-RECORD. PERFORM WRITE-LINE 2 TIMES.
033300     MOVE CCVS-H-2 TO DUMMY-RECORD. PERFORM WRITE-LINE 5 TIMES.
033400     MOVE CCVS-H-3 TO DUMMY-RECORD. PERFORM WRITE-LINE 3 TIMES.
033500 COLUMN-NAMES-ROUTINE.
033600     MOVE CCVS-C-1 TO DUMMY-RECORD. PERFORM WRITE-LINE.
033700     MOVE CCVS-C-2 TO DUMMY-RECORD. PERFORM WRITE-LINE 2 TIMES.
033800     MOVE HYPHEN-LINE TO DUMMY-RECORD. PERFORM WRITE-LINE.
033900 END-ROUTINE.
034000     MOVE HYPHEN-LINE TO DUMMY-RECORD. PERFORM WRITE-LINE 5 TIMES.
034100 END-RTN-EXIT.
034200     MOVE CCVS-E-1 TO DUMMY-RECORD. PERFORM WRITE-LINE 2 TIMES.
034300 END-ROUTINE-1.
034400      ADD ERROR-COUNTER TO ERROR-HOLD ADD INSPECT-COUNTER TO
034500      ERROR-HOLD. ADD DELETE-CNT TO ERROR-HOLD.
034600      ADD PASS-COUNTER TO ERROR-HOLD.
034700*     IF PASS-COUNTER EQUAL TO ERROR-HOLD GO TO END-ROUTINE-12.
034800      MOVE PASS-COUNTER TO CCVS-E-4-1.
034900      MOVE ERROR-HOLD TO CCVS-E-4-2.
035000      MOVE CCVS-E-4 TO CCVS-E-2-2.
035100      MOVE CCVS-E-2 TO DUMMY-RECORD PERFORM WRITE-LINE.
035200  END-ROUTINE-12.
035300      MOVE "TEST(S) FAILED" TO ENDER-DESC.
035400     IF       ERROR-COUNTER IS EQUAL TO ZERO
035500         MOVE "NO " TO ERROR-TOTAL
035600         ELSE
035700         MOVE ERROR-COUNTER TO ERROR-TOTAL.
035800     MOVE     CCVS-E-2 TO DUMMY-RECORD.
035900     PERFORM WRITE-LINE.
036000 END-ROUTINE-13.
036100     IF DELETE-CNT IS EQUAL TO ZERO
036200         MOVE "NO " TO ERROR-TOTAL  ELSE
036300         MOVE DELETE-CNT TO ERROR-TOTAL.
036400     MOVE "TEST(S) DELETED     " TO ENDER-DESC.
036500     MOVE CCVS-E-2 TO DUMMY-RECORD. PERFORM WRITE-LINE.
036600      IF   INSPECT-COUNTER EQUAL TO ZERO
036700          MOVE "NO " TO ERROR-TOTAL
036800      ELSE MOVE INSPECT-COUNTER TO ERROR-TOTAL.
036900      MOVE "TEST(S) REQUIRE INSPECTION" TO ENDER-DESC.
037000      MOVE CCVS-E-2 TO DUMMY-RECORD. PERFORM WRITE-LINE.
037100     MOVE CCVS-E-3 TO DUMMY-RECORD. PERFORM WRITE-LINE.
037200 WRITE-LINE.
037300     ADD 1 TO RECORD-COUNT.
037400Y    IF RECORD-COUNT GREATER 50
037500Y        MOVE DUMMY-RECORD TO DUMMY-HOLD
037600Y        MOVE SPACE TO DUMMY-RECORD
037700Y        WRITE DUMMY-RECORD AFTER ADVANCING PAGE
037800Y        MOVE CCVS-C-1 TO DUMMY-RECORD PERFORM WRT-LN
037900Y        MOVE CCVS-C-2 TO DUMMY-RECORD PERFORM WRT-LN 2 TIMES
038000Y        MOVE HYPHEN-LINE TO DUMMY-RECORD PERFORM WRT-LN
038100Y        MOVE DUMMY-HOLD TO DUMMY-RECORD
038200Y        MOVE ZERO TO RECORD-COUNT.
038300     PERFORM WRT-LN.
038400 WRT-LN.
038500     WRITE    DUMMY-RECORD AFTER ADVANCING 1 LINES.
038600     MOVE SPACE TO DUMMY-RECORD.
038700 BLANK-LINE-PRINT.
038800     PERFORM WRT-LN.
038900 FAIL-ROUTINE.
039000     IF COMPUTED-X NOT EQUAL TO SPACE GO TO FAIL-ROUTINE-WRITE.
039100     IF CORRECT-X NOT EQUAL TO SPACE GO TO FAIL-ROUTINE-WRITE.
039200     MOVE "NO FURTHER INFORMATION, SEE PROGRAM." TO INFO-TEXT.
039300     MOVE XXINFO TO DUMMY-RECORD. PERFORM WRITE-LINE 2 TIMES.
039400     GO TO FAIL-ROUTINE-EX.
039500 FAIL-ROUTINE-WRITE.
039600     MOVE TEST-COMPUTED TO PRINT-REC PERFORM WRITE-LINE
039700     MOVE TEST-CORRECT TO PRINT-REC PERFORM WRITE-LINE 2 TIMES.
039800 FAIL-ROUTINE-EX. EXIT.
039900 BAIL-OUT.
040000     IF COMPUTED-A NOT EQUAL TO SPACE GO TO BAIL-OUT-WRITE.
040100     IF CORRECT-A EQUAL TO SPACE GO TO BAIL-OUT-EX.
040200 BAIL-OUT-WRITE.
040300     MOVE CORRECT-A TO XXCORRECT. MOVE COMPUTED-A TO XXCOMPUTED.
040400     MOVE XXINFO TO DUMMY-RECORD. PERFORM WRITE-LINE 2 TIMES.
040500 BAIL-OUT-EX. EXIT.
040600 CCVS1-EXIT.
040700     EXIT.
040800 SECT-SQ220A-0001 SECTION.
040900 WRITE-INIT-GF-01.
041000     MOVE "SQ-VS7" TO XFILE-NAME (1).
041100     MOVE CCVS-PGM-ID TO XPROGRAM-NAME (1).
041200     MOVE "RC" TO CHARS-OR-RECORDS (1).
041300     MOVE 0001 TO XBLOCK-SIZE (1).
041400     MOVE 000450 TO RECORDS-IN-FILE (1).
041500     MOVE "SQ" TO XFILE-ORGANIZATION (1).
041600     MOVE "S" TO XLABEL-TYPE (1).
041700     MOVE 000000 TO XRECORD-NUMBER (1).
041800     MOVE ZERO TO COUNT-OF-RECS.
041900     OPEN OUTPUT SQ-VS7.
042000     MOVE "MULTIPLE LENGTH RECS" TO SQ-VS7-FILLER.
042100 WRITE-TEST-GF-01.
042200     PERFORM WRITE-SHORT-REC.
042300     PERFORM WRITE-LONG-REC.
042400     PERFORM WRITE-SHORT-REC 10 TIMES.
042500     PERFORM WRITE-LONG-REC 100 TIMES.
042600     PERFORM WRITE-SHORT-REC 338 TIMES.
042700 WRITE-WRITE-GF-01.
042800     MOVE "CREATE FILE SQ-VS7" TO FEATURE.
042900     MOVE "WRITE-TEST-GF-01" TO PAR-NAME.
043000     MOVE "FILE CREATED, RECS =" TO COMPUTED-A.
043100     MOVE COUNT-OF-RECS TO CORRECT-18V0.
043200     MOVE "FILE HAS 120 AND 151 CHAR RECS" TO RE-MARK.
043300     PERFORM PRINT-DETAIL.
043400*        A SEQUENTIAL MASS STORAGE FILE CONTAINING 450
043500*    RECORDS HAS BEEN CREATED.  THE FILE CONTAINS RECORDS
043600*    OF 120 CHARACTERS AND RECORDS OF 151 CHARACTERS.  THE
043700*    SEQUENCE IN WHICH THE RECORDS WERE WRITTEN IS S-L-10S-
043800*    100L-338S.
043900 WRITE-CLOSE-GF-01.
044000     CLOSE SQ-VS7.
044100     GO TO READ-INIT-F1-01.
044200 WRITE-SHORT-REC.
044300     MOVE "R1-M-G" TO XRECORD-NAME (1).
044400     MOVE 000120 TO XRECORD-LENGTH (1).
044500     ADD 1 TO COUNT-OF-RECS.
044600     MOVE COUNT-OF-RECS TO XRECORD-NUMBER (1).
044700     MOVE "SHORT" TO LONG-OR-SHORT.
044800     MOVE COUNT-OF-RECS TO SQ-VS7-RECNO.
044900     MOVE FILE-RECORD-INFO-P1-120 (1) TO SQ-VS7R1-FIRST.
045000     MOVE 120 TO RECORD-LENGTH.
045100     WRITE SQ-VS7R1-M-G-120.
045200 WRITE-LONG-REC.
045300     MOVE "R2-M-G" TO XRECORD-NAME (1).
045400     MOVE 000151 TO XRECORD-LENGTH (1).
045500     ADD 1 TO COUNT-OF-RECS.
045600     MOVE COUNT-OF-RECS TO XRECORD-NUMBER (1).
045700     MOVE "LONG" TO LONG-OR-SHORT.
045800     MOVE COUNT-OF-RECS TO SQ-VS7-RECNO.
045900     MOVE FILE-RECORD-INFO-P1-120 (1) TO SQ-VS7R2-FIRST.
046000     MOVE 151 TO RECORD-LENGTH.
046100     WRITE SQ-VS7R2-M-G-151.
046200 READ-INIT-F1-01.
046300     MOVE ZERO TO RECORD-LENGTH.
046400     MOVE ZERO TO COUNT-OF-RECS.
046500     MOVE ZERO TO EOF-FLAG.
046600     MOVE ZERO TO RECORDS-IN-ERROR.
046700     MOVE ZERO TO ERROR-FLAG.
046800     OPEN INPUT SQ-VS7.
046900 READ-TEST-F1-01.
047000     PERFORM READ-SHORT-REC THRU READ-SHORT-REC-EXIT.
047100     IF EOF-FLAG EQUAL TO 1
047200         MOVE "EOF ON FIRST READ" TO RE-MARK
047300         GO TO READ-EOF-F1-06.
047400     IF ERROR-FLAG EQUAL TO 1
047500         GO TO READ-FAIL-F1-01.
047600 READ-PASS-F1-01.
047700     PERFORM PASS.
047800     GO TO READ-WRITE-F1-01.
047900 READ-FAIL-F1-01.
048000     MOVE " FILE NOT OK. SEE PROGRAM & VII-52 OR -44" TO RE-MARK.
048100     PERFORM FAIL.
048200 READ-WRITE-F1-01.
048300     MOVE "READ SHORT RECORD" TO FEATURE.
048400     MOVE "READ-TEST-F1-01" TO PAR-NAME.
048500     MOVE "EXPECTED RECORD LENGTH: 120" TO RE-MARK.
048600     MOVE RECORD-LENGTH TO COMPUTED-N.
048700     MOVE 120 TO CORRECT-N.
048800     PERFORM PRINT-DETAIL.
048900     GO TO READ-INIT-F1-02.
049000 READ-SHORT-REC.
049100*   READ  <FILE>     NEXT RECORD     AT END                      *
049200******************************************************************
049300     IF EOF-FLAG EQUAL TO 1
049400         GO TO READ-SHORT-REC-EXIT.
049500     READ SQ-VS7 NEXT RECORD AT END
049600         MOVE 1 TO EOF-FLAG
049700         GO TO READ-SHORT-REC-EXIT.
049800     ADD 1 TO COUNT-OF-RECS.
049900     MOVE SQ-VS7R1-FIRST TO FILE-RECORD-INFO-P1-120 (1).
050000     IF XRECORD-NAME (1) NOT EQUAL TO "R1-M-G"
050100         GO TO READ-SHORT-REC-ERROR.
050200     IF  RECORD-LENGTH     NOT EQUAL TO 120
050300         GO TO READ-SHORT-REC-ERROR.
050400     IF COUNT-OF-RECS NOT EQUAL TO XRECORD-NUMBER (1)
050500         GO TO READ-SHORT-REC-ERROR.
050600     IF XLABEL-TYPE (1) EQUAL TO "S"
050700         GO TO READ-SHORT-REC-EXIT.
050800 READ-SHORT-REC-ERROR.
050900     ADD 1 TO RECORDS-IN-ERROR.
051000     MOVE 1 TO ERROR-FLAG.
051100 READ-SHORT-REC-EXIT.
051200     EXIT.
051300 READ-INIT-F1-02.
051400     MOVE ZERO TO ERROR-FLAG.
051500 READ-TEST-F1-02.
051600     PERFORM READ-LONG-REC THRU READ-LONG-REC-EXIT.
051700     IF EOF-FLAG EQUAL TO 1
051800         MOVE "EOF ON SECOND READ" TO RE-MARK
051900         GO TO READ-EOF-F1-06.
052000     IF ERROR-FLAG EQUAL TO 1
052100         GO TO READ-FAIL-F1-02.
052200 READ-PASS-F1-02.
052300     PERFORM PASS.
052400     GO TO READ-WRITE-F1-02.
052500 READ-FAIL-F1-02.
052600     MOVE "SEE VII-52 WRITE LONG RECORD OR VII-44 READ" TO RE-MARK
052700     MOVE RECORD-LENGTH TO COMPUTED-N.
052800     MOVE 151 TO CORRECT-N.
052900     PERFORM FAIL.
053000 READ-WRITE-F1-02.
053100     MOVE "READ LONG RECORD" TO FEATURE.
053200     MOVE "READ-TEST-F1-02" TO PAR-NAME.
053300     MOVE "EXPECTED RECORD LENGTH: 151" TO RE-MARK.
053400     PERFORM PRINT-DETAIL.
053500     GO TO READ-INIT-F1-03.
053600 READ-LONG-REC.
053700     IF EOF-FLAG EQUAL TO 1
053800         GO TO READ-LONG-REC-EXIT.
053900     READ SQ-VS7 END
054000         MOVE 1 TO EOF-FLAG
054100         GO TO READ-LONG-REC-EXIT.
054200     ADD 1 TO COUNT-OF-RECS.
054300     MOVE SQ-VS7R2-FIRST TO FILE-RECORD-INFO-P1-120 (1).
054400     IF XRECORD-NAME (1) NOT EQUAL TO "R2-M-G"
054500         GO TO READ-LONG-REC-ERROR.
054600     IF  RECORD-LENGTH     NOT EQUAL TO 151
054700         GO TO READ-LONG-REC-ERROR.
054800     MOVE COUNT-OF-RECS TO SAVE-COUNT-OF-RECS.
054900     IF SAVE-COUNT-OF-RECS NOT EQUAL TO SQ-VS7-RECNO
055000         GO TO READ-LONG-REC-ERROR.
055100     IF LONG-OR-SHORT EQUAL TO "LONG "
055200         GO TO READ-LONG-REC-EXIT.
055300 READ-LONG-REC-ERROR.
055400     ADD 1 TO RECORDS-IN-ERROR.
055500     MOVE 1 TO ERROR-FLAG.
055600 READ-LONG-REC-EXIT.
055700     EXIT.
055800 READ-INIT-F1-03.
055900     MOVE ZERO TO ERROR-FLAG.
056000 READ-TEST-F1-03.
056100     PERFORM READ-SHORT-REC THRU READ-SHORT-REC-EXIT 10 TIMES.
056200     IF EOF-FLAG EQUAL TO 1
056300         MOVE "UNEXPECTED EOF" TO RE-MARK
056400         GO TO READ-EOF-F1-06.
056500     IF ERROR-FLAG EQUAL TO 1
056600         GO TO READ-FAIL-F1-03.
056700 READ-PASS-F1-03.
056800     PERFORM PASS.
056900     GO TO READ-WRITE-F1-03.
057000 READ-FAIL-F1-03.
057100     MOVE "SEE VII-52 WRITE SHORT REC   OR VII-44 READ" TO RE-MARK
057200     MOVE RECORD-LENGTH TO COMPUTED-N.
057300     MOVE 120 TO CORRECT-N.
057400     PERFORM FAIL.
057500 READ-WRITE-F1-03.
057600     MOVE "READ SHORT RECORDS" TO FEATURE.
057700     MOVE "READ-TEST-F1-03" TO PAR-NAME.
057800     MOVE "EXPECTED RECORD LENGTH: 120" TO RE-MARK.
057900     PERFORM PRINT-DETAIL.
058000 READ-INIT-F1-04.
058100     MOVE ZERO TO ERROR-FLAG.
058200 READ-TEST-F1-04.
058300     PERFORM READ-LONG-REC THRU READ-LONG-REC-EXIT 100 TIMES.
058400     IF EOF-FLAG EQUAL TO 1
058500         MOVE "UNEXPECTED EOF" TO RE-MARK
058600         GO TO READ-EOF-F1-06.
058700     IF ERROR-FLAG EQUAL TO 1
058800         GO TO READ-FAIL-F1-04.
058900 READ-PASS-F1-04.
059000     PERFORM PASS.
059100     GO TO READ-WRITE-F1-04.
059200 READ-FAIL-F1-04.
059300     MOVE RECORD-LENGTH TO COMPUTED-N.
059400     MOVE 151 TO CORRECT-N.
059500     PERFORM FAIL.
059600 READ-WRITE-F1-04.
059700     MOVE "READ LONG RECORDS" TO FEATURE.
059800     MOVE "READ-TEST-F1-04" TO PAR-NAME.
059900     MOVE "EXPECTED RECORD LENGTH: 151" TO RE-MARK.
060000     PERFORM PRINT-DETAIL.
060100 READ-INIT-F1-05.
060200     MOVE ZERO TO ERROR-FLAG.
060300 READ-TEST-F1-05.
060400     PERFORM READ-SHORT-REC THRU READ-SHORT-REC-EXIT 338 TIMES.
060500     IF EOF-FLAG EQUAL TO 1
060600         MOVE "UNEXPECTED EOF" TO RE-MARK
060700         GO TO READ-EOF-F1-06.
060800     IF ERROR-FLAG EQUAL TO 1
060900         GO TO READ-FAIL-F1-05.
061000 READ-PASS-F1-05.
061100     PERFORM PASS.
061200     GO TO READ-WRITE-F1-05.
061300 READ-FAIL-F1-05.
061400     MOVE "SEE VII-52 WRITE LONG RECORD OR VII-44 READ" TO RE-MARK
061500     MOVE RECORD-LENGTH TO COMPUTED-N.
061600     MOVE 120 TO CORRECT-N.
061700     PERFORM FAIL.
061800 READ-WRITE-F1-05.
061900     MOVE "READ SHORT RECORDS" TO FEATURE.
062000     MOVE "READ-TEST-F1-05" TO PAR-NAME.
062100     MOVE "EXPECTED RECORD LENGTH: 120" TO RE-MARK.
062200     PERFORM PRINT-DETAIL.
062300 READ-INIT-F1-06.
062400     READ SQ-VS7 RECORD END
062500         GO TO READ-TEST-F1-06.
062600     MOVE "MORE THAN 450 RECORDS" TO RE-MARK.
062700     GO TO READ-FAIL-F1-06.
062800 READ-EOF-F1-06.
062900     MOVE "RECORDS READ =" TO COMPUTED-A.
063000     MOVE COUNT-OF-RECS TO CORRECT-18V0.
063100     GO TO READ-FAIL-F1-06.
063200 READ-TEST-F1-06.
063300     IF RECORDS-IN-ERROR NOT EQUAL TO ZERO
063400         MOVE "RECORDS IN ERROR =" TO COMPUTED-A
063500     MOVE RECORDS-IN-ERROR TO CORRECT-18V0
063600         GO TO READ-FAIL-F1-06.
063700 READ-PASS-F1-06.
063800     PERFORM PASS.
063900     GO TO READ-WRITE-F1-06.
064000 READ-FAIL-F1-06.
064100     PERFORM FAIL.
064200 READ-WRITE-F1-06.
064300     MOVE "READ-TEST-F1-06" TO PAR-NAME.
064400     MOVE "VERIFY FILE SQ-VS7" TO FEATURE.
064500     PERFORM PRINT-DETAIL.
064600 READ-CLOSE-F1-06.
064700     CLOSE SQ-VS7.
064800 SECT-SQ220A-0002 SECTION.
064900*        THIS SECTION CHECKS IF THE ENTIRE RECORD AREA IS
065000*    WRITTEN ON THE MASS STORAGE DEVICE WHEN A SHORT RECORD
065100*    IS WRITTEN.  THE RECORD NUMBER IN CHARACTERS 126 THROUGH
065200*    130 IS UNIQUE FOR EACH RECORD.
065300 INFO-INIT-01.
065400     OPEN INPUT SQ-VS7.
065500     MOVE ZERO TO COUNT-OF-RECS.
065600 INFO-TEST-01.
065700     READ SQ-VS7 AT END
065800         GO TO INFO-END.
065900     ADD 1 TO COUNT-OF-RECS.
066000     IF SQ-VS7-RECNO NOT EQUAL TO "00001"
066100         GO TO NO-INFO-01.
066200     MOVE "MAXIMUM RECORD SIZE WRITTEN" TO RE-MARK.
066300     MOVE "RECORD READ =" TO COMPUTED-A.
066400     MOVE 0001 TO CORRECT-18V0.
066500     GO TO INFO-WRITE-01.
066600 NO-INFO-01.
066700     MOVE "NO DEFINITE CONCLUSION POSSIBLE" TO RE-MARK.
066800 INFO-WRITE-01.
066900     MOVE "READ SHORT RECORD" TO FEATURE.
067000     MOVE "INFO-TEST-01" TO PAR-NAME.
067100     PERFORM PRINT-DETAIL.
067200 INFO-INIT-02.
067300     READ SQ-VS7 RECORD AT END
067400         GO TO INFO-END.
067500     READ SQ-VS7 END
067600         GO TO INFO-END.
067700 INFO-TEST-02.
067800     READ SQ-VS7 AT END
067900         GO TO INFO-END.
068000     IF SQ-VS7-RECNO NOT EQUAL TO "00004"
068100         GO TO NO-INFO-02.
068200     MOVE "MAXIMUM RECORD SIZE WRITTEN" TO RE-MARK.
068300     MOVE "RECORD READ =" TO COMPUTED-A.
068400     MOVE 0004 TO CORRECT-18V0.
068500     GO TO INFO-WRITE-02.
068600 NO-INFO-02.
068700     MOVE "NO DEFINITE CONCLUSION POSSIBLE" TO RE-MARK.
068800 INFO-WRITE-02.
068900     MOVE "READ SHORT RECORD" TO FEATURE.
069000     MOVE "INFO-TEST-02" TO PAR-NAME.
069100     PERFORM PRINT-DETAIL.
069200 INFO-INIT-03.
069300     ADD 3 TO COUNT-OF-RECS.
069400 INFO-INIT-03-1.
069500     READ SQ-VS7 RECORD
069600         END GO TO INFO-END.
069700     ADD 1 TO COUNT-OF-RECS.
069800     IF COUNT-OF-RECS EQUAL TO 450
069900         GO TO INFO-TEST-03.
070000     GO TO INFO-INIT-03-1.
070100 INFO-TEST-03.
070200     IF SQ-VS7-RECNO NOT EQUAL TO "00450"
070300         GO TO NO-INFO-03.
070400     MOVE "MAXIMUM RECORD SIZE WRITTEN" TO RE-MARK.
070500     MOVE "RECORD READ =" TO COMPUTED-A.
070600     MOVE 0450 TO CORRECT-18V0.
070700     GO TO INFO-WRITE-03.
070800 NO-INFO-03.
070900     MOVE "NO DEFINITE CONCLUSION POSSIBLE" TO RE-MARK.
071000 INFO-WRITE-03.
071100     MOVE "READ SHORT RECORD" TO FEATURE.
071200     MOVE "INFO-TEST-03" TO PAR-NAME.
071300     PERFORM PRINT-DETAIL.
071400 INFO-END.
071500     CLOSE SQ-VS7.
071600 TERMINATE-ROUTINE.
071700     EXIT.
071800 CCVS-EXIT SECTION.
071900 CCVS-999999.
072000     GO TO CLOSE-FILES.
