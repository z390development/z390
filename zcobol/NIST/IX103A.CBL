000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID.
000300     IX103A.
000400****************************************************************
000500*                                                              *
000600*    VALIDATION FOR:-                                          *
000700*                                                              *
000800*    "ON-SITE VALIDATION, NATIONAL INSTITUTE OF STD & TECH.     ".
000900*                                                              *
001000*    "COBOL 85 VERSION 4.2, Apr  1993 SSVG                      ".
001100*                                                              *
001200****************************************************************
001300*
001400*         NEW TEST:
001500*              SELECT ...      INDEXED  ...
001600*              (WITHOUT THE OPTIONAL WORD <ORGANIZATION>)
001700*
001800*    THIS PROGRAM IS THE THIRD OF A SERIES.  ITS FUNCTION
001900*    IS TO PROCESS THE FILE SEQUENTIALLY (ACCESS MODE IS
002000*    SEQUENTIAL).  THE FILE USED IS THAT RESULTING FROM IX102.
002100*
002200*    FIRST, THE FILE IS VERIFIED FOR ACCURACY OF ITS 500 RECORDS.
002300*    SECONDLY, RECORDS OF THE FILE ARE SELECTIVELY DELETED AND
002400*    THIRDLY THE ACCURACY OF EACH RECORD IN THE FILE IS AGAIN
002500*    VERIFIED.
002600*
002700*
002800*       X-CARDS  WHICH MUST BE REPLACED FOR THIS PROGRAM ARE
002900*
003000*             X-24   INDEXED FILE IMPLEMENTOR-NAME IN ASSGN TO
003100*                    CLAUSE FOR DATA FILE IX-FS1
003200*             X-44   INDEXED FILE IMPLEMENTOR-NAME IN ASSGN TO
003300*                    CLAUSE FOR INDEX FILE IX-FS1
003400*             X-55   IMPLEMENTOR-NAME FOR SYSTEM PRINTER
003500*             X-62   IMPLEMENTOR-NAME FOR RAW-DATA
003600*             X-82   IMPLEMENTOR-NAME FOR SOURCE-COMPUTER
003700*             X-83   IMPLEMENTOR-NAME FOR OBJECT-COMPUTER
003800*
003900*         NOTE:  X-CARDS 44 AND 62          ARE OPTIONAL
004000*               AND NEED ONLY TO BE PRESENT IF THE COMPILER RE-
004100*               QUIRES THIS CODE BE AVAILABLE FOR PROPER PROGRAM
004200*               COMPILATION AND EXECUTION. IF THE VP-ROUTINE IS
004300*               USED THE  X-CARDS MAY BE AUTOMATICALLY SELECTED
004400*               FOR INCLUSION IN THE PROGRAM BY SPECIFYING THE
004500*               APPROPRIATE LETTER IN THE "*OPT" VP-ROUTINE
004600*               CONTROL CARD. THE LETTER  CORRESPONDS TO A
004700*               CHARACTER IN POSITION 7 OF THE SOURCE LINE AND
004800*               THEY ARE AS FOLLOWS
004900*
005000*                  J  SELECTS X-CARD 44
005100*                  P  SELECTS X-CARD 62
005200*
005300******************************************************
005400 ENVIRONMENT DIVISION.
005500 CONFIGURATION SECTION.
005600 SOURCE-COMPUTER.
005700     XXXXX082.
005800 OBJECT-COMPUTER.
005900     XXXXX083.
006000 INPUT-OUTPUT SECTION.
006100 FILE-CONTROL.
006200P    SELECT RAW-DATA   ASSIGN TO
006300P    XXXXX062
006400P           ORGANIZATION IS INDEXED
006500P           ACCESS MODE IS RANDOM
006600P           RECORD KEY IS RAW-DATA-KEY.
006700     SELECT PRINT-FILE ASSIGN TO
006800     XXXXX055.
006900     SELECT   IX-FS1 ASSIGN TO
007000     XXXXD024
007100J    XXXXD044
007200                     INDEXED
007300     RECORD    IX-FS1-KEY.
007400*    THE ABSENCE OF THE ACCESS CLAUSE IS TREATED AS THOUGH
007500*        SEQUENTIAL HAD BEEN SPECIFIED.
007600 DATA DIVISION.
007700 FILE SECTION.
007800P
007900PFD  RAW-DATA.
008000P
008100P01  RAW-DATA-SATZ.
008200P    05  RAW-DATA-KEY        PIC X(6).
008300P    05  C-DATE              PIC 9(6).
008400P    05  C-TIME              PIC 9(8).
008500P    05  C-NO-OF-TESTS       PIC 99.
008600P    05  C-OK                PIC 999.
008700P    05  C-ALL               PIC 999.
008800P    05  C-FAIL              PIC 999.
008900P    05  C-DELETED           PIC 999.
009000P    05  C-INSPECT           PIC 999.
009100P    05  C-NOTE              PIC X(13).
009200P    05  C-INDENT            PIC X.
009300P    05  C-ABORT             PIC X(8).
009400 FD  PRINT-FILE.
009500 01  PRINT-REC PICTURE X(120).
009600 01  DUMMY-RECORD PICTURE X(120).
009700 FD  IX-FS1
009800C    LABEL RECORD STANDARD
009900C    DATA RECORDS ARE  IX-FS1R1-F-G-240
010000     ; BLOCK CONTAINS 01 RECORDS
010100     RECORD CONTAINS  240.
010200 01  IX-FS1R1-F-G-240.
010300     05 IX-FS1-REC-120   PIC  X(120).
010400     05 IX-FS1-REC-120-240.
010500        10 FILLER   PIC X(8).
010600        10 IX-FS1-KEY   PIC X(29).
010700        10 FILLER        PIC X(83).
010800 WORKING-STORAGE SECTION.
010900 01  WRK-CS-09V00-006 PIC S9(09) USAGE COMP VALUE ZERO.
011000 01  WRK-CS-09V00-007 PIC S9(09) USAGE COMP VALUE ZERO.
011100 01  WRK-CS-09V00-008 PIC S9(09) USAGE COMP VALUE ZERO.
011200 01  WRK-CS-09V00-009 PIC S9(09) USAGE COMP VALUE ZERO.
011300 01  WRK-CS-09V00-010 PIC S9(09) USAGE COMP VALUE ZERO.
011400 01  WRK-CS-09V00-011 PIC S9(09) USAGE COMP VALUE ZERO.
011500 01  I-O-ERROR-IX-FS1 PIC X(3) VALUE "NO ".
011600 01  IX-WRK-KEY.
011700     03 FILLER         PIC X(10).
011800     03 WRK-DU-09V00-001  PIC 9(9).
011900     03 FILLER         PIC X(10).
012000 01  DUMMY-WRK-REC.
012100     02 DUMMY-WRK1       PIC X(120).
012200     02 DUMMY-WRK2  REDEFINES  DUMMY-WRK1.
012300        03 FILLER   PIC X(5).
012400        03 DUMMY-WRK-INDENT-5  PIC X(115).
012500 01  FILE-RECORD-INFORMATION-REC.
012600     03 FILE-RECORD-INFO-SKELETON.
012700        05 FILLER                 PICTURE X(48)       VALUE
012800             "FILE=      ,RECORD=      /0,RECNO=000000,UPDT=00".
012900        05 FILLER                 PICTURE X(46)       VALUE
013000             ",ODO=0000,PGM=     ,LRECL=000000,BLKSIZ  =0000".
013100        05 FILLER                 PICTURE X(26)       VALUE
013200             ",LFIL=000000,ORG=  ,LBLR= ".
013300        05 FILLER                 PICTURE X(37)       VALUE
013400             ",RECKEY=                             ".
013500        05 FILLER                 PICTURE X(38)       VALUE
013600             ",ALTKEY1=                             ".
013700        05 FILLER                 PICTURE X(38)       VALUE
013800             ",ALTKEY2=                             ".
013900        05 FILLER                 PICTURE X(7)        VALUE SPACE.
014000     03 FILE-RECORD-INFO          OCCURS  10  TIMES.
014100        05 FILE-RECORD-INFO-P1-120.
014200           07 FILLER              PIC X(5).
014300           07 XFILE-NAME           PIC X(6).
014400           07 FILLER              PIC X(8).
014500           07 XRECORD-NAME         PIC X(6).
014600           07 FILLER              PIC X(1).
014700           07 REELUNIT-NUMBER     PIC 9(1).
014800           07 FILLER              PIC X(7).
014900           07 XRECORD-NUMBER       PIC 9(6).
015000           07 FILLER              PIC X(6).
015100           07 UPDATE-NUMBER       PIC 9(2).
015200           07 FILLER              PIC X(5).
015300           07 ODO-NUMBER          PIC 9(4).
015400           07 FILLER              PIC X(5).
015500           07 XPROGRAM-NAME        PIC X(5).
015600           07 FILLER              PIC X(7).
015700           07 XRECORD-LENGTH       PIC 9(6).
015800           07 FILLER              PIC X(7).
015900           07 CHARS-OR-RECORDS    PIC X(2).
016000           07 FILLER              PIC X(1).
016100           07 XBLOCK-SIZE          PIC 9(4).
016200           07 FILLER              PIC X(6).
016300           07 RECORDS-IN-FILE     PIC 9(6).
016400           07 FILLER              PIC X(5).
016500           07 XFILE-ORGANIZATION   PIC X(2).
016600           07 FILLER              PIC X(6).
016700           07 XLABEL-TYPE          PIC X(1).
016800        05 FILE-RECORD-INFO-P121-240.
016900           07 FILLER              PIC X(8).
017000           07 XRECORD-KEY          PIC X(29).
017100           07 FILLER              PIC X(9).
017200           07 ALTERNATE-KEY1      PIC X(29).
017300           07 FILLER              PIC X(9).
017400           07 ALTERNATE-KEY2      PIC X(29).
017500           07 FILLER              PIC X(7).
017600 01  TEST-RESULTS.
017700     02 FILLER                   PIC X      VALUE SPACE.
017800     02 FEATURE                  PIC X(20)  VALUE SPACE.
017900     02 FILLER                   PIC X      VALUE SPACE.
018000     02 P-OR-F                   PIC X(5)   VALUE SPACE.
018100     02 FILLER                   PIC X      VALUE SPACE.
018200     02  PAR-NAME.
018300       03 FILLER                 PIC X(19)  VALUE SPACE.
018400       03  PARDOT-X              PIC X      VALUE SPACE.
018500       03 DOTVALUE               PIC 99     VALUE ZERO.
018600     02 FILLER                   PIC X(8)   VALUE SPACE.
018700     02 RE-MARK                  PIC X(61).
018800 01  TEST-COMPUTED.
018900     02 FILLER                   PIC X(30)  VALUE SPACE.
019000     02 FILLER                   PIC X(17)  VALUE
019100            "       COMPUTED=".
019200     02 COMPUTED-X.
019300     03 COMPUTED-A               PIC X(20)  VALUE SPACE.
019400     03 COMPUTED-N               REDEFINES COMPUTED-A
019500                                 PIC -9(9).9(9).
019600     03 COMPUTED-0V18 REDEFINES COMPUTED-A   PIC -.9(18).
019700     03 COMPUTED-4V14 REDEFINES COMPUTED-A   PIC -9(4).9(14).
019800     03 COMPUTED-14V4 REDEFINES COMPUTED-A   PIC -9(14).9(4).
019900     03       CM-18V0 REDEFINES COMPUTED-A.
020000         04 COMPUTED-18V0                    PIC -9(18).
020100         04 FILLER                           PIC X.
020200     03 FILLER PIC X(50) VALUE SPACE.
020300 01  TEST-CORRECT.
020400     02 FILLER PIC X(30) VALUE SPACE.
020500     02 FILLER PIC X(17) VALUE "       CORRECT =".
020600     02 CORRECT-X.
020700     03 CORRECT-A                  PIC X(20) VALUE SPACE.
020800     03 CORRECT-N    REDEFINES CORRECT-A     PIC -9(9).9(9).
020900     03 CORRECT-0V18 REDEFINES CORRECT-A     PIC -.9(18).
021000     03 CORRECT-4V14 REDEFINES CORRECT-A     PIC -9(4).9(14).
021100     03 CORRECT-14V4 REDEFINES CORRECT-A     PIC -9(14).9(4).
021200     03      CR-18V0 REDEFINES CORRECT-A.
021300         04 CORRECT-18V0                     PIC -9(18).
021400         04 FILLER                           PIC X.
021500     03 FILLER PIC X(2) VALUE SPACE.
021600     03 COR-ANSI-REFERENCE             PIC X(48) VALUE SPACE.
021700 01  CCVS-C-1.
021800     02 FILLER  PIC IS X(99)    VALUE IS " FEATURE              PA
021900-    "SS  PARAGRAPH-NAME
022000-    "       REMARKS".
022100     02 FILLER                     PIC X(20)    VALUE SPACE.
022200 01  CCVS-C-2.
022300     02 FILLER                     PIC X        VALUE SPACE.
022400     02 FILLER                     PIC X(6)     VALUE "TESTED".
022500     02 FILLER                     PIC X(15)    VALUE SPACE.
022600     02 FILLER                     PIC X(4)     VALUE "FAIL".
022700     02 FILLER                     PIC X(94)    VALUE SPACE.
022800 01  REC-SKL-SUB                   PIC 9(2)     VALUE ZERO.
022900 01  REC-CT                        PIC 99       VALUE ZERO.
023000 01  DELETE-COUNTER                PIC 999      VALUE ZERO.
023100 01  ERROR-COUNTER                 PIC 999      VALUE ZERO.
023200 01  INSPECT-COUNTER               PIC 999      VALUE ZERO.
023300 01  PASS-COUNTER                  PIC 999      VALUE ZERO.
023400 01  TOTAL-ERROR                   PIC 999      VALUE ZERO.
023500 01  ERROR-HOLD                    PIC 999      VALUE ZERO.
023600 01  DUMMY-HOLD                    PIC X(120)   VALUE SPACE.
023700 01  RECORD-COUNT                  PIC 9(5)     VALUE ZERO.
023800 01  ANSI-REFERENCE                PIC X(48)    VALUE SPACES.
023900 01  CCVS-H-1.
024000     02  FILLER                    PIC X(39)    VALUE SPACES.
024100     02  FILLER                    PIC X(42)    VALUE
024200     "OFFICIAL COBOL COMPILER VALIDATION SYSTEM".
024300     02  FILLER                    PIC X(39)    VALUE SPACES.
024400 01  CCVS-H-2A.
024500   02  FILLER                        PIC X(40)  VALUE SPACE.
024600   02  FILLER                        PIC X(7)   VALUE "CCVS85 ".
024700   02  FILLER                        PIC XXXX   VALUE
024800     "4.2 ".
024900   02  FILLER                        PIC X(28)  VALUE
025000            " COPY - NOT FOR DISTRIBUTION".
025100   02  FILLER                        PIC X(41)  VALUE SPACE.
025200
025300 01  CCVS-H-2B.
025400   02  FILLER                        PIC X(15)  VALUE
025500            "TEST RESULT OF ".
025600   02  TEST-ID                       PIC X(9).
025700   02  FILLER                        PIC X(4)   VALUE
025800            " IN ".
025900   02  FILLER                        PIC X(12)  VALUE
026000     " HIGH       ".
026100   02  FILLER                        PIC X(22)  VALUE
026200            " LEVEL VALIDATION FOR ".
026300   02  FILLER                        PIC X(58)  VALUE
026400     "ON-SITE VALIDATION, NATIONAL INSTITUTE OF STD & TECH.     ".
026500 01  CCVS-H-3.
026600     02  FILLER                      PIC X(34)  VALUE
026700            " FOR OFFICIAL USE ONLY    ".
026800     02  FILLER                      PIC X(58)  VALUE
026900     "COBOL 85 VERSION 4.2, Apr  1993 SSVG                      ".
027000     02  FILLER                      PIC X(28)  VALUE
027100            "  COPYRIGHT   1985 ".
027200 01  CCVS-E-1.
027300     02 FILLER                       PIC X(52)  VALUE SPACE.
027400     02 FILLER  PIC X(14) VALUE IS "END OF TEST-  ".
027500     02 ID-AGAIN                     PIC X(9).
027600     02 FILLER                       PIC X(45)  VALUE SPACES.
027700 01  CCVS-E-2.
027800     02  FILLER                      PIC X(31)  VALUE SPACE.
027900     02  FILLER                      PIC X(21)  VALUE SPACE.
028000     02 CCVS-E-2-2.
028100         03 ERROR-TOTAL              PIC XXX    VALUE SPACE.
028200         03 FILLER                   PIC X      VALUE SPACE.
028300         03 ENDER-DESC               PIC X(44)  VALUE
028400            "ERRORS ENCOUNTERED".
028500 01  CCVS-E-3.
028600     02  FILLER                      PIC X(22)  VALUE
028700            " FOR OFFICIAL USE ONLY".
028800     02  FILLER                      PIC X(12)  VALUE SPACE.
028900     02  FILLER                      PIC X(58)  VALUE
029000     "ON-SITE VALIDATION, NATIONAL INSTITUTE OF STD & TECH.     ".
029100     02  FILLER                      PIC X(13)  VALUE SPACE.
029200     02 FILLER                       PIC X(15)  VALUE
029300             " COPYRIGHT 1985".
029400 01  CCVS-E-4.
029500     02 CCVS-E-4-1                   PIC XXX    VALUE SPACE.
029600     02 FILLER                       PIC X(4)   VALUE " OF ".
029700     02 CCVS-E-4-2                   PIC XXX    VALUE SPACE.
029800     02 FILLER                       PIC X(40)  VALUE
029900      "  TESTS WERE EXECUTED SUCCESSFULLY".
030000 01  XXINFO.
030100     02 FILLER                       PIC X(19)  VALUE
030200            "*** INFORMATION ***".
030300     02 INFO-TEXT.
030400       04 FILLER                     PIC X(8)   VALUE SPACE.
030500       04 XXCOMPUTED                 PIC X(20).
030600       04 FILLER                     PIC X(5)   VALUE SPACE.
030700       04 XXCORRECT                  PIC X(20).
030800     02 INF-ANSI-REFERENCE           PIC X(48).
030900 01  HYPHEN-LINE.
031000     02 FILLER  PIC IS X VALUE IS SPACE.
031100     02 FILLER  PIC IS X(65)    VALUE IS "************************
031200-    "*****************************************".
031300     02 FILLER  PIC IS X(54)    VALUE IS "************************
031400-    "******************************".
031500 01  CCVS-PGM-ID                     PIC X(9)   VALUE
031600     "IX103A".
031700 PROCEDURE DIVISION.
031800 DECLARATIVES.
031900 USE-IX103-TEST  SECTION.
032000     USE   AFTER  STANDARD  EXCEPTION PROCEDURE
032100             IX-FS1.
032200 USE-PAR-001.
032300     ADD   1   TO WRK-CS-09V00-009.
032400 USE-PAR-EXIT.
032500     EXIT.
032600 END DECLARATIVES.
032700 CCVS1 SECTION.
032800 OPEN-FILES.
032900P    OPEN I-O RAW-DATA.
033000P    MOVE CCVS-PGM-ID TO RAW-DATA-KEY.
033100P    READ RAW-DATA INVALID KEY GO TO END-E-1.
033200P    MOVE "ABORTED " TO C-ABORT.
033300P    ADD 1 TO C-NO-OF-TESTS.
033400P    ACCEPT C-DATE  FROM DATE.
033500P    ACCEPT C-TIME  FROM TIME.
033600P    REWRITE RAW-DATA-SATZ INVALID KEY GO TO END-E-1.
033700PEND-E-1.
033800P    CLOSE RAW-DATA.
033900     OPEN    OUTPUT PRINT-FILE.
034000     MOVE  CCVS-PGM-ID TO TEST-ID. MOVE CCVS-PGM-ID TO ID-AGAIN.
034100     MOVE    SPACE TO TEST-RESULTS.
034200     PERFORM HEAD-ROUTINE THRU COLUMN-NAMES-ROUTINE.
034300     MOVE    ZERO TO REC-SKL-SUB.
034400     PERFORM CCVS-INIT-FILE 9 TIMES.
034500 CCVS-INIT-FILE.
034600     ADD     1 TO REC-SKL-SUB.
034700     MOVE    FILE-RECORD-INFO-SKELETON
034800          TO FILE-RECORD-INFO (REC-SKL-SUB).
034900 CCVS-INIT-EXIT.
035000     GO TO CCVS1-EXIT.
035100 CLOSE-FILES.
035200P    OPEN I-O RAW-DATA.
035300P    MOVE CCVS-PGM-ID TO RAW-DATA-KEY.
035400P    READ RAW-DATA INVALID KEY GO TO END-E-2.
035500P    MOVE "OK.     " TO C-ABORT.
035600P    MOVE PASS-COUNTER TO C-OK.
035700P    MOVE ERROR-HOLD   TO C-ALL.
035800P    MOVE ERROR-COUNTER TO C-FAIL.
035900P    MOVE DELETE-COUNTER TO C-DELETED.
036000P    MOVE INSPECT-COUNTER TO C-INSPECT.
036100P    REWRITE RAW-DATA-SATZ INVALID KEY GO TO END-E-2.
036200PEND-E-2.
036300P    CLOSE RAW-DATA.
036400     PERFORM END-ROUTINE THRU END-ROUTINE-13. CLOSE PRINT-FILE.
036500 TERMINATE-CCVS.
036600S    EXIT PROGRAM.
036700STERMINATE-CALL.
036800     STOP     RUN.
036900 INSPT. MOVE "INSPT" TO P-OR-F. ADD 1 TO INSPECT-COUNTER.
037000 PASS.  MOVE "PASS " TO P-OR-F.  ADD 1 TO PASS-COUNTER.
037100 FAIL.  MOVE "FAIL*" TO P-OR-F.  ADD 1 TO ERROR-COUNTER.
037200 DE-LETE.  MOVE "*****" TO P-OR-F.  ADD 1 TO DELETE-COUNTER.
037300     MOVE "****TEST DELETED****" TO RE-MARK.
037400 PRINT-DETAIL.
037500     IF REC-CT NOT EQUAL TO ZERO
037600             MOVE "." TO PARDOT-X
037700             MOVE REC-CT TO DOTVALUE.
037800     MOVE     TEST-RESULTS TO PRINT-REC. PERFORM WRITE-LINE.
037900     IF P-OR-F EQUAL TO "FAIL*"  PERFORM WRITE-LINE
038000        PERFORM FAIL-ROUTINE THRU FAIL-ROUTINE-EX
038100          ELSE PERFORM BAIL-OUT THRU BAIL-OUT-EX.
038200     MOVE SPACE TO P-OR-F. MOVE SPACE TO COMPUTED-X.
038300     MOVE SPACE TO CORRECT-X.
038400     IF     REC-CT EQUAL TO ZERO  MOVE SPACE TO PAR-NAME.
038500     MOVE     SPACE TO RE-MARK.
038600 HEAD-ROUTINE.
038700     MOVE CCVS-H-1  TO DUMMY-RECORD. PERFORM WRITE-LINE 2 TIMES.
038800     MOVE CCVS-H-2A TO DUMMY-RECORD. PERFORM WRITE-LINE 2 TIMES.
038900     MOVE CCVS-H-2B TO DUMMY-RECORD. PERFORM WRITE-LINE 3 TIMES.
039000     MOVE CCVS-H-3  TO DUMMY-RECORD. PERFORM WRITE-LINE 3 TIMES.
039100 COLUMN-NAMES-ROUTINE.
039200     MOVE CCVS-C-1 TO DUMMY-RECORD. PERFORM WRITE-LINE.
039300     MOVE CCVS-C-2 TO DUMMY-RECORD. PERFORM WRITE-LINE 2 TIMES.
039400     MOVE HYPHEN-LINE TO DUMMY-RECORD. PERFORM WRITE-LINE.
039500 END-ROUTINE.
039600     MOVE HYPHEN-LINE TO DUMMY-RECORD. PERFORM WRITE-LINE 5 TIMES.
039700 END-RTN-EXIT.
039800     MOVE CCVS-E-1 TO DUMMY-RECORD. PERFORM WRITE-LINE 2 TIMES.
039900 END-ROUTINE-1.
040000      ADD ERROR-COUNTER TO ERROR-HOLD ADD INSPECT-COUNTER TO
040100      ERROR-HOLD. ADD DELETE-COUNTER TO ERROR-HOLD.
040200      ADD PASS-COUNTER TO ERROR-HOLD.
040300*     IF PASS-COUNTER EQUAL TO ERROR-HOLD GO TO END-ROUTINE-12.
040400      MOVE PASS-COUNTER TO CCVS-E-4-1.
040500      MOVE ERROR-HOLD TO CCVS-E-4-2.
040600      MOVE CCVS-E-4 TO CCVS-E-2-2.
040700      MOVE CCVS-E-2 TO DUMMY-RECORD PERFORM WRITE-LINE.
040800  END-ROUTINE-12.
040900      MOVE "TEST(S) FAILED" TO ENDER-DESC.
041000     IF       ERROR-COUNTER IS EQUAL TO ZERO
041100         MOVE "NO " TO ERROR-TOTAL
041200         ELSE
041300         MOVE ERROR-COUNTER TO ERROR-TOTAL.
041400     MOVE     CCVS-E-2 TO DUMMY-RECORD.
041500     PERFORM WRITE-LINE.
041600 END-ROUTINE-13.
041700     IF DELETE-COUNTER IS EQUAL TO ZERO
041800         MOVE "NO " TO ERROR-TOTAL  ELSE
041900         MOVE DELETE-COUNTER TO ERROR-TOTAL.
042000     MOVE "TEST(S) DELETED     " TO ENDER-DESC.
042100     MOVE CCVS-E-2 TO DUMMY-RECORD. PERFORM WRITE-LINE.
042200      IF   INSPECT-COUNTER EQUAL TO ZERO
042300          MOVE "NO " TO ERROR-TOTAL
042400      ELSE MOVE INSPECT-COUNTER TO ERROR-TOTAL.
042500      MOVE "TEST(S) REQUIRE INSPECTION" TO ENDER-DESC.
042600      MOVE CCVS-E-2 TO DUMMY-RECORD. PERFORM WRITE-LINE.
042700     MOVE CCVS-E-3 TO DUMMY-RECORD. PERFORM WRITE-LINE.
042800 WRITE-LINE.
042900     ADD 1 TO RECORD-COUNT.
043000Y    IF RECORD-COUNT GREATER 42
043100Y        MOVE DUMMY-RECORD TO DUMMY-HOLD
043200Y        MOVE SPACE TO DUMMY-RECORD
043300Y        WRITE DUMMY-RECORD AFTER ADVANCING PAGE
043400Y        MOVE CCVS-H-1  TO DUMMY-RECORD PERFORM WRT-LN 2 TIMES
043500Y        MOVE CCVS-H-2A TO DUMMY-RECORD PERFORM WRT-LN 2 TIMES
043600Y        MOVE CCVS-H-2B TO DUMMY-RECORD PERFORM WRT-LN 3 TIMES
043700Y        MOVE CCVS-H-3  TO DUMMY-RECORD PERFORM WRT-LN 3 TIMES
043800Y        MOVE CCVS-C-1  TO DUMMY-RECORD PERFORM WRT-LN
043900Y        MOVE CCVS-C-2  TO DUMMY-RECORD PERFORM WRT-LN
044000Y        MOVE HYPHEN-LINE TO DUMMY-RECORD PERFORM WRT-LN
044100Y        MOVE DUMMY-HOLD TO DUMMY-RECORD
044200Y        MOVE ZERO TO RECORD-COUNT.
044300     PERFORM WRT-LN.
044400 WRT-LN.
044500     WRITE    DUMMY-RECORD AFTER ADVANCING 1 LINES.
044600     MOVE SPACE TO DUMMY-RECORD.
044700 BLANK-LINE-PRINT.
044800     PERFORM WRT-LN.
044900 FAIL-ROUTINE.
045000     IF     COMPUTED-X NOT EQUAL TO SPACE
045100            GO TO   FAIL-ROUTINE-WRITE.
045200     IF     CORRECT-X NOT EQUAL TO SPACE GO TO FAIL-ROUTINE-WRITE.
045300     MOVE   ANSI-REFERENCE TO INF-ANSI-REFERENCE.
045400     MOVE  "NO FURTHER INFORMATION, SEE PROGRAM." TO INFO-TEXT.
045500     MOVE   XXINFO TO DUMMY-RECORD. PERFORM WRITE-LINE 2 TIMES.
045600     MOVE   SPACES TO INF-ANSI-REFERENCE.
045700     GO TO  FAIL-ROUTINE-EX.
045800 FAIL-ROUTINE-WRITE.
045900     MOVE   TEST-COMPUTED TO PRINT-REC PERFORM WRITE-LINE
046000     MOVE   ANSI-REFERENCE TO COR-ANSI-REFERENCE.
046100     MOVE   TEST-CORRECT TO PRINT-REC PERFORM WRITE-LINE 2 TIMES.
046200     MOVE   SPACES TO COR-ANSI-REFERENCE.
046300 FAIL-ROUTINE-EX. EXIT.
046400 BAIL-OUT.
046500     IF     COMPUTED-A NOT EQUAL TO SPACE GO TO BAIL-OUT-WRITE.
046600     IF     CORRECT-A EQUAL TO SPACE GO TO BAIL-OUT-EX.
046700 BAIL-OUT-WRITE.
046800     MOVE CORRECT-A TO XXCORRECT. MOVE COMPUTED-A TO XXCOMPUTED.
046900     MOVE   ANSI-REFERENCE TO INF-ANSI-REFERENCE.
047000     MOVE   XXINFO TO DUMMY-RECORD. PERFORM WRITE-LINE 2 TIMES.
047100     MOVE   SPACES TO INF-ANSI-REFERENCE.
047200 BAIL-OUT-EX. EXIT.
047300 CCVS1-EXIT.
047400     EXIT.
047500 SECT-IX-03-001 SECTION.
047600 INX-INIT-006.
047700*    THIS FILE "IX-FS1" IS ACCESSED SEQUENTIALLY AND HAS
047800*    ASSOCIATED WITH IT A RECORD KEY WHICH AT ALL TIMES SHOULD
047900*    CONTAIN THE INDEX OF THE RECORD PREVIOUSLY READ.
048000     OPEN INPUT IX-FS1.
048100     MOVE     "INX-TEST-006" TO   PAR-NAME.
048200     MOVE     ZERO TO             WRK-CS-09V00-006.
048300     MOVE     ZERO TO             WRK-CS-09V00-007.
048400     MOVE     ZERO TO             WRK-CS-09V00-008.
048500     MOVE     ZERO TO             WRK-CS-09V00-009.
048600     MOVE     ZERO TO             WRK-CS-09V00-010.
048700     MOVE     ZERO TO             WRK-CS-09V00-011.
048800     MOVE     SPACE TO  FILE-RECORD-INFO (1).
048900     MOVE    ZERO TO WRK-DU-09V00-001.
049000     MOVE    IX-FS1-KEY TO COMPUTED-A.
049100     MOVE    SPACE TO P-OR-F.
049200     MOVE    "INFORMATION" TO CORRECT-A.
049300     MOVE    "KEY AFTER OPEN" TO RE-MARK.
049400     MOVE    "RECORD KEY ON OPEN"  TO FEATURE.
049500     PERFORM PRINT-DETAIL.
049600     MOVE    "INX-TEST-006"  TO PAR-NAME.
049700     MOVE    "READ SEQUENTIAL  "  TO FEATURE.
049800 INX-TEST-006-R.
049900     ADD      1 TO WRK-CS-09V00-006.
050000     READ     IX-FS1
050100              AT END GO TO READ-TEST-F1-01.
050200     MOVE     IX-FS1R1-F-G-240    TO FILE-RECORD-INFO (1).
050300     IF       UPDATE-NUMBER (1) EQUAL TO 00
050400             ADD 1 TO WRK-CS-09V00-007
050500              GO TO INX-TEST-006-2.
050600     IF       UPDATE-NUMBER (1) EQUAL TO 01
050700              ADD 1 TO WRK-CS-09V00-008
050800              GO TO INX-TEST-006-2.
050900     ADD      1 TO WRK-CS-09V00-009.
051000 INX-TEST-006-2.
051100     MOVE    XRECORD-KEY (1)  TO  IX-WRK-KEY.
051200     IF      WRK-DU-09V00-001  NOT EQUAL TO XRECORD-NUMBER (1)
051300              ADD 1 TO  WRK-CS-09V00-010.
051400     IF       WRK-CS-09V00-006  GREATER 501
051500              GO TO READ-TEST-F1-01.
051600     GO TO    INX-TEST-006-R.
051700 READ-TEST-F1-01.
051800     MOVE    "READ-TEST-F1-01"  TO PAR-NAME.
051900     MOVE    "READ SEQUENTIAL  "  TO FEATURE.
052000*
052100*  TEST   1
052200*
052300     IF       WRK-CS-09V00-006 NOT EQUAL TO 501
052400              MOVE "INCORRECT RECORD COUNT"  TO RE-MARK
052500              MOVE  WRK-CS-09V00-006 TO COMPUTED-18V0
052600              MOVE  500  TO             CORRECT-18V0
052700              MOVE "IX-28 4.5.2                       " TO RE-MARK
052800              PERFORM FAIL
052900              ELSE
053000              PERFORM PASS.
053100     PERFORM  PRINT-DETAIL.
053200 READ-TEST-F1-02.
053300     MOVE    "READ-TEST-F1-02"  TO PAR-NAME.
053400     MOVE    "READ SEQUENTIAL  "  TO FEATURE.
053500*
053600*  TEST   2
053700*
053800     IF       WRK-CS-09V00-007 EQUAL TO 400
053900              PERFORM PASS
054000              ELSE
054100              MOVE "NON-UPDATED RECORDS" TO COMPUTED-A
054200              MOVE  WRK-CS-09V00-007 TO CORRECT-18V0
054300              MOVE "SHOULD BE 400; IX-28 4.5.2" TO RE-MARK
054400              PERFORM FAIL.
054500     PERFORM  PRINT-DETAIL.
054600 READ-TEST-F1-03.
054700     MOVE    "READ-TEST-F1-03"  TO PAR-NAME.
054800     MOVE    "READ SEQUENTIAL  "  TO FEATURE.
054900*
055000*  TEST   3
055100*
055200     IF      WRK-CS-09V00-008 EQUAL TO 100
055300              PERFORM PASS
055400              ELSE
055500             MOVE WRK-CS-09V00-008 TO COMPUTED-18V0
055600             MOVE 100             TO  CORRECT-18V0
055700              MOVE "IX-28 4.5.2                       " TO RE-MARK
055800              PERFORM FAIL.
055900     PERFORM  PRINT-DETAIL.
056000 READ-TEST-F1-04.
056100     MOVE    "READ-TEST-F1-04"  TO PAR-NAME.
056200     MOVE    "READ SEQUENTIAL  "  TO FEATURE.
056300*
056400*  TEST   4
056500*
056600     IF      WRK-CS-09V00-009 EQUAL TO ZERO
056700             PERFORM PASS
056800             ELSE
056900             MOVE WRK-CS-09V00-009 TO COMPUTED-18V0
057000             MOVE  ZERO            TO CORRECT-18V0
057100             MOVE "BAD-UPDATES" TO RE-MARK
057200             PERFORM FAIL.
057300     PERFORM PRINT-DETAIL.
057400 READ-TEST-F1-05.
057500     MOVE    "READ-TEST-F1-05"  TO PAR-NAME.
057600     MOVE    "READ SEQUENTIAL  "  TO FEATURE.
057700*
057800*  TEST   5
057900*
058000     IF      WRK-CS-09V00-010 EQUAL TO ZERO
058100             PERFORM PASS
058200             ELSE
058300             MOVE WRK-CS-09V00-010 TO COMPUTED-18V0
058400             MOVE ZERO             TO CORRECT-18V0
058500             MOVE "KEY VS RECORD; IX-28" TO RE-MARK
058600             PERFORM FAIL.
058700     PERFORM PRINT-DETAIL.
058800     CLOSE    IX-FS1.
058900 DELETE-INIT-GF-01.
059000*
059100*  TEST   1
059200*
059300     MOVE     "DELETE-TEST-GF-01" TO PAR-NAME
059400     OPEN     I-O IX-FS1.
059500     MOVE     ZERO TO WRK-CS-09V00-006
059600     MOVE     ZERO TO WRK-CS-09V00-007
059700     MOVE     ZERO TO WRK-CS-09V00-008
059800     MOVE     ZERO TO WRK-CS-09V00-009
059900     MOVE     ZERO TO WRK-CS-09V00-010
060000     MOVE     ZERO TO WRK-CS-09V00-011
060100     MOVE     SPACE TO  FILE-RECORD-INFO (1).
060200     MOVE    "DELETE   "   TO FEATURE.
060300 DELETE-TEST-GF-01.
060400     ADD      1 TO WRK-CS-09V00-006
060500     ADD      1 TO WRK-CS-09V00-007.
060600     READ     IX-FS1
060700              AT END
060800             MOVE  "AT END PATH TAKEN"  TO RE-MARK
060900             GO TO  DELETE-TEST-GF-01-3.
061000     MOVE     IX-FS1R1-F-G-240 TO FILE-RECORD-INFO (1).
061100     IF       WRK-CS-09V00-007  EQUAL TO 4
061200              GO TO DELETE-TEST-GF-01-2.
061300     IF       WRK-CS-09V00-006 GREATER 501
061400              MOVE  "AT END NOT TAKEN"  TO RE-MARK
061500              GO TO DELETE-TEST-GF-01-3.
061600     GO TO    DELETE-TEST-GF-01.
061700 DELETE-TEST-GF-01-2.
061800     MOVE  CCVS-PGM-ID  TO  XPROGRAM-NAME (1).
061900     MOVE  99 TO UPDATE-NUMBER (1).
062000     MOVE     FILE-RECORD-INFO (1) TO IX-FS1R1-F-G-240.
062100     DELETE   IX-FS1.
062200     MOVE     ZERO TO  WRK-CS-09V00-007.
062300     ADD      1 TO  WRK-CS-09V00-008
062400     GO TO    DELETE-TEST-GF-01.
062500 DELETE-TEST-GF-01-3.
062600     IF       WRK-CS-09V00-006 NOT EQUAL TO 501
062700              MOVE WRK-CS-09V00-006 TO COMPUTED-18V0
062800              MOVE              501 TO CORRECT-18V0
062900              MOVE "IX-21 4.3.2                       " TO RE-MARK
063000              PERFORM FAIL
063100              ELSE
063200              PERFORM PASS.
063300     PERFORM  PRINT-DETAIL.
063400 DELETE-TEST-GF-02.
063500     MOVE     "DELETE-TEST-GF-02" TO PAR-NAME
063600     MOVE    "DELETE   "   TO FEATURE.
063700*
063800*  TEST   2
063900*
064000     IF       WRK-CS-09V00-008 NOT EQUAL TO 125
064100              MOVE WRK-CS-09V00-008 TO COMPUTED-18V0
064200              MOVE 125              TO CORRECT-18V0
064300              MOVE "DELETED RECORDS" TO RE-MARK
064400              PERFORM FAIL
064500              ELSE
064600              PERFORM PASS.
064700     PERFORM  PRINT-DETAIL.
064800 DELETE-TEST-GF-03.
064900     MOVE     "DELETE-TEST-GF-03" TO PAR-NAME
065000     MOVE    "DELETE   "   TO FEATURE.
065100*
065200*  TEST   3
065300*
065400       IF WRK-CS-09V00-009 EQUAL TO ZERO
065500         PERFORM PASS
065600         ELSE
065700         PERFORM FAIL
065800         MOVE WRK-CS-09V00-009 TO COMPUTED-18V0
065900         MOVE ZERO TO CORRECT-18V0
066000         MOVE "INVALID KEY" TO RE-MARK.
066100       PERFORM PRINT-DETAIL.
066200     CLOSE    IX-FS1.
066300 DELETE-INIT-GF-04.
066400     MOVE     "DELETE-TEST-GF-04" TO PAR-NAME
066500     MOVE    "DELETE   "   TO FEATURE.
066600*
066700*  TEST   4
066800*
066900     MOVE     "DELETE-TEST-GF-04" TO PAR-NAME.
067000     MOVE     ZERO TO   WRK-CS-09V00-006
067100     MOVE     ZERO TO   WRK-CS-09V00-007
067200     MOVE     ZERO TO   WRK-CS-09V00-008
067300     MOVE     ZERO TO   WRK-CS-09V00-009
067400     MOVE     ZERO TO   WRK-CS-09V00-010
067500     MOVE     ZERO TO   WRK-CS-09V00-011
067600     MOVE     SPACE  TO  FILE-RECORD-INFO (1).
067700     MOVE    ZERO TO WRK-DU-09V00-001.
067800     OPEN     INPUT  IX-FS1.
067900 DELETE-TEST-GF-04.
068000     ADD      1 TO WRK-CS-09V00-006.
068100     ADD      1 TO WRK-CS-09V00-007.
068200     ADD      1 TO WRK-CS-09V00-008.
068300     READ     IX-FS1  AT END  GO TO DELETE-TEST-GF-04-3.
068400     MOVE     IX-FS1R1-F-G-240 TO FILE-RECORD-INFO (1).
068500     IF       UPDATE-NUMBER (1) EQUAL TO 99
068600              ADD  1 TO WRK-CS-09V00-009.
068700     IF       WRK-CS-09V00-007  EQUAL TO 4
068800              MOVE 01 TO WRK-CS-09V00-007
068900              ADD 1 TO WRK-CS-09V00-008.
069000     MOVE    XRECORD-KEY (1)  TO  IX-WRK-KEY.
069100     MOVE    WRK-CS-09V00-008  TO WRK-DU-09V00-001.
069200     IF      IX-WRK-KEY  EQUAL TO IX-FS1-KEY
069300              ADD 1 TO  WRK-CS-09V00-010.
069400     IF       XRECORD-NUMBER (1) EQUAL TO  WRK-CS-09V00-008
069500              ADD 1 TO  WRK-CS-09V00-011.
069600     IF       WRK-CS-09V00-006 GREATER  501
069700              GO TO DELETE-TEST-GF-04-3.
069800     GO TO    DELETE-TEST-GF-04.
069900 DELETE-TEST-GF-04-3.
070000     IF       WRK-CS-09V00-006 NOT EQUAL TO 376
070100              MOVE "INCORRECT RECORD COUNT"  TO RE-MARK
070200              MOVE WRK-CS-09V00-006 TO COMPUTED-18V0
070300              MOVE 376 TO CORRECT-18V0
070400              MOVE "IX-21 4.3.2                       " TO RE-MARK
070500              PERFORM  FAIL
070600              ELSE
070700              PERFORM  PASS.
070800     PERFORM  PRINT-DETAIL.
070900 DELETE-TEST-GF-05.
071000     MOVE     "DELETE-TEST-GF-05" TO PAR-NAME
071100     MOVE    "DELETE   "   TO FEATURE.
071200*
071300*  TEST   5
071400*
071500     IF       WRK-CS-09V00-009 NOT EQUAL TO ZERO
071600             MOVE    ZERO TO CORRECT-18V0
071700              MOVE WRK-CS-09V00-009 TO COMPUTED-18V0
071800              MOVE "DELETED RECORDS" TO RE-MARK
071900              MOVE "IX-21 4.3.2                       " TO RE-MARK
072000              PERFORM FAIL
072100              ELSE
072200              PERFORM PASS.
072300     PERFORM  PRINT-DETAIL.
072400 DELETE-TEST-GF-06.
072500     MOVE     "DELETE-TEST-GF-06" TO PAR-NAME
072600     MOVE    "DELETE   "   TO FEATURE.
072700*
072800*  TEST   6
072900*
073000     IF       WRK-CS-09V00-010 NOT EQUAL TO 375
073100             MOVE    375  TO CORRECT-18V0
073200              MOVE "KEY MISMATCH" TO RE-MARK
073300              MOVE WRK-CS-09V00-010 TO COMPUTED-18V0
073400              MOVE "IX-21 4.3.2                       " TO RE-MARK
073500              PERFORM FAIL
073600              ELSE
073700              PERFORM PASS.
073800     PERFORM  PRINT-DETAIL.
073900 DELETE-TEST-GF-07.
074000     MOVE     "DELETE-TEST-GF-07" TO PAR-NAME
074100     MOVE    "DELETE   "   TO FEATURE.
074200*
074300*  TEST   7
074400*
074500     IF      WRK-CS-09V00-011  NOT EQUAL TO  375
074600             MOVE   375  TO CORRECT-18V0
074700             MOVE   "INCORRECT RECORD FOUND"  TO RE-MARK
074800             MOVE   WRK-CS-09V00-011  TO COMPUTED-18V0
074900              MOVE "IX-21 4.3.2                       " TO RE-MARK
075000             PERFORM   FAIL
075100             ELSE
075200             PERFORM   PASS.
075300     PERFORM   PRINT-DETAIL.
075400     CLOSE    IX-FS1.
075500 CCVS-EXIT SECTION.
075600 CCVS-999999.
075700     GO TO CLOSE-FILES.
