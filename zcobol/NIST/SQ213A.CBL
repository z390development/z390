000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID.
000300     SQ213A.
000400****************************************************************
000500*                                                              *
000600*    VALIDATION FOR:-                                          *
000700*    " HIGH       ".
000800*                                                              *
000900*    CREATION DATE     /     VALIDATION DATE                   *
001000*    "4.2 ".
001100*                                                              *
001200*         THE ROUTINE SQ213A TESTS THE USE OF THE USE AFTER ERROR
001300*    PROCEDURE FOR EXTEND AND FILE-NAME SERIES.  SQ213A IS
001400*    BASICALLY A REWRITE OF SQ205 WITH THE ADDITION OF THE USE
001500*    PROCEDURES.  MAGNETIC TAPE FILE SQ-FS1 IS FIRST CREATED WITH
001600*    750 RECORDS.  THEN IT IS REOPENED WITH EXTEND AND AN
001700*    ADDITIONAL 250 RECORDS ARE WRITTEN.  FINALLY IT IS READ AND
001800*    VALIDATED FOR CORRECTNESS.  MASS-STORAGE FILE SQ-FS2 IS
001900*    CREATED AS A SINGLE OUTPUT FILE WITH 1000 RECORDS, AFTERWHICH
002000*    IT IS READ AND VALIDATED FOR CORRECTNESS.  THE TEST FOR THE
002100*    USE PROCEDURE MERELY INDICATES WHETHER OR NOT THE USE
002200*    PROCEDURES WERE REFERENCED.
002300 ENVIRONMENT DIVISION.
002400 CONFIGURATION SECTION.
002500 SOURCE-COMPUTER.
002600     XXXXX082.
002700 OBJECT-COMPUTER.
002800     XXXXX083.
002900 INPUT-OUTPUT SECTION.
003000 FILE-CONTROL.
003100P    SELECT RAW-DATA   ASSIGN TO
003200P    XXXXX062
003300P           ORGANIZATION IS INDEXED
003400P           ACCESS MODE IS RANDOM
003500P           RECORD KEY IS RAW-DATA-KEY.
003600     SELECT PRINT-FILE ASSIGN TO
003700     XXXXX055.
003800     SELECT SQ-FS1 ASSIGN TO
003900     XXXXX001
004000     ORGANIZATION IS SEQUENTIAL
004100     ACCESS MODE IS SEQUENTIAL.
004200     SELECT SQ-FS2 ASSIGN TO
004300     XXXXX014
004400     ORGANIZATION IS SEQUENTIAL
004500     ACCESS MODE IS SEQUENTIAL.
004600 DATA DIVISION.
004700 FILE SECTION.
004800P
004900PFD  RAW-DATA.
005000P
005100P01  RAW-DATA-SATZ.
005200P    05  RAW-DATA-KEY        PIC X(6).
005300P    05  C-DATE              PIC 9(6).
005400P    05  C-TIME              PIC 9(8).
005500P    05  C-NO-OF-TESTS       PIC 99.
005600P    05  C-OK                PIC 999.
005700P    05  C-ALL               PIC 999.
005800P    05  C-FAIL              PIC 999.
005900P    05  C-DELETED           PIC 999.
006000P    05  C-INSPECT           PIC 999.
006100P    05  C-NOTE              PIC X(13).
006200P    05  C-INDENT            PIC X.
006300P    05  C-ABORT             PIC X(8).
006400 FD  PRINT-FILE
006500C    LABEL RECORDS
006600C    XXXXX084
006700C    DATA RECORD IS PRINT-REC DUMMY-RECORD
006800               .
006900 01  PRINT-REC PICTURE X(120).
007000 01  DUMMY-RECORD PICTURE X(120).
007100 FD  SQ-FS1
007200C    LABEL RECORDS ARE STANDARD
007300     RECORD CONTAINS 126 CHARACTERS
007400     BLOCK CONTAINS 126 CHARACTERS.
007500 01  SQ-FS1R1-F-G-126.
007600     02 SQ-FS1R1-F-G-120 PIC X(120).
007700     02 SQ-FS1R1-F-G-006 PIC X(6).
007800 FD  SQ-FS2
007900C    LABEL RECORDS ARE STANDARD
008000     RECORD 126
008100     BLOCK CONTAINS 126 CHARACTERS.
008200 01  SQ-FS2R1-F-G-126.
008300     02 SQ-FS2R1-F-G-120 PIC X(120).
008400     02 SQ-FS2R1-F-G-006 PIC X(6).
008500 WORKING-STORAGE SECTION.
008600 77  RECORDS-IN-ERROR   PIC  9(4) VALUE 0.
008700 77  WRK-RECORD-COUNT   PIC  9(4) VALUE 0.
008800 01  COUNT-OF-RECS PIC 9999.
008900 01  EXTEND-ERROR       PIC 9999  VALUE 0.
009000 01  FN-SERIES-ERROR    PIC 9999  VALUE 0.
009100 01  FILE-RECORD-INFORMATION-REC.
009200     03 FILE-RECORD-INFO-SKELETON.
009300        05 FILLER                 PICTURE X(48)       VALUE
009400             "FILE=      ,RECORD=      /0,RECNO=000000,UPDT=00".
009500        05 FILLER                 PICTURE X(46)       VALUE
009600             ",ODO=0000,PGM=     ,LRECL=000000,BLKSIZ  =0000".
009700        05 FILLER                 PICTURE X(26)       VALUE
009800             ",LFIL=000000,ORG=  ,LBLR= ".
009900        05 FILLER                 PICTURE X(37)       VALUE
010000             ",RECKEY=                             ".
010100        05 FILLER                 PICTURE X(38)       VALUE
010200             ",ALTKEY1=                             ".
010300        05 FILLER                 PICTURE X(38)       VALUE
010400             ",ALTKEY2=                             ".
010500        05 FILLER                 PICTURE X(7)        VALUE SPACE.
010600     03 FILE-RECORD-INFO          OCCURS  10  TIMES.
010700        05 FILE-RECORD-INFO-P1-120.
010800           07 FILLER              PIC X(5).
010900           07 XFILE-NAME           PIC X(6).
011000           07 FILLER              PIC X(8).
011100           07 XRECORD-NAME         PIC X(6).
011200           07 FILLER              PIC X(1).
011300           07 REELUNIT-NUMBER     PIC 9(1).
011400           07 FILLER              PIC X(7).
011500           07 XRECORD-NUMBER       PIC 9(6).
011600           07 FILLER              PIC X(6).
011700           07 UPDATE-NUMBER       PIC 9(2).
011800           07 FILLER              PIC X(5).
011900           07 ODO-NUMBER          PIC 9(4).
012000           07 FILLER              PIC X(5).
012100           07 XPROGRAM-NAME        PIC X(5).
012200           07 FILLER              PIC X(7).
012300           07 XRECORD-LENGTH       PIC 9(6).
012400           07 FILLER              PIC X(7).
012500           07 CHARS-OR-RECORDS    PIC X(2).
012600           07 FILLER              PIC X(1).
012700           07 XBLOCK-SIZE          PIC 9(4).
012800           07 FILLER              PIC X(6).
012900           07 RECORDS-IN-FILE     PIC 9(6).
013000           07 FILLER              PIC X(5).
013100           07 XFILE-ORGANIZATION   PIC X(2).
013200           07 FILLER              PIC X(6).
013300           07 XLABEL-TYPE          PIC X(1).
013400        05 FILE-RECORD-INFO-P121-240.
013500           07 FILLER              PIC X(8).
013600           07 XRECORD-KEY          PIC X(29).
013700           07 FILLER              PIC X(9).
013800           07 ALTERNATE-KEY1      PIC X(29).
013900           07 FILLER              PIC X(9).
014000           07 ALTERNATE-KEY2      PIC X(29).
014100           07 FILLER              PIC X(7).
014200 01  TEST-RESULTS.
014300     02 FILLER                    PICTURE X VALUE SPACE.
014400     02 FEATURE                   PICTURE X(20) VALUE SPACE.
014500     02 FILLER                    PICTURE X VALUE SPACE.
014600     02 P-OR-F                    PICTURE X(5) VALUE SPACE.
014700     02 FILLER                    PICTURE X  VALUE SPACE.
014800     02  PAR-NAME.
014900       03 FILLER PICTURE X(12) VALUE SPACE.
015000       03  PARDOT-X PICTURE X  VALUE SPACE.
015100       03 DOTVALUE PICTURE 99  VALUE ZERO.
015200       03 FILLER PIC X(5) VALUE SPACE.
015300     02 FILLER PIC X(10) VALUE SPACE.
015400     02 RE-MARK PIC X(61).
015500 01  TEST-COMPUTED.
015600     02 FILLER PIC X(30) VALUE SPACE.
015700     02 FILLER PIC X(17) VALUE "       COMPUTED=".
015800     02 COMPUTED-X.
015900     03 COMPUTED-A                PICTURE X(20) VALUE SPACE.
016000     03 COMPUTED-N REDEFINES COMPUTED-A PICTURE -9(9).9(9).
016100     03 COMPUTED-0V18 REDEFINES COMPUTED-A  PICTURE -.9(18).
016200     03 COMPUTED-4V14 REDEFINES COMPUTED-A  PICTURE -9(4).9(14).
016300     03 COMPUTED-14V4 REDEFINES COMPUTED-A  PICTURE -9(14).9(4).
016400     03       CM-18V0 REDEFINES COMPUTED-A.
016500         04 COMPUTED-18V0                   PICTURE -9(18).
016600         04 FILLER                          PICTURE X.
016700     03 FILLER PIC X(50) VALUE SPACE.
016800 01  TEST-CORRECT.
016900     02 FILLER PIC X(30) VALUE SPACE.
017000     02 FILLER PIC X(17) VALUE "       CORRECT =".
017100     02 CORRECT-X.
017200     03 CORRECT-A                 PICTURE X(20) VALUE SPACE.
017300     03 CORRECT-N REDEFINES CORRECT-A PICTURE -9(9).9(9).
017400     03 CORRECT-0V18 REDEFINES CORRECT-A    PICTURE -.9(18).
017500     03 CORRECT-4V14 REDEFINES CORRECT-A    PICTURE -9(4).9(14).
017600     03 CORRECT-14V4 REDEFINES CORRECT-A    PICTURE -9(14).9(4).
017700     03      CR-18V0 REDEFINES CORRECT-A.
017800         04 CORRECT-18V0                    PICTURE -9(18).
017900         04 FILLER                          PICTURE X.
018000     03 FILLER PIC X(50) VALUE SPACE.
018100 01  CCVS-C-1.
018200     02 FILLER PICTURE IS X(99) VALUE IS " FEATURE              PA
018300-    "SS  PARAGRAPH-NAME
018400-    "        REMARKS".
018500     02 FILLER PICTURE IS X(20) VALUE IS SPACE.
018600 01  CCVS-C-2.
018700     02 FILLER PICTURE IS X VALUE IS SPACE.
018800     02 FILLER PICTURE IS X(6) VALUE IS "TESTED".
018900     02 FILLER PICTURE IS X(15) VALUE IS SPACE.
019000     02 FILLER PICTURE IS X(4) VALUE IS "FAIL".
019100     02 FILLER PICTURE IS X(94) VALUE IS SPACE.
019200 01  REC-SKL-SUB PICTURE 9(2) VALUE ZERO.
019300 01  REC-CT PICTURE 99 VALUE ZERO.
019400 01  DELETE-CNT                   PICTURE 999  VALUE ZERO.
019500 01  ERROR-COUNTER PICTURE IS 999 VALUE IS ZERO.
019600 01  INSPECT-COUNTER PIC 999 VALUE ZERO.
019700 01  PASS-COUNTER PIC 999 VALUE ZERO.
019800 01  TOTAL-ERROR PIC 999 VALUE ZERO.
019900 01  ERROR-HOLD PIC 999 VALUE ZERO.
020000 01  DUMMY-HOLD PIC X(120) VALUE SPACE.
020100 01  RECORD-COUNT PIC 9(5) VALUE ZERO.
020200 01  CCVS-H-1.
020300     02  FILLER   PICTURE X(27)  VALUE SPACE.
020400     02 FILLER PICTURE X(67) VALUE
020500     " FEDERAL SOFTWARE TESTING CENTER COBOL COMPILER VALIDATION
020600-    " SYSTEM".
020700     02  FILLER     PICTURE X(26)  VALUE SPACE.
020800 01  CCVS-H-2.
020900     02 FILLER PICTURE X(52) VALUE IS
021000     "CCVS85 FSTC COPY, NOT FOR DISTRIBUTION.".
021100     02 FILLER PICTURE IS X(19) VALUE IS "TEST RESULTS SET-  ".
021200     02 TEST-ID PICTURE IS X(9).
021300     02 FILLER PICTURE IS X(40) VALUE IS SPACE.
021400 01  CCVS-H-3.
021500     02  FILLER PICTURE X(34) VALUE
021600     " FOR OFFICIAL USE ONLY    ".
021700     02  FILLER PICTURE X(58) VALUE
021800     "COBOL 85 VERSION 4.2, Apr  1993 SSVG                      ".
021900     02  FILLER PICTURE X(28) VALUE
022000     "  COPYRIGHT   1985 ".
022100 01  CCVS-E-1.
022200     02 FILLER PICTURE IS X(52) VALUE IS SPACE.
022300     02 FILLER PICTURE IS X(14) VALUE IS "END OF TEST-  ".
022400     02 ID-AGAIN PICTURE IS X(9).
022500     02 FILLER PICTURE X(45) VALUE IS
022600     " NTIS DISTRIBUTION COBOL 85".
022700 01  CCVS-E-2.
022800     02  FILLER                   PICTURE X(31)  VALUE
022900     SPACE.
023000     02  FILLER                   PICTURE X(21)  VALUE SPACE.
023100     02 CCVS-E-2-2.
023200         03 ERROR-TOTAL PICTURE IS XXX VALUE IS SPACE.
023300         03 FILLER PICTURE IS X VALUE IS SPACE.
023400         03 ENDER-DESC PIC X(46) VALUE "ERRORS ENCOUNTERED".
023500 01  CCVS-E-3.
023600     02  FILLER PICTURE X(22) VALUE
023700     " FOR OFFICIAL USE ONLY".
023800     02  FILLER PICTURE X(12) VALUE SPACE.
023900     02  FILLER PICTURE X(58) VALUE
024000     "ON-SITE VALIDATION, NATIONAL INSTITUTE OF STD & TECH.     ".
024100     02  FILLER PICTURE X(13) VALUE SPACE.
024200     02 FILLER PIC X(15) VALUE " COPYRIGHT 1985".
024300 01  CCVS-E-4.
024400     02 CCVS-E-4-1 PIC XXX VALUE SPACE.
024500     02 FILLER PIC XXXX VALUE " OF ".
024600     02 CCVS-E-4-2 PIC XXX VALUE SPACE.
024700     02 FILLER PIC X(40) VALUE
024800      "  TESTS WERE EXECUTED SUCCESSFULLY".
024900 01  XXINFO.
025000     02 FILLER PIC X(30) VALUE "        *** INFORMATION  ***".
025100     02 INFO-TEXT.
025200     04 FILLER PIC X(20) VALUE SPACE.
025300     04 XXCOMPUTED PIC X(20).
025400     04 FILLER PIC X(5) VALUE SPACE.
025500     04 XXCORRECT PIC X(20).
025600 01  HYPHEN-LINE.
025700     02 FILLER PICTURE IS X VALUE IS SPACE.
025800     02 FILLER PICTURE IS X(65) VALUE IS "************************
025900-    "*****************************************".
026000     02 FILLER PICTURE IS X(54) VALUE IS "************************
026100-    "******************************".
026200 01  CCVS-PGM-ID PIC X(6) VALUE
026300     "SQ213A".
026400 PROCEDURE DIVISION.
026500 DECLARATIVES.
026600 SECT-SQ213A-0001 SECTION.
026700     USE AFTER ERROR PROCEDURE EXTEND.
026800 EXTEND-ERROR-PROCESS.
026900     MOVE 1 TO EXTEND-ERROR.
027000 SECT-SQ213A-0002 SECTION.
027100     USE AFTER EXCEPTION PROCEDURE ON SQ-FS2, PRINT-FILE.
027200 FN-SERIES-ERROR-PROCESS.
027300     MOVE 1 TO FN-SERIES-ERROR.
027400 END DECLARATIVES.
027500 CCVS1 SECTION.
027600 OPEN-FILES.
027700P    OPEN I-O RAW-DATA.
027800P    MOVE CCVS-PGM-ID TO RAW-DATA-KEY.
027900P    READ RAW-DATA INVALID KEY GO TO END-E-1.
028000P    MOVE "ABORTED " TO C-ABORT.
028100P    ADD 1 TO C-NO-OF-TESTS.
028200P    ACCEPT C-DATE  FROM DATE.
028300P    ACCEPT C-TIME  FROM TIME.
028400P    REWRITE RAW-DATA-SATZ INVALID KEY GO TO END-E-1.
028500PEND-E-1.
028600P    CLOSE RAW-DATA.
028700     OPEN     OUTPUT PRINT-FILE.
028800     MOVE CCVS-PGM-ID TO TEST-ID. MOVE CCVS-PGM-ID TO ID-AGAIN.
028900     MOVE    SPACE TO TEST-RESULTS.
029000     PERFORM  HEAD-ROUTINE THRU COLUMN-NAMES-ROUTINE.
029100     MOVE ZERO TO REC-SKL-SUB.
029200     PERFORM CCVS-INIT-FILE 9 TIMES.
029300 CCVS-INIT-FILE.
029400     ADD 1 TO REC-SKL-SUB.
029500     MOVE FILE-RECORD-INFO-SKELETON TO
029600                  FILE-RECORD-INFO (REC-SKL-SUB).
029700 CCVS-INIT-EXIT.
029800     GO TO CCVS1-EXIT.
029900 CLOSE-FILES.
030000     PERFORM END-ROUTINE THRU END-ROUTINE-13. CLOSE PRINT-FILE.
030100P    OPEN I-O RAW-DATA.
030200P    MOVE CCVS-PGM-ID TO RAW-DATA-KEY.
030300P    READ RAW-DATA INVALID KEY GO TO END-E-2.
030400P    MOVE "OK.     " TO C-ABORT.
030500P    MOVE PASS-COUNTER TO C-OK.
030600P    MOVE ERROR-HOLD   TO C-ALL.
030700P    MOVE ERROR-COUNTER TO C-FAIL.
030800P    MOVE DELETE-CNT TO C-DELETED.
030900P    MOVE INSPECT-COUNTER TO C-INSPECT.
031000P    REWRITE RAW-DATA-SATZ INVALID KEY GO TO END-E-2.
031100PEND-E-2.
031200P    CLOSE RAW-DATA.
031300 TERMINATE-CCVS.
031400S    EXIT PROGRAM.
031500STERMINATE-CALL.
031600     STOP     RUN.
031700 INSPT. MOVE "INSPT" TO P-OR-F. ADD 1 TO INSPECT-COUNTER.
031800 PASS.  MOVE "PASS " TO P-OR-F.  ADD 1 TO PASS-COUNTER.
031900 FAIL.  MOVE "FAIL*" TO P-OR-F.  ADD 1 TO ERROR-COUNTER.
032000 DE-LETE.  MOVE "*****" TO P-OR-F.  ADD 1 TO DELETE-CNT.
032100     MOVE "****TEST DELETED****" TO RE-MARK.
032200 PRINT-DETAIL.
032300     IF REC-CT NOT EQUAL TO ZERO
032400             MOVE "." TO PARDOT-X
032500             MOVE REC-CT TO DOTVALUE.
032600     MOVE     TEST-RESULTS TO PRINT-REC. PERFORM WRITE-LINE.
032700     IF P-OR-F EQUAL TO "FAIL*"  PERFORM WRITE-LINE
032800        PERFORM FAIL-ROUTINE THRU FAIL-ROUTINE-EX
032900          ELSE PERFORM BAIL-OUT THRU BAIL-OUT-EX.
033000     MOVE SPACE TO P-OR-F. MOVE SPACE TO COMPUTED-X.
033100     MOVE SPACE TO CORRECT-X.
033200     IF     REC-CT EQUAL TO ZERO  MOVE SPACE TO PAR-NAME.
033300     MOVE     SPACE TO RE-MARK.
033400 HEAD-ROUTINE.
033500     MOVE CCVS-H-1 TO DUMMY-RECORD. PERFORM WRITE-LINE 2 TIMES.
033600     MOVE CCVS-H-2 TO DUMMY-RECORD. PERFORM WRITE-LINE 5 TIMES.
033700     MOVE CCVS-H-3 TO DUMMY-RECORD. PERFORM WRITE-LINE 3 TIMES.
033800 COLUMN-NAMES-ROUTINE.
033900     MOVE CCVS-C-1 TO DUMMY-RECORD. PERFORM WRITE-LINE.
034000     MOVE CCVS-C-2 TO DUMMY-RECORD. PERFORM WRITE-LINE 2 TIMES.
034100     MOVE HYPHEN-LINE TO DUMMY-RECORD. PERFORM WRITE-LINE.
034200 END-ROUTINE.
034300     MOVE HYPHEN-LINE TO DUMMY-RECORD. PERFORM WRITE-LINE 5 TIMES.
034400 END-RTN-EXIT.
034500     MOVE CCVS-E-1 TO DUMMY-RECORD. PERFORM WRITE-LINE 2 TIMES.
034600 END-ROUTINE-1.
034700      ADD ERROR-COUNTER TO ERROR-HOLD ADD INSPECT-COUNTER TO
034800      ERROR-HOLD. ADD DELETE-CNT TO ERROR-HOLD.
034900      ADD PASS-COUNTER TO ERROR-HOLD.
035000*     IF PASS-COUNTER EQUAL TO ERROR-HOLD GO TO END-ROUTINE-12.
035100      MOVE PASS-COUNTER TO CCVS-E-4-1.
035200      MOVE ERROR-HOLD TO CCVS-E-4-2.
035300      MOVE CCVS-E-4 TO CCVS-E-2-2.
035400      MOVE CCVS-E-2 TO DUMMY-RECORD PERFORM WRITE-LINE.
035500  END-ROUTINE-12.
035600      MOVE "TEST(S) FAILED" TO ENDER-DESC.
035700     IF       ERROR-COUNTER IS EQUAL TO ZERO
035800         MOVE "NO " TO ERROR-TOTAL
035900         ELSE
036000         MOVE ERROR-COUNTER TO ERROR-TOTAL.
036100     MOVE     CCVS-E-2 TO DUMMY-RECORD.
036200     PERFORM WRITE-LINE.
036300 END-ROUTINE-13.
036400     IF DELETE-CNT IS EQUAL TO ZERO
036500         MOVE "NO " TO ERROR-TOTAL  ELSE
036600         MOVE DELETE-CNT TO ERROR-TOTAL.
036700     MOVE "TEST(S) DELETED     " TO ENDER-DESC.
036800     MOVE CCVS-E-2 TO DUMMY-RECORD. PERFORM WRITE-LINE.
036900      IF   INSPECT-COUNTER EQUAL TO ZERO
037000          MOVE "NO " TO ERROR-TOTAL
037100      ELSE MOVE INSPECT-COUNTER TO ERROR-TOTAL.
037200      MOVE "TEST(S) REQUIRE INSPECTION" TO ENDER-DESC.
037300      MOVE CCVS-E-2 TO DUMMY-RECORD. PERFORM WRITE-LINE.
037400     MOVE CCVS-E-3 TO DUMMY-RECORD. PERFORM WRITE-LINE.
037500 WRITE-LINE.
037600     ADD 1 TO RECORD-COUNT.
037700Y    IF RECORD-COUNT GREATER 50
037800Y        MOVE DUMMY-RECORD TO DUMMY-HOLD
037900Y        MOVE SPACE TO DUMMY-RECORD
038000Y        WRITE DUMMY-RECORD AFTER ADVANCING PAGE
038100Y        MOVE CCVS-C-1 TO DUMMY-RECORD PERFORM WRT-LN
038200Y        MOVE CCVS-C-2 TO DUMMY-RECORD PERFORM WRT-LN 2 TIMES
038300Y        MOVE HYPHEN-LINE TO DUMMY-RECORD PERFORM WRT-LN
038400Y        MOVE DUMMY-HOLD TO DUMMY-RECORD
038500Y        MOVE ZERO TO RECORD-COUNT.
038600     PERFORM WRT-LN.
038700 WRT-LN.
038800     WRITE    DUMMY-RECORD AFTER ADVANCING 1 LINES.
038900     MOVE SPACE TO DUMMY-RECORD.
039000 BLANK-LINE-PRINT.
039100     PERFORM WRT-LN.
039200 FAIL-ROUTINE.
039300     IF COMPUTED-X NOT EQUAL TO SPACE GO TO FAIL-ROUTINE-WRITE.
039400     IF CORRECT-X NOT EQUAL TO SPACE GO TO FAIL-ROUTINE-WRITE.
039500     MOVE "NO FURTHER INFORMATION, SEE PROGRAM." TO INFO-TEXT.
039600     MOVE XXINFO TO DUMMY-RECORD. PERFORM WRITE-LINE 2 TIMES.
039700     GO TO FAIL-ROUTINE-EX.
039800 FAIL-ROUTINE-WRITE.
039900     MOVE TEST-COMPUTED TO PRINT-REC PERFORM WRITE-LINE
040000     MOVE TEST-CORRECT TO PRINT-REC PERFORM WRITE-LINE 2 TIMES.
040100 FAIL-ROUTINE-EX. EXIT.
040200 BAIL-OUT.
040300     IF COMPUTED-A NOT EQUAL TO SPACE GO TO BAIL-OUT-WRITE.
040400     IF CORRECT-A EQUAL TO SPACE GO TO BAIL-OUT-EX.
040500 BAIL-OUT-WRITE.
040600     MOVE CORRECT-A TO XXCORRECT. MOVE COMPUTED-A TO XXCOMPUTED.
040700     MOVE XXINFO TO DUMMY-RECORD. PERFORM WRITE-LINE 2 TIMES.
040800 BAIL-OUT-EX. EXIT.
040900 CCVS1-EXIT.
041000     EXIT.
041100 SECT-SQ213A-0003 SECTION.
041200 OPEN-INIT-GF-01.
041300*             THIS IS A TEST FOR OPEN EXTEND FOR MAGNETIC TAPE.
041400*             A FILE OF 750 RECORDS IS CREATED THEN RE-OPENED
041500*             WITH EXTEND.  250 RECORDS ARE ADDED TO THE FILE.
041600*             THE FILE IS THEN READ AND VALIDATED.
041700     MOVE "SQ-FS1" TO XFILE-NAME (1).
041800     MOVE "R1-F-G" TO XRECORD-NAME (1).
041900     MOVE "SQ213"  TO XPROGRAM-NAME (1).
042000     MOVE 000126  TO XRECORD-LENGTH (1).
042100     MOVE "RC"    TO CHARS-OR-RECORDS (1).
042200     MOVE 0001    TO XBLOCK-SIZE (1).
042300     MOVE 001000  TO RECORDS-IN-FILE (1).
042400     MOVE "SQ"    TO XFILE-ORGANIZATION (1).
042500     MOVE "S"     TO XLABEL-TYPE (1).
042600     MOVE 000001  TO XRECORD-NUMBER (1).
042700     OPEN OUTPUT SQ-FS1.
042800 OPEN-TEST-01-01.
042900     MOVE FILE-RECORD-INFO-P1-120 (1) TO SQ-FS1R1-F-G-120.
043000     MOVE SPACES TO SQ-FS1R1-F-G-006.
043100     WRITE SQ-FS1R1-F-G-126.
043200     IF XRECORD-NUMBER (1) EQUAL TO 750
043300              GO TO OPEN-TEST-01-02.
043400     ADD 1 TO XRECORD-NUMBER (1).
043500     GO TO OPEN-TEST-01-01.
043600 OPEN-TEST-01-02.
043700     MOVE "CREATE FILE SQ-FS1" TO FEATURE.
043800     MOVE "OPEN-TEST-GF-01-02"  TO PAR-NAME.
043900     MOVE "FILE CREATED, RECS =" TO COMPUTED-A.
044000     MOVE XRECORD-NUMBER (1) TO CORRECT-18V0.
044100     PERFORM PASS.
044200     PERFORM PRINT-DETAIL.
044300     CLOSE SQ-FS1.
044400 OPEN-TEST-01-03.
044500     OPEN EXTEND SQ-FS1.
044600     ADD 1 TO XRECORD-NUMBER (1).
044700 OPEN-TEST-01-04.
044800     MOVE FILE-RECORD-INFO-P1-120 (1) TO SQ-FS1R1-F-G-120.
044900     MOVE "EXTEND" TO SQ-FS1R1-F-G-006.
045000     WRITE SQ-FS1R1-F-G-126.
045100     IF XRECORD-NUMBER (1) EQUAL 1000
045200              GO TO OPEN-TEST-GF-01-05.
045300     ADD 1 TO XRECORD-NUMBER (1).
045400     GO TO OPEN-TEST-01-04.
045500 OPEN-TEST-GF-01-05.
045600     MOVE "OPEN O SQ-FS1 EXTEND" TO FEATURE.
045700     MOVE "OPEN-TEST-GF-01-03" TO PAR-NAME.
045800     MOVE "FILE EXTENDED RECS=" TO COMPUTED-A.
045900     MOVE XRECORD-NUMBER (1) TO CORRECT-18V0.
046000     PERFORM PASS.
046100     PERFORM PRINT-DETAIL.
046200     CLOSE SQ-FS1.
046300 READ-TEST-F1-01.
046400     OPEN INPUT SQ-FS1.
046500     MOVE ZERO TO WRK-RECORD-COUNT.
046600 READ-TEST-F1-01-07.
046700     READ SQ-FS1
046800              ; AT END  MOVE "PREMATURE EOF" TO RE-MARK
046900                        PERFORM FAIL
047000                        GO TO READ-WRITE-F1-01.
047100     MOVE SQ-FS1R1-F-G-120 TO FILE-RECORD-INFO-P1-120 (1).
047200     ADD 1 TO WRK-RECORD-COUNT.
047300     IF XFILE-NAME (1) NOT EQUAL TO "SQ-FS1"
047400              ADD 1 TO RECORDS-IN-ERROR
047500              GO TO READ-TEST-F1-01-08.
047600     IF WRK-RECORD-COUNT NOT EQUAL TO XRECORD-NUMBER (1)
047700              ADD 1 TO RECORDS-IN-ERROR
047800              GO TO READ-TEST-F1-01-08.
047900     IF SQ-FS1R1-F-G-006 NOT EQUAL TO SPACES
048000              ADD 1 TO RECORDS-IN-ERROR.
048100 READ-TEST-F1-01-08.
048200     IF WRK-RECORD-COUNT NOT EQUAL TO 750
048300              GO TO READ-TEST-F1-01-07.
048400 READ-TEST-F1-01-09.
048500     READ SQ-FS1 RECORD
048600              ; END GO TO READ-TEST-F1-01-10.
048700     MOVE SQ-FS1R1-F-G-120 TO FILE-RECORD-INFO-P1-120 (1).
048800     ADD 1 TO WRK-RECORD-COUNT.
048900     IF XFILE-NAME (1) NOT EQUAL TO "SQ-FS1"
049000              ADD 1 TO RECORDS-IN-ERROR
049100              GO TO READ-TEST-F1-01-09.
049200     IF WRK-RECORD-COUNT NOT EQUAL TO XRECORD-NUMBER (1)
049300              ADD 1 TO RECORDS-IN-ERROR
049400              GO TO READ-TEST-F1-01-09.
049500     IF SQ-FS1R1-F-G-006 NOT EQUAL TO "EXTEND"
049600              ADD 1 TO RECORDS-IN-ERROR.
049700     GO TO READ-TEST-F1-01-09.
049800 READ-TEST-F1-01-10.
049900     IF RECORDS-IN-ERROR EQUAL ZERO
050000              GO TO READ-PASS-F1-01.
050100     GO TO READ-FAIL-F1-01.
050200 READ-DELETE-F1-01.
050300     PERFORM DE-LETE.
050400     GO TO READ-WRITE-F1-01.
050500 READ-FAIL-F1-01.
050600     MOVE "ERRORS IN READING SQ-FS1; VII-39; OPEN .. EXTEND"
050700          TO RE-MARK.
050800     MOVE "RECORDS IN ERROR =" TO COMPUTED-A.
050900     MOVE RECORDS-IN-ERROR TO CORRECT-18V0.
051000     PERFORM FAIL.
051100     GO TO READ-WRITE-F1-01.
051200 READ-PASS-F1-01.
051300     PERFORM PASS.
051400     MOVE "FILE VERIFIED RECS =" TO COMPUTED-A.
051500     MOVE WRK-RECORD-COUNT TO CORRECT-18V0.
051600 READ-WRITE-F1-01.
051700     MOVE "READ-TEST-F1-01" TO PAR-NAME.
051800     MOVE "VERIFY FILE SQ-FS1" TO FEATURE.
051900     PERFORM PRINT-DETAIL.
052000 READ-CLOSE-F1-01.
052100     CLOSE SQ-FS1.
052200 WRITE-INIT-002.
052300*         THIS TEST CREATES A MASS-STORAGE FILE OF 1000 RECORDS.
052400*         THEN IT IS READ AND VALIDATED FOR CORRECTNESS.
052500     MOVE "SQ-FS2" TO XFILE-NAME (2).
052600     MOVE "R1-F-G" TO XRECORD-NAME (2).
052700     MOVE "SQ213"  TO XPROGRAM-NAME (2).
052800     MOVE 000126  TO XRECORD-LENGTH (2).
052900     MOVE "RC"    TO CHARS-OR-RECORDS (2).
053000     MOVE 0001    TO XBLOCK-SIZE (2).
053100     MOVE 001000  TO RECORDS-IN-FILE (2).
053200     MOVE "SQ"    TO XFILE-ORGANIZATION (2).
053300     MOVE "S"     TO XLABEL-TYPE (2).
053400     MOVE 000001  TO XRECORD-NUMBER (2).
053500     OPEN OUTPUT SQ-FS2.
053600 WRITE-TEST-GF-01-1.
053700     MOVE FILE-RECORD-INFO-P1-120 (2) TO SQ-FS2R1-F-G-120.
053800     MOVE SPACES TO SQ-FS2R1-F-G-006.
053900     WRITE SQ-FS2R1-F-G-126.
054000     IF XRECORD-NUMBER (2) EQUAL TO 1000
054100              GO TO WRITE-TEST-GF-01-2.
054200     ADD 1 TO XRECORD-NUMBER (2).
054300     GO TO WRITE-TEST-GF-01-1.
054400 WRITE-TEST-GF-01-2.
054500     MOVE "CREATE FILE SQ-FS2" TO FEATURE.
054600     MOVE "WRITE-TEST-GF-01" TO PAR-NAME.
054700     MOVE "FILE CREATED, RECS =" TO COMPUTED-A.
054800     MOVE XRECORD-NUMBER (2) TO CORRECT-18V0.
054900     PERFORM PASS. PERFORM PRINT-DETAIL.
055000     CLOSE SQ-FS2.
055100 READ-INIT-F1-02.
055200     OPEN INPUT SQ-FS2.
055300     MOVE ZERO TO WRK-RECORD-COUNT.
055400     MOVE ZERO TO RECORDS-IN-ERROR.
055500 READ-TEST-F1-02-09.
055600     READ SQ-FS2 RECORD
055700              AT END GO TO READ-TEST-F1-02-10.
055800     MOVE SQ-FS2R1-F-G-120 TO FILE-RECORD-INFO-P1-120 (2)
055900     ADD 1 TO WRK-RECORD-COUNT.
056000     IF XFILE-NAME (2) NOT EQUAL TO "SQ-FS2"
056100              ADD 1 TO RECORDS-IN-ERROR
056200              GO TO READ-TEST-F1-02-09.
056300     IF WRK-RECORD-COUNT NOT EQUAL TO XRECORD-NUMBER (2)
056400              ADD 1 TO RECORDS-IN-ERROR
056500              GO TO READ-TEST-F1-02-09.
056600     GO TO READ-TEST-F1-02-09.
056700 READ-TEST-F1-02-10.
056800     IF RECORDS-IN-ERROR EQUAL ZERO
056900              GO TO READ-PASS-F1-02.
057000     GO TO READ-FAIL-F1-02.
057100 READ-DELETE-F1-02.
057200     PERFORM DE-LETE.
057300     GO TO READ-WRITE-F1-02.
057400 READ-FAIL-F1-02.
057500     MOVE "ERRORS IN READING SQ-FS2; VII-39, -52, -44" TO RE-MARK.
057600     MOVE "RECORDS IN ERROR =" TO COMPUTED-A.
057700     MOVE RECORDS-IN-ERROR TO CORRECT-18V0.
057800     PERFORM FAIL.
057900     GO TO READ-WRITE-F1-02.
058000 READ-PASS-F1-02.
058100     PERFORM PASS.
058200     MOVE "FILE VERIFIED RECS =" TO COMPUTED-A.
058300     MOVE WRK-RECORD-COUNT TO CORRECT-18V0.
058400 READ-WRITE-F1-02.
058500     MOVE "READ-TEST-F1-02" TO PAR-NAME.
058600     MOVE "VERIFY FILE SQ-FS2" TO FEATURE.
058700     PERFORM PRINT-DETAIL.
058800 READ-CLOSE-F1-02.
058900     CLOSE SQ-FS2.
059000 USE-INIT-GF-01.
059100     MOVE "USE PROCEDURE TESTS" TO FEATURE.
059200     MOVE "USE-TEST-GF-01" TO PAR-NAME.
059300 USE-TEST-GF-01-01.
059400     IF EXTEND-ERROR EQUAL ZERO
059500         GO TO USE-PASS-GF-01.
059600     GO TO USE-FAIL-GF-01.
059700 USE-DELETE-GF-01.
059800     PERFORM DE-LETE.
059900     GO TO USE-WRITE-GF-01.
060000 USE-FAIL-GF-01.
060100     MOVE "VII-50 -51; UNEXSPECTED USE PERFORMED" TO RE-MARK.
060200     MOVE "RECORDS IN ERROR =" TO COMPUTED-A.
060300     MOVE RECORDS-IN-ERROR TO CORRECT-18V0.
060400     PERFORM FAIL.
060500     GO TO USE-WRITE-GF-01.
060600 USE-PASS-GF-01.
060700     PERFORM PASS.
060800     MOVE "FILE VERIFIED RECS =" TO COMPUTED-A.
060900     MOVE WRK-RECORD-COUNT TO CORRECT-18V0.
061000 USE-WRITE-GF-01.
061100     MOVE "USE-TEST-GF-01"  TO PAR-NAME.
061200     PERFORM PRINT-DETAIL.
061300 USE-TEST-GF-02.
061400     IF FN-SERIES-ERROR EQUAL ZERO
061500         GO TO USE-PASS-GF-02.
061600     GO TO USE-FAIL-GF-02.
061700 USE-DELETE-GF-02.
061800     PERFORM DE-LETE.
061900     GO TO USE-WRITE-GF-02.
062000 USE-FAIL-GF-02.
062100     MOVE "VII-50 -51; UNEXSPECTED USE PERFORMED" TO RE-MARK.
062200     MOVE "RECORDS IN ERROR =" TO COMPUTED-A.
062300     MOVE RECORDS-IN-ERROR TO CORRECT-18V0.
062400     PERFORM FAIL.
062500     GO TO USE-WRITE-GF-02.
062600 USE-PASS-GF-02.
062700     PERFORM PASS.
062800     MOVE "FILE VERIFIED RECS =" TO COMPUTED-A.
062900     MOVE WRK-RECORD-COUNT TO CORRECT-18V0.
063000 USE-WRITE-GF-02.
063100     MOVE "USE-TEST-GF-02"  TO PAR-NAME.
063200     PERFORM PRINT-DETAIL.
063300 SQ213A-END-ROUTINE.
063400     MOVE "END OF SQ213A VALIDATION TESTS" TO PRINT-REC.
063500     WRITE PRINT-REC AFTER ADVANCING 1 LINE.
063600 TERMINATE-SQ213A.
063700     EXIT.
063800 CCVS-EXIT SECTION.
063900 CCVS-999999.
064000     GO TO CLOSE-FILES.
