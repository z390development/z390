000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID.
000300     SQ121A.
000400****************************************************************
000500*                                                              *
000600*    VALIDATION FOR:-                                          *
000700*    "ON-SITE VALIDATION, NATIONAL INSTITUTE OF STD & TECH.     ".
000800*                                                              *
000900*    CREATION DATE     /     VALIDATION DATE                   *
001000*    "COBOL 85 VERSION 4.2, Apr  1993 SSVG                      ".
001100*                                                              *
001200****************************************************************
001300
001400*         THE ROUTINE SQ121A TESTS THE USE OF THE USE AFTER ERROR
001500*    PROCEDURE ON I-O.  SQ121A IS BASICALLY A REWRITE OF SQ115A
001600*    WITH THE ADDITION OF THE USE PROCEDURE.
001700*        THIS ROUTINE CREATES A MASS STORAGE FILE CONTAINING
001800*    550 RECORDS.  EACH RECORD CONTAINS 126 CHARACTERS. THE
001900*    FILE IS CLOSED AND OPENED AS AN INPUT-OUTPUT FILE. EVERY
002000*    TENTH RECORD IS REWRITTEN.  THE FILE IS CLOSED AND OPENED
002100*    AGAIN AS AN INPUT FILE.  FIELDS IN EACH RECORD ARE CHECKED
002200*    TO ENSURE THAT THE RECORDS REWRITTEN ARE CORRECT AND THAT
002300*    THE RECORDS WHICH WERE NOT UPDATED WERE NOT CHANGED.
002400*    THE READ STATEMENT WITHIN THE REWRITE SECTION OF SQ121A DOES
002500*    NOT HAVE AN AT END CLAUSE.  EOF PROCESSING IS HANDLED BY
002600*    SETTING AN EOF-FLAG IN THE DECLARATIVE SECTION.  ANY
002700*    PERMANENT ERRORS ENCOUNTERED DURING THE REWRITE OF SQ-FS5
002800*    ARE TREATED AS INFORMATION ITEMS.
002900*
003000*    USED X-CARDS:
003100*         XXXXX014
003200*         XXXXX055
003300*     P   XXXXX062
003400*         XXXXX082
003500*         XXXXX083
003600*     C   XXXXX084
003700*
003800*
003900 ENVIRONMENT DIVISION.
004000 CONFIGURATION SECTION.
004100 SOURCE-COMPUTER.
004200     XXXXX082.
004300 OBJECT-COMPUTER.
004400     XXXXX083.
004500 INPUT-OUTPUT SECTION.
004600 FILE-CONTROL.
004700P    SELECT RAW-DATA   ASSIGN TO
004800P    XXXXX062
004900P           ORGANIZATION IS INDEXED
005000P           ACCESS MODE IS RANDOM
005100P           RECORD KEY IS RAW-DATA-KEY.
005200     SELECT PRINT-FILE ASSIGN TO
005300     XXXXX055.
005400     SELECT SQ-FS5 ASSIGN
005500     XXXXX014
005600     ORGANIZATION SEQUENTIAL
005700     ACCESS MODE SEQUENTIAL
005800     FILE STATUS IS STAT-GROUP.
005900 DATA DIVISION.
006000 FILE SECTION.
006100P
006200PFD  RAW-DATA.
006300P
006400P01  RAW-DATA-SATZ.
006500P    05  RAW-DATA-KEY        PIC X(6).
006600P    05  C-DATE              PIC 9(6).
006700P    05  C-TIME              PIC 9(8).
006800P    05  C-NO-OF-TESTS       PIC 99.
006900P    05  C-OK                PIC 999.
007000P    05  C-ALL               PIC 999.
007100P    05  C-FAIL              PIC 999.
007200P    05  C-DELETED           PIC 999.
007300P    05  C-INSPECT           PIC 999.
007400P    05  C-NOTE              PIC X(13).
007500P    05  C-INDENT            PIC X.
007600P    05  C-ABORT             PIC X(8).
007700 FD  PRINT-FILE
007800C    LABEL RECORDS
007900C    XXXXX084
008000C    DATA RECORD IS PRINT-REC DUMMY-RECORD
008100               .
008200 01  PRINT-REC PICTURE X(120).
008300 01  DUMMY-RECORD PICTURE X(120).
008400 FD  SQ-FS5
008500C    LABEL RECORD STANDARD
008600               .
008700 01  SQ-FS5R1-F-G-126.
008800     02  SQ-FS5-120  PICTURE X(120).
008900     02  SQ-FS5-UPDATE PICTURE X(6).
009000 WORKING-STORAGE SECTION.
009100 01  COUNT-OF-RECORDS PIC S9(5) COMPUTATIONAL.
009200 01  RECORDS-IN-ERROR PIC S9(5) COMP VALUE ZERO.
009300 01  ERROR-FLAG PIC 9.
009400 01  STAT-GROUP.
009500     02 INPUT-STAT1     PIC X.
009600     02 INPUT-STAT2     PIC X.
009700 01  EOF-FLAG          PIC 9 VALUE 0.
009800 01  PERM-ERRORS       PIC 9 VALUE 0.
009900 01  LOOP-COUNT PIC 99.
010000 01  FILE-RECORD-INFORMATION-REC.
010100     03 FILE-RECORD-INFO-SKELETON.
010200        05 FILLER                 PICTURE X(48)       VALUE
010300             "FILE=      ,RECORD=      /0,RECNO=000000,UPDT=00".
010400        05 FILLER                 PICTURE X(46)       VALUE
010500             ",ODO=0000,PGM=     ,LRECL=000000,BLKSIZ  =0000".
010600        05 FILLER                 PICTURE X(26)       VALUE
010700             ",LFIL=000000,ORG=  ,LBLR= ".
010800        05 FILLER                 PICTURE X(37)       VALUE
010900             ",RECKEY=                             ".
011000        05 FILLER                 PICTURE X(38)       VALUE
011100             ",ALTKEY1=                             ".
011200        05 FILLER                 PICTURE X(38)       VALUE
011300             ",ALTKEY2=                             ".
011400        05 FILLER                 PICTURE X(7)        VALUE SPACE.
011500     03 FILE-RECORD-INFO          OCCURS  10  TIMES.
011600        05 FILE-RECORD-INFO-P1-120.
011700           07 FILLER              PIC X(5).
011800           07 XFILE-NAME           PIC X(6).
011900           07 FILLER              PIC X(8).
012000           07 XRECORD-NAME         PIC X(6).
012100           07 FILLER              PIC X(1).
012200           07 REELUNIT-NUMBER     PIC 9(1).
012300           07 FILLER              PIC X(7).
012400           07 XRECORD-NUMBER       PIC 9(6).
012500           07 FILLER              PIC X(6).
012600           07 UPDATE-NUMBER       PIC 9(2).
012700           07 FILLER              PIC X(5).
012800           07 ODO-NUMBER          PIC 9(4).
012900           07 FILLER              PIC X(5).
013000           07 XPROGRAM-NAME        PIC X(5).
013100           07 FILLER              PIC X(7).
013200           07 XRECORD-LENGTH       PIC 9(6).
013300           07 FILLER              PIC X(7).
013400           07 CHARS-OR-RECORDS    PIC X(2).
013500           07 FILLER              PIC X(1).
013600           07 XBLOCK-SIZE          PIC 9(4).
013700           07 FILLER              PIC X(6).
013800           07 RECORDS-IN-FILE     PIC 9(6).
013900           07 FILLER              PIC X(5).
014000           07 XFILE-ORGANIZATION   PIC X(2).
014100           07 FILLER              PIC X(6).
014200           07 XLABEL-TYPE          PIC X(1).
014300        05 FILE-RECORD-INFO-P121-240.
014400           07 FILLER              PIC X(8).
014500           07 XRECORD-KEY          PIC X(29).
014600           07 FILLER              PIC X(9).
014700           07 ALTERNATE-KEY1      PIC X(29).
014800           07 FILLER              PIC X(9).
014900           07 ALTERNATE-KEY2      PIC X(29).
015000           07 FILLER              PIC X(7).
015100 01  TEST-RESULTS.
015200     02 FILLER                    PICTURE X VALUE SPACE.
015300     02 FEATURE                   PICTURE X(20) VALUE SPACE.
015400     02 FILLER                    PICTURE X VALUE SPACE.
015500     02 P-OR-F                    PICTURE X(5) VALUE SPACE.
015600     02 FILLER                    PICTURE X  VALUE SPACE.
015700     02  PAR-NAME.
015800       03 FILLER PICTURE X(12) VALUE SPACE.
015900       03  PARDOT-X PICTURE X  VALUE SPACE.
016000       03 DOTVALUE PICTURE 99  VALUE ZERO.
016100       03 FILLER PIC X(5) VALUE SPACE.
016200     02 FILLER PIC X(10) VALUE SPACE.
016300     02 RE-MARK PIC X(61).
016400 01  TEST-COMPUTED.
016500     02 FILLER PIC X(30) VALUE SPACE.
016600     02 FILLER PIC X(17) VALUE "       COMPUTED=".
016700     02 COMPUTED-X.
016800     03 COMPUTED-A                PICTURE X(20) VALUE SPACE.
016900     03 COMPUTED-N REDEFINES COMPUTED-A PICTURE -9(9).9(9).
017000     03 COMPUTED-0V18 REDEFINES COMPUTED-A  PICTURE -.9(18).
017100     03 COMPUTED-4V14 REDEFINES COMPUTED-A  PICTURE -9(4).9(14).
017200     03 COMPUTED-14V4 REDEFINES COMPUTED-A  PICTURE -9(14).9(4).
017300     03       CM-18V0 REDEFINES COMPUTED-A.
017400         04 COMPUTED-18V0                   PICTURE -9(18).
017500         04 FILLER                          PICTURE X.
017600     03 FILLER PIC X(50) VALUE SPACE.
017700 01  TEST-CORRECT.
017800     02 FILLER PIC X(30) VALUE SPACE.
017900     02 FILLER PIC X(17) VALUE "       CORRECT =".
018000     02 CORRECT-X.
018100     03 CORRECT-A                 PICTURE X(20) VALUE SPACE.
018200     03 CORRECT-N REDEFINES CORRECT-A PICTURE -9(9).9(9).
018300     03 CORRECT-0V18 REDEFINES CORRECT-A    PICTURE -.9(18).
018400     03 CORRECT-4V14 REDEFINES CORRECT-A    PICTURE -9(4).9(14).
018500     03 CORRECT-14V4 REDEFINES CORRECT-A    PICTURE -9(14).9(4).
018600     03      CR-18V0 REDEFINES CORRECT-A.
018700         04 CORRECT-18V0                    PICTURE -9(18).
018800         04 FILLER                          PICTURE X.
018900     03 FILLER PIC X(50) VALUE SPACE.
019000 01  CCVS-C-1.
019100     02 FILLER PICTURE IS X(99) VALUE IS " FEATURE              PA
019200-    "SS  PARAGRAPH-NAME
019300-    "        REMARKS".
019400     02 FILLER PICTURE IS X(20) VALUE IS SPACE.
019500 01  CCVS-C-2.
019600     02 FILLER PICTURE IS X VALUE IS SPACE.
019700     02 FILLER PICTURE IS X(6) VALUE IS "TESTED".
019800     02 FILLER PICTURE IS X(15) VALUE IS SPACE.
019900     02 FILLER PICTURE IS X(4) VALUE IS "FAIL".
020000     02 FILLER PICTURE IS X(94) VALUE IS SPACE.
020100 01  REC-SKL-SUB PICTURE 9(2) VALUE ZERO.
020200 01  REC-CT PICTURE 99 VALUE ZERO.
020300 01  DELETE-CNT                   PICTURE 999  VALUE ZERO.
020400 01  ERROR-COUNTER PICTURE IS 999 VALUE IS ZERO.
020500 01  INSPECT-COUNTER PIC 999 VALUE ZERO.
020600 01  PASS-COUNTER PIC 999 VALUE ZERO.
020700 01  TOTAL-ERROR PIC 999 VALUE ZERO.
020800 01  ERROR-HOLD PIC 999 VALUE ZERO.
020900 01  DUMMY-HOLD PIC X(120) VALUE SPACE.
021000 01  RECORD-COUNT PIC 9(5) VALUE ZERO.
021100 01  CCVS-H-1.
021200     02  FILLER   PICTURE X(27)  VALUE SPACE.
021300     02 FILLER PICTURE X(67) VALUE
021400     " FEDERAL SOFTWARE TESTING CENTER COBOL COMPILER VALIDATION
021500-    " SYSTEM".
021600     02  FILLER     PICTURE X(26)  VALUE SPACE.
021700 01  CCVS-H-2.
021800     02 FILLER PICTURE X(52) VALUE IS
021900     "CCVS85 FSTC COPY, NOT FOR DISTRIBUTION.".
022000     02 FILLER PICTURE IS X(19) VALUE IS "TEST RESULTS SET-  ".
022100     02 TEST-ID PICTURE IS X(9).
022200     02 FILLER PICTURE IS X(40) VALUE IS SPACE.
022300 01  CCVS-H-3.
022400     02  FILLER PICTURE X(34) VALUE
022500     " FOR OFFICIAL USE ONLY    ".
022600     02  FILLER PICTURE X(58) VALUE
022700     "COBOL 85 VERSION 4.2, Apr  1993 SSVG                      ".
022800     02  FILLER PICTURE X(28) VALUE
022900     "  COPYRIGHT   1985 ".
023000 01  CCVS-E-1.
023100     02 FILLER PICTURE IS X(52) VALUE IS SPACE.
023200     02 FILLER PICTURE IS X(14) VALUE IS "END OF TEST-  ".
023300     02 ID-AGAIN PICTURE IS X(9).
023400     02 FILLER PICTURE X(45) VALUE IS
023500     " NTIS DISTRIBUTION COBOL 85".
023600 01  CCVS-E-2.
023700     02  FILLER                   PICTURE X(31)  VALUE
023800     SPACE.
023900     02  FILLER                   PICTURE X(21)  VALUE SPACE.
024000     02 CCVS-E-2-2.
024100         03 ERROR-TOTAL PICTURE IS XXX VALUE IS SPACE.
024200         03 FILLER PICTURE IS X VALUE IS SPACE.
024300         03 ENDER-DESC PIC X(46) VALUE "ERRORS ENCOUNTERED".
024400 01  CCVS-E-3.
024500     02  FILLER PICTURE X(22) VALUE
024600     " FOR OFFICIAL USE ONLY".
024700     02  FILLER PICTURE X(12) VALUE SPACE.
024800     02  FILLER PICTURE X(58) VALUE
024900     "ON-SITE VALIDATION, NATIONAL INSTITUTE OF STD & TECH.     ".
025000     02  FILLER PICTURE X(13) VALUE SPACE.
025100     02 FILLER PIC X(15) VALUE " COPYRIGHT 1985".
025200 01  CCVS-E-4.
025300     02 CCVS-E-4-1 PIC XXX VALUE SPACE.
025400     02 FILLER PIC XXXX VALUE " OF ".
025500     02 CCVS-E-4-2 PIC XXX VALUE SPACE.
025600     02 FILLER PIC X(40) VALUE
025700      "  TESTS WERE EXECUTED SUCCESSFULLY".
025800 01  XXINFO.
025900     02 FILLER PIC X(30) VALUE "        *** INFORMATION  ***".
026000     02 INFO-TEXT.
026100     04 FILLER PIC X(20) VALUE SPACE.
026200     04 XXCOMPUTED PIC X(20).
026300     04 FILLER PIC X(5) VALUE SPACE.
026400     04 XXCORRECT PIC X(20).
026500 01  HYPHEN-LINE.
026600     02 FILLER PICTURE IS X VALUE IS SPACE.
026700     02 FILLER PICTURE IS X(65) VALUE IS "************************
026800-    "*****************************************".
026900     02 FILLER PICTURE IS X(54) VALUE IS "************************
027000-    "******************************".
027100 01  CCVS-PGM-ID PIC X(6) VALUE
027200     "SQ121A".
027300 PROCEDURE DIVISION.
027400 DECLARATIVES.
027500 SECT-SQ121A-0001 SECTION.
027600     USE AFTER STANDARD ERROR PROCEDURE ON I-O.
027700 I-O-ERROR-PROCESS.
027800     IF INPUT-STAT1 EQUAL TO "1"
027900          MOVE 1 TO EOF-FLAG.
028000     IF INPUT-STAT1 GREATER THAN "1"
028100          MOVE 1 TO PERM-ERRORS.
028200 END DECLARATIVES.
028300 CCVS1 SECTION.
028400 OPEN-FILES.
028500P    OPEN I-O RAW-DATA.
028600P    MOVE CCVS-PGM-ID TO RAW-DATA-KEY.
028700P    READ RAW-DATA INVALID KEY GO TO END-E-1.
028800P    MOVE "ABORTED " TO C-ABORT.
028900P    ADD 1 TO C-NO-OF-TESTS.
029000P    ACCEPT C-DATE  FROM DATE.
029100P    ACCEPT C-TIME  FROM TIME.
029200P    REWRITE RAW-DATA-SATZ INVALID KEY GO TO END-E-1.
029300PEND-E-1.
029400P    CLOSE RAW-DATA.
029500     OPEN     OUTPUT PRINT-FILE.
029600     MOVE CCVS-PGM-ID TO TEST-ID. MOVE CCVS-PGM-ID TO ID-AGAIN.
029700     MOVE    SPACE TO TEST-RESULTS.
029800     PERFORM  HEAD-ROUTINE THRU COLUMN-NAMES-ROUTINE.
029900     MOVE ZERO TO REC-SKL-SUB.
030000     PERFORM CCVS-INIT-FILE 9 TIMES.
030100 CCVS-INIT-FILE.
030200     ADD 1 TO REC-SKL-SUB.
030300     MOVE FILE-RECORD-INFO-SKELETON TO
030400                  FILE-RECORD-INFO (REC-SKL-SUB).
030500 CCVS-INIT-EXIT.
030600     GO TO CCVS1-EXIT.
030700 CLOSE-FILES.
030800     PERFORM END-ROUTINE THRU END-ROUTINE-13. CLOSE PRINT-FILE.
030900P    OPEN I-O RAW-DATA.
031000P    MOVE CCVS-PGM-ID TO RAW-DATA-KEY.
031100P    READ RAW-DATA INVALID KEY GO TO END-E-2.
031200P    MOVE "OK.     " TO C-ABORT.
031300P    MOVE PASS-COUNTER TO C-OK.
031400P    MOVE ERROR-HOLD   TO C-ALL.
031500P    MOVE ERROR-COUNTER TO C-FAIL.
031600P    MOVE DELETE-CNT TO C-DELETED.
031700P    MOVE INSPECT-COUNTER TO C-INSPECT.
031800P    REWRITE RAW-DATA-SATZ INVALID KEY GO TO END-E-2.
031900PEND-E-2.
032000P    CLOSE RAW-DATA.
032100 TERMINATE-CCVS.
032200S    EXIT PROGRAM.
032300STERMINATE-CALL.
032400     STOP     RUN.
032500 INSPT. MOVE "INSPT" TO P-OR-F. ADD 1 TO INSPECT-COUNTER.
032600 PASS.  MOVE "PASS " TO P-OR-F.  ADD 1 TO PASS-COUNTER.
032700 FAIL.  MOVE "FAIL*" TO P-OR-F.  ADD 1 TO ERROR-COUNTER.
032800 DE-LETE.  MOVE "*****" TO P-OR-F.  ADD 1 TO DELETE-CNT.
032900     MOVE "****TEST DELETED****" TO RE-MARK.
033000 PRINT-DETAIL.
033100     IF REC-CT NOT EQUAL TO ZERO
033200             MOVE "." TO PARDOT-X
033300             MOVE REC-CT TO DOTVALUE.
033400     MOVE     TEST-RESULTS TO PRINT-REC. PERFORM WRITE-LINE.
033500     IF P-OR-F EQUAL TO "FAIL*"  PERFORM WRITE-LINE
033600        PERFORM FAIL-ROUTINE THRU FAIL-ROUTINE-EX
033700          ELSE PERFORM BAIL-OUT THRU BAIL-OUT-EX.
033800     MOVE SPACE TO P-OR-F. MOVE SPACE TO COMPUTED-X.
033900     MOVE SPACE TO CORRECT-X.
034000     IF     REC-CT EQUAL TO ZERO  MOVE SPACE TO PAR-NAME.
034100     MOVE     SPACE TO RE-MARK.
034200 HEAD-ROUTINE.
034300     MOVE CCVS-H-1 TO DUMMY-RECORD. PERFORM WRITE-LINE 2 TIMES.
034400     MOVE CCVS-H-2 TO DUMMY-RECORD. PERFORM WRITE-LINE 5 TIMES.
034500     MOVE CCVS-H-3 TO DUMMY-RECORD. PERFORM WRITE-LINE 3 TIMES.
034600 COLUMN-NAMES-ROUTINE.
034700     MOVE CCVS-C-1 TO DUMMY-RECORD. PERFORM WRITE-LINE.
034800     MOVE CCVS-C-2 TO DUMMY-RECORD. PERFORM WRITE-LINE 2 TIMES.
034900     MOVE HYPHEN-LINE TO DUMMY-RECORD. PERFORM WRITE-LINE.
035000 END-ROUTINE.
035100     MOVE HYPHEN-LINE TO DUMMY-RECORD. PERFORM WRITE-LINE 5 TIMES.
035200 END-RTN-EXIT.
035300     MOVE CCVS-E-1 TO DUMMY-RECORD. PERFORM WRITE-LINE 2 TIMES.
035400 END-ROUTINE-1.
035500      ADD ERROR-COUNTER TO ERROR-HOLD ADD INSPECT-COUNTER TO
035600      ERROR-HOLD. ADD DELETE-CNT TO ERROR-HOLD.
035700      ADD PASS-COUNTER TO ERROR-HOLD.
035800*     IF PASS-COUNTER EQUAL TO ERROR-HOLD GO TO END-ROUTINE-12.
035900      MOVE PASS-COUNTER TO CCVS-E-4-1.
036000      MOVE ERROR-HOLD TO CCVS-E-4-2.
036100      MOVE CCVS-E-4 TO CCVS-E-2-2.
036200      MOVE CCVS-E-2 TO DUMMY-RECORD PERFORM WRITE-LINE.
036300  END-ROUTINE-12.
036400      MOVE "TEST(S) FAILED" TO ENDER-DESC.
036500     IF       ERROR-COUNTER IS EQUAL TO ZERO
036600         MOVE "NO " TO ERROR-TOTAL
036700         ELSE
036800         MOVE ERROR-COUNTER TO ERROR-TOTAL.
036900     MOVE     CCVS-E-2 TO DUMMY-RECORD.
037000     PERFORM WRITE-LINE.
037100 END-ROUTINE-13.
037200     IF DELETE-CNT IS EQUAL TO ZERO
037300         MOVE "NO " TO ERROR-TOTAL  ELSE
037400         MOVE DELETE-CNT TO ERROR-TOTAL.
037500     MOVE "TEST(S) DELETED     " TO ENDER-DESC.
037600     MOVE CCVS-E-2 TO DUMMY-RECORD. PERFORM WRITE-LINE.
037700      IF   INSPECT-COUNTER EQUAL TO ZERO
037800          MOVE "NO " TO ERROR-TOTAL
037900      ELSE MOVE INSPECT-COUNTER TO ERROR-TOTAL.
038000      MOVE "TEST(S) REQUIRE INSPECTION" TO ENDER-DESC.
038100      MOVE CCVS-E-2 TO DUMMY-RECORD. PERFORM WRITE-LINE.
038200     MOVE CCVS-E-3 TO DUMMY-RECORD. PERFORM WRITE-LINE.
038300 WRITE-LINE.
038400     ADD 1 TO RECORD-COUNT.
038500Y    IF RECORD-COUNT GREATER 50
038600Y        MOVE DUMMY-RECORD TO DUMMY-HOLD
038700Y        MOVE SPACE TO DUMMY-RECORD
038800Y        WRITE DUMMY-RECORD AFTER ADVANCING PAGE
038900Y        MOVE CCVS-C-1 TO DUMMY-RECORD PERFORM WRT-LN
039000Y        MOVE CCVS-C-2 TO DUMMY-RECORD PERFORM WRT-LN 2 TIMES
039100Y        MOVE HYPHEN-LINE TO DUMMY-RECORD PERFORM WRT-LN
039200Y        MOVE DUMMY-HOLD TO DUMMY-RECORD
039300Y        MOVE ZERO TO RECORD-COUNT.
039400     PERFORM WRT-LN.
039500 WRT-LN.
039600     WRITE    DUMMY-RECORD AFTER ADVANCING 1 LINES.
039700     MOVE SPACE TO DUMMY-RECORD.
039800 BLANK-LINE-PRINT.
039900     PERFORM WRT-LN.
040000 FAIL-ROUTINE.
040100     IF COMPUTED-X NOT EQUAL TO SPACE GO TO FAIL-ROUTINE-WRITE.
040200     IF CORRECT-X NOT EQUAL TO SPACE GO TO FAIL-ROUTINE-WRITE.
040300     MOVE "NO FURTHER INFORMATION, SEE PROGRAM." TO INFO-TEXT.
040400     MOVE XXINFO TO DUMMY-RECORD. PERFORM WRITE-LINE 2 TIMES.
040500     GO TO FAIL-ROUTINE-EX.
040600 FAIL-ROUTINE-WRITE.
040700     MOVE TEST-COMPUTED TO PRINT-REC PERFORM WRITE-LINE
040800     MOVE TEST-CORRECT TO PRINT-REC PERFORM WRITE-LINE 2 TIMES.
040900 FAIL-ROUTINE-EX. EXIT.
041000 BAIL-OUT.
041100     IF COMPUTED-A NOT EQUAL TO SPACE GO TO BAIL-OUT-WRITE.
041200     IF CORRECT-A EQUAL TO SPACE GO TO BAIL-OUT-EX.
041300 BAIL-OUT-WRITE.
041400     MOVE CORRECT-A TO XXCORRECT. MOVE COMPUTED-A TO XXCOMPUTED.
041500     MOVE XXINFO TO DUMMY-RECORD. PERFORM WRITE-LINE 2 TIMES.
041600 BAIL-OUT-EX. EXIT.
041700 CCVS1-EXIT.
041800     EXIT.
041900 SECT-SQ-115-0001 SECTION.
042000 SEQ-INIT-013.
042100     MOVE "SQ-FS5" TO XFILE-NAME (1).
042200     MOVE "R1-F-G" TO XRECORD-NAME (1).
042300     MOVE CCVS-PGM-ID TO XPROGRAM-NAME (1).
042400     MOVE 000126 TO XRECORD-LENGTH (1).
042500     MOVE "RC" TO CHARS-OR-RECORDS (1).
042600     MOVE 0001 TO XBLOCK-SIZE (1).
042700     MOVE 000550 TO RECORDS-IN-FILE (1).
042800     MOVE "SQ" TO XFILE-ORGANIZATION (1).
042900     MOVE "S" TO XLABEL-TYPE (1).
043000     MOVE 000001 TO XRECORD-NUMBER (1).
043100     OPEN OUTPUT SQ-FS5.
043200     MOVE ZERO TO COUNT-OF-RECORDS.
043300 SEQ-TEST-013.
043400     MOVE FILE-RECORD-INFO-P1-120 (1) TO SQ-FS5-120.
043500     MOVE "FIRST " TO SQ-FS5-UPDATE.
043600     WRITE SQ-FS5R1-F-G-126.
043700     ADD 1 TO COUNT-OF-RECORDS.
043800     IF COUNT-OF-RECORDS EQUAL TO 550
043900          GO TO SEQ-WRITE-013.
044000     ADD 1 TO XRECORD-NUMBER (1).
044100     GO TO SEQ-TEST-013.
044200 SEQ-WRITE-013.
044300     MOVE "CREATE FILE SQ-FS5" TO FEATURE.
044400     MOVE "SEQ-TEST-013" TO PAR-NAME.
044500     MOVE "FILE CREATED, RECS =" TO COMPUTED-A.
044600     MOVE COUNT-OF-RECORDS TO CORRECT-18V0.
044700     PERFORM PRINT-DETAIL.
044800     CLOSE SQ-FS5.
044900*         A SEQUENTIAL MASS STORAGE FILE WITH 126 CHARACTER
045000*    RECORDS HAS BEEN CREATED.  THE FILE CONTAINS 550 RECORDS.
045100 SEQ-INIT-014.
045200     MOVE ZERO TO COUNT-OF-RECORDS.
045300*        THIS TEST READS AND CHECKS THE FILE CREATED
045400*    IN SEQ-TEST-013.
045500     OPEN INPUT SQ-FS5.
045600 SEQ-TEST-014.
045700     READ SQ-FS5 AT END
045800          GO TO SEQ-TEST-014-1.
045900     ADD 1 TO COUNT-OF-RECORDS.
046000     MOVE SQ-FS5-120 TO FILE-RECORD-INFO-P1-120 (1).
046100     IF COUNT-OF-RECORDS GREATER THAN 550
046200         MOVE "MORE THAN 550 RECORDS" TO RE-MARK
046300         GO TO SEQ-FAIL-014.
046400     IF COUNT-OF-RECORDS NOT EQUAL TO XRECORD-NUMBER (1)
046500         ADD 1 TO RECORDS-IN-ERROR
046600         GO TO SEQ-TEST-014.
046700     IF XFILE-NAME (1) NOT EQUAL TO "SQ-FS5"
046800         ADD 1 TO RECORDS-IN-ERROR
046900         GO TO SEQ-TEST-014.
047000     IF UPDATE-NUMBER (1) NOT EQUAL TO ZERO
047100         ADD 1 TO RECORDS-IN-ERROR
047200         GO TO SEQ-TEST-014.
047300     IF SQ-FS5-UPDATE EQUAL TO "FIRST "
047400         GO TO SEQ-TEST-014.
047500     ADD 1 TO RECORDS-IN-ERROR.
047600     GO TO SEQ-TEST-014.
047700 SEQ-TEST-014-1.
047800     IF RECORDS-IN-ERROR EQUAL TO ZERO
047900         GO TO SEQ-PASS-014.
048000     MOVE "ERRORS IN READING SQ-FS5" TO RE-MARK.
048100 SEQ-FAIL-014.
048200     MOVE "RECORDS IN ERROR =" TO COMPUTED-A.
048300     MOVE RECORDS-IN-ERROR TO CORRECT-18V0.
048400     PERFORM FAIL.
048500     GO TO SEQ-WRITE-014.
048600 SEQ-PASS-014.
048700     PERFORM PASS.
048800     MOVE "FILE VERIFIED RECS =" TO COMPUTED-A.
048900     MOVE COUNT-OF-RECORDS TO CORRECT-18V0.
049000 SEQ-WRITE-014.
049100     MOVE "SEQ-TEST-014" TO PAR-NAME.
049200     MOVE "VERIFY FILE SQ-FS5" TO FEATURE.
049300     PERFORM PRINT-DETAIL.
049400 SEQ-CLOSE-014.
049500     CLOSE SQ-FS5.
049600 REWRITE-INIT-GF-01.
049700     OPEN I-O SQ-FS5.
049800     MOVE ZERO TO COUNT-OF-RECORDS.
049900     MOVE ZERO TO EOF-FLAG.
050000*        THIS TEST REWRITES EVERY TENTH RECORD
050100*    OF THE FILE SQ-FS5.
050200 REWRITE-TEST-GF-01.
050300     PERFORM READ-SQ-FS5 THRU READ-SQ-FS5-EXIT 10 TIMES.
050400     IF EOF-FLAG EQUAL TO 1
050500          GO TO REWRITE-TEST-GF-01-1.
050600     MOVE SQ-FS5-120 TO FILE-RECORD-INFO-P1-120 (1).
050700     ADD 1 TO UPDATE-NUMBER (1).
050800     MOVE FILE-RECORD-INFO-P1-120 (1) TO SQ-FS5-120.
050900     MOVE "SECOND" TO SQ-FS5-UPDATE.
051000     REWRITE SQ-FS5R1-F-G-126.
051100     GO TO REWRITE-TEST-GF-01.
051200 READ-SQ-FS5.
051300     IF EOF-FLAG EQUAL TO 1
051400          GO TO READ-SQ-FS5-EXIT.
051500     READ SQ-FS5 RECORD.
051600     IF EOF-FLAG EQUAL TO 1
051700          GO TO READ-SQ-FS5-EXIT.
051800     ADD 1 TO COUNT-OF-RECORDS.
051900 READ-SQ-FS5-EXIT.
052000     EXIT.
052100 REWRITE-TEST-GF-01-1.
052200     IF COUNT-OF-RECORDS EQUAL TO 550
052300         GO TO REWRITE-PASS-GF-01.
052400 REWRITE-FAIL-GF-01.
052500     MOVE "VII-48 4.5.2                              " TO RE-MARK.
052600     PERFORM FAIL.
052700     MOVE "550 RECORDS SHOULD BE READ" TO RE-MARK.
052800     MOVE "RECORDS READ =" TO COMPUTED-A.
052900     MOVE COUNT-OF-RECORDS TO CORRECT-18V0.
053000     GO TO REWRITE-WRITE-GF-01.
053100 REWRITE-PASS-GF-01.
053200     PERFORM PASS.
053300 REWRITE-WRITE-GF-01.
053400     MOVE "RWRT-TEST-GF-01" TO PAR-NAME.
053500     MOVE "REWRITE FILE SQ-FS5" TO FEATURE.
053600     PERFORM PRINT-DETAIL.
053700     IF PERM-ERRORS EQUAL TO 1
053800     MOVE "PERMANENT ERRORS ENCOUNTERED ON PREVIOUS I-O OPERATION"
053900          TO PRINT-REC
054000          PERFORM WRITE-LINE.
054100 REWRITE-CLOSE-GF-01.
054200     CLOSE SQ-FS5.
054300 REWRITE-INIT-GF-02.
054400     MOVE ZERO TO COUNT-OF-RECORDS.
054500     MOVE ZERO TO EOF-FLAG.
054600     OPEN INPUT SQ-FS5.
054700*        THIS TEST READS AND CHECKS THE FILE WHICH WAS
054800*    REWRITTEN IN REWRITE-TEST-01.
054900     MOVE ZERO TO RECORDS-IN-ERROR.
055000     MOVE ZERO TO LOOP-COUNT.
055100 REWRITE-TEST-GF-02.
055200     READ SQ-FS5  END
055300          MOVE 1 TO EOF-FLAG
055400          GO TO REWRITE-TEST-GF-02-2.
055500     ADD 1 TO COUNT-OF-RECORDS.
055600     IF COUNT-OF-RECORDS GREATER THAN 550
055700          MOVE "MORE THAN 550 RECORDS" TO RE-MARK
055800          GO TO REWRITE-FAIL-GF-02.
055900     ADD 1 TO LOOP-COUNT.
056000     MOVE SQ-FS5-120 TO FILE-RECORD-INFO-P1-120 (1).
056100     IF XFILE-NAME (1) NOT EQUAL TO "SQ-FS5"
056200          ADD 1 TO RECORDS-IN-ERROR
056300          GO TO REWRITE-TEST-GF-02.
056400     IF LOOP-COUNT EQUAL TO 10
056500          MOVE ZERO TO LOOP-COUNT
056600          GO TO REWRITE-TEST-GF-02-1.
056700     IF UPDATE-NUMBER (1) NOT EQUAL TO ZERO
056800          ADD 1 TO RECORDS-IN-ERROR
056900          GO TO REWRITE-TEST-GF-02.
057000     IF SQ-FS5-UPDATE EQUAL TO "FIRST "
057100          GO TO REWRITE-TEST-GF-02.
057200     ADD 1 TO RECORDS-IN-ERROR.
057300     GO TO REWRITE-TEST-GF-02.
057400 REWRITE-TEST-GF-02-1.
057500     IF UPDATE-NUMBER (1) NOT EQUAL TO 1
057600          ADD 1 TO RECORDS-IN-ERROR
057700          GO TO REWRITE-TEST-GF-02.
057800     IF SQ-FS5-UPDATE EQUAL TO "SECOND"
057900          GO TO REWRITE-TEST-GF-02.
058000     ADD 1 TO RECORDS-IN-ERROR.
058100     GO TO REWRITE-TEST-GF-02.
058200 REWRITE-TEST-GF-02-2.
058300     IF COUNT-OF-RECORDS NOT EQUAL TO 550
058400         MOVE "LESS THAN 550 RECORDS" TO RE-MARK
058500         MOVE "RECORDS READ =" TO COMPUTED-A
058600         MOVE COUNT-OF-RECORDS TO CORRECT-18V0
058700         GO TO REWRITE-FAIL-GF-02.
058800     IF RECORDS-IN-ERROR NOT EQUAL TO ZERO
058900         MOVE "ERRORS IN READING SQ-FS5" TO RE-MARK
059000         MOVE "RECORDS IN ERROR =" TO COMPUTED-A
059100         MOVE RECORDS-IN-ERROR TO CORRECT-18V0
059200         GO TO REWRITE-FAIL-GF-02.
059300 REWRITE-PASS-GF-02.
059400     PERFORM PASS.
059500     GO TO REWRITE-WRITE-GF-02.
059600 REWRITE-FAIL-GF-02.
059700     PERFORM FAIL.
059800 REWRITE-WRITE-GF-02.
059900     MOVE "RWRT-TEST-GF-02" TO PAR-NAME.
060000     MOVE "VERIFY FILE SQ-FS5" TO FEATURE.
060100     PERFORM PRINT-DETAIL.
060200 REWRITE-CLOSE-GF-02.
060300     CLOSE SQ-FS5.
060400 TERMINATE-ROUTINE.
060500     EXIT.
060600 CCVS-EXIT SECTION.
060700 CCVS-999999.
060800     GO TO CLOSE-FILES.
