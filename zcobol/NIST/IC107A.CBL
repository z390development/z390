000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID.
000300     IC107A.
000400****************************************************************
000500*                                                              *
000600*    VALIDATION FOR:-                                          *
000700*                                                              *
000800*    "ON-SITE VALIDATION, NATIONAL INSTITUTE OF STD & TECH.     ".
000900*                                                              *
001000*    "COBOL 85 VERSION 4.2, Apr  1993 SSVG                      ".
001100*                                                              *
001200****************************************************************
001300*                                                              *
001400*      X-CARDS USED BY THIS PROGRAM ARE :-                     *
001500*                                                              *
001600*        X-55  - SYSTEM PRINTER NAME.                          *
001700*        X-82  - SOURCE COMPUTER NAME.                         *
001800*        X-83  - OBJECT COMPUTER NAME.                         *
001900*                                                              *
002000****************************************************************
002100*        THE SUBPROGRAM IC107 CONTAINS TABLES AND AN INDEX
002200*    DATA ITEM WHICH ARE DEFINED IN THE LINKAGE SECTION AND
002300*    NAMED AS OPERANDS IN THE USING PHRASE OF THE PROCEDURE
002400*    DIVISION HEADER.  ONE OF THE TABLES HAS AN INDEX DEFINED
002500*    FOR IT.  THIS INDEX SHOULD BE SEPARATE FROM THE INDEX
002600*    DEFINED FOR THE SAME TABLE IN THE MAIN PROGRAM IC106,
002700*    BUT NO SPACE SHOULD BE ALLOCATED FOR THE TABLES DEFINED
002800*    IN THE LINKAGE SECTION.  THE INDEX DATA ITEM IS SET IN
002900*    THE MAIN PROGRAM PRIOR TO CALLING IC107, AND IT IS USED
003000*    IN THIS SUBPROGRAM TO SET AN INDEX FOR REFERENCING THE
003100*    TABLE IN THE SUBPROGRAM.
003200 ENVIRONMENT DIVISION.
003300 CONFIGURATION SECTION.
003400 SOURCE-COMPUTER.
003500     XXXXX082.
003600 OBJECT-COMPUTER.
003700     XXXXX083.
003800 INPUT-OUTPUT SECTION.
003900 FILE-CONTROL.
004000     SELECT PRINT-FILE ASSIGN TO
004100     XXXXX055.
004200 DATA DIVISION.
004300 FILE SECTION.
004400 FD  PRINT-FILE.
004500 01  PRINT-REC PICTURE X(120).
004600 01  DUMMY-RECORD PICTURE X(120).
004700 WORKING-STORAGE SECTION.
004800 77  IDN3  USAGE IS INDEX.
004900 77  S1  PICTURE 99.
005000 77  AL-CON PICTURE XXX  VALUE "XYZ".
005100 LINKAGE SECTION.
005200 77  IDN2  USAGE IS INDEX.
005300 01  GROUP-1.
005400     02  DN1 PICTURE X OCCURS 10 TIMES
005500                       INDEXED BY IN3.
005600 01  GROUP-2.
005700     02    GROUP-21.
005800         06 DN2 PIC X OCCURS 10 TIMES.
005900     02     GROUP-2-1 REDEFINES GROUP-21.
006000         03  FILLER  PICTURE X(7).
006100         03  DN3     PICTURE XXX.
006200 PROCEDURE DIVISION USING IDN2 GROUP-1 GROUP-2.
006300 SECT-IC107-0001 SECTION.
006400 LINK-TEST-02-01.
006500     SET IN3 TO IDN2.
006600     IF DN1 (IN3) EQUAL TO "F"
006700         MOVE "G" TO DN2 (7).
006800 LINK-TEST-02-02.
006900     SET IDN3 TO IDN2.
007000     SET IN3 TO IDN3.
007100     IF IN3 EQUAL TO 6
007200         MOVE "F" TO DN2 (6).
007300*        THE TESTS IN LINK-TEST-02 USE THE INDEX DATA ITEM
007400*    WHICH IS DEFINED IN THE LINKAGE SECTION AND WAS SET IN
007500*    THE MAIN PROGRAM BEFORE THIS SUBPROGRAM WAS CALLED.
007600 LINK-TEST-03-01.
007700     SET IN3 TO 1.
007800     MOVE 1 TO S1.
007900     MOVE DN1 (IN3) TO DN2 (S1).
008000 LINK-TEST-03-02.
008100     SET IN3 UP BY 1.
008200     ADD 1 TO S1.
008300     MOVE DN1 (IN3) TO DN2 (S1).
008400*        THE TESTS IN LINK-TEST-03 SET THE INDEX DEFINED IN THE
008500*    TABLE IN THE LINKAGE SECTION AND USE THE INDEX TO REFERENCE
008600*    THE TABLE ITEMS.
008700 LINK-TEST-04-01.
008800     SET IN3 TO 3.
008900     SET IDN2 TO IN3.
009000 LINK-TEST-04-02.
009100     IF IDN2 IS EQUAL TO IN3
009200         MOVE "C" TO DN2 (3).
009300*        THE TESTS IN LINK-TEST-04 RESET THE INDEX DATA ITEM
009400*    TO CORRESPOND TO TABLE POSITION 3, AND COMPARES THE
009500*    INDEX DATA ITEM TO THE INDEX.
009600 LINK-TEST-05-01.
009700     MOVE 4 TO S1.
009800     SET IN3 TO S1.
009900     MOVE DN1 (IN3) TO DN2 (S1).
010000 LINK-TEST-05-02.
010100     MOVE DN1 (5) TO DN2 (5).
010200*        THE TESTS IN LINK-TEST-05 MOVE DATA FROM THE FIRST
010300*    TABLE DEFINED IN THE LINKAGE SECTION TO THE SECOND TABLE.
010400 LINK-TEST-06.
010500     MOVE AL-CON TO DN3.
010600*        THIS TEST MOVES DATA TO THE REDEFINED FIELD IN A TABLE
010700*    IN THE LINKAGE SECTION.
010800 EXIT-IC107.
010900     EXIT PROGRAM.
