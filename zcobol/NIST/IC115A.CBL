000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID.
000300     IC115A.
000400****************************************************************
000500*                                                              *
000600*    VALIDATION FOR:-                                          *
000700*                                                              *
000800*    "ON-SITE VALIDATION, NATIONAL INSTITUTE OF STD & TECH.     ".
000900*                                                              *
001000*    "COBOL 85 VERSION 4.2, Apr  1993 SSVG                      ".
001100*                                                              *
001200****************************************************************
001300*                                                              *
001400*      X-CARDS USED BY THIS PROGRAM ARE :-                     *
001500*                                                              *
001600*        X-55  - SYSTEM PRINTER NAME.                          *
001700*        X-82  - SOURCE COMPUTER NAME.                         *
001800*        X-83  - OBJECT COMPUTER NAME.                         *
001900*                                                              *
002000****************************************************************
002100*
002200*         THE ROUTINE IC115 IS A SUBPROGRAM CALLED BY IC114.
002300*    THIS SUBPROGRAM CONTAINS A FILE SECTION, A WORKING-STORAGE
002400*    SECTION AND A LINKAGE SECTION.  THE FILE SQ-FS3 IS CREATED
002500*    AND VERIFIED IN THIS ROUTINE.  THE FILE IS OPENED AND READ
002600*    AGAIN.  EACH RECORD IS CHECKED BY MOVING IT TO THE LINKAGE
002700*    SECTION AND RETURNING TO THE MAIN PROGRAM TO VERIFY THE
002800*    RECORD CONTENTS.  THE PRINTING OF THE OUTPUT REPORT FOR THE
002900*    TEST RESULTS IS PERFORMED BY RETURNING TO THE MAIN PROGRAM
003000*    IC114.
003100*
003200*         THIS SUBPROGRAM IS ADAPTED FROM THE SEQUENTIAL I-O
003300*    ROUTINE SQ104.  IF THAT ROUTINE DOES NOT PERFORM CORRECTLY
003400*    THEN THE RESULTS OF THESE TESTS ARE INCONCLUSIVE.
003500*
003600********************************************
003700 ENVIRONMENT DIVISION.
003800 CONFIGURATION SECTION.
003900 SOURCE-COMPUTER.
004000     XXXXX082.
004100 OBJECT-COMPUTER.
004200     XXXXX083.
004300 INPUT-OUTPUT SECTION.
004400 FILE-CONTROL.
004500     SELECT SQ-FS3 ASSIGN TO
004600     XXXXX014
004700     ORGANIZATION IS SEQUENTIAL
004800     ACCESS MODE IS SEQUENTIAL.
004900 DATA DIVISION.
005000 FILE SECTION.
005100 FD  SQ-FS3
005200     BLOCK CONTAINS 120 CHARACTERS
005300     RECORD CONTAINS 120 CHARACTERS
005400     LABEL RECORDS ARE STANDARD
005500C    VALUE OF
005600C    XXXXX074
005700C    IS
005800C    XXXXX075
005900G    XXXXX069
006000     DATA RECORD SQ-FS3R1-F-G-120.
006100 01  SQ-FS3R1-F-G-120.
006200     02  FILLER PIC X(120).
006300 WORKING-STORAGE SECTION.
006400 01  FILE-RECORD-INFORMATION-REC.
006500     03 FILE-RECORD-INFO-SKELETON.
006600        05 FILLER                 PICTURE X(48)       VALUE
006700             "FILE=      ,RECORD=      /0,RECNO=000000,UPDT=00".
006800        05 FILLER                 PICTURE X(46)       VALUE
006900             ",ODO=0000,PGM=     ,LRECL=000000,BLKSIZ  =0000".
007000        05 FILLER                 PICTURE X(26)       VALUE
007100             ",LFIL=000000,ORG=  ,LBLR= ".
007200        05 FILLER                 PICTURE X(37)       VALUE
007300             ",RECKEY=                             ".
007400        05 FILLER                 PICTURE X(38)       VALUE
007500             ",ALTKEY1=                             ".
007600        05 FILLER                 PICTURE X(38)       VALUE
007700             ",ALTKEY2=                             ".
007800        05 FILLER                 PICTURE X(7)        VALUE SPACE.
007900     03 FILE-RECORD-INFO          OCCURS  10  TIMES.
008000        05 FILE-RECORD-INFO-P1-120.
008100           07 FILLER              PIC X(5).
008200           07 XFILE-NAME           PIC X(6).
008300           07 FILLER              PIC X(8).
008400           07 XRECORD-NAME         PIC X(6).
008500           07 FILLER              PIC X(1).
008600           07 REELUNIT-NUMBER     PIC 9(1).
008700           07 FILLER              PIC X(7).
008800           07 XRECORD-NUMBER       PIC 9(6).
008900           07 FILLER              PIC X(6).
009000           07 UPDATE-NUMBER       PIC 9(2).
009100           07 FILLER              PIC X(5).
009200           07 ODO-NUMBER          PIC 9(4).
009300           07 FILLER              PIC X(5).
009400           07 XPROGRAM-NAME        PIC X(5).
009500           07 FILLER              PIC X(7).
009600           07 XRECORD-LENGTH       PIC 9(6).
009700           07 FILLER              PIC X(7).
009800           07 CHARS-OR-RECORDS    PIC X(2).
009900           07 FILLER              PIC X(1).
010000           07 XBLOCK-SIZE          PIC 9(4).
010100           07 FILLER              PIC X(6).
010200           07 RECORDS-IN-FILE     PIC 9(6).
010300           07 FILLER              PIC X(5).
010400           07 XFILE-ORGANIZATION   PIC X(2).
010500           07 FILLER              PIC X(6).
010600           07 XLABEL-TYPE          PIC X(1).
010700        05 FILE-RECORD-INFO-P121-240.
010800           07 FILLER              PIC X(8).
010900           07 XRECORD-KEY          PIC X(29).
011000           07 FILLER              PIC X(9).
011100           07 ALTERNATE-KEY1      PIC X(29).
011200           07 FILLER              PIC X(9).
011300           07 ALTERNATE-KEY2      PIC X(29).
011400           07 FILLER              PIC X(7).
011500 01  CCVS-PGM-ID PIC X(6) VALUE
011600     "IC115A".
011700 LINKAGE SECTION.
011800 01  GROUP-LINKAGE-VARIABLES.
011900         02  COUNT-OF-RECORDS  PICTURE 9(6).
012000     02  RECORDS-IN-ERROR  PICTURE 9(6).
012100     02  ERROR-FLAG  PICTURE 9.
012200     02  EOF-FLAG  PICTURE 9.
012300     02  CALL-FLAG  PICTURE 9.
012400 01  FILE-REC-SQ-FS3.
012500     02  FILLER  PICTURE X(120).
012600 PROCEDURE DIVISION USING GROUP-LINKAGE-VARIABLES
012700                          FILE-REC-SQ-FS3.
012800 SECT-IC115-0001 SECTION.
012900 INIT-PARAGRAPH.
013000     GO TO SECT-IC115-0002 SECT-IC115-0003 SECT-IC115-0004
013100           SECT-IC115-0005 SECT-IC115-0006
013200              DEPENDING ON CALL-FLAG.
013300*        THE IDENTIFIER CALL-FLAG CONTROLS THE SUBPROGRAM
013400*    PROCESSING AND IT IS SET BY THE CALLING PROGRAM IC114.
013500 SECT-IC115-0002 SECTION.
013600 LINK-INIT-10.
013700     MOVE FILE-RECORD-INFO-SKELETON TO
013800              FILE-RECORD-INFO (1).
013900     MOVE "SQ-FS3" TO XFILE-NAME (1).
014000     MOVE "R1-F-G" TO XRECORD-NAME (1).
014100     MOVE CCVS-PGM-ID TO XPROGRAM-NAME (1).
014200     MOVE 120 TO XRECORD-LENGTH (1).
014300     MOVE "CH" TO CHARS-OR-RECORDS (1).
014400     MOVE 120 TO XBLOCK-SIZE (1).
014500     MOVE 000649 TO RECORDS-IN-FILE (1).
014600     MOVE "SQ" TO XFILE-ORGANIZATION (1).
014700     MOVE "S" TO XLABEL-TYPE (1).
014800     MOVE 000001 TO XRECORD-NUMBER (1).
014900     OPEN OUTPUT SQ-FS3.
015000 LINK-TEST-10.
015100     MOVE FILE-RECORD-INFO-P1-120 (1) TO SQ-FS3R1-F-G-120.
015200     WRITE SQ-FS3R1-F-G-120.
015300     IF XRECORD-NUMBER (1) EQUAL TO 649
015400              GO TO LINK-TEST-10-01.
015500     ADD 1 TO XRECORD-NUMBER (1).
015600     GO TO LINK-TEST-10.
015700 LINK-TEST-10-01.
015800     CLOSE SQ-FS3.
015900     MOVE XRECORD-NUMBER (1) TO COUNT-OF-RECORDS.
016000*         A MASS STORAGE SEQUENTIAL FILE WITH 120 CHARACTERS PER
016100*    RECORD HAS BEEN CREATED.  THE FILE CONTAINS 649 RECORDS.
016200 LINK-EXIT-10.
016300     EXIT PROGRAM.
016400 SECT-IC115-0003 SECTION.
016500 LINK-INIT-11.
016600*         THIS TEST READS AND CHECKS THE FILE CREATED IN
016700*    SECT-IC115-0002.
016800     OPEN INPUT SQ-FS3.
016900 LINK-TEST-11.
017000     READ SQ-FS3 RECORD
017100              AT END GO TO LINK-CLOSE-11.
017200     MOVE SQ-FS3R1-F-G-120 TO FILE-RECORD-INFO-P1-120 (1).
017300     ADD 1 TO COUNT-OF-RECORDS.
017400     IF COUNT-OF-RECORDS GREATER THAN 649
017500              MOVE 1 TO ERROR-FLAG
017600              GO TO LINK-CLOSE-11.
017700     IF COUNT-OF-RECORDS NOT EQUAL TO XRECORD-NUMBER (1)
017800              GO TO LINK-FAIL-11.
017900     IF XFILE-NAME (1) NOT EQUAL TO "SQ-FS3"
018000              GO TO LINK-FAIL-11.
018100     IF XLABEL-TYPE (1) NOT EQUAL TO "S"
018200              GO TO LINK-FAIL-11.
018300     GO TO LINK-TEST-11.
018400 LINK-FAIL-11.
018500     ADD 1 TO RECORDS-IN-ERROR.
018600     MOVE 1 TO ERROR-FLAG.
018700 LINK-CLOSE-11.
018800     CLOSE SQ-FS3.
018900 LINK-EXIT-11.
019000     EXIT PROGRAM.
019100 SECT-IC115-0004 SECTION.
019200 LINK-INIT-12.
019300     OPEN INPUT SQ-FS3.
019400 LINK-INIT-12-EXIT.
019500     EXIT PROGRAM.
019600 SECT-IC115-0005 SECTION.
019700 LINK-TEST-12.
019800     READ SQ-FS3 RECORD
019900           AT END MOVE 1 TO EOF-FLAG.
020000     MOVE SQ-FS3R1-F-G-120 TO FILE-REC-SQ-FS3.
020100*
020200*         THE MAIN PROGRAM IC114 REPEATLY CALLS THE SUBPROGRAM
020300*    IC115 TO READ THE FILE SQ-FS3.  THE DATA RECORD IS MOVED
020400*    TO A LINKAGE RECORD FOR CHECKING OF THE CONTENTS BY THE
020500*    MAIN PROGRAM.
020600*
020700 LINK-EXIT-12.
020800     EXIT PROGRAM.
020900 SECT-IC115-0006 SECTION.
021000 LINK-CLOSE-12.
021100     CLOSE SQ-FS3.
021200 LINK-CLOSE-EXIT-12.
021300     EXIT PROGRAM.
