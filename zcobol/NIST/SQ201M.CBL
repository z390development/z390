000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID.
000300     SQ201M.
000400****************************************************************
000500*                                                              *
000600*    VALIDATION FOR:-                                          *
000700*    " HIGH       ".
000800*                                                              *
000900*    CREATION DATE     /     VALIDATION DATE                   *
001000*    "4.2 ".
001100*                                                              *
001200*    THIS ROUTINE TESTS THE WRITE ... ADVANCIN  STATEMENT IN
001300*    COMBINATION WITH THE
001400*                NOT AT END-OF-PAGE
001500*    AND THE
001600*                END-WRITE        CLAUSES.
001700*
001800*        THE ROUTINE SQ201M TESTS THE USE OF THE LEVEL 2 WRITE
001900*    STATEMENT AND THE LINAGE CLAUSE FOR A FILE DESIGNATED AS
002000*    PRINTER OUTPUT.  THESE STATEMENTS CONTROL THE VERTICAL
002100*    POSITIONING OF EACH LINE ON A PRINTED PAGE.  THE LINAGE
002200*    CLAUSE SPECIFICALLY CONTROLS THE VERTICAL FORMAT OF A
002300*    LOGICAL PRINT PAGE.  SQ201M TESTS (1) THE ACCURACY OF THE
002400*    LINAGE-COUNTER, (2) THE WRITE ADVANCING PAGE, AND (3) THE
002500*    FOUR COMBINATIONS OF THE END-OF-PAGE PHRASE.  IT IS ASSUMED
002600*    THAT ALL LEVEL 2 NUCLEUS OPTIONS ARE AVAILABLE IN TESTING
002700*    SQ201M. A LINAGE CLAUSE WITH COMPLETE FOOTING, TOP, AND
002800*    BOTTOM SECTIONS AND UTILIZING INTEGER ITEMS IS USED WITH
002900*    THIS TEST.
003000 ENVIRONMENT DIVISION.
003100 CONFIGURATION SECTION.
003200 SOURCE-COMPUTER.
003300     XXXXX082.
003400 OBJECT-COMPUTER.
003500     XXXXX083.
003600 INPUT-OUTPUT SECTION.
003700 FILE-CONTROL.
003800P    SELECT RAW-DATA   ASSIGN TO
003900P    XXXXX062
004000P           ORGANIZATION IS INDEXED
004100P           ACCESS MODE IS RANDOM
004200P           RECORD KEY IS RAW-DATA-KEY.
004300     SELECT PRINT-FILE ASSIGN TO
004400     XXXXX055.
004500 DATA DIVISION.
004600 FILE SECTION.
004700P
004800PFD  RAW-DATA.
004900P
005000P01  RAW-DATA-SATZ.
005100P    05  RAW-DATA-KEY        PIC X(6).
005200P    05  C-DATE              PIC 9(6).
005300P    05  C-TIME              PIC 9(8).
005400P    05  C-NO-OF-TESTS       PIC 99.
005500P    05  C-OK                PIC 999.
005600P    05  C-ALL               PIC 999.
005700P    05  C-FAIL              PIC 999.
005800P    05  C-DELETED           PIC 999.
005900P    05  C-INSPECT           PIC 999.
006000P    05  C-NOTE              PIC X(13).
006100P    05  C-INDENT            PIC X.
006200P    05  C-ABORT             PIC X(8).
006300 FD  PRINT-FILE
006400C    LABEL RECORDS
006500C    XXXXX084
006600C    DATA RECORD IS PRINT-REC DUMMY-RECORD
006700     LINAGE IS 50 LINES
006800              WITH FOOTING AT 45
006900              LINES AT TOP 10
007000              LINES AT BOTTOM 6.
007100 01  PRINT-REC PICTURE X(120).
007200 01  DUMMY-RECORD PICTURE X(120).
007300 WORKING-STORAGE SECTION.
007400 01  WRITE-SWITCH       PIC 9  VALUE 0.
007500 01  END-WRITE-SWITCH   PIC 9  VALUE 1.
007600 01  LC-HOLD            PIC 99.
007700 01  IDENTIFIER-2       PIC 99.
007800 01  TOP-LINE           PIC X(120)   VALUE "THIS LINE WAS WRITTEN
007900-    "BY A WRITE ADVANCING PAGE OPERATION.  IT SHOULD APPEAR AS TH
008000-    "E FIRST LINE OF A NEW LOGICAL PAGE.".
008100 01  DETAIL-LINE.
008200     02 FILLER          PIC X(20)    VALUE SPACE.
008300     02 FILLER          PIC X(13)    VALUE "THIS IS LINE ".
008400     02 DETAIL-LINE-NO  PIC 999.
008500     02 FILLER          PIC X(52)   VALUE " OF 132 DETAIL LINES.".
008600     02 FILLER          PIC X(18)    VALUE "LINAGE-COUNTER IS ".
008700     02 DETAIL-LC       PIC 99.
008800     02 FILLER          PIC X(12)    VALUE ".".
008900 01  FOOT-LINE.
009000     02 FILLER          PIC X(20)    VALUE SPACE.
009100     02 FILLER          PIC X(13)    VALUE "THIS IS LINE ".
009200     02 FOOT-COUNT      PIC 999.
009300     02 FILLER          PIC X(47)    VALUE " OF 6 FOOTING LINES.
009400-    "LINAGE-COUNTER SHOULD BE ".
009500     02 FOOT-LINE-NO    PIC 99.
009600     02 FILLER          PIC X(21)   VALUE ".  LINAGE-COUNTER IS ".
009700     02 FOOT-LC         PIC 99.
009800     02 FILLER          PIC X(12)    VALUE ".".
009900 01  EOP-MESSAGE-1      PIC X(120)   VALUE "          THIS IS A TE
010000-    "ST FOR THE EOP PHRASE.  50 LINES SHOULD PRINT IN THE PAGE BO
010100-    "DY INCLUDING 44 DETAIL LINES AND".
010200 01  EOP-MESSAGE-2      PIC X(120)   VALUE "          6 FOOTING LI
010300-    "NES.  THESE LINES SHOULD BE CONSECUTIVE ON ONE LOGICAL PAGE
010400-    "AND BE FOLLOWED BY 16 TOP AND BOTTOM".
010500 01  EOP-MESSAGE-3      PIC X(120)   VALUE "          BLANK LINES.
010600-    "  THE CORRECT AND COMPUTED LINAGE-COUNTER VALUES IN THE FOOT
010700-    "ING LINES SHOULD BE EQUAL.".
010800 01  TEST-RESULTS.
010900     02 FILLER                    PICTURE X VALUE SPACE.
011000     02 FEATURE                   PICTURE X(20) VALUE SPACE.
011100     02 FILLER                    PICTURE X VALUE SPACE.
011200     02 P-OR-F                    PICTURE X(5) VALUE SPACE.
011300     02 FILLER                    PICTURE X  VALUE SPACE.
011400     02  PAR-NAME.
011500       03 FILLER PICTURE X(12) VALUE SPACE.
011600       03  PARDOT-X PICTURE X  VALUE SPACE.
011700       03 DOTVALUE PICTURE 99  VALUE ZERO.
011800       03 FILLER PIC X(5) VALUE SPACE.
011900     02 FILLER PIC X(10) VALUE SPACE.
012000     02 RE-MARK PIC X(61).
012100 01  TEST-COMPUTED.
012200     02 FILLER PIC X(30) VALUE SPACE.
012300     02 FILLER PIC X(17) VALUE "       COMPUTED=".
012400     02 COMPUTED-X.
012500     03 COMPUTED-A                PICTURE X(20) VALUE SPACE.
012600     03 COMPUTED-N REDEFINES COMPUTED-A PICTURE -9(9).9(9).
012700     03 COMPUTED-0V18 REDEFINES COMPUTED-A  PICTURE -.9(18).
012800     03 COMPUTED-4V14 REDEFINES COMPUTED-A  PICTURE -9(4).9(14).
012900     03 COMPUTED-14V4 REDEFINES COMPUTED-A  PICTURE -9(14).9(4).
013000     03       CM-18V0 REDEFINES COMPUTED-A.
013100         04 COMPUTED-18V0                   PICTURE -9(18).
013200         04 FILLER                          PICTURE X.
013300     03 FILLER PIC X(50) VALUE SPACE.
013400 01  TEST-CORRECT.
013500     02 FILLER PIC X(30) VALUE SPACE.
013600     02 FILLER PIC X(17) VALUE "       CORRECT =".
013700     02 CORRECT-X.
013800     03 CORRECT-A                 PICTURE X(20) VALUE SPACE.
013900     03 CORRECT-N REDEFINES CORRECT-A PICTURE -9(9).9(9).
014000     03 CORRECT-0V18 REDEFINES CORRECT-A    PICTURE -.9(18).
014100     03 CORRECT-4V14 REDEFINES CORRECT-A    PICTURE -9(4).9(14).
014200     03 CORRECT-14V4 REDEFINES CORRECT-A    PICTURE -9(14).9(4).
014300     03      CR-18V0 REDEFINES CORRECT-A.
014400         04 CORRECT-18V0                    PICTURE -9(18).
014500         04 FILLER                          PICTURE X.
014600     03 FILLER PIC X(50) VALUE SPACE.
014700 01  CCVS-C-1.
014800     02 FILLER PICTURE IS X(99) VALUE IS " FEATURE              PA
014900-    "SS  PARAGRAPH-NAME
015000-    "        REMARKS".
015100     02 FILLER PICTURE IS X(20) VALUE IS SPACE.
015200 01  CCVS-C-2.
015300     02 FILLER PICTURE IS X VALUE IS SPACE.
015400     02 FILLER PICTURE IS X(6) VALUE IS "TESTED".
015500     02 FILLER PICTURE IS X(15) VALUE IS SPACE.
015600     02 FILLER PICTURE IS X(4) VALUE IS "FAIL".
015700     02 FILLER PICTURE IS X(94) VALUE IS SPACE.
015800 01  REC-SKL-SUB PICTURE 9(2) VALUE ZERO.
015900 01  REC-CT PICTURE 99 VALUE ZERO.
016000 01  DELETE-CNT                   PICTURE 999  VALUE ZERO.
016100 01  ERROR-COUNTER PICTURE IS 999 VALUE IS ZERO.
016200 01  INSPECT-COUNTER PIC 999 VALUE 11.
016300 01  PASS-COUNTER PIC 999 VALUE ZERO.
016400 01  TOTAL-ERROR PIC 999 VALUE ZERO.
016500 01  ERROR-HOLD PIC 999 VALUE ZERO.
016600 01  DUMMY-HOLD PIC X(120) VALUE SPACE.
016700 01  RECORD-COUNT PIC 9(5) VALUE ZERO.
016800 01  CCVS-H-1.
016900     02  FILLER   PICTURE X(27)  VALUE SPACE.
017000     02 FILLER PICTURE X(67) VALUE
017100     " FEDERAL SOFTWARE TESTING CENTER COBOL COMPILER VALIDATION
017200-    " SYSTEM".
017300     02  FILLER     PICTURE X(26)  VALUE SPACE.
017400 01  CCVS-H-2.
017500     02 FILLER PICTURE X(52) VALUE IS
017600     "CCVS85 FSTC COPY, NOT FOR DISTRIBUTION.".
017700     02 FILLER PICTURE IS X(19) VALUE IS "TEST RESULTS SET-  ".
017800     02 TEST-ID PICTURE IS X(9).
017900     02 FILLER PICTURE IS X(40) VALUE IS SPACE.
018000 01  CCVS-H-3.
018100     02  FILLER PICTURE X(34) VALUE
018200     " FOR OFFICIAL USE ONLY    ".
018300     02  FILLER PICTURE X(58) VALUE
018400     "COBOL 85 VERSION 4.2, Apr  1993 SSVG                      ".
018500     02  FILLER PICTURE X(28) VALUE
018600     "  COPYRIGHT   1985 ".
018700 01  CCVS-E-1.
018800     02 FILLER PICTURE IS X(52) VALUE IS SPACE.
018900     02 FILLER PICTURE IS X(14) VALUE IS "END OF TEST-  ".
019000     02 ID-AGAIN PICTURE IS X(9).
019100     02 FILLER PICTURE X(45) VALUE IS
019200     " NTIS DISTRIBUTION COBOL 85".
019300 01  CCVS-E-2.
019400     02  FILLER                   PICTURE X(31)  VALUE
019500     SPACE.
019600     02  FILLER                   PICTURE X(21)  VALUE SPACE.
019700     02 CCVS-E-2-2.
019800         03 ERROR-TOTAL PICTURE IS XXX VALUE IS SPACE.
019900         03 FILLER PICTURE IS X VALUE IS SPACE.
020000         03 ENDER-DESC PIC X(46) VALUE "ERRORS ENCOUNTERED".
020100 01  CCVS-E-3.
020200     02  FILLER PICTURE X(22) VALUE
020300     " FOR OFFICIAL USE ONLY".
020400     02  FILLER PICTURE X(12) VALUE SPACE.
020500     02  FILLER PICTURE X(58) VALUE
020600     "ON-SITE VALIDATION, NATIONAL INSTITUTE OF STD & TECH.     ".
020700     02  FILLER PICTURE X(13) VALUE SPACE.
020800     02 FILLER PIC X(15) VALUE " COPYRIGHT 1985".
020900 01  CCVS-E-4.
021000     02 CCVS-E-4-1 PIC XXX VALUE SPACE.
021100     02 FILLER PIC XXXX VALUE " OF ".
021200     02 CCVS-E-4-2 PIC XXX VALUE SPACE.
021300     02 FILLER PIC X(40) VALUE
021400      "  TESTS WERE EXECUTED SUCCESSFULLY".
021500 01  XXINFO.
021600     02 FILLER PIC X(30) VALUE "        *** INFORMATION  ***".
021700     02 INFO-TEXT.
021800     04 FILLER PIC X(20) VALUE SPACE.
021900     04 XXCOMPUTED PIC X(20).
022000     04 FILLER PIC X(5) VALUE SPACE.
022100     04 XXCORRECT PIC X(20).
022200 01  HYPHEN-LINE.
022300     02 FILLER PICTURE IS X VALUE IS SPACE.
022400     02 FILLER PICTURE IS X(65) VALUE IS "************************
022500-    "*****************************************".
022600     02 FILLER PICTURE IS X(54) VALUE IS "************************
022700-    "******************************".
022800 01  CCVS-PGM-ID PIC X(6) VALUE
022900     "SQ201M".
023000 PROCEDURE DIVISION.
023100 CCVS1 SECTION.
023200 OPEN-FILES.
023300P    OPEN I-O RAW-DATA.
023400P    MOVE CCVS-PGM-ID TO RAW-DATA-KEY.
023500P    MOVE "ABORTED " TO C-ABORT.
023600P    READ RAW-DATA INVALID KEY GO TO END-E-1.
023700P    MOVE "ABORTED " TO C-ABORT.
023800P    ADD 1 TO C-NO-OF-TESTS.
023900P    ACCEPT C-DATE  FROM DATE.
024000P    ACCEPT C-TIME  FROM TIME.
024100P    REWRITE RAW-DATA-SATZ INVALID KEY GO TO END-E-1.
024200PEND-E-1.
024300P    CLOSE RAW-DATA.
024400     OPEN     OUTPUT PRINT-FILE.
024500     MOVE CCVS-PGM-ID TO TEST-ID. MOVE CCVS-PGM-ID TO ID-AGAIN.
024600     MOVE    SPACE TO TEST-RESULTS.
024700     GO TO CCVS1-EXIT.
024800 CLOSE-FILES.
024900     PERFORM END-ROUTINE THRU END-ROUTINE-13. CLOSE PRINT-FILE.
025000P    OPEN I-O RAW-DATA.
025100P    MOVE CCVS-PGM-ID TO RAW-DATA-KEY.
025200P    READ RAW-DATA INVALID KEY GO TO END-E-2.
025300P    MOVE "OK.     " TO C-ABORT.
025400P    MOVE PASS-COUNTER TO C-OK.
025500P    MOVE ERROR-HOLD   TO C-ALL.
025600P    MOVE ERROR-COUNTER TO C-FAIL.
025700P    MOVE DELETE-CNT TO C-DELETED.
025800P    MOVE INSPECT-COUNTER TO C-INSPECT.
025900P    REWRITE RAW-DATA-SATZ INVALID KEY GO TO END-E-2.
026000PEND-E-2.
026100P    CLOSE RAW-DATA.
026200 TERMINATE-CCVS.
026300S    EXIT PROGRAM.
026400STERMINATE-CALL.
026500     STOP     RUN.
026600 INSPT. MOVE "INSPT" TO P-OR-F. ADD 1 TO INSPECT-COUNTER.
026700 PASS.  MOVE "PASS " TO P-OR-F.  ADD 1 TO PASS-COUNTER.
026800 FAIL.  MOVE "FAIL*" TO P-OR-F.  ADD 1 TO ERROR-COUNTER.
026900 DE-LETE.  MOVE "*****" TO P-OR-F.  ADD 1 TO DELETE-CNT.
027000     MOVE "****TEST DELETED****" TO RE-MARK.
027100 PRINT-DETAIL.
027200     IF REC-CT NOT EQUAL TO ZERO
027300             MOVE "." TO PARDOT-X
027400             MOVE REC-CT TO DOTVALUE.
027500     MOVE     TEST-RESULTS TO PRINT-REC. PERFORM WRITE-LINE.
027600     IF P-OR-F EQUAL TO "FAIL*"  PERFORM WRITE-LINE
027700        PERFORM FAIL-ROUTINE THRU FAIL-ROUTINE-EX
027800          ELSE PERFORM BAIL-OUT THRU BAIL-OUT-EX.
027900     MOVE SPACE TO P-OR-F. MOVE SPACE TO COMPUTED-X.
028000     MOVE SPACE TO CORRECT-X.
028100     IF     REC-CT EQUAL TO ZERO  MOVE SPACE TO PAR-NAME.
028200     MOVE     SPACE TO RE-MARK.
028300 HEAD-ROUTINE.
028400     MOVE CCVS-H-1 TO DUMMY-RECORD. PERFORM WRITE-LINE 2 TIMES.
028500     MOVE CCVS-H-2 TO DUMMY-RECORD. PERFORM WRITE-LINE 5 TIMES.
028600     MOVE CCVS-H-3 TO DUMMY-RECORD. PERFORM WRITE-LINE 3 TIMES.
028700 COLUMN-NAMES-ROUTINE.
028800     MOVE CCVS-C-1 TO DUMMY-RECORD. PERFORM WRITE-LINE.
028900     MOVE CCVS-C-2 TO DUMMY-RECORD. PERFORM WRITE-LINE 2 TIMES.
029000     MOVE HYPHEN-LINE TO DUMMY-RECORD. PERFORM WRITE-LINE.
029100 END-ROUTINE.
029200     MOVE HYPHEN-LINE TO DUMMY-RECORD. PERFORM WRITE-LINE 5 TIMES.
029300 END-RTN-EXIT.
029400     MOVE CCVS-E-1 TO DUMMY-RECORD. PERFORM WRITE-LINE 2 TIMES.
029500 END-ROUTINE-1.
029600      ADD ERROR-COUNTER TO ERROR-HOLD ADD INSPECT-COUNTER TO
029700      ERROR-HOLD. ADD DELETE-CNT TO ERROR-HOLD.
029800      ADD PASS-COUNTER TO ERROR-HOLD.
029900*     IF PASS-COUNTER EQUAL TO ERROR-HOLD GO TO END-ROUTINE-12.
030000      MOVE PASS-COUNTER TO CCVS-E-4-1.
030100      MOVE ERROR-HOLD TO CCVS-E-4-2.
030200      MOVE CCVS-E-4 TO CCVS-E-2-2.
030300      MOVE CCVS-E-2 TO DUMMY-RECORD PERFORM WRITE-LINE.
030400  END-ROUTINE-12.
030500      MOVE "TEST(S) FAILED" TO ENDER-DESC.
030600     IF       ERROR-COUNTER IS EQUAL TO ZERO
030700         MOVE "NO " TO ERROR-TOTAL
030800         ELSE
030900         MOVE ERROR-COUNTER TO ERROR-TOTAL.
031000     MOVE     CCVS-E-2 TO DUMMY-RECORD.
031100     PERFORM WRITE-LINE.
031200 END-ROUTINE-13.
031300     IF DELETE-CNT IS EQUAL TO ZERO
031400         MOVE "NO " TO ERROR-TOTAL  ELSE
031500         MOVE DELETE-CNT TO ERROR-TOTAL.
031600     MOVE "TEST(S) DELETED     " TO ENDER-DESC.
031700     MOVE CCVS-E-2 TO DUMMY-RECORD. PERFORM WRITE-LINE.
031800      IF   INSPECT-COUNTER EQUAL TO ZERO
031900          MOVE "NO " TO ERROR-TOTAL
032000      ELSE MOVE INSPECT-COUNTER TO ERROR-TOTAL.
032100      MOVE "TEST(S) REQUIRE INSPECTION" TO ENDER-DESC.
032200      MOVE CCVS-E-2 TO DUMMY-RECORD. PERFORM WRITE-LINE.
032300     MOVE CCVS-E-3 TO DUMMY-RECORD. PERFORM WRITE-LINE.
032400 WRITE-LINE.
032500     ADD 1 TO RECORD-COUNT.
032600Y    IF RECORD-COUNT GREATER 50
032700Y        MOVE DUMMY-RECORD TO DUMMY-HOLD
032800Y        MOVE SPACE TO DUMMY-RECORD
032900Y        WRITE DUMMY-RECORD AFTER ADVANCING PAGE
033000Y        MOVE CCVS-C-1 TO DUMMY-RECORD PERFORM WRT-LN
033100Y        MOVE CCVS-C-2 TO DUMMY-RECORD PERFORM WRT-LN 2 TIMES
033200Y        MOVE HYPHEN-LINE TO DUMMY-RECORD PERFORM WRT-LN
033300Y        MOVE DUMMY-HOLD TO DUMMY-RECORD
033400Y        MOVE ZERO TO RECORD-COUNT.
033500     PERFORM WRT-LN.
033600 WRT-LN.
033700     WRITE    DUMMY-RECORD AFTER ADVANCING 1 LINES.
033800     MOVE SPACE TO DUMMY-RECORD.
033900 BLANK-LINE-PRINT.
034000     PERFORM WRT-LN.
034100 FAIL-ROUTINE.
034200     IF COMPUTED-X NOT EQUAL TO SPACE GO TO FAIL-ROUTINE-WRITE.
034300     IF CORRECT-X NOT EQUAL TO SPACE GO TO FAIL-ROUTINE-WRITE.
034400     MOVE "NO FURTHER INFORMATION, SEE PROGRAM." TO INFO-TEXT.
034500     MOVE XXINFO TO DUMMY-RECORD. PERFORM WRITE-LINE 2 TIMES.
034600     GO TO FAIL-ROUTINE-EX.
034700 FAIL-ROUTINE-WRITE.
034800     MOVE TEST-COMPUTED TO PRINT-REC PERFORM WRITE-LINE
034900     MOVE TEST-CORRECT TO PRINT-REC PERFORM WRITE-LINE 2 TIMES.
035000 FAIL-ROUTINE-EX. EXIT.
035100 BAIL-OUT.
035200     IF COMPUTED-A NOT EQUAL TO SPACE GO TO BAIL-OUT-WRITE.
035300     IF CORRECT-A EQUAL TO SPACE GO TO BAIL-OUT-EX.
035400 BAIL-OUT-WRITE.
035500     MOVE CORRECT-A TO XXCORRECT. MOVE COMPUTED-A TO XXCOMPUTED.
035600     MOVE XXINFO TO DUMMY-RECORD. PERFORM WRITE-LINE 2 TIMES.
035700 BAIL-OUT-EX. EXIT.
035800 CCVS1-EXIT.
035900     EXIT.
036000 SECT-SQ201M-0001 SECTION.
036100 WRT-TEST-001.
036200*             THIS TEST CHECKS THE LINAGE-COUNTER UPON COMPLETION
036300*             OF AN OPEN COMMAND.  IT SHOULD BE EQUAL TO 1.
036400     CLOSE PRINT-FILE.
036500     OPEN OUTPUT PRINT-FILE.
036600     IF LINAGE-COUNTER EQUAL TO 1
036700              PERFORM PASS
036800              GO TO WRT-WRITE-001.
036900     GO TO WRT-FAIL-001.
037000 WRT-DELETE-001.
037100     PERFORM DE-LETE.
037200     GO TO WRT-WRITE-001.
037300 WRT-FAIL-001.
037400     MOVE "VII-5 1.3.8; VII-29 D." TO RE-MARK.
037500     PERFORM FAIL.
037600     MOVE LINAGE-COUNTER TO COMPUTED-18V0.
037700     MOVE 1 TO CORRECT-18V0.
037800 WRT-WRITE-001.
037900     MOVE "LINAGE-CT AFTER OPEN" TO FEATURE.
038000     MOVE "WRT-TEST-01" TO PAR-NAME.
038100     MOVE "FILE IS CLOSED, THEN OPENED" TO RE-MARK.
038200     PERFORM PRINT-DETAIL.
038300     MOVE SPACE TO DUMMY-RECORD.
038400     WRITE PRINT-REC AFTER ADVANCING 4 LINES.
038500 WRT-INIT-GF-001.
038600     PERFORM HEAD-ROUTINE THRU COLUMN-NAMES-ROUTINE.
038700     MOVE "TEST WRT-TEST-01 MUST BE PRINTED BEFORE THE HEADER OF T
038800-    "HIS LIST"  TO PRINT-REC.
038900     WRITE PRINT-REC AFTER ADVANCING 4 LINES.
039000     MOVE "THIS PROGRAM TESTS THE STATEMENT:" TO PRINT-REC.
039100     WRITE PRINT-REC AFTER ADVANCING 4 LINES.
039200     MOVE "   WRITE ...   ADVANCING ... " TO PRINT-REC.
039300     WRITE PRINT-REC AFTER ADVANCING 2 LINE.
039400     MOVE "THE RULES ARE DESCRIBED ON PAGE VII-52 THROUGH VII-56."
039500          TO PRINT-REC.
039600     WRITE PRINT-REC AFTER ADVANCING 2 LINE.
039700     MOVE "THE LOGICAL PAGE SIZE IS EQUAL TO       66"
039800          TO PRINT-REC.
039900     WRITE PRINT-REC AFTER ADVANCING 4 LINE.
040000 WRT-TEST-002.
040100*             THIS TEST CHECKS THE LINAGE-COUNTER UPON COMPLETION
040200*             OF A WRITE ADVANCING PAGE OPERATION.
040300*             IT SHOULD BE EQUAL TO 1.
040400     MOVE SPACE TO DUMMY-RECORD.
040500     WRITE DUMMY-RECORD AFTER ADVANCING PAGE.
040600     IF LINAGE-COUNTER EQUAL TO 1
040700              PERFORM PASS
040800              GO TO WRT-WRITE-002.
040900     GO TO WRT-FAIL-002.
041000 WRT-DELETE-002.
041100     PERFORM DE-LETE.
041200     GO TO WRT-WRITE-002.
041300 WRT-FAIL-002.
041400     MOVE "VII-5 1.3.8; VII-28 C (1)" TO RE-MARK.
041500     PERFORM FAIL.
041600     MOVE LINAGE-COUNTER TO COMPUTED-18V0.
041700     MOVE 1 TO CORRECT-18V0.
041800 WRT-WRITE-002.
041900     MOVE "L-C AFTER WRITE PAGE" TO FEATURE.
042000     MOVE "WRT-TEST-02" TO PAR-NAME.
042100     PERFORM PRINT-DETAIL.
042200 WRT-TEST-003.
042300*             THIS TEST CHECKS THE LINAGE-COUNTER UPON COMPLETION
042400*             OF A WRITE AFTER ADVANCING 1 LINE OPERATION ON WHICH
042500*             LOGICAL PAGE OVERFLOW OCCURS.  IT SHOULD EQUAL 1.
042600     MOVE SPACE TO DUMMY-RECORD.
042700     WRITE DUMMY-RECORD AFTER ADVANCING PAGE.
042800     PERFORM BLANK-LINE-PRINT 50 TIMES.
042900     IF LINAGE-COUNTER EQUAL TO 1
043000              PERFORM PASS
043100              GO TO WRT-WRITE-003.
043200     GO TO WRT-FAIL-003.
043300 WRT-DELETE-003.
043400     PERFORM DE-LETE.
043500     GO TO WRT-WRITE-003.
043600 WRT-FAIL-003.
043700     MOVE "VII-5 1.3.8; VII-29 C  4)" TO RE-MARK.
043800     PERFORM FAIL.
043900     MOVE LINAGE-COUNTER TO COMPUTED-18V0.
044000     MOVE 1 TO CORRECT-18V0.
044100 WRT-WRITE-003.
044200     MOVE "L-C AFT PAGE OVERFLW" TO FEATURE.
044300     MOVE "WRT-TEST-03" TO PAR-NAME.
044400     PERFORM PRINT-DETAIL.
044500 WRT-TEST-004.
044600*             THIS TEST CHECKS THE LINAGE-COUNTER UPON COMPLETION
044700*             OF A WRITE OPERATION WITHOUT AN ADVANCING PHRASE.
044800*             IT SHOULD BE INCREMENTED BY 1.
044900     MOVE LINAGE-COUNTER TO LC-HOLD.
045000     ADD 1 TO LC-HOLD.
045100     MOVE SPACE TO DUMMY-RECORD.
045200     WRITE DUMMY-RECORD.
045300     IF LC-HOLD EQUAL TO LINAGE-COUNTER
045400              PERFORM PASS
045500              GO TO WRT-WRITE-004.
045600     GO TO WRT-FAIL-004.
045700 WRT-DELETE-004.
045800     PERFORM DE-LETE.
045900     GO TO WRT-WRITE-004.
046000 WRT-FAIL-004.
046100     MOVE "VII-5 1.3.8; VII-29 C  3)" TO RE-MARK.
046200     PERFORM FAIL.
046300     MOVE LINAGE-COUNTER TO COMPUTED-18V0.
046400     MOVE LC-HOLD TO CORRECT-18V0.
046500 WRT-WRITE-004.
046600     MOVE "L-C AFT WRT W/O ADV" TO FEATURE.
046700     MOVE "WRT-TEST-04" TO PAR-NAME.
046800     PERFORM PRINT-DETAIL.
046900 WRT-TEST-005.
047000*             THIS TEST CHECKS THE LINAGE-COUNTER UPON COMPLETION
047100*             OF A WRITE ADVANCING INTEGER LINE OPERATION.
047200     MOVE LINAGE-COUNTER TO LC-HOLD.
047300     ADD 5 TO LC-HOLD.
047400     MOVE SPACE TO DUMMY-RECORD.
047500     WRITE DUMMY-RECORD BEFORE ADVANCING 5 LINE.
047600     IF LINAGE-COUNTER EQUAL TO LC-HOLD
047700              PERFORM PASS
047800              GO TO WRT-WRITE-005.
047900     GO TO WRT-FAIL-005.
048000 WRT-DELETE-005.
048100     PERFORM DE-LETE.
048200     GO TO WRT-WRITE-005.
048300 WRT-FAIL-005.
048400     MOVE "VII-5 1.3.8; VII-28 (9) C  1) INTEGER" TO RE-MARK.
048500     PERFORM FAIL.
048600     MOVE LINAGE-COUNTER TO COMPUTED-18V0.
048700     MOVE LC-HOLD TO CORRECT-18V0.
048800 WRT-WRITE-005.
048900     MOVE "L-C AFT WRT ADV INT" TO FEATURE.
049000     MOVE "WRT-TEST-05" TO PAR-NAME.
049100     PERFORM PRINT-DETAIL.
049200 WRT-TEST-006.
049300*             THIS TEST CHECKS THE LINAGE-COUNTER UPON COMPLETION
049400*             OF A WRITE IDENTIFIER-2 LINES OPERATION.
049500     MOVE 4 TO IDENTIFIER-2.
049600     MOVE LINAGE-COUNTER TO LC-HOLD.
049700     ADD 4 TO LC-HOLD.
049800     MOVE SPACE TO DUMMY-RECORD.
049900     WRITE DUMMY-RECORD BEFORE ADVANCING IDENTIFIER-2 LINES.
050000     IF LINAGE-COUNTER EQUAL TO LC-HOLD
050100              PERFORM PASS
050200              GO TO WRT-WRITE-006.
050300     GO TO WRT-FAIL-006.
050400 WRT-DELETE-006.
050500     PERFORM DE-LETE.
050600     GO TO WRT-WRITE-006.
050700 WRT-FAIL-006.
050800     MOVE "VII-5 1.3.8; VII-29 (9) C  2) IDENTIFIER-2" TO RE-MARK.
050900     PERFORM FAIL.
051000     MOVE LINAGE-COUNTER TO COMPUTED-18V0.
051100     MOVE LC-HOLD TO CORRECT-18V0.
051200 WRT-WRITE-006.
051300     MOVE "L-C AFT WRT ADV ID-2" TO FEATURE.
051400     MOVE "WRT-TEST-06" TO PAR-NAME.
051500     PERFORM PRINT-DETAIL.
051600 WRT-INIT-007.
051700     PERFORM BLANK-LINE-PRINT 10 TIMES.
051800     MOVE "THE FOLLOWING SQ201M TESTS CANNOT BE TESTED USING THE N
051900-    "ORMAL PASS/FAIL METHODS.  A VISUAL CHECK WILL HAVE TO TO BE
052000-    "MADE" TO PRINT-REC.
052100     WRITE PRINT-REC AFTER ADVANCING 1 LINE.
052200     MOVE "TO DETERMINE THE ACCURACY OF EACH TEST" TO PRINT-REC.
052300     WRITE PRINT-REC AFTER ADVANCING 1 LINE.
052400     MOVE SPACE TO DUMMY-RECORD.
052500     PERFORM BLANK-LINE-PRINT 10 TIMES.
052600 WRT-TEST-007.
052700*             THIS IS A TEST FOR WRITE AFTER ADVANCING PAGE.
052800*             THE RECORD SHOULD PRINT ON THE FIRST LINE OF THE
052900*             NEXT LOGICAL PAGE.
053000     MOVE "WRT AFT ADV PAGE" TO FEATURE.
053100     MOVE "WRT-TEST-07" TO PAR-NAME.
053200     PERFORM PRINT-DETAIL.
053300     MOVE TOP-LINE TO PRINT-REC.
053400     WRITE PRINT-REC AFTER ADVANCING PAGE.
053500     MOVE SPACE TO DUMMY-RECORD.
053600 WRT-TEST-008.
053700******************************************************************
053800*                                                                *
053900*             THIS IS A TEST FOR WRITE ... NOT AT END-OF-PAGE... *
054000*                                          ---                   *
054100******************************************************************
054200     MOVE 0 TO WRITE-SWITCH.
054300     PERFORM WRITE-EOP-MESSAGE.
054400     MOVE SPACE TO DUMMY-RECORD.
054500     PERFORM BLANK-LINE-PRINT UNTIL LINAGE-COUNTER EQUAL 1.
054600     MOVE 1 TO DETAIL-LINE-NO.
054700     PERFORM EOP-TEST-1 132 TIMES.
054800 VAR-TEST-008.
054900     IF WRITE-SWITCH NOT = 1
055000         GO TO VAR-FAIL-008.
055100 VAR-PASS-008.
055200     PERFORM PASS.
055300     GO TO VAR-WRITE-008.
055400 VAR-FAIL-008.
055500     MOVE "<NOT AT END-OF-PAGE> NOT ENCOUNTERED; VII-53 GR (9)"
055600                TO RE-MARK.
055700     PERFORM FAIL.
055800 VAR-WRITE-008.
055900     MOVE "NOT END-OF-PAGE" TO FEATURE.
056000     MOVE "VAR-TEST-008" TO PAR-NAME.
056100     PERFORM PRINT-DETAIL.
056200     MOVE SPACE TO DUMMY-RECORD.
056300     PERFORM BLANK-LINE-PRINT 5 TIMES.
056400 WRT-TEST-009.
056500*             THIS IS A TEST FOR WRITE ...; AT EOP ...
056600******************************************************************
056700*                                                                *
056800*             THIS IS A TEST FOR WRITE ... NOT AT EOP        ... *
056900*                                          ---                   *
057000******************************************************************
057100     MOVE 0 TO WRITE-SWITCH.
057200     PERFORM WRITE-EOP-MESSAGE.
057300     MOVE SPACE TO DUMMY-RECORD.
057400     PERFORM BLANK-LINE-PRINT UNTIL LINAGE-COUNTER EQUAL 1.
057500     MOVE 1 TO DETAIL-LINE-NO.
057600     PERFORM EOP-TEST-2 132 TIMES.
057700 VAR-TEST-009.
057800     IF WRITE-SWITCH NOT = 1
057900         GO TO VAR-FAIL-009.
058000 VAR-PASS-009.
058100     PERFORM PASS.
058200     GO TO VAR-WRITE-009.
058300 VAR-FAIL-009.
058400     MOVE "<NOT AT EOP> NOT ENCOUNTERED; VII-53 GR (9)" TO RE-MARK
058500     PERFORM FAIL.
058600 VAR-WRITE-009.
058700     MOVE "NOT AT EOP"         TO FEATURE.
058800     MOVE "VAR-TEST-009" TO PAR-NAME.
058900     PERFORM PRINT-DETAIL.
059000     MOVE 0 TO WRITE-SWITCH.
059100     MOVE SPACE TO DUMMY-RECORD.
059200     PERFORM BLANK-LINE-PRINT 5 TIMES.
059300 WRT-TEST-010.
059400*             THIS IS A TEST FOR WRITE... ; END-OF-PAGE ...
059500******************************************************************
059600*                                                                *
059700*             THIS IS A TEST FOR WRITE ... NOT END-OF-PAGE   ... *
059800*                                          ---                   *
059900*         AND    END-WRITE                                       *
060000******************************************************************
060100     MOVE 0 TO WRITE-SWITCH.
060200     MOVE 0 TO END-WRITE-SWITCH.
060300     PERFORM WRITE-EOP-MESSAGE.
060400     MOVE SPACE TO DUMMY-RECORD.
060500     PERFORM BLANK-LINE-PRINT UNTIL LINAGE-COUNTER EQUAL 1.
060600     MOVE 1 TO DETAIL-LINE-NO.
060700     PERFORM EOP-TEST-3 132 TIMES.
060800 VAR-TEST-010.
060900     IF WRITE-SWITCH NOT = 1
061000         GO TO VAR-FAIL-010.
061100 VAR-PASS-010.
061200     PERFORM PASS.
061300     GO TO VAR-WRITE-010.
061400 VAR-FAIL-010.
061500     MOVE "<NOT END-OF-PAGE> NOT ENCOUNTERED; VII-53 GR (9)"
061600          TO RE-MARK.
061700     PERFORM FAIL.
061800 VAR-WRITE-010.
061900     MOVE "NOT END-OF-PAGE" TO FEATURE.
062000     MOVE "VAR-TEST-010" TO PAR-NAME.
062100     PERFORM PRINT-DETAIL.
062200     MOVE 0 TO WRITE-SWITCH.
062300     MOVE SPACE TO DUMMY-RECORD.
062400     PERFORM BLANK-LINE-PRINT 5 TIMES.
062500 VAR-TEST-010-1.
062600     IF END-WRITE-SWITCH NOT EQUAL TO 1
062700         GO TO VAR-FAIL-010-1.
062800 VAR-PASS-010-1.
062900     PERFORM PASS.
063000     GO TO VAR-WRITE-010-1.
063100 VAR-FAIL-010-1.
063200     MOVE "<END-WRITE> NOT CORRECT; IV-27 4.4.4" TO RE-MARK.
063300     PERFORM FAIL.
063400 VAR-WRITE-010-1.
063500     MOVE "END-WRITE;NOT END-OF" TO FEATURE.
063600     MOVE "VAR-TEST-010-1" TO PAR-NAME.
063700     PERFORM PRINT-DETAIL.
063800     MOVE SPACE TO DUMMY-RECORD.
063900     PERFORM BLANK-LINE-PRINT 5 TIMES.
064000 WRT-TEST-011.
064100*             THIS IS A TEST FOR WRITE ...EOP...
064200******************************************************************
064300*                                                                *
064400*             THIS IS A TEST FOR WRITE ... NOT EOP           ... *
064500*                                          ---                   *
064600******************************************************************
064700     MOVE 0 TO WRITE-SWITCH.
064800     MOVE 0 TO END-WRITE-SWITCH.
064900*    MOVE "EOP" TO FEATURE.
065000*    MOVE "WRT-TEST-11" TO PAR-NAME.
065100*    PERFORM PRINT-DETAIL.
065200     PERFORM WRITE-EOP-MESSAGE.
065300     MOVE SPACE TO DUMMY-RECORD.
065400     PERFORM BLANK-LINE-PRINT UNTIL LINAGE-COUNTER EQUAL 1.
065500     MOVE 1 TO DETAIL-LINE-NO.
065600     PERFORM EOP-TEST-4 132 TIMES.
065700 VAR-TEST-011.
065800     IF WRITE-SWITCH NOT = 1
065900         GO TO VAR-FAIL-011.
066000 VAR-PASS-011.
066100     PERFORM PASS.
066200     GO TO VAR-WRITE-011.
066300 VAR-FAIL-011.
066400     MOVE "<NOT EOP> NOT ENCOUNTERED; VII-53 GR (9)" TO RE-MARK.
066500     PERFORM FAIL.
066600 VAR-WRITE-011.
066700     MOVE "NOT EOP" TO FEATURE.
066800     MOVE "VAR-TEST-011" TO PAR-NAME.
066900     PERFORM PRINT-DETAIL.
067000     MOVE 0 TO WRITE-SWITCH.
067100     MOVE SPACE TO DUMMY-RECORD.
067200     PERFORM BLANK-LINE-PRINT 5 TIMES.
067300 VAR-TEST-011-1.
067400     IF END-WRITE-SWITCH NOT EQUAL TO 1
067500         GO TO VAR-FAIL-011-1.
067600 VAR-PASS-011-1.
067700     PERFORM PASS.
067800     GO TO VAR-WRITE-011-1.
067900 VAR-FAIL-011-1.
068000     MOVE "<END-WRITE> NOT CORRECT; IV-27 4.4.4"   TO RE-MARK.
068100     PERFORM FAIL.
068200 VAR-WRITE-011-1.
068300     MOVE "END-WRITE; NOT EOP" TO FEATURE.
068400     MOVE "VAR-TEST-011-1" TO PAR-NAME.
068500     PERFORM PRINT-DETAIL.
068600     MOVE 0 TO WRITE-SWITCH.
068700     MOVE 0 TO END-WRITE-SWITCH.
068800     MOVE SPACE TO DUMMY-RECORD.
068900     PERFORM BLANK-LINE-PRINT 5 TIMES.
069000 SQ201M-END-ROUTINE.
069100     MOVE "END OF SQ201M VALIDATION TESTS" TO PRINT-REC.
069200     WRITE PRINT-REC AFTER ADVANCING 1 LINE.
069300     GO TO CCVS-EXIT.
069400 EOP-TEST-1.
069500     MOVE LINAGE-COUNTER TO DETAIL-LC.
069600     MOVE DETAIL-LINE TO PRINT-REC.
069700     WRITE PRINT-REC BEFORE ADVANCING 1 LINE AT END-OF-PAGE
069800              MOVE 1 TO FOOT-COUNT
069900              MOVE 45 TO FOOT-LINE-NO
070000              PERFORM PRINT-FOOTING 6 TIMES
070100******************************************************************
070200*                                                                *
070300*         NEW: NOT AT END-OF-PAGE                                *
070400*              ---                                               *
070500******************************************************************
070600        NOT AT END-OF-PAGE
070700             MOVE 1 TO WRITE-SWITCH.
070800     ADD 1 TO DETAIL-LINE-NO.
070900 EOP-TEST-2.
071000     MOVE LINAGE-COUNTER TO DETAIL-LC.
071100     MOVE DETAIL-LINE TO PRINT-REC.
071200     WRITE PRINT-REC BEFORE ADVANCING 1 LINE ; AT EOP
071300              MOVE 1 TO FOOT-COUNT
071400              MOVE 45 TO FOOT-LINE-NO
071500              PERFORM PRINT-FOOTING 6 TIMES
071600******************************************************************
071700*                                                                *
071800*         NEW: NOT AT EOP                                        *
071900*                                                                *
072000******************************************************************
072100        NOT AT EOP
072200             MOVE 1 TO WRITE-SWITCH.
072300     ADD 1 TO DETAIL-LINE-NO.
072400 EOP-TEST-3.
072500     MOVE LINAGE-COUNTER TO DETAIL-LC.
072600     MOVE DETAIL-LINE TO PRINT-REC.
072700     IF END-WRITE-SWITCH EQUAL TO 1 OR END-WRITE-SWITCH EQUAL TO 0
072800         WRITE PRINT-REC BEFORE ADVANCING 1 LINE ; END-OF-PAGE
072900              MOVE 1 TO FOOT-COUNT
073000              MOVE 45 TO FOOT-LINE-NO
073100              PERFORM PRINT-FOOTING 6 TIMES
073200******************************************************************
073300*                                                                *
073400*         NEW: NOT END-OF-PAGE                                   *
073500*              ---                                               *
073600******************************************************************
073700           NOT END-OF-PAGE
073800             MOVE 1 TO WRITE-SWITCH
073900          END-WRITE
074000        MOVE 1 TO END-WRITE-SWITCH.
074100     ADD 1 TO DETAIL-LINE-NO.
074200 EOP-TEST-4.
074300     MOVE LINAGE-COUNTER TO DETAIL-LC.
074400     MOVE DETAIL-LINE TO PRINT-REC.
074500     IF END-WRITE-SWITCH EQUAL TO 1 OR END-WRITE-SWITCH EQUAL TO 0
074600         WRITE PRINT-REC BEFORE ADVANCING 1 LINE EOP
074700              MOVE 1 TO FOOT-COUNT
074800              MOVE 45 TO FOOT-LINE-NO
074900              PERFORM PRINT-FOOTING 6 TIMES
075000******************************************************************
075100*                                                                *
075200*         NEW: NOT EOP                                           *
075300*              ---                                               *
075400******************************************************************
075500            NOT EOP
075600             MOVE 1 TO WRITE-SWITCH
075700          END-WRITE
075800        MOVE 1 TO END-WRITE-SWITCH.
075900     ADD 1 TO DETAIL-LINE-NO.
076000 PRINT-FOOTING.
076100     MOVE LINAGE-COUNTER TO FOOT-LC.
076200     MOVE FOOT-LINE TO PRINT-REC.
076300     WRITE PRINT-REC BEFORE ADVANCING 1 LINE.
076400     ADD 1 TO FOOT-COUNT.
076500     ADD 1 TO FOOT-LINE-NO.
076600 WRITE-EOP-MESSAGE.
076700     PERFORM BLANK-LINE-PRINT 5 TIMES.
076800     MOVE EOP-MESSAGE-1 TO PRINT-REC.
076900     WRITE PRINT-REC BEFORE ADVANCING 1 LINE.
077000     MOVE EOP-MESSAGE-2 TO PRINT-REC.
077100     WRITE PRINT-REC BEFORE ADVANCING 1 LINE.
077200     MOVE EOP-MESSAGE-3 TO PRINT-REC.
077300     WRITE PRINT-REC BEFORE ADVANCING 1 LINE.
077400     MOVE SPACE TO DUMMY-RECORD.
077500     PERFORM BLANK-LINE-PRINT 5 TIMES.
077600 CCVS-EXIT SECTION.
077700 CCVS-999999.
077800     GO TO CLOSE-FILES.
