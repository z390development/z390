000100 IDENTIFICATION DIVISION.
000200 PROGRAM-ID.
000300     RL104A.
000400****************************************************************
000500*                                                              *
000600*    VALIDATION FOR:-                                          *
000700*                                                              *
000800*    "ON-SITE VALIDATION, NATIONAL INSTITUTE OF STD & TECH.     ".
000900*                                                              *
001000*    "COBOL 85 VERSION 4.2, Apr  1993 SSVG                      ".
001100*                                                              *
001200****************************************************************
001300*                                                              *
001400*      X-CARDS USED BY THIS PROGRAM ARE :-                     *
001500*                                                              *
001600*        X-55  - SYSTEM PRINTER NAME.                          *
001700*        X-82  - SOURCE COMPUTER NAME.                         *
001800*        X-83  - OBJECT COMPUTER NAME.                         *
001900*                                                              *
002000****************************************************************
002100*            THIS PROGRAM TESTS THE SYNTACTICAL CONSTRUCTS AND
002200*            SEMANTIC ACTIONS ASSOCIATED WITH THE FOLLOWING
002300*            ELEMENTS:
002400*
002500*            (1)  FILE STATUS
002600*            (2)  USE AFTER EXCEPTION PROCEDURE ON FILE-NAME
002700*            (3)  READ
002800*            (4)  WRITE
002900*            (5)  REWRITE
003000*            (6)  RELATIVE KEY
003100*            (7)  ACCESS MODE
003200*
003300*            THIS PROGRAM CREATES A RELATIVE I-O FILE SEQUENTIALLY
003400*            (ACCESS MODE SEQUENTIAL) AND THEN UPDATES SELECTIVE
003500*            RECORDS OF THE FILE.  THE FILE STATUS CONTENTS ARE
003600*            CAPTURED AND TESTED FOR ACCURACY FOR EACH OPEN, CLOSE
003700*            READ AND REWRITE STATEMENT USED.  THE READ, WRITE AND
003800*            REWRITE STATEMENTS ARE USED WITHOUT THE APPROPRIATE
003900*            AT END ON INVALID KEY PHRASES.  THE OMISSION OF THESE
004000*            PHRASES ARE PERMITTED IF AN APPLICABLE USE PROCEDURE
004100*            HAS BEEN SPECIFIED.
004200*
004300****************************************************************
004400 ENVIRONMENT DIVISION.
004500 CONFIGURATION SECTION.
004600 SOURCE-COMPUTER.
004700     XXXXX082.
004800 OBJECT-COMPUTER.
004900     XXXXX083.
005000 INPUT-OUTPUT SECTION.
005100 FILE-CONTROL.
005200     SELECT PRINT-FILE ASSIGN TO
005300     XXXXX055.
005400     SELECT   RL-FS2 ASSIGN
005500     XXXXX022
005600              ORGANIZATION RELATIVE
005700              ACCESS SEQUENTIAL
005800              RELATIVE  RL-FS2-KEY
005900         FILE STATUS IS RL-FS2-STATUS.
006000 DATA DIVISION.
006100 FILE SECTION.
006200 FD  PRINT-FILE.
006300 01  PRINT-REC PICTURE X(120).
006400 01  DUMMY-RECORD PICTURE X(120).
006500 FD  RL-FS2
006600C    VALUE OF
006700C    XXXXX074
006800C    IS
006900C    XXXXX076
007000G    XXXXX069
007100     LABEL RECORDS ARE STANDARD
007200     BLOCK CONTAINS 1 RECORDS
007300     DATA RECORD RL-FS2R1-F-G-240.
007400 01  RL-FS2R1-F-G-240.
007500     05 RL-FS2-WRK-120 PIC X(120).
007600     05 RL-FS2-GRP-120.
007700        10 RL-FS2-WRK-XN-0001-O120F
007800                        PICTURE X OCCURS 120 TIMES.
007900 WORKING-STORAGE SECTION.
008000 01  GRP-0001.
008100     05 RL-FS2-KEY   PIC 9(8)  VALUE ZERO.
008200     05 WRK-CS-09V00-012  PIC S9(9) USAGE COMP  VALUE ZERO.
008300     05 WRK-CS-09V00-013  PIC S9(9) USAGE COMP  VALUE ZERO.
008400     05 WRK-CS-09V00-014  PIC S9(9) USAGE COMP  VALUE ZERO.
008500     05 WRK-CS-09V00-015  PIC S9(9) USAGE COMP  VALUE ZERO.
008600     05 WRK-CS-09V00-016  PIC S9(9) USAGE COMP  VALUE ZERO.
008700     05 WRK-CS-09V00-017  PIC S9(9) USAGE COMP  VALUE ZERO.
008800     05 WRK-CS-09V00-018  PIC S9(9) USAGE COMP  VALUE ZERO.
008900     05 RL-FS2-STATUS     PIC  XX  VALUE  SPACE.
009000     05 WRK-XN-0002-001   PIC  X(2) VALUE  SPACE.
009100     05 WRK-XN-0002-002   PIC  X(2) VALUE  SPACE.
009200     05 WRK-XN-0002-003   PIC  X(2) VALUE  SPACE.
009300     05 WRK-XN-0002-004   PIC  X(2) VALUE  SPACE.
009400     05 WRK-XN-0002-005   PIC  X(2) VALUE  SPACE.
009500     05 WRK-XN-0002-006   PIC  X(2) VALUE  SPACE.
009600     05 WRK-XN-0002-007   PIC  X(2) VALUE  SPACE.
009700     05 WRK-XN-0002-008   PIC  X(2) VALUE  SPACE.
009800     05 WRK-XN-0002-009   PIC  X(2) VALUE  SPACE.
009900 01  FILE-RECORD-INFORMATION-REC.
010000     03 FILE-RECORD-INFO-SKELETON.
010100        05 FILLER                 PICTURE X(48)       VALUE
010200             "FILE=      ,RECORD=      /0,RECNO=000000,UPDT=00".
010300        05 FILLER                 PICTURE X(46)       VALUE
010400             ",ODO=0000,PGM=     ,LRECL=000000,BLKSIZ  =0000".
010500        05 FILLER                 PICTURE X(26)       VALUE
010600             ",LFIL=000000,ORG=  ,LBLR= ".
010700        05 FILLER                 PICTURE X(37)       VALUE
010800             ",RECKEY=                             ".
010900        05 FILLER                 PICTURE X(38)       VALUE
011000             ",ALTKEY1=                             ".
011100        05 FILLER                 PICTURE X(38)       VALUE
011200             ",ALTKEY2=                             ".
011300        05 FILLER                 PICTURE X(7)        VALUE SPACE.
011400     03 FILE-RECORD-INFO          OCCURS  10  TIMES.
011500        05 FILE-RECORD-INFO-P1-120.
011600           07 FILLER              PIC X(5).
011700           07 XFILE-NAME           PIC X(6).
011800           07 FILLER              PIC X(8).
011900           07 XRECORD-NAME         PIC X(6).
012000           07 FILLER              PIC X(1).
012100           07 REELUNIT-NUMBER     PIC 9(1).
012200           07 FILLER              PIC X(7).
012300           07 XRECORD-NUMBER       PIC 9(6).
012400           07 FILLER              PIC X(6).
012500           07 UPDATE-NUMBER       PIC 9(2).
012600           07 FILLER              PIC X(5).
012700           07 ODO-NUMBER          PIC 9(4).
012800           07 FILLER              PIC X(5).
012900           07 XPROGRAM-NAME        PIC X(5).
013000           07 FILLER              PIC X(7).
013100           07 XRECORD-LENGTH       PIC 9(6).
013200           07 FILLER              PIC X(7).
013300           07 CHARS-OR-RECORDS    PIC X(2).
013400           07 FILLER              PIC X(1).
013500           07 XBLOCK-SIZE          PIC 9(4).
013600           07 FILLER              PIC X(6).
013700           07 RECORDS-IN-FILE     PIC 9(6).
013800           07 FILLER              PIC X(5).
013900           07 XFILE-ORGANIZATION   PIC X(2).
014000           07 FILLER              PIC X(6).
014100           07 XLABEL-TYPE          PIC X(1).
014200        05 FILE-RECORD-INFO-P121-240.
014300           07 FILLER              PIC X(8).
014400           07 XRECORD-KEY          PIC X(29).
014500           07 FILLER              PIC X(9).
014600           07 ALTERNATE-KEY1      PIC X(29).
014700           07 FILLER              PIC X(9).
014800           07 ALTERNATE-KEY2      PIC X(29).
014900           07 FILLER              PIC X(7).
015000 01  TEST-RESULTS.
015100     02 FILLER                   PIC X      VALUE SPACE.
015200     02 FEATURE                  PIC X(20)  VALUE SPACE.
015300     02 FILLER                   PIC X      VALUE SPACE.
015400     02 P-OR-F                   PIC X(5)   VALUE SPACE.
015500     02 FILLER                   PIC X      VALUE SPACE.
015600     02  PAR-NAME.
015700       03 FILLER                 PIC X(19)  VALUE SPACE.
015800       03  PARDOT-X              PIC X      VALUE SPACE.
015900       03 DOTVALUE               PIC 99     VALUE ZERO.
016000     02 FILLER                   PIC X(8)   VALUE SPACE.
016100     02 RE-MARK                  PIC X(61).
016200 01  TEST-COMPUTED.
016300     02 FILLER                   PIC X(30)  VALUE SPACE.
016400     02 FILLER                   PIC X(17)  VALUE
016500            "       COMPUTED=".
016600     02 COMPUTED-X.
016700     03 COMPUTED-A               PIC X(20)  VALUE SPACE.
016800     03 COMPUTED-N               REDEFINES COMPUTED-A
016900                                 PIC -9(9).9(9).
017000     03 COMPUTED-0V18 REDEFINES COMPUTED-A   PIC -.9(18).
017100     03 COMPUTED-4V14 REDEFINES COMPUTED-A   PIC -9(4).9(14).
017200     03 COMPUTED-14V4 REDEFINES COMPUTED-A   PIC -9(14).9(4).
017300     03       CM-18V0 REDEFINES COMPUTED-A.
017400         04 COMPUTED-18V0                    PIC -9(18).
017500         04 FILLER                           PIC X.
017600     03 FILLER PIC X(50) VALUE SPACE.
017700 01  TEST-CORRECT.
017800     02 FILLER PIC X(30) VALUE SPACE.
017900     02 FILLER PIC X(17) VALUE "       CORRECT =".
018000     02 CORRECT-X.
018100     03 CORRECT-A                  PIC X(20) VALUE SPACE.
018200     03 CORRECT-N    REDEFINES CORRECT-A     PIC -9(9).9(9).
018300     03 CORRECT-0V18 REDEFINES CORRECT-A     PIC -.9(18).
018400     03 CORRECT-4V14 REDEFINES CORRECT-A     PIC -9(4).9(14).
018500     03 CORRECT-14V4 REDEFINES CORRECT-A     PIC -9(14).9(4).
018600     03      CR-18V0 REDEFINES CORRECT-A.
018700         04 CORRECT-18V0                     PIC -9(18).
018800         04 FILLER                           PIC X.
018900     03 FILLER PIC X(2) VALUE SPACE.
019000     03 COR-ANSI-REFERENCE             PIC X(48) VALUE SPACE.
019100 01  CCVS-C-1.
019200     02 FILLER  PIC IS X(99)    VALUE IS " FEATURE              PA
019300-    "SS  PARAGRAPH-NAME
019400-    "       REMARKS".
019500     02 FILLER                     PIC X(20)    VALUE SPACE.
019600 01  CCVS-C-2.
019700     02 FILLER                     PIC X        VALUE SPACE.
019800     02 FILLER                     PIC X(6)     VALUE "TESTED".
019900     02 FILLER                     PIC X(15)    VALUE SPACE.
020000     02 FILLER                     PIC X(4)     VALUE "FAIL".
020100     02 FILLER                     PIC X(94)    VALUE SPACE.
020200 01  REC-SKL-SUB                   PIC 9(2)     VALUE ZERO.
020300 01  REC-CT                        PIC 99       VALUE ZERO.
020400 01  DELETE-COUNTER                PIC 999      VALUE ZERO.
020500 01  ERROR-COUNTER                 PIC 999      VALUE ZERO.
020600 01  INSPECT-COUNTER               PIC 999      VALUE ZERO.
020700 01  PASS-COUNTER                  PIC 999      VALUE ZERO.
020800 01  TOTAL-ERROR                   PIC 999      VALUE ZERO.
020900 01  ERROR-HOLD                    PIC 999      VALUE ZERO.
021000 01  DUMMY-HOLD                    PIC X(120)   VALUE SPACE.
021100 01  RECORD-COUNT                  PIC 9(5)     VALUE ZERO.
021200 01  ANSI-REFERENCE                PIC X(48)    VALUE SPACES.
021300 01  CCVS-H-1.
021400     02  FILLER                    PIC X(39)    VALUE SPACES.
021500     02  FILLER                    PIC X(42)    VALUE
021600     "OFFICIAL COBOL COMPILER VALIDATION SYSTEM".
021700     02  FILLER                    PIC X(39)    VALUE SPACES.
021800 01  CCVS-H-2A.
021900   02  FILLER                        PIC X(40)  VALUE SPACE.
022000   02  FILLER                        PIC X(7)   VALUE "CCVS85 ".
022100   02  FILLER                        PIC XXXX   VALUE
022200     "4.2 ".
022300   02  FILLER                        PIC X(28)  VALUE
022400            " COPY - NOT FOR DISTRIBUTION".
022500   02  FILLER                        PIC X(41)  VALUE SPACE.
022600
022700 01  CCVS-H-2B.
022800   02  FILLER                        PIC X(15)  VALUE
022900            "TEST RESULT OF ".
023000   02  TEST-ID                       PIC X(9).
023100   02  FILLER                        PIC X(4)   VALUE
023200            " IN ".
023300   02  FILLER                        PIC X(12)  VALUE
023400     " HIGH       ".
023500   02  FILLER                        PIC X(22)  VALUE
023600            " LEVEL VALIDATION FOR ".
023700   02  FILLER                        PIC X(58)  VALUE
023800     "ON-SITE VALIDATION, NATIONAL INSTITUTE OF STD & TECH.     ".
023900 01  CCVS-H-3.
024000     02  FILLER                      PIC X(34)  VALUE
024100            " FOR OFFICIAL USE ONLY    ".
024200     02  FILLER                      PIC X(58)  VALUE
024300     "COBOL 85 VERSION 4.2, Apr  1993 SSVG                      ".
024400     02  FILLER                      PIC X(28)  VALUE
024500            "  COPYRIGHT   1985 ".
024600 01  CCVS-E-1.
024700     02 FILLER                       PIC X(52)  VALUE SPACE.
024800     02 FILLER  PIC X(14) VALUE IS "END OF TEST-  ".
024900     02 ID-AGAIN                     PIC X(9).
025000     02 FILLER                       PIC X(45)  VALUE SPACES.
025100 01  CCVS-E-2.
025200     02  FILLER                      PIC X(31)  VALUE SPACE.
025300     02  FILLER                      PIC X(21)  VALUE SPACE.
025400     02 CCVS-E-2-2.
025500         03 ERROR-TOTAL              PIC XXX    VALUE SPACE.
025600         03 FILLER                   PIC X      VALUE SPACE.
025700         03 ENDER-DESC               PIC X(44)  VALUE
025800            "ERRORS ENCOUNTERED".
025900 01  CCVS-E-3.
026000     02  FILLER                      PIC X(22)  VALUE
026100            " FOR OFFICIAL USE ONLY".
026200     02  FILLER                      PIC X(12)  VALUE SPACE.
026300     02  FILLER                      PIC X(58)  VALUE
026400     "ON-SITE VALIDATION, NATIONAL INSTITUTE OF STD & TECH.     ".
026500     02  FILLER                      PIC X(13)  VALUE SPACE.
026600     02 FILLER                       PIC X(15)  VALUE
026700             " COPYRIGHT 1985".
026800 01  CCVS-E-4.
026900     02 CCVS-E-4-1                   PIC XXX    VALUE SPACE.
027000     02 FILLER                       PIC X(4)   VALUE " OF ".
027100     02 CCVS-E-4-2                   PIC XXX    VALUE SPACE.
027200     02 FILLER                       PIC X(40)  VALUE
027300      "  TESTS WERE EXECUTED SUCCESSFULLY".
027400 01  XXINFO.
027500     02 FILLER                       PIC X(19)  VALUE
027600            "*** INFORMATION ***".
027700     02 INFO-TEXT.
027800       04 FILLER                     PIC X(8)   VALUE SPACE.
027900       04 XXCOMPUTED                 PIC X(20).
028000       04 FILLER                     PIC X(5)   VALUE SPACE.
028100       04 XXCORRECT                  PIC X(20).
028200     02 INF-ANSI-REFERENCE           PIC X(48).
028300 01  HYPHEN-LINE.
028400     02 FILLER  PIC IS X VALUE IS SPACE.
028500     02 FILLER  PIC IS X(65)    VALUE IS "************************
028600-    "*****************************************".
028700     02 FILLER  PIC IS X(54)    VALUE IS "************************
028800-    "******************************".
028900 01  CCVS-PGM-ID                     PIC X(9)   VALUE
029000     "RL104A".
029100 PROCEDURE DIVISION.
029200 DECLARATIVES.
029300 RL-FS2-01 SECTION.
029400     USE      AFTER STANDARD EXCEPTION PROCEDURE ON RL-FS2.
029500 RL-FS2-01-01.
029600     ADD      1 TO  WRK-CS-09V00-013.
029700     GO TO    RL-FS2-01-03
029800              RL-FS2-01-05
029900              DEPENDING ON  WRK-CS-09V00-012.
030000     GO TO    RL-FS2-01-EXIT.
030100 RL-FS2-01-03.
030200*ENTRY FROM SEGMENT REL-TEST-009.
030300*    SHOULD NOT ENTER HERE UNLESS SPACE ALLOCATION TOO SMALL.
030400     ADD     1   TO WRK-CS-09V00-014.
030500 RL-FS2-01-05.
030600     ADD      1 TO WRK-CS-09V00-017.
030700     IF       XRECORD-NUMBER (2) EQUAL TO 500
030800              MOVE   RL-FS2-STATUS TO WRK-XN-0002-002
030900              MOVE  "10" TO WRK-XN-0002-003.
031000 RL-FS2-01-EXIT.
031100     EXIT.
031200 END DECLARATIVES.
031300 CCVS1 SECTION.
031400 OPEN-FILES.
031500     OPEN    OUTPUT PRINT-FILE.
031600     MOVE  CCVS-PGM-ID TO TEST-ID. MOVE CCVS-PGM-ID TO ID-AGAIN.
031700     MOVE    SPACE TO TEST-RESULTS.
031800     PERFORM HEAD-ROUTINE THRU COLUMN-NAMES-ROUTINE.
031900     MOVE    ZERO TO REC-SKL-SUB.
032000     PERFORM CCVS-INIT-FILE 9 TIMES.
032100 CCVS-INIT-FILE.
032200     ADD     1 TO REC-SKL-SUB.
032300     MOVE    FILE-RECORD-INFO-SKELETON
032400          TO FILE-RECORD-INFO (REC-SKL-SUB).
032500 CCVS-INIT-EXIT.
032600     GO TO CCVS1-EXIT.
032700 CLOSE-FILES.
032800     PERFORM END-ROUTINE THRU END-ROUTINE-13. CLOSE PRINT-FILE.
032900 TERMINATE-CCVS.
033000S    EXIT PROGRAM.
033100STERMINATE-CALL.
033200     STOP     RUN.
033300 INSPT. MOVE "INSPT" TO P-OR-F. ADD 1 TO INSPECT-COUNTER.
033400 PASS.  MOVE "PASS " TO P-OR-F.  ADD 1 TO PASS-COUNTER.
033500 FAIL.  MOVE "FAIL*" TO P-OR-F.  ADD 1 TO ERROR-COUNTER.
033600 DE-LETE.  MOVE "*****" TO P-OR-F.  ADD 1 TO DELETE-COUNTER.
033700     MOVE "****TEST DELETED****" TO RE-MARK.
033800 PRINT-DETAIL.
033900     IF REC-CT NOT EQUAL TO ZERO
034000             MOVE "." TO PARDOT-X
034100             MOVE REC-CT TO DOTVALUE.
034200     MOVE     TEST-RESULTS TO PRINT-REC. PERFORM WRITE-LINE.
034300     IF P-OR-F EQUAL TO "FAIL*"  PERFORM WRITE-LINE
034400        PERFORM FAIL-ROUTINE THRU FAIL-ROUTINE-EX
034500          ELSE PERFORM BAIL-OUT THRU BAIL-OUT-EX.
034600     MOVE SPACE TO P-OR-F. MOVE SPACE TO COMPUTED-X.
034700     MOVE SPACE TO CORRECT-X.
034800     IF     REC-CT EQUAL TO ZERO  MOVE SPACE TO PAR-NAME.
034900     MOVE     SPACE TO RE-MARK.
035000 HEAD-ROUTINE.
035100     MOVE CCVS-H-1  TO DUMMY-RECORD. PERFORM WRITE-LINE 2 TIMES.
035200     MOVE CCVS-H-2A TO DUMMY-RECORD. PERFORM WRITE-LINE 2 TIMES.
035300     MOVE CCVS-H-2B TO DUMMY-RECORD. PERFORM WRITE-LINE 3 TIMES.
035400     MOVE CCVS-H-3  TO DUMMY-RECORD. PERFORM WRITE-LINE 3 TIMES.
035500 COLUMN-NAMES-ROUTINE.
035600     MOVE CCVS-C-1 TO DUMMY-RECORD. PERFORM WRITE-LINE.
035700     MOVE CCVS-C-2 TO DUMMY-RECORD. PERFORM WRITE-LINE 2 TIMES.
035800     MOVE HYPHEN-LINE TO DUMMY-RECORD. PERFORM WRITE-LINE.
035900 END-ROUTINE.
036000     MOVE HYPHEN-LINE TO DUMMY-RECORD. PERFORM WRITE-LINE 5 TIMES.
036100 END-RTN-EXIT.
036200     MOVE CCVS-E-1 TO DUMMY-RECORD. PERFORM WRITE-LINE 2 TIMES.
036300 END-ROUTINE-1.
036400      ADD ERROR-COUNTER TO ERROR-HOLD ADD INSPECT-COUNTER TO
036500      ERROR-HOLD. ADD DELETE-COUNTER TO ERROR-HOLD.
036600      ADD PASS-COUNTER TO ERROR-HOLD.
036700*     IF PASS-COUNTER EQUAL TO ERROR-HOLD GO TO END-ROUTINE-12.
036800      MOVE PASS-COUNTER TO CCVS-E-4-1.
036900      MOVE ERROR-HOLD TO CCVS-E-4-2.
037000      MOVE CCVS-E-4 TO CCVS-E-2-2.
037100      MOVE CCVS-E-2 TO DUMMY-RECORD PERFORM WRITE-LINE.
037200  END-ROUTINE-12.
037300      MOVE "TEST(S) FAILED" TO ENDER-DESC.
037400     IF       ERROR-COUNTER IS EQUAL TO ZERO
037500         MOVE "NO " TO ERROR-TOTAL
037600         ELSE
037700         MOVE ERROR-COUNTER TO ERROR-TOTAL.
037800     MOVE     CCVS-E-2 TO DUMMY-RECORD.
037900     PERFORM WRITE-LINE.
038000 END-ROUTINE-13.
038100     IF DELETE-COUNTER IS EQUAL TO ZERO
038200         MOVE "NO " TO ERROR-TOTAL  ELSE
038300         MOVE DELETE-COUNTER TO ERROR-TOTAL.
038400     MOVE "TEST(S) DELETED     " TO ENDER-DESC.
038500     MOVE CCVS-E-2 TO DUMMY-RECORD. PERFORM WRITE-LINE.
038600      IF   INSPECT-COUNTER EQUAL TO ZERO
038700          MOVE "NO " TO ERROR-TOTAL
038800      ELSE MOVE INSPECT-COUNTER TO ERROR-TOTAL.
038900      MOVE "TEST(S) REQUIRE INSPECTION" TO ENDER-DESC.
039000      MOVE CCVS-E-2 TO DUMMY-RECORD. PERFORM WRITE-LINE.
039100     MOVE CCVS-E-3 TO DUMMY-RECORD. PERFORM WRITE-LINE.
039200 WRITE-LINE.
039300     ADD 1 TO RECORD-COUNT.
039400Y    IF RECORD-COUNT GREATER 50
039500Y        MOVE DUMMY-RECORD TO DUMMY-HOLD
039600Y        MOVE SPACE TO DUMMY-RECORD
039700Y        WRITE DUMMY-RECORD AFTER ADVANCING PAGE
039800Y        MOVE CCVS-C-1 TO DUMMY-RECORD PERFORM WRT-LN
039900Y        MOVE CCVS-C-2 TO DUMMY-RECORD PERFORM WRT-LN 2 TIMES
040000Y        MOVE HYPHEN-LINE TO DUMMY-RECORD PERFORM WRT-LN
040100Y        MOVE DUMMY-HOLD TO DUMMY-RECORD
040200Y        MOVE ZERO TO RECORD-COUNT.
040300     PERFORM WRT-LN.
040400 WRT-LN.
040500     WRITE    DUMMY-RECORD AFTER ADVANCING 1 LINES.
040600     MOVE SPACE TO DUMMY-RECORD.
040700 BLANK-LINE-PRINT.
040800     PERFORM WRT-LN.
040900 FAIL-ROUTINE.
041000     IF     COMPUTED-X NOT EQUAL TO SPACE
041100            GO TO   FAIL-ROUTINE-WRITE.
041200     IF     CORRECT-X NOT EQUAL TO SPACE GO TO FAIL-ROUTINE-WRITE.
041300     MOVE   ANSI-REFERENCE TO INF-ANSI-REFERENCE.
041400     MOVE  "NO FURTHER INFORMATION, SEE PROGRAM." TO INFO-TEXT.
041500     MOVE   XXINFO TO DUMMY-RECORD. PERFORM WRITE-LINE 2 TIMES.
041600     MOVE   SPACES TO INF-ANSI-REFERENCE.
041700     GO TO  FAIL-ROUTINE-EX.
041800 FAIL-ROUTINE-WRITE.
041900     MOVE   TEST-COMPUTED TO PRINT-REC PERFORM WRITE-LINE
042000     MOVE   ANSI-REFERENCE TO COR-ANSI-REFERENCE.
042100     MOVE   TEST-CORRECT TO PRINT-REC PERFORM WRITE-LINE 2 TIMES.
042200     MOVE   SPACES TO COR-ANSI-REFERENCE.
042300 FAIL-ROUTINE-EX. EXIT.
042400 BAIL-OUT.
042500     IF     COMPUTED-A NOT EQUAL TO SPACE GO TO BAIL-OUT-WRITE.
042600     IF     CORRECT-A EQUAL TO SPACE GO TO BAIL-OUT-EX.
042700 BAIL-OUT-WRITE.
042800     MOVE CORRECT-A TO XXCORRECT. MOVE COMPUTED-A TO XXCOMPUTED.
042900     MOVE   ANSI-REFERENCE TO INF-ANSI-REFERENCE.
043000     MOVE   XXINFO TO DUMMY-RECORD. PERFORM WRITE-LINE 2 TIMES.
043100     MOVE   SPACES TO INF-ANSI-REFERENCE.
043200 BAIL-OUT-EX. EXIT.
043300 CCVS1-EXIT.
043400     EXIT.
043500 SECT-RL-04-001 SECTION.
043600 REL-INIT-009.
043700     MOVE    "REL-TEST-009" TO PAR-NAME.
043800     MOVE     "CREATE RL-FS2" TO  FEATURE
043900     MOVE     "RL-FS2"        TO  XFILE-NAME (2).
044000     MOVE     "R1-F-G"        TO  XRECORD-NAME (2).
044100     MOVE CCVS-PGM-ID         TO  XPROGRAM-NAME (2).
044200     MOVE     000240          TO  XRECORD-LENGTH (2).
044300     MOVE     "RC"            TO  CHARS-OR-RECORDS (2).
044400     MOVE     0001            TO  XBLOCK-SIZE       (2).
044500     MOVE     000500          TO  RECORDS-IN-FILE  (2).
044600     MOVE     "RL"            TO  XFILE-ORGANIZATION (2).
044700     MOVE     "S"             TO  XLABEL-TYPE        (2).
044800     MOVE     000001          TO  XRECORD-NUMBER     (2).
044900*INITIALIZE  RECORD WORK AREA NUMBER 2.
045000     MOVE     1         TO  WRK-CS-09V00-012.
045100     MOVE     ZERO      TO  WRK-CS-09V00-013 WRK-CS-09V00-014
045200                            WRK-CS-09V00-015 WRK-CS-09V00-016
045300                            WRK-CS-09V00-017 WRK-CS-09V00-018.
045400     MOVE     SPACE     TO  RL-FS2-STATUS.
045500     MOVE     90000002  TO  RL-FS2-KEY.
045600     MOVE     01 TO REC-CT.
045700     OPEN     OUTPUT    RL-FS2.
045800     MOVE     RL-FS2-STATUS TO WRK-XN-0002-001.
045900*CAPTURE STATUS KEY AFTER  OPEN STATEMENT IS EXECUTED.
046000 REL-TEST-009-R.
046100     MOVE    "99"  TO RL-FS2-STATUS.
046200     MOVE     FILE-RECORD-INFO-P1-120 (2) TO RL-FS2-WRK-120.
046300     MOVE     FILE-RECORD-INFO-P1-120 (2) TO
046400               RL-FS2-GRP-120.
046500     WRITE    RL-FS2R1-F-G-240.
046600     IF        RL-FS2-STATUS NOT EQUAL TO "00"
046700              GO TO  REL-TEST-009-2.
046800     IF       XRECORD-NUMBER (2) EQUAL TO 500
046900             GO TO REL-TEST-009-2.
047000     ADD      01 TO  XRECORD-NUMBER (2).
047100     GO TO    REL-TEST-009-R.
047200 REL-TEST-009-2.
047300     IF       WRK-CS-09V00-014 NOT EQUAL TO ZERO
047400              MOVE "EXCEPTIONS/ERRORS" TO RE-MARK
047500              MOVE ZERO TO CORRECT-18V0
047600              MOVE WRK-CS-09V00-014 TO COMPUTED-18V0
047700              PERFORM   FAIL
047800              ELSE
047900              PERFORM   PASS.
048000     PERFORM  PRINT-DETAIL.
048100     ADD      01 TO REC-CT.
048200*    .01
048300     IF       XRECORD-NUMBER (2) NOT EQUAL TO 500
048400              MOVE "INCORRECT COUNT" TO RE-MARK
048500              MOVE  500 TO CORRECT-18V0
048600             MOVE    XRECORD-NUMBER (2)  TO COMPUTED-18V0
048700              PERFORM  FAIL
048800              ELSE
048900              PERFORM PASS.
049000     PERFORM  PRINT-DETAIL.
049100     ADD      01 TO REC-CT.
049200*    .02
049300     IF       WRK-XN-0002-001 NOT EQUAL TO  "00"
049400              MOVE "STATUS/OPEN" TO RE-MARK
049500              MOVE WRK-XN-0002-001 TO COMPUTED-A
049600              MOVE "00" TO CORRECT-A
049700              PERFORM  FAIL
049800              ELSE
049900              PERFORM  PASS.
050000     PERFORM  PRINT-DETAIL.
050100     ADD      01 TO REC-CT.
050200*    .03
050300     IF        RL-FS2-STATUS NOT EQUAL TO "00"
050400              MOVE "STATUS/WRITE" TO RE-MARK
050500              MOVE RL-FS2-STATUS TO COMPUTED-A
050600              MOVE "00" TO CORRECT-A
050700              PERFORM   FAIL
050800              ELSE
050900              PERFORM   PASS.
051000     PERFORM  PRINT-DETAIL.
051100     ADD      01 TO REC-CT.
051200*    .04
051300     MOVE     SPACE  TO  RL-FS2-STATUS.
051400     CLOSE    RL-FS2.
051500     IF        RL-FS2-STATUS NOT EQUAL TO "00"
051600              MOVE "CLOSE/STATUS" TO RE-MARK
051700              MOVE   RL-FS2-STATUS TO COMPUTED-A
051800              MOVE "00" TO CORRECT-A
051900              PERFORM FAIL
052000              ELSE
052100              PERFORM PASS.
052200     PERFORM  PRINT-DETAIL.
052300     ADD      01 TO REC-CT.
052400*    .05
052500 REL-INIT-010.
052600     MOVE    "REL-TEST-010" TO PAR-NAME.
052700     MOVE     2     TO  WRK-CS-09V00-012.
052800     MOVE     ZERO  TO  WRK-CS-09V00-013.
052900     MOVE     ZERO  TO  WRK-CS-09V00-014.
053000     MOVE     ZERO  TO  WRK-CS-09V00-015.
053100     MOVE     ZERO  TO  WRK-CS-09V00-016.
053200     MOVE     ZERO  TO  WRK-CS-09V00-017.
053300     MOVE     ZERO  TO  WRK-CS-09V00-018.
053400     MOVE     01 TO REC-CT.
053500     OPEN     I-O  RL-FS2.
053600     MOVE     SPACE     TO  WRK-XN-0002-002
053700     MOVE     SPACE     TO  WRK-XN-0002-003
053800     MOVE     SPACE     TO  WRK-XN-0002-004
053900     MOVE      RL-FS2-STATUS TO WRK-XN-0002-001
054000     MOVE     SPACE TO RL-FS2-STATUS.
054100*CAPTURE THE CONTENTS OF STATUS KEY AFTER OPEN IS EXECUTED.
054200     MOVE    "USE/FILE STATUE"  TO FEATURE.
054300 REL-TEST-010-R.
054400     ADD      1 TO WRK-CS-09V00-014.
054500     ADD      1 TO WRK-CS-09V00-015.
054600     READ     RL-FS2.
054700     IF        RL-FS2-STATUS EQUAL TO "10"
054800              GO TO  REL-TEST-010-3.
054900     MOVE     RL-FS2-WRK-120 TO FILE-RECORD-INFO-P1-120 (2).
055000     IF       WRK-CS-09V00-015 EQUAL TO 5
055100              ADD 01 TO UPDATE-NUMBER (2)
055200              MOVE FILE-RECORD-INFO-P1-120 (2) TO RL-FS2-WRK-120
055300              REWRITE RL-FS2R1-F-G-240
055400           MOVE ZERO TO WRK-CS-09V00-015
055500              GO TO  REL-TEST-010-2.
055600     IF       WRK-CS-09V00-014 GREATER 500
055700              GO TO  REL-TEST-010-3.
055800     GO TO    REL-TEST-010-R.
055900 REL-TEST-010-2.
056000     IF        RL-FS2-STATUS NOT EQUAL TO "00"
056100              ADD 1 TO WRK-CS-09V00-016.
056200     MOVE    SPACE TO RL-FS2-STATUS.
056300     GO TO    REL-TEST-010-R.
056400 REL-TEST-010-3.
056500     IF       WRK-CS-09V00-013 NOT EQUAL TO 1
056600              MOVE  "EXCEPTIONS/ERRORS" TO RE-MARK
056700              MOVE WRK-CS-09V00-013 TO COMPUTED-18V0
056800              MOVE  1 TO CORRECT-18V0
056900              PERFORM FAIL
057000              ELSE
057100              PERFORM PASS.
057200     PERFORM  PRINT-DETAIL.
057300     ADD      01 TO REC-CT.
057400*    .01
057500     IF       WRK-CS-09V00-014 NOT EQUAL TO 501
057600              MOVE "INCORRECT COUNT" TO RE-MARK
057700              MOVE WRK-CS-09V00-014 TO COMPUTED-18V0
057800              MOVE 501 TO CORRECT-18V0
057900              PERFORM FAIL
058000              ELSE
058100             PERFORM PASS.
058200     PERFORM  PRINT-DETAIL.
058300     ADD      01 TO REC-CT.
058400*    .02
058500     IF       WRK-XN-0002-001 NOT EQUAL TO "00"
058600              MOVE "OPEN/STATUS"  TO  RE-MARK
058700              MOVE WRK-XN-0002-001 TO COMPUTED-A
058800              MOVE  "00" TO CORRECT-A
058900              PERFORM FAIL
059000              ELSE
059100              PERFORM PASS.
059200     PERFORM  PRINT-DETAIL.
059300     ADD      01 TO REC-CT.
059400*    .03
059500     IF        RL-FS2-STATUS NOT EQUAL  TO "10"
059600              MOVE "ATEND/STATUS" TO RE-MARK
059700              MOVE  RL-FS2-STATUS TO COMPUTED-A
059800              MOVE "10" TO CORRECT-A
059900              PERFORM FAIL
060000              ELSE
060100              PERFORM PASS.
060200     PERFORM  PRINT-DETAIL.
060300     ADD      01 TO REC-CT.
060400*    .04
060500     IF       WRK-XN-0002-002 NOT EQUAL TO "10"
060600              MOVE  "EXCEPTIN/STATUS" TO RE-MARK
060700              MOVE  WRK-XN-0002-002 TO COMPUTED-A
060800              MOVE  "10" TO CORRECT-A
060900              PERFORM FAIL
061000              ELSE
061100              PERFORM PASS.
061200     PERFORM  PRINT-DETAIL.
061300     ADD      01 TO REC-CT.
061400*    .05
061500     IF       WRK-XN-0002-003 NOT EQUAL TO "10"
061600              MOVE "NO/EXCEPTION" TO  RE-MARK
061700              MOVE WRK-XN-0002-003 TO COMPUTED-A
061800              MOVE "10" TO CORRECT-A
061900              PERFORM FAIL
062000              ELSE
062100              PERFORM PASS.
062200     PERFORM  PRINT-DETAIL
062300     ADD      01 TO REC-CT.
062400*    .06
062500     MOVE     SPACE TO RL-FS2-STATUS.
062600     CLOSE    RL-FS2
062700     IF        RL-FS2-STATUS NOT EQUAL TO "00"
062800              MOVE "CLOSE/STATUS" TO RE-MARK
062900              MOVE  RL-FS2-STATUS TO  COMPUTED-A
063000              MOVE "00" TO CORRECT-A
063100              PERFORM FAIL
063200              ELSE
063300              PERFORM PASS.
063400     PERFORM  PRINT-DETAIL.
063500     ADD      01 TO REC-CT.
063600*    .07
063700 CCVS-EXIT SECTION.
063800 CCVS-999999.
063900     GO TO CLOSE-FILES.
