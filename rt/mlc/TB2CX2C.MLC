         TITLE 'Test B2C and X2C changes for issue #458'
TB2CX2C  CSECT
         STM   14,12,12(13)        Save caller's registers
         LR    12,15               R12 = base register
         USING TB2CX2C,12          Establish addressability
         LM    3,5,TTbl            R3 -> 1st, R4 = len 1, R5 -> last
         SR    2,2                 Test number
LP1      DS    0H
         AHI   2,1                 Current test number
         LM    6,9,0(3)            R6 --> gen val, R7 --> gen val len,
*                                  R8 --> exp val, R9 = exp val len
         C     9,0(,7)             Exp val len = gen val len?
         BNE   Error1              No; error
         LTR   9,9                 Length 0?
         BZ    NX1                 Yes; next test
         BCTR  9,0                 Length code
         EX    9,Compare           Gen val = exp val?
         BNE   Error2              No; error
NX1      DS    0H
         BXLE  3,4,LP1             Next test
         WTO   'All tests successful'
         SR    15,15               All tests okay
         B     Exit                Exit with return code
Error1   DS    0H
         SNAP  ID=1,PDATA=(GPR),                                       X
               TEXT='Length error; R2 = failing test number'
         LA    15,8                Set error return code
         B     Exit                Exit with return code
Error2   DS    0H
         SNAP  ID=2,PDATA=(GPR),                                       X
               TEXT='Value error; R2 = failing test number'
         LA    15,8                Set error return code
         B     Exit                Exit with return code
Exit     DS    0H
         L     14,12(,13)          Restore caller's registers
         LM    0,12,20(13)         ... except R15
         BR    14                  Return to caller
*
         LTORG
*
Compare  CLC   0(*-*,6),0(8)       Compare gen val to exp val
*
         DS    0D
TTbl     DC    A(T1,16,TN,0)           Table of tests
T1       DS    0D
         DC    A(B1,B1L,B1V,L'B1V)     1
         DC    A(B2,B2L,B2V,L'B2V)     2
         DC    A(B3,B3L,B3V,L'B3V)     3
         DC    A(X1,X1L,X1V,L'X1V)     4
         DC    A(X2,X2L,X2V,L'X2V)     5
         DC    A(X3,X3L,X3V,L'X3V)     6
         DC    A(X4,X4L,X4V,L'X4V)     7
***********************************************************************
*        Put new tests above this line
***********************************************************************
TN       EQU   *-16                Last test
B1V      DC    C''
B2V      DC    C'3'
B3V      DC    C'*1'
X1V      DC    C''
X2V      DC    X'F3'
X3V      DC    X'F1F2F3F4F5'
X4V      DC    X'00F1F2'
*
         GBLC  &C
         GBLA  &A
***********************************************************************
***      B2C('')
&C       SETC B2C('')
B1       DC    C'&C'               '' C''
B1L      DC    AL4(L'B1)
***      B2C('11110011')
&C       SETC B2C('11110011')
B2       DC    C'&C'               '11110011' C'3'
B2L      DC    AL4(L'B2)
*
***      B2C('101110011110001')
&C       SETC B2C('101110011110001')
B3       DC    C'&C'               '101110011110001' C'*1'
B3L      DC    AL4(L'B3)
***********************************************************************
***      X2C('')
&C       SETC X2C('')
X1       DC    C'&C'               '' C''
X1L      DC    AL4(L'X1)
*
***      X2C('F3')
&C       SETC X2C('F3')
X2       DC    C'&C'               'F3' C'3'
X2L      DC    AL4(L'X2)
*
***      X2C('F1F2F3F4F5')
&C       SETC X2C('F1F2F3F4F5')
X3       DC    C'&C'               'F1F2F3F4F5' C'12345'
X3L      DC    AL4(L'X3)
*
***      X2C('0F1F2')
&C       SETC X2C('0F1F2')
X4       DC    C'&C'               '0F1F2' C'n12' n=X'00'
X4L      DC    AL4(L'X4)
***********************************************************************
*
         END

