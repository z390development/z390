*********************************************************************
* z390 - Mainframe assembler emulator and run-time engine
* Copyright (C) 2021 z390 Assembler LLC
*
* This file is part of z390.
*
* z390 is free software; you can redistribute it and/or modify
* it under the terms of the GNU General Public License as published by
* the Free Software Foundation; either version 2 of the License, or
* (at your option) any later version.
* z390 is distributed in the hope that it will be useful,
* but WITHOUT ANY WARRANTY; without even the implied warranty of
* MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
* GNU General Public License for more details.
*
* You should have received a copy of the GNU General Public License 
* along with this program; if not, see https://www.gnu.org/licenses.
*********************************************************************
* Author - Don Higgins                                              *
* Date   - 09/30/05                                                 *
*********************************************************************
* 01/22/07 RPI 542 CORRECT GETMAIN OUTPUT R1=ADDR
*********************************************************************
         MACRO
         TESTGM &TYPE,&LV,&LOC,&HL,&REG
         GETMAIN &TYPE,LV=&LV,LOC=&LOC
         CL    R1,=A(LINE)                         RPI 542
         RT1   CC&HL
         LR    &REG,R1                             RPI 542
         LR    R14,R1                              RPI 542
         LA    R15,GMLEN
         L     R1,=X'EE000000'
         MVCL  R14,R0
         MEND
         TITLE 'TESTMEM1 REGRSSION TEST GETMAIN AND FREEMAIN'
TESTMEM1 RT1   MAIN
         EQUREGS
FQE24A   DC    A(0)
FQE24    DC    AL8(0)
FQE31A   DC    A(0)
FQE31    DC    AL8(0)
LINE     EQU   X'1000000'
GMLEN    EQU   100
         RT1   START
         L     R2,=A(ZCVT)
         USING IHAZCVT,R2
         L     R3,ZCVTFQ24
         ST    R3,FQE24A    SAVE 24 BIT FQE ADDR
         MVC   FQE24,0(R3)  SAVE 24 BIT FQE
         L     R3,ZCVTFQ31
         ST    R3,FQE31A    SAVE 31 BIT FQE ADDR
         MVC   FQE31,0(R3)  SAVE 31 BIT FQE
         DROP  R2
         L     R2,=A(GMLEN)
         TESTGM R,GMLEN,,L,R3
         TESTGM R,GMLEN,,L,R4
         TESTGM R,GMLEN,,L,R5
         TESTGM RC,(R2),BELOW,L,R6
         TESTGM RC,(R2),,H,R7
         TESTGM RC,(R2),,H,R8
         TESTGM RU,(R2),ABOVE,H,R9
         FREEMAIN A=(R9),LV=(R2)
         FREEMAIN A=(R3),LV=(R2)
         FREEMAIN A=(R5),LV=(R2)
         FREEMAIN A=(R4),LV=(R2)
         FREEMAIN A=(R7),LV=(R2)
         FREEMAIN A=(R6),LV=(R2)
         FREEMAIN A=(R8),LV=(R2)
         L     R2,=A(ZCVT)
         USING IHAZCVT,R2
         L     R3,ZCVTFQ24
         CL    R3,FQE24A    VER  24 BIT FQE ADDR
         RT1   CCE
         CLC   FQE24(4),0(R3)  VER  24 BIT FQE PTR
         RT1   CCE
         CLC   FQE24+4(4),4(R3)  VER  24 BIT FQE LEN
         RT1   CCE
         L     R3,ZCVTFQ31
         CL    R3,FQE31A    VER  31 BIT FQE ADDR
         RT1   CCE
         CLC   FQE31(4),0(R3)  VER  31 BIT FQE PTR
         RT1   CCE
         CLC   FQE31+4(4),4(R3)  VER  31 BIT FQE LEN
         RT1   CCE
         WTO   'TESTMEM1 ENDED OK'
         RT1   END
         ZCVTD
         END
