*********************************************************************
* Copyright 2005 Automated Software Tools Corporation               *
* This source code is part of z390 assembler/emulator package       *
* The z390 package is distributed under GNU general public license  *
* Author - Don Higgins                                              *
* Date   - 09/30/05                                                 *
*********************************************************************
* 01/29/06 RPI 193 test mz390 symbol length for implicit CPZ
* 08/29/06 RPI 415 test AIF reference to ordinary symbol length
* 08/30/06 RPI 419 test AIF reference to ordinary symbol setc parm
*          with labeled using prefix and trailing expression oper. 
* 09/08/06 RPI 442 test AIF reference to dsect sym defined via *  loc
* 01/22/08 RPI 790 update tests for I' and S'                        
*********************************************************************
TESTSYM1 SUBENTRY
         WTO   'TESTSYM1 - TEST SYMBOL T,L,I,S OPERATORS'
* TEST I' AND S'
         MACRO
&N       TESTIS &DC,&I,&S
         B     TESTIS&SYSNDX
&N       DC    &DC
TESTIS&SYSNDX DS 0H
         CLC  =AL1(I'&N),=AL1(&I)
         BNE  ERR2
         CLC  =AL1(S'&N),=AL1(&S)
         BNE  ERR3
         MEND
PI1S2    TESTIS P'7.50,8.1',1,2
ZI1S2    TESTIS Z'7.50,8.1',1,2
DDI14S2  TESTIS DD'7.50,8.1',14,2
EDI5S2   TESTIS ED'7.50,8.1',5,2
LDI32S2  TESTIS LD'7.50,8.1',32,2
FI26S5   TESTIS FS5'1',26,5
HI10S5   TESTIS HS5'1',10,5
         WTO    'TESTSYM1 ENDED OK'
         SUBEXIT   
ERR2     WTO   'INVALID I' VALUE'
         SUBEXIT RC=16
ERR3     WTO   'INVALID S' VALUE'
         SUBEXIT RC=16
* TEST DS/DC SYMBOL TYPE AND LENGTH
         MACRO
         TSYM  &SDS,&ST,&SL
&T       SETC  T'&SDS
&L       SETA  L'&SDS
         MNOTE 'DS=&SDS T=&T L=&L'
         AIF   (&T NE &ST).ERR
         AIF   (&L NE &SL).ERR
         MEXIT
.ERR     ANOP
         MNOTE 12,'SYMBOL TYPE OR LENGTH ERROR'        
         MEND
         TSYM  SA,A,4
         TSYM  SAL3,R,3
         TSYM  S2A,A,4
         TSYM  SEAL3,R,3
         TSYM  SCL2,C,2
         TSYM  SF,F,4
         TSYM  SH,H,2
         TSYM  SY,Y,2
         TSYM  SF1,G,1
         TSYM  SH2,G,2
         TSYM  SY3,R,3
         TSYM  SE,E,4
         TSYM  SD,D,8
         TSYM  SL,L,16
         TSYM  SE1,K,1
         TSYM  SD2,K,2
         TSYM  SL3,K,3
         TSYM  SS,S,2
         TSYM  SS1,R,1
         TSYM  SP,P,1
         TSYM  SP2,P,2
         TSYM  SZ,Z,1
         TSYM  SZ2,Z,2
         TSYM  TESTSYM1,J,1
         TSYM  LR,I,2
         TSYM  ST,I,4
         TSYM  MVC,I,6
         TSYM  SCD2,C,2
         TSYM  SPD2,P,2
         TSYM  SZD2,Z,2
* TEST NUMERIC AND OMITTED TYPES
         MACRO 
         TTYPE &P1,&P2
&TYPE    SETC  T'&P1
         MNOTE 'P1=&P1 T''P1 = &TYPE' 
&TYPE    SETC  T'&P2
         MNOTE 'P2=&P2 T''P2 = &TYPE' 
         MEND
         TTYPE A
         TTYPE 
         TTYPE ,B
         TTYPE 0
         TTYPE 9
ADUP     EQU   3
SA       DS    A
SAL3     DS    AL3
S2A      DS    2A
SEAL3    DS    (ADUP)AL3
SCL2     DS    CL2
SCD2     DC    C'12'
SF       DS    F
SH       DS    H
SY       DS    Y
SF1      DS    FL1
SH2      DS    HL2
SY3      DS    YL3
SS       DS    S
SS1      DS    SL1
SE       DS    E
SD       DS    D
SL       DS    L
SE1      DS    EL1
SD2      DS    DL2
SL3      DS    LL3
SP       DS    P
SP2      DS    PL2
SPD2     DC    P'123'
SZ       DS    Z
SZ2      DS    ZL2
SZD2     DC    Z'12'
LR       LR    1,1
ST       ST    1,0(1)
MVC      MVC   0(1,1),0(2)
* TEST AIF REF TO ORDINARY SYMBOL LENGTH OF DSECT
D        DSECT
DF1      DS   XL10
DL       EQU  *-D
         AIF  (DL EQ 10).OK1 
&DL      SETA  DL 
         MNOTE 8,'DL=&DL NE 10 ERROR'
         AGO   .OK1END
.OK1     MNOTE 'DL EQ 10 OK'
.OK1END  ANOP
&PARM    SETC  'LABEL.DL+2'
         AIF  (&PARM EQ 10).OK2  
         MNOTE 8,'LABEL.DL+2 LENGTH NOT 10'
         AGO   .OK2END
.OK2     MNOTE 'LABEL.DL+2 EQ 10 OK'
.OK2END  ANOP
* TEST AIF REF TO DSECT SYM DEFINED BY EQU *
CVTFIX   DSECT     
         DS   8X
CVTMAP   EQU  *
CVT      EQU  CVTMAP,,T'CVTMAP
&LCVT    SETA CVT
         MNOTE 'LCVT=&LCVT'
         AIF  (&LCVT EQ 8).OK3
         MNOTE 8,'LCVT NOT EQUAL 8 ERROR'
.OK3     ANOP
         END
