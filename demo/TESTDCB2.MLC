*********************************************************************
* z390 - Mainframe assembler emulator and run-time engine
* Copyright (C) 2024 z390 Assembler LLC
*
* This file is part of z390.
*
* z390 is free software; you can redistribute it and/or modify
* it under the terms of the GNU General Public License as published by
* the Free Software Foundation; either version 2 of the License, or
* (at your option) any later version.
* z390 is distributed in the hope that it will be useful,
* but WITHOUT ANY WARRANTY; without even the implied warranty of
* MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
* GNU General Public License for more details.
*
* You should have received a copy of the GNU General Public License 
* along with this program; if not, see https://www.gnu.org/licenses.

* Author - Don Higgins and John Ganci                               *
* Date   - 06/04/2024                                               *
*********************************************************************
* Same as demo/TESTDCB1 but illustrates EODAD that returns to GET
*********************************************************************
*
TESTDCB2 SUBENTRY
         WTO   'TESTDCB2 Copy SYSUT1 ASC test file to SYSUT2 ASC text'
         OPEN  MF=(E,OFILES)       Open all files
         MVI   EOFFLAG,C'N'        Set not EOF on input file
LOOP     DS    0H
         GET   SYSUT1,RECORD       Get next/first record
AFTERGET DS    0H
         CLI   EOFFLAG,C'Y'        No more records?
         BE    ELOOP               Yes: all done
         AP    PTOT,=P'1'          Count records
         MVC   DTOT,=X'40202020'   Convert record count
         ED    DTOT,PTOT           ... to printable decimal 
         PUT   SYSOUT,MSG          Put rec# || record
         PUT   SYSUT2,RECORD       Put record
         B     LOOP
ELOOP    DS    0H
         CLOSE MF=(E,CFILES)
         WTO   'TESTDCB2 Ended OK'
         SUBEXIT
***********************************************************************
*        EODAD routine
***********************************************************************
EOF      DS    0H
         MVI   EOFFLAG,C'Y'        Set end of file reached on input
         C     14,@AFTERGET        Belt and suspenders test
         BER   14                  Return to GET
         DC    H'0'                Should not get here!
*
@AFTERGET DC   A(AFTERGET)         For belt and suspenders R14 test
OFILES   OPEN  MF=L,(SYSUT1,INPUT,SYSUT2,OUTPUT,SYSOUT,OUTPUT)
CFILES   CLOSE MF=L,(SYSUT1,,SYSUT2,,SYSOUT)
SYSUT1   DCB   DDNAME=SYSUT1,RECFM=FT,LRECL=80,EODAD=EOF,MACRF=GM
SYSUT2   DCB   DDNAME=SYSUT2,RECFM=FT,LRECL=80,MACRF=PM
SYSOUT   DCB   DDNAME=SYSOUT,RECFM=FT,BLKSIZE=120,MACRF=PM
EOFFLAG  DS    CL1                 Set to C'Y' when EOF on input file
PTOT     DC    PL2'0'
MSG      DS    0CL120
         DC    C'REC#='
DTOT     DC    CL4' ',C' TEXT='
RECORD   DC    CL80' '
         DC    (MSG+120-*)C' '
         DCBD
         END
