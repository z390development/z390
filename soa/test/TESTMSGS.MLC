*********************************************************************
* z390 - Mainframe assembler emulator and run-time engine
* Copyright (C) 2021 z390 Assembler LLC
*
* This file is part of z390.
*
* z390 is free software; you can redistribute it and/or modify
* it under the terms of the GNU General Public License as published by
* the Free Software Foundation; either version 2 of the License, or
* (at your option) any later version.
* z390 is distributed in the hope that it will be useful,
* but WITHOUT ANY WARRANTY; without even the implied warranty of
* MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
* GNU General Public License for more details.
*
* You should have received a copy of the GNU General Public License 
* along with this program; if not, see https://www.gnu.org/licenses.
* Author - Don Higgins                                              *
* Date   - 02/22/07                                                 *
*********************************************************************
* TESTMSGS start server for 4 digit port in PARM(1234)              
*     1.  read 48 character message from port connection        
*     2.  display message on log                  
*     3.  return msg
*     4.  repeat until msg = STOP
*********************************************************************
* Maintenance:
* 02/22/07 RPI 553 code new TCPIO test server          
* 07/23/07 RPI 664 correct fetch of EXEC PARM(...) address
* 07/23/07 RPI 664 add CONN= option to TCPIO SEND              
*********************************************************************
TESTMSGS SUBENTRY
         L     R2,0(R1)  RPI 582 ADDR OF EXEC PARM
         WTO   'TESTMSGC TC[OP TEST SERVER'
         PACK  PWORK,2(4,R2)
         CVB   R4,PWORK        R4=PORT
         TCPIO OPEN,PORT=(R4)
         LTR   R15,R15
         BNZ   ABORT
         ZAP   RETRY,=P'2'
LOOP     EQU   *
         WTO   'TESTMSGS RECEIVE NEXT MESSAGE'
         TCPIO RECEIVE,PORT=(R4),MSG=RMSG,LMSG==A(L'RMSG)
         LTR   R15,R15
         BZ    OK     
         SP    RETRY,=P'1'
         BNM   LOOP
         WTO   'TESTMSGS RECEIVE RETRY FAILED'
         B     ABORT
OK       EQU   *
         ZAP   RETRY,=P'2'
         WTO   MF=(E,WTORMSG)
         CLC   RMSGCMD+5(4),=C'STOP'
         BE    STOP
         TCPIO SEND,PORT=(R4),CONN=(R2),MSG=RMSG,LMSG==A(L'RMSG) RPI664
         LTR   R15,R15
         BNZ   ABORT
         B     LOOP
STOP     EQU   *
         TCPIO CLOSE,PORT=(R4)
         LTR   R15,R15
         BNZ   ABORT
         WTO   'TESTMSGS STOPPED OK BY STOP COMMAND'
         SUBEXIT
ABORT    EQU   *
         LR    R5,R15
         WTO   'TESTMSGS ABORTING DUE TO ERROR'
         LR    R15,R5
         ABEND 111,DUMP
         LTORG
PWORK    DC    PL8'0'
RETRY    DC    PL3'0'
WTORMSG  DC    AL2(WTORMSG_END-*,0),C'TESTMSGS RECV MSG='
RMSG     DS    0CL48
RMSGID   DC    CL8' '
RMSGCMD  DC    CL40' '
WTORMSG_END EQU *
         EQUREGS
         END
